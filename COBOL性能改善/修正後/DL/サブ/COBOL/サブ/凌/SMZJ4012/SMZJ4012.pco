      ******************************************************************
      * $Header$
      ******************************************************************
      *
      * システムＩＤ   : T-STAR
      * システム名称   : 次期TSTARシステム
      * プログラムＩＤ : SMZJ4012
      * プログラム名称 : 取引事由判定
      * 処理概要       : 取引事由により、各フラグを判定する
      * 参照ＤＢ       : なし
      * 引数           : IN      取引事由
      *                : IN      判定対象区分
      *                : OUT     評価損益データフラグ
      *                : OUT     未収計算
      *                : OUT     C/A位置付けフラグ
      *                : OUT     権利行使・割当フラグ
      *                : OUT     売買フラグ
      *                : OUT     為替差損益フラグ
      *                : OUT     株式配当新株判定フラグ
      *                : OUT     初期設定元本フラグ
      *                : OUT     CB転換・ワラント行使フラグ
      * 改訂履歴
      * 年月日        区分     所属    担当      内容
      * ------------  ----    ------  -------  -------------
      * 20080610      新規     SCS     梁偉     新規作成
      * 20080804      修正     SCS     楊超     残タスク9
      * 20080825      修正     SCS     楊超
      * 20080906      修正     SCS     楊超
      * 20081204      修正     SCS     顧建鋒   残タスク157
      * 20090122      修正     SCS     徐曉俊   残タスク150
      * 20090227      修正     SCS     李菁     残タスク190
      * 20090309      修正     SCS     徐曉俊   残タスク165対応
      * 20090330      修正     SCS     王  粒   テーマ94対応
      * 20090413      修正     SCS     王飛     テーマ109対応
      * 20090422      修正     SCS     王飛     テーマ24対応
      * 20090623      修正     SCS     何傑文   総合障害1050
      * 20090626      修正     SCS     李菁     テーマ760
      * 20091113      修正     SCS     高帥     テーマ1841
      * 20100608      修正     SCS     樊玉華   テーマ2748
      ******************************************************************
       IDENTIFICATION         DIVISION.
       PROGRAM-ID.    SMZJ4012.
       AUTHOR.        Nomura.
       DATE-WRITTEN.  08-06-10.
       DATE-COMPILED.
      ******************************************************************
      *
      *        環  境  部
      *
      ******************************************************************
       ENVIRONMENT            DIVISION.
       CONFIGURATION          SECTION.
       SOURCE-COMPUTER.       L1000V.
      *SOURCE-COMPUTER.       L1000V   WITH DEBUGGING MODE.
       OBJECT-COMPUTER.       RP5470.
       INPUT-OUTPUT           SECTION.
       FILE-CONTROL.
      ******************************************************************
      *
      *        デ  ー  タ  部
      *
      ******************************************************************
       DATA                   DIVISION.
      ******************************************************************
      *        作業場所節
      ******************************************************************
       WORKING-STORAGE        SECTION.
       01  IDNAME                  PIC  X(80)  VALUE
       "$Id$".
      ******************************************************************
      *         コンスタント  エリア
      ******************************************************************
      *----------  共通コンスタント定義の宣言
       01  CONSTANT-AREA.
         05  CNS-MZCNS.
           COPY  "MZCNS.cpy".
      *----------  プログラム名
         05  CNS-PGM-ID            PIC  X(08)  VALUE  "SMZJ4012".
      *----------  判定対象区分: '1'(評価損益データフラグ)
         05  CNS-HANTEI-TARGET-1   PIC  X(01)  VALUE  "1".
      *----------  判定対象区分: '2'(未収計算)
         05  CNS-HANTEI-TARGET-2   PIC  X(01)  VALUE  "2".
      *----------  判定対象区分: '3'(C/A位置付けフラグ)
         05  CNS-HANTEI-TARGET-3   PIC  X(01)  VALUE  "3".
      *----------  判定対象区分: '4'(売買現先スタートフラグ)
         05  CNS-HANTEI-TARGET-4   PIC  X(01)  VALUE  "4".
      *----------  判定対象区分: '5'(権利行使・割当フラグ)
         05  CNS-HANTEI-TARGET-5   PIC  X(01)  VALUE  "5".
      *----------  判定対象区分: '6'(売買フラグ)
         05  CNS-HANTEI-TARGET-6   PIC  X(01)  VALUE  "6".
      *----------  判定対象区分: '7'(為替差損益フラグ)
         05  CNS-HANTEI-TARGET-7   PIC  X(01)  VALUE  "7".
      *----------  判定対象区分: '8'(株式配当新株判定フラグ)
         05  CNS-HANTEI-TARGET-8   PIC  X(01)  VALUE  "8".
      *----------  判定対象区分: '9'(初期設定元本フラグ)
         05  CNS-HANTEI-TARGET-9   PIC  X(01)  VALUE  "9".
      *----------  判定対象区分: 'a'(CB転換・ワラント行使フラグ)
         05  CNS-HANTEI-TARGET-A   PIC  X(01)  VALUE  "a".
      *----------  判定対象区分: 'b'(取引属性逆転フラグ)
         05  CNS-HANTEI-TARGET-B   PIC  X(01)  VALUE  "b".
      *----------  取引事由: 'FTPU'(評価益計上)
         05  CNS-FTPU              PIC  X(04)  VALUE  "FTPU".
      *----------  取引事由: 'FTLU'(評価損計上)
         05  CNS-FTLU              PIC  X(04)  VALUE  "FTLU".
      *----------  取引事由: 'FTPV'(前払金評価益計上)
         05  CNS-FTPV              PIC  X(04)  VALUE  "FTPV".
      *----------  取引事由: 'FTLV'(前払金評価損計上)
         05  CNS-FTLV              PIC  X(04)  VALUE  "FTLV".
      *----------  取引事由: 'FTPW'(前払金評価益戻入)
         05  CNS-FTPW              PIC  X(04)  VALUE  "FTPW".
      *----------  取引事由: 'FTCU'(未収収益計上)
         05  CNS-FTCU              PIC  X(04)  VALUE  "FTCU".
      *----------  取引事由: 'FTPR'(評価益戻入)
         05  CNS-FTPR              PIC  X(04)  VALUE  "FTPR".
      *----------  取引事由: 'FTLR'(評価損戻入)
         05  CNS-FTLR              PIC  X(04)  VALUE  "FTLR".
      *----------  取引事由: 'FTLW'(前払金評価損戻入)
         05  CNS-FTLW              PIC  X(04)  VALUE  "FTLW".
      *----------  取引事由: 'FTCR'(未収収益戻入)
         05  CNS-FTCR              PIC  X(04)  VALUE  "FTCR".
      *----------  取引事由: 'AMRT'(アモチゼ−ション)
         05  CNS-AMRT              PIC  X(04)  VALUE  "AMRT".
      *----------  取引事由: 'ACUM'(アキュミュレ−ション)
         05  CNS-ACUM              PIC  X(04)  VALUE  "ACUM".
      *----------  取引事由: 'FTSQ'(配当付株数計上)
         05  CNS-FTSQ              PIC  X(04)  VALUE  "FTSQ".
      *----------  取引事由: 'DIVS'(現金による配当金)
         05  CNS-DIVS              PIC  X(04)  VALUE  "DIVS".
      *----------  取引事由: 'DVAP'(配当調整金支払)
         05  CNS-DVAP              PIC  X(04)  VALUE  "DVAP".
      *----------  取引事由: 'DVAI'(配当調整金入金)
         05  CNS-DVAI              PIC  X(04)  VALUE  "DVAI".
      *----------  取引事由: 'CPN'(利金入金)
         05  CNS-CPN               PIC  X(03)  VALUE  "CPN".
      *----------  取引事由: 'MRGD'(会社合併（減少))
         05  CNS-MRGD              PIC  X(04)  VALUE  "MRGD".
      *----------  取引事由: 'CONS'(新旧併合（新株）)
         05  CNS-CONS              PIC  X(04)  VALUE  "CONS".
      *----------  取引事由: 'CID'(有償増資（権利落による減少）)
         05  CNS-CID               PIC  X(03)  VALUE  "CID".
      *----------  取引事由: 'SDID'(株式配当による減少)
         05  CNS-SDID              PIC  X(04)  VALUE  "SDID".
      *----------  取引事由: 'CIQD'(分割（利益準備金）・減)
         05  CNS-CIQD              PIC  X(04)  VALUE  "CIQD".
      *----------  取引事由: 'CISD'（株式分割（分割元）)
         05  CNS-CISD              PIC  X(04)  VALUE  "CISD".
      *----------  取引事由: 'OCID'（会社分割（分割元）)
         05  CNS-OCID              PIC  X(04)  VALUE  "OCID".
      *----------  取引事由: 'ISD'（有償増資（新株発行による減少）)
         05  CNS-ISD               PIC  X(03)  VALUE  "ISD".
      *----------  取引事由: 'MRG'(会社合併(増加))
         05  CNS-MRG               PIC  X(03)  VALUE  "MRG".
      *----------  取引事由: 'CONI'(新旧併合(親株))
         05  CNS-CONI              PIC  X(04)  VALUE  "CONI".
      *----------  取引事由: 'CII'(有償増資（権利落による増加）)
         05  CNS-CII               PIC  X(03)  VALUE  "CII".
      *----------  取引事由: 'ISI'(有償増資（新株発行による増加）)
         05  CNS-ISI               PIC  X(03)  VALUE  "ISI".
      *----------  取引事由: 'OPBA'(オプション権利割当による新規買建)
         05  CNS-OPBA              PIC  X(04)  VALUE  "OPBA".
      *----------  取引事由: 'OPSA'(オプション権利割当による新規売建)
         05  CNS-OPSA              PIC  X(04)  VALUE  "OPSA".
      *----------  取引事由: 'SETA'(オプション権利割当による反対売買)
         05  CNS-SETA              PIC  X(04)  VALUE  "SETA".
      *----------  取引事由: 'OPBE'(オプション権利行使による新規買建)
         05  CNS-OPBE              PIC  X(04)  VALUE  "OPBE".
      *----------  取引事由: 'OPSE'(オプション権利行使による新規売建)
         05  CNS-OPSE              PIC  X(04)  VALUE  "OPSE".
      *----------  取引事由: 'SETE'(オプション権利行使による反対売買)
         05  CNS-SETE              PIC  X(04)  VALUE  "SETE".
      *----------  取引事由: 'FSEL'(先物現渡による売却)
         05  CNS-FSEL              PIC  X(04)  VALUE  "FSEL".
      *----------  取引事由: 'OSOT'(オプション権利行使／割当による売却)
         05  CNS-OSOT              PIC  X(04)  VALUE  "OSOT".
      *----------  取引事由: 'REQ'(単元株未満の買取請求による売却)
         05  CNS-REQ               PIC  X(03)  VALUE  "REQ".
      *----------  取引事由: 'RSLL'(端数株式調整売却)
         05  CNS-RSLL              PIC  X(04)  VALUE  "RSLL".
      *----------  取引事由: 'SELL'(一般的な売却)
         05  CNS-SELL              PIC  X(04)  VALUE  "SELL".
      *----------  取引事由: 'SSEL'(信用現渡による売却)
         05  CNS-SSEL              PIC  X(04)  VALUE  "SSEL".
      *----------  取引事由: 'TSOT'(ＴＢＡ割当による売却)
         05  CNS-TSOT              PIC  X(04)  VALUE  "TSOT".
      *----------  取引事由: 'DMY1'(購入による経過利息)
         05  CNS-DMY1              PIC  X(04)  VALUE  "DMY1".
      *----------  取引事由: 'DMY2'(購入による利払額面)
         05  CNS-DMY2              PIC  X(04)  VALUE  "DMY2".
      *----------  取引事由: 'DMY3'(売却による経過利息)
         05  CNS-DMY3              PIC  X(04)  VALUE  "DMY3".
      *----------  取引事由: 'DMY4'(売却による利払額面)
         05  CNS-DMY4              PIC  X(04)  VALUE  "DMY4".
      *----------  取引事由: 'DMY5'(ノンカバー為替差損益)
         05  CNS-DMY5              PIC  X(04)  VALUE  "DMY5".
      *----------  取引事由: 'DMY6'(収益計上による経過利息)
         05  CNS-DMY6              PIC  X(04)  VALUE  "DMY6".
      *----------  取引事由: 'DMY7'(損失計上による経過利息)
         05  CNS-DMY7              PIC  X(04)  VALUE  "DMY7".
      *----------  取引事由: 'DMY8'(その他増による経過利息)
         05  CNS-DMY8              PIC  X(04)  VALUE  "DMY8".
      *----------  取引事由: 'DMY9'(その他減による経過利息)
         05  CNS-DMY9              PIC  X(04)  VALUE  "DMY9".
      *----------  取引事由: 'TAX'(源泉税)
         05  CNS-TAX               PIC  X(04)  VALUE  "TAX".
      *----------  取引事由: 'RCPB'(購入による経過利息損益計上)
         05  CNS-RCPB              PIC  X(04)  VALUE  "RCPB".
      *----------  取引事由: 'RCPS'(売却による経過利息損益計上)
         05  CNS-RCPS              PIC  X(04)  VALUE  "RCPS".
      *----------  取引事由: 'EXLC'(為替差損（売買）計上)
         05  CNS-EXLC              PIC  X(04)  VALUE  "EXLC".
      *----------  取引事由: 'EXLI'(為替差損（利配）計上)
         05  CNS-EXLI              PIC  X(04)  VALUE  "EXLI".
      *----------  取引事由: 'EXPC'(為替差益（売買）計上)
         05  CNS-EXPC              PIC  X(04)  VALUE  "EXPC".
      *----------  取引事由: 'EXPI'(為替差益（利配）計上)
         05  CNS-EXPI              PIC  X(04)  VALUE  "EXPI".
      *----------  取引事由: 'SDII'(株式配当による増加)
         05  CNS-SDII              PIC  X(04)  VALUE  "SDII".
      *----------  取引事由: 'ODIV'(子会社株式配当による増加)
         05  CNS-ODIV              PIC  X(04)  VALUE  "ODIV".
      *----------  取引事由: 'CIQI'(分割（利益準備金）・増)
         05  CNS-CIQI              PIC  X(04)  VALUE  "CIQI".
      *----------  取引事由: 'CISI'（株式分割（分割先）)
         05  CNS-CISI              PIC  X(04)  VALUE  "CISI".
      *----------  取引事由: 'OCII'（会社分割（分割先）)
         05  CNS-OCII              PIC  X(04)  VALUE  "OCII".
      *----------  取引事由: 'INFI'（初期設定元本)
         05  CNS-INFI              PIC  X(04)  VALUE  "INFI".
      *----------  取引事由: 'CBB'(CB転換による取得)
         05  CNS-CBB               PIC  X(03)  VALUE  "CBB".
      *----------  取引事由: 'OSIW'（ワラント行使による購入)
         05  CNS-OSIW              PIC  X(04)  VALUE  "OSIW".
      *----------  取引事由: 'GNSS'（売現先スタ−ト)
         05  CNS-GNSS              PIC  X(04)  VALUE  "GNSS".
      *----------  取引事由: 'GNBS'（買現先スタ−ト)
         05  CNS-GNBS              PIC  X(04)  VALUE  "GNBS".
      *----------  取引事由: 'FALU'（調整損計上)
         05  CNS-FALU              PIC  X(04)  VALUE  "FALU".
      *----------  取引事由: 'FAPU'（調整益計上)
         05  CNS-FAPU              PIC  X(04)  VALUE  "FAPU".
      *----------  取引事由: 'FTLK'(還付金残高評価損計上)
         05  CNS-FTLK              PIC  X(04)  VALUE  "FTLK".
      *----------  取引事由: 'FTPK'(還付金残高評価益計上)
         05  CNS-FTPK              PIC  X(04)  VALUE  "FTPK".
      *----------  取引事由: 'FTLS'(未収金評価損計上)
         05  CNS-FTLS              PIC  X(04)  VALUE  "FTLS".
      *----------  取引事由: 'FTPS'(未収金評価益計上)
         05  CNS-FTPS              PIC  X(04)  VALUE  "FTPS".
      *----------  取引事由: 'FTLB'(未払金評価損計上)
         05  CNS-FTLB              PIC  X(04)  VALUE  "FTLB".
      *----------  取引事由: 'FTPB'(未払金評価益計上)
         05  CNS-FTPB              PIC  X(04)  VALUE  "FTPB".
      *----------  取引事由: 'FALR'（調整損戻入)
         05  CNS-FALR              PIC  X(04)  VALUE  "FALR".
      *----------  取引事由: 'FAPR'（調整益戻入)
         05  CNS-FAPR              PIC  X(04)  VALUE  "FAPR".
      *----------  取引事由: 'FTLL'(還付金残高評価損戻入)
         05  CNS-FTLL              PIC  X(04)  VALUE  "FTLL".
      *----------  取引事由: 'FTPL'(還付金残高評価益戻入)
         05  CNS-FTPL              PIC  X(04)  VALUE  "FTPL".
      *----------  取引事由: 'FTLT'(未収金評価損戻入)
         05  CNS-FTLT              PIC  X(04)  VALUE  "FTLT".
      *----------  取引事由: 'FTPT'(未収金評価益戻入)
         05  CNS-FTPT              PIC  X(04)  VALUE  "FTPT".
      *----------  取引事由: 'FTLC'(未払金評価損戻入)
         05  CNS-FTLC              PIC  X(04)  VALUE  "FTLC".
      *----------  取引事由: 'FTPC'(未払金評価益戻入)
         05  CNS-FTPC             PIC  X(04)  VALUE  "FTPC".
      *----------  取引事由: 'FTSG' (切り離し法評価益計上)
         05  CNS-FTSG              PIC  X(04)  VALUE  "FTSG".
      *----------  取引事由: 'FTSL' (切り離し法評価損計上)
         05  CNS-FTSL              PIC  X(04)  VALUE  "FTSL".
      *----------  取引事由: 'INTA'(利息)
         05  CNS-INTA              PIC  X(04)  VALUE  "INTA".
      *----------  取引事由: 'INTF'(分配金受取)
         05  CNS-INTF              PIC  X(04)  VALUE  "INTF".
      *----------  取引事由: 'MATU'(満期償還)
         05  CNS-MATU              PIC  X(04)  VALUE  "MATU".
      *----------  取引事由: 'GANC'(収益計上簿価異動（キャピタル）)
         05  CNS-GANC              PIC  X(04)  VALUE  "GANC".
      *----------  取引事由: 'GANI'(収益計上簿価異動（インカム）)
         05  CNS-GANI              PIC  X(04)  VALUE  "GANI".
      *----------  取引事由: 'LOOC'(損失計上簿価異動（キャピタル）)
         05  CNS-LOOC              PIC  X(04)  VALUE  "LOOC".
      *----------  取引事由: 'LOOI'(損失計上簿価異動（インカム）)
         05  CNS-LOOI              PIC  X(04)  VALUE  "LOOI".
      *----------  取引事由: 'PYIN'(キャッシュ支払簿価異動)
         05  CNS-PYIN              PIC  X(04)  VALUE  "PYIN".
      *----------  取引事由: 'RCOT'(キャッシュ受取簿価異動)
         05  CNS-RCOT              PIC  X(04)  VALUE  "RCOT".
      *----------  評価損益データフラグ: '8'(評価損益計上)
         05  CNS-HYK-SNEKI-DATA-FLG-8
                                   PIC  X(01)  VALUE  "8".
      *----------  評価損益データフラグ: '2'(評価損益戻入)
         05  CNS-HYK-SNEKI-DATA-FLG-2
                                   PIC  X(01)  VALUE  "2".
      *----------  評価損益データフラグ: '7'(アモチ/アキュム)
         05  CNS-HYK-SNEKI-DATA-FLG-7
                                   PIC  X(01)  VALUE  "7".
      *----------  評価損益データフラグ: '5'(評価損益でない)
         05  CNS-HYK-SNEKI-DATA-FLG-5
                                   PIC  X(01)  VALUE  "5".
      *----------  評価損益データフラグ: '6'(アモチ/アキュム)
         05  CNS-HYK-SNEKI-DATA-FLG-6
                                   PIC  X(01)  VALUE  "6".
      *----------  未収計算対象フラグ: '1'(対象)
         05  CNS-MSKS-TARGET-FLG-1 PIC  X(01)  VALUE  "1".
      *----------  未収計算種類: '21'(権利計上)
         05  CNS-MSKS-SHRU-21      PIC  X(02)  VALUE  "21".
      *----------  未収計算種類: '22'(配当金入金)
         05  CNS-MSKS-SHRU-22      PIC  X(02)  VALUE  "22".
      *----------  未収計算種類: '11'(利金入金)
         05  CNS-MSKS-SHRU-11      PIC  X(02)  VALUE  "11".
      *----------  未収計算種類: '31'(その他入金)
         05  CNS-MSKS-SHRU-31      PIC  X(02)  VALUE  "31".
      *----------  未収計算種類: '32'(その他エンド)
         05  CNS-MSKS-SHRU-32      PIC  X(02)  VALUE  "32".
      *----------  C/A位置付けフラグ: '1'(分割元)
         05  CNS-CA-POS-FLG-1      PIC  X(01)  VALUE  "1".
      *----------  C/A位置付けフラグ: '2'(分割先)
         05  CNS-CA-POS-FLG-2      PIC  X(01)  VALUE  "2".
      *----------  売買現先スタートフラグ: '0'
         05  CNS-BS-GK-ST-FLG-0    PIC  X(01)  VALUE  "0".
      *----------  売買現先スタートフラグ: '1'
         05  CNS-BS-GK-ST-FLG-1    PIC  X(01)  VALUE  "1".
      *---------- 権利行使・割当フラグ: '2'(権利割当)
         05  CNS-KRKS-WARIATE-FLG-2
                                   PIC  X(01)  VALUE  "2".
      *---------- 権利行使・割当フラグ: '1'(権利行使)
         05  CNS-KRKS-WARIATE-FLG-1
                                   PIC  X(01)  VALUE  "1".
      *---------- 権利行使・割当フラグ: '0'(なし)
         05  CNS-KRKS-WARIATE-FLG-0
                                   PIC  X(01)  VALUE  "0".
      *---------- 売買フラグ: '1'(売却)
         05  CNS-URIKAI-FLG-1      PIC  X(01)  VALUE  "1".
      *---------- 売買フラグ: '2'(無効)
         05  CNS-URIKAI-FLG-2      PIC  X(01)  VALUE  "2".
      *---------- 為替差損益フラグ："1" 益
         05  CNS-UYTK-KWS-SNEKI-FLG-1
                                   PIC  X(01)  VALUE  "1".
      *---------- 為替差損益フラグ："2" 損
         05  CNS-UYTK-KWS-SNEKI-FLG-2
                                   PIC  X(01)  VALUE  "2".
      *---------- 株式配当新株判定フラグ：'1'(対象)
         05  CNS-KH-SKB-HT-FLG-1   PIC  X(01)  VALUE  "1".
      *---------- 株式配当新株判定フラグ：'0'(対象外)
         05  CNS-KH-SKB-HT-FLG-0   PIC  X(01)  VALUE  "0".
      *---------- 初期設定元本フラグ：'1'(である)
         05  CNS-SHOKI-SET-GNP-FLG-1
                                   PIC  X(01)  VALUE  "1".
      *---------- 初期設定元本フラグ：'0'(でない)
         05  CNS-SHOKI-SET-GNP-FLG-0
                                   PIC  X(01)  VALUE  "0".
      *---------- CB転換・ワラント行使フラグ:'1'(である)
         05  CNS-CB-TENKAN-WRNT-FLG-1
                                   PIC  X(01)  VALUE  "1".
      *---------- CB転換・ワラント行使フラグ:'0'(でない)
         05  CNS-CB-TENKAN-WRNT-FLG-0
                                   PIC  X(01)  VALUE  "0".
      *---------- 取引属性逆転フラグ:'1'(である)
         05  CNS-TRHK-ZKSE-GT-FLG-1
                                   PIC  X(01)  VALUE  "1".
      *---------- 取引属性逆転フラグ:'0'(でない)
         05  CNS-TRHK-ZKSE-GT-FLG-0
                                   PIC  X(01)  VALUE  "0".
      ******************************************************************
      *                ワ−ク  エリア
      ******************************************************************
       01  WORK-AREA.
         05  WK-RUN-SDATETIME      PIC  X(14).                          GUT2013
         05  WK-RUN-EDATETIME      PIC  X(14).                          GUT2013
      *----------  取引事由
         05  WK-TORI-JIYU          PIC  X(04).
      *----------  判定対象区分
         05  WK-HANTEI-TARGET-KBN  PIC  X(01).
      ******************************************************************
      *        サブプログラム  パラメ−タ  エリア
      ******************************************************************
      *----------  SCZY1170 リターンパッケージセットルーチン
       01  PARM-SCZY1170-IN.
      *----------  リターンパッケージセットルーチンコピー句
           COPY  "SCZY1170.cpy".
       01  PARM-SCZY1170-RTNPKG.
         05  PARM-SCZY1170-ARG2.
      *----------  RTNパッケージ
           COPY  "CZRTNPACKAGE.cpy".
      ******************************************************************
      *        連絡節
      ******************************************************************
       LINKAGE                SECTION.
       01  PARM-SMZJ4012-INOUT.
           COPY  "SMZJ4012.cpy".
       01  PARM-SMZJ4012-RTNPKG.
      *----------  RTNパッケージ
         05  PARM-SMZJ4012-ARG3.
           COPY  "CZRTNPACKAGE.cpy".
      ******************************************************************
      *
      *        PROCEDURE      DIVISION
      *
      ******************************************************************
       PROCEDURE              DIVISION
           USING  PARM-SMZJ4012-INOUT
                  PARM-SMZJ4012-RTNPKG.
      ******************************************************************
      *        取引事由判定
      ******************************************************************
       BASE-RTN               SECTION.
           MOVE FUNCTION CURRENT-DATE(1:14) TO WK-RUN-SDATETIME
      *--------------------<DEBUG>--------------------------------------
           DISPLAY "プログラムID:        ["CNS-PGM-ID"]"                GUT2013
           DISPLAY "セクション:          [BASE-RTN]"                    GUT2013
           DISPLAY "セクションの開始時間:["WK-RUN-SDATETIME"]"          GUT2013
      *-----------------------------------------------------------------
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":****  SMZJ4012  START  ****"
      *-----------------------------------------------------------------
      *----------  3.1 前処理
           PERFORM  INIT-RTN
      *----------  3.2 主処理
           PERFORM  MAIN-RTN
      *----------  3.3 後処理
           PERFORM  END-RTN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":****  SMZJ4012  END  ****"
      *-----------------------------------------------------------------
           MOVE FUNCTION CURRENT-DATE(1:14) TO WK-RUN-EDATETIME
      *--------------------<DEBUG>--------------------------------------
           DISPLAY "プログラムID:        ["CNS-PGM-ID"]"                GUT2013
           DISPLAY "セクション:          [BASE-RTN]"                    GUT2013
           DISPLAY "セクションの終了時間:["WK-RUN-EDATETIME"]"          GUT2013
      *-----------------------------------------------------------------
           CONTINUE.
       BASE-EXT.
           GOBACK.
      ******************************************************************
      *    3.1  前処理
      ******************************************************************
       INIT-RTN               SECTION.
      *-----------------------<DEBUG>-----------------------------------
      D    DISPLAY  CNS-PGM-ID":****  前処理  START  ****"
      *-----------------------------------------------------------------
      *----------  3.1.1 初期化処理
           PERFORM  INITIALIZE-RTN
      *-----------------------<DEBUG>-----------------------------------
      D    DISPLAY  CNS-PGM-ID":****  前処理  END  ****"
      *-----------------------------------------------------------------
           CONTINUE.
       INIT-EXT.
           EXIT.
      ******************************************************************
      *    3.1.1  初期化処理
      ******************************************************************
       INITIALIZE-RTN         SECTION.
      *-----------------------<DEBUG>-----------------------------------
      D    DISPLAY  CNS-PGM-ID":****  初期化処理  START  ****"
      *-----------------------------------------------------------------
           INITIALIZE  WORK-AREA
                       PARM-SCZY1170-IN
                       PARM-SCZY1170-RTNPKG
                       PARM-SMZJ4012-ARG2
                       PARM-SMZJ4012-RTNPKG
      *-----------------------<DEBUG>-----------------------------------
      D    DISPLAY  CNS-PGM-ID":WORK-AREA=["WORK-AREA"]"
      D    DISPLAY  CNS-PGM-ID":PARM-SCZY1170-IN    =["
      D             PARM-SCZY1170-IN    "]"
      D    DISPLAY  CNS-PGM-ID":PARM-SCZY1170-RTNPKG=["
      D             PARM-SCZY1170-RTNPKG"]"
      D    DISPLAY  CNS-PGM-ID":PARM-SMZJ4012-ARG2  =["
      D             PARM-SMZJ4012-ARG2  "]"
      D    DISPLAY  CNS-PGM-ID":PARM-SMZJ4012-RTNPKG  =["
      D             PARM-SMZJ4012-RTNPKG  "]"
      *-----------------------------------------------------------------
           MOVE  CNS-RTN-000
             TO  RTN-CD                 OF  PARM-SCZY1170-ARG1
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":RTN-CD  OF  PARM-SCZY1170-ARG1=["
      D             RTN-CD              OF  PARM-SCZY1170-ARG1"]"
      *-----------------------------------------------------------------
      *---------- 取引事由
           MOVE  TORI-JIYU              OF  PARM-SMZJ4012-ARG1
             TO  WK-TORI-JIYU
      *---------- 判定対象区分
           MOVE  HANTEI-TARGET-KBN      OF  PARM-SMZJ4012-ARG1
             TO  WK-HANTEI-TARGET-KBN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":取引事由      =["WK-TORI-JIYU"]"
      D    DISPLAY  CNS-PGM-ID":判定対象区分  =["WK-HANTEI-TARGET-KBN"]"
      *-----------------------------------------------------------------
           CONTINUE.
       INITIALIZE-EXT.
           EXIT.
      ******************************************************************
      *    3.2  主処理
      ******************************************************************
       MAIN-RTN               SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":****  主処理  START  ****"
      D    DISPLAY  CNS-PGM-ID":判定対象区分=["WK-HANTEI-TARGET-KBN"]"
      *-----------------------------------------------------------------
           EVALUATE  WK-HANTEI-TARGET-KBN
      *---------- 3.2.1 ARG1.判定対象区分＝'1'(評価損益データフラグ)
             WHEN  CNS-HANTEI-TARGET-1
      *---------- 評価損益データフラグ編集
               PERFORM  HYKSN-DATA-FLG-RTN
      *---------- 3.2.2 ARG1.判定対象区分＝'2'(未収計算)
             WHEN  CNS-HANTEI-TARGET-2
      *---------- 未収計算編集
               PERFORM  MSKS-RTN
      *---------- 3.2.3 ARG1.判定対象区分＝'3'(C/A位置付けフラグ)
             WHEN  CNS-HANTEI-TARGET-3
      *---------- C/A位置付けフラグ編集
               PERFORM  CA-POS-FLG-RTN
      *---------- ARG1.判定対象区分＝'4'(売買現先スタートフラグ)
             WHEN  CNS-HANTEI-TARGET-4
               PERFORM  BS-GK-ST-FLG-RTN
      *---------- 3.2.5 ARG1.判定対象区分＝'5'(権利行使・割当フラグ)
             WHEN  CNS-HANTEI-TARGET-5
      *---------- 権利行使・割当フラグ
               PERFORM  KRKS-WARIATE-FLG-RTN
      *---------- 3.2.6 ARG1.判定対象区分＝'6'(売買フラグ)
             WHEN  CNS-HANTEI-TARGET-6
      *---------- 売買フラグ編集
               PERFORM  URIKAI-FLG-RTN
      *---------- 3.2.7 ARG1.判定対象区分＝'7'(為替差損益フラグ)
             WHEN  CNS-HANTEI-TARGET-7
      *---------- 為替差損益フラグ編集
               PERFORM  UYTK-KWS-SK-FLG-RTN
      *---------- 3.2.8 ARG1.判定対象区分＝'8'(株式配当新株判定フラグ)
             WHEN  CNS-HANTEI-TARGET-8
      *---------- 株式配当新株判定フラグ編集
               PERFORM  KH-SKB-HT-FLG-RTN
      *---------- 3.2.9 ARG1.判定対象区分＝'9'(初期設定元本フラグ)
             WHEN  CNS-HANTEI-TARGET-9
      *---------- 初期設定元本フラグ編集
               PERFORM  SHOKI-SET-GNP-FLG-RTN
      *---------- 3.2.10 ARG1.判定対象区分＝'a'(CB転換・ワラント行使フラグ)
             WHEN  CNS-HANTEI-TARGET-A
      *---------- CB転換・ワラント行使フラグ編集
               PERFORM  CB-TENKAN-WRNT-FLG-RTN
      *---------- 3.2.11 ARG1.判定対象区分＝'b'(取引属性逆転フラグ)
             WHEN  CNS-HANTEI-TARGET-B
      *---------- 取引属性逆転フラグ編集
               PERFORM  TRHK-ZKSE-GT-FLG-RTN
      *---------- 上記以外
             WHEN  OTHER
               CONTINUE
           END-EVALUATE
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":***** 主処理  END  *****"
      *-----------------------------------------------------------------
           CONTINUE.
       MAIN-EXT.
           EXIT.
      ******************************************************************
      *    3.2.1 評価損益データフラグ編集
      ******************************************************************
       HYKSN-DATA-FLG-RTN     SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D    ":***  評価損益データフラグ編集  START  ***"
      D    DISPLAY  CNS-PGM-ID":取引事由      =["WK-TORI-JIYU"]"
      *-----------------------------------------------------------------
           EVALUATE  WK-TORI-JIYU
             WHEN  CNS-FTPU
             WHEN  CNS-FTLU
             WHEN  CNS-FTPV
             WHEN  CNS-FTLV
             WHEN  CNS-FTCU
             WHEN  CNS-FALU
             WHEN  CNS-FAPU
             WHEN  CNS-FTLK
             WHEN  CNS-FTPK
             WHEN  CNS-FTLS
             WHEN  CNS-FTPS
             WHEN  CNS-FTLB
             WHEN  CNS-FTPB
      *---------- '8'(評価損益計上)
               MOVE  CNS-HYK-SNEKI-DATA-FLG-8
                 TO  HYK-SNEKI-DATA-FLG OF  PARM-SMZJ4012-ARG2
             WHEN  CNS-FTPR
             WHEN  CNS-FTLR
             WHEN  CNS-FTLW
             WHEN  CNS-FTPW
             WHEN  CNS-FTCR
             WHEN  CNS-FALR
             WHEN  CNS-FAPR
             WHEN  CNS-FTLL
             WHEN  CNS-FTPL
             WHEN  CNS-FTLT
             WHEN  CNS-FTPT
             WHEN  CNS-FTLC
             WHEN  CNS-FTPC
      *---------- '2'(評価損益戻入)
               MOVE  CNS-HYK-SNEKI-DATA-FLG-2
                 TO  HYK-SNEKI-DATA-FLG OF  PARM-SMZJ4012-ARG2
             WHEN  CNS-AMRT
             WHEN  CNS-ACUM
      *---------- '6'(アモチ/アキュム)
               MOVE  CNS-HYK-SNEKI-DATA-FLG-6
                 TO  HYK-SNEKI-DATA-FLG OF  PARM-SMZJ4012-ARG2
             WHEN  CNS-FTSG
             WHEN  CNS-FTSL
      *---------- '7'(切り離し法評価損益計上)
               MOVE  CNS-HYK-SNEKI-DATA-FLG-7
                 TO  HYK-SNEKI-DATA-FLG OF  PARM-SMZJ4012-ARG2
             WHEN  OTHER
      *---------- '5'(評価損益でない)
               MOVE  CNS-HYK-SNEKI-DATA-FLG-5
                 TO  HYK-SNEKI-DATA-FLG OF  PARM-SMZJ4012-ARG2
           END-EVALUATE
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":評価損益データフラグ=["
      D             HYK-SNEKI-DATA-FLG  OF  PARM-SMZJ4012-ARG2"]"
      D    DISPLAY  CNS-PGM-ID":***  評価損益データフラグ編集  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       HYKSN-DATA-FLG-EXT.
           EXIT.
      ******************************************************************
      *    3.2.2 未収計算編集
      ******************************************************************
       MSKS-RTN               SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D    ":***  未収計算編集  START  ***"
      D    DISPLAY  CNS-PGM-ID":取引事由      =["WK-TORI-JIYU"]"
      *-----------------------------------------------------------------
           EVALUATE  WK-TORI-JIYU
             WHEN  CNS-FTSQ
               MOVE  CNS-MSKS-TARGET-FLG-1
                 TO  MSKS-TARGET-FLG    OF  PARM-SMZJ4012-ARG2
               MOVE  CNS-MSKS-SHRU-21
                 TO  MSKS-SHRU          OF  PARM-SMZJ4012-ARG2
             WHEN  CNS-DIVS
             WHEN  CNS-DVAP
             WHEN  CNS-DVAI
               MOVE  CNS-MSKS-TARGET-FLG-1
                 TO  MSKS-TARGET-FLG    OF  PARM-SMZJ4012-ARG2
               MOVE  CNS-MSKS-SHRU-22
                 TO  MSKS-SHRU          OF  PARM-SMZJ4012-ARG2
             WHEN  CNS-CPN
               MOVE  CNS-MSKS-TARGET-FLG-1
                 TO  MSKS-TARGET-FLG    OF  PARM-SMZJ4012-ARG2
               MOVE  CNS-MSKS-SHRU-11
                 TO  MSKS-SHRU          OF  PARM-SMZJ4012-ARG2
             WHEN  CNS-INTA
             WHEN  CNS-INTF
               MOVE  CNS-MSKS-TARGET-FLG-1
                 TO  MSKS-TARGET-FLG    OF  PARM-SMZJ4012-ARG2
               MOVE  CNS-MSKS-SHRU-31
                 TO  MSKS-SHRU          OF  PARM-SMZJ4012-ARG2
             WHEN  CNS-MATU
               MOVE  CNS-MSKS-TARGET-FLG-1
                 TO  MSKS-TARGET-FLG    OF  PARM-SMZJ4012-ARG2
               MOVE  CNS-MSKS-SHRU-32
                 TO  MSKS-SHRU          OF  PARM-SMZJ4012-ARG2
             WHEN  OTHER
               MOVE  SPACE
                 TO  MSKS-TARGET-FLG    OF  PARM-SMZJ4012-ARG2
           END-EVALUATE
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":未収計算対象フラグ=["
      D             MSKS-TARGET-FLG     OF  PARM-SMZJ4012-ARG2"]"
      D    DISPLAY  CNS-PGM-ID":未収計算種類=["
      D             MSKS-SHRU           OF  PARM-SMZJ4012-ARG2"]"
      D    DISPLAY  CNS-PGM-ID":***  未収計算編集  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       MSKS-EXT.
           EXIT.
      ******************************************************************
      *    3.2.3 C/A位置付けフラグ編集
      ******************************************************************
       CA-POS-FLG-RTN         SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D    ":***  C/A位置付けフラグ編集  START  ***"
      D    DISPLAY  CNS-PGM-ID":取引事由      =["WK-TORI-JIYU"]"
      *-----------------------------------------------------------------
           EVALUATE  WK-TORI-JIYU
             WHEN  CNS-MRGD
             WHEN  CNS-CONS
             WHEN  CNS-CID
             WHEN  CNS-SDID
             WHEN  CNS-CIQD
             WHEN  CNS-CISD
             WHEN  CNS-OCID
             WHEN  CNS-ISD
      *---------- '1'(分割元)
               MOVE  CNS-CA-POS-FLG-1
                 TO  CA-POS-FLG         OF  PARM-SMZJ4012-ARG2
             WHEN  CNS-MRG
             WHEN  CNS-CONI
             WHEN  CNS-CII
             WHEN  CNS-ISI
             WHEN  CNS-SDII
             WHEN  CNS-CIQI
             WHEN  CNS-CISI
             WHEN  CNS-OCII
      *---------- '2'(分割先)
               MOVE  CNS-CA-POS-FLG-2
                 TO  CA-POS-FLG         OF  PARM-SMZJ4012-ARG2
             WHEN  OTHER
      *---------- スペース
               MOVE  SPACE
                 TO  CA-POS-FLG         OF  PARM-SMZJ4012-ARG2
           END-EVALUATE
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":C/A位置付けフラグ=["
      D             CA-POS-FLG          OF  PARM-SMZJ4012-ARG2"]"
      D    DISPLAY  CNS-PGM-ID":***  C/A位置付けフラグ編集  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       CA-POS-FLG-EXT.
           EXIT.
      ******************************************************************
      *    3.2.4 売買現先スタートフラグ
      ******************************************************************
       BS-GK-ST-FLG-RTN       SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D    ":***  売買現先スタートフラグ  START  ***"
      D    DISPLAY  CNS-PGM-ID":取引事由      =["WK-TORI-JIYU"]"
      *-----------------------------------------------------------------
           EVALUATE  WK-TORI-JIYU
             WHEN  CNS-GNSS
             WHEN  CNS-GNBS
               MOVE  CNS-BS-GK-ST-FLG-1
                 TO  BS-GK-ST-FLG       OF  PARM-SMZJ4012-ARG2
             WHEN  OTHER
               MOVE  CNS-BS-GK-ST-FLG-0
                 TO  BS-GK-ST-FLG       OF  PARM-SMZJ4012-ARG2
           END-EVALUATE
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":売買現先スタートフラグ=["
      D             BS-GK-ST-FLG        OF  PARM-SMZJ4012-ARG2"]"
      D    DISPLAY  CNS-PGM-ID":***  売買現先スタートフラグ  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       BS-GK-ST-FLG-EXT.
           EXIT.
      ******************************************************************
      *    3.2.5 権利行使・割当フラグ
      ******************************************************************
       KRKS-WARIATE-FLG-RTN   SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D    ":***  権利行使・割当フラグ  START  ***"
      D    DISPLAY  CNS-PGM-ID":取引事由      =["WK-TORI-JIYU"]"
      *-----------------------------------------------------------------
           EVALUATE  WK-TORI-JIYU
             WHEN  CNS-OPBA
             WHEN  CNS-OPSA
             WHEN  CNS-SETA
      *---------- '2'(権利割当)
               MOVE  CNS-KRKS-WARIATE-FLG-2
                 TO  KRKS-WARIATE-FLG   OF  PARM-SMZJ4012-ARG2
             WHEN  CNS-OPBE
             WHEN  CNS-OPSE
             WHEN  CNS-SETE
      *---------- '1'(権利行使)
               MOVE  CNS-KRKS-WARIATE-FLG-1
                 TO  KRKS-WARIATE-FLG   OF  PARM-SMZJ4012-ARG2
             WHEN  OTHER
      *---------- '0'(なし)
               MOVE  CNS-KRKS-WARIATE-FLG-0
                 TO  KRKS-WARIATE-FLG   OF  PARM-SMZJ4012-ARG2
           END-EVALUATE
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":権利行使・割当フラグ=["
      D             KRKS-WARIATE-FLG    OF  PARM-SMZJ4012-ARG2"]"
      D    DISPLAY  CNS-PGM-ID":***  権利行使・割当フラグ  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       KRKS-WARIATE-FLG-EXT.
           EXIT.
      ******************************************************************
      *    3.2.6 売買フラグ編集
      ******************************************************************
       URIKAI-FLG-RTN         SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D    ":***  売買フラグ編集  START  ***"
      D    DISPLAY  CNS-PGM-ID":取引事由      =["WK-TORI-JIYU"]"
      *-----------------------------------------------------------------
           EVALUATE  WK-TORI-JIYU
             WHEN  CNS-FSEL
             WHEN  CNS-OSOT
             WHEN  CNS-REQ
             WHEN  CNS-RSLL
             WHEN  CNS-SELL
             WHEN  CNS-SSEL
             WHEN  CNS-TSOT
      *---------- '1'(売却)
               MOVE  CNS-URIKAI-FLG-1
                 TO  URIKAI-FLG         OF  PARM-SMZJ4012-ARG2
             WHEN  CNS-DMY1
             WHEN  CNS-DMY2
             WHEN  CNS-DMY3
             WHEN  CNS-DMY4
             WHEN  CNS-DMY5
             WHEN  CNS-DMY6
             WHEN  CNS-DMY7
             WHEN  CNS-DMY8
             WHEN  CNS-DMY9
             WHEN  CNS-TAX
             WHEN  CNS-EXLC
             WHEN  CNS-EXLI
             WHEN  CNS-EXPC
             WHEN  CNS-EXPI
             WHEN  CNS-CPN
             WHEN  CNS-DIVS
             WHEN  CNS-DVAI
             WHEN  CNS-INTA
             WHEN  CNS-INTF
             WHEN  CNS-RCPB
             WHEN  CNS-RCPS
      *---------- '2'(無効)
               MOVE  CNS-URIKAI-FLG-2
                 TO  URIKAI-FLG         OF  PARM-SMZJ4012-ARG2
             WHEN  OTHER
      *---------- スペース
               MOVE  SPACE
                 TO  URIKAI-FLG         OF  PARM-SMZJ4012-ARG2
           END-EVALUATE
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":売買フラグ=["
      D             URIKAI-FLG          OF  PARM-SMZJ4012-ARG2"]"
      D    DISPLAY  CNS-PGM-ID":***  売買フラグ編集  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       URIKAI-FLG-EXT.
           EXIT.
      ******************************************************************
      *    3.2.7 為替差損益フラグ編集
      ******************************************************************
       UYTK-KWS-SK-FLG-RTN    SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":***  為替差損益フラグ編集  START  ***"
      D    DISPLAY  CNS-PGM-ID":取引事由 =["WK-TORI-JIYU"]"
      *-----------------------------------------------------------------
           EVALUATE  WK-TORI-JIYU
             WHEN  CNS-EXLC
             WHEN  CNS-EXLI
      *---------- '2'(損)
               MOVE  CNS-UYTK-KWS-SNEKI-FLG-2
                 TO  UYTK-KWS-SNEKI-FLG OF  PARM-SMZJ4012-ARG2
             WHEN  CNS-EXPC
             WHEN  CNS-EXPI
      *---------- '1'(益)
               MOVE  CNS-UYTK-KWS-SNEKI-FLG-1
                 TO  UYTK-KWS-SNEKI-FLG OF  PARM-SMZJ4012-ARG2
             WHEN  OTHER
               MOVE  SPACE
                 TO  UYTK-KWS-SNEKI-FLG OF  PARM-SMZJ4012-ARG2
           END-EVALUATE
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":為替差損益フラグ =["
      D             UYTK-KWS-SNEKI-FLG  OF  PARM-SMZJ4012-ARG2"]"
      D    DISPLAY  CNS-PGM-ID":***  為替差損益フラグ編集  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       UYTK-KWS-SK-FLG-EXT.
           EXIT.
      ******************************************************************
      *    3.2.8 株式配当新株判定フラグ編集
      ******************************************************************
       KH-SKB-HT-FLG-RTN      SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":***  株式配当新株判定フラグ編集"
      D             "  START  ***"
      D    DISPLAY  CNS-PGM-ID":取引事由 =["WK-TORI-JIYU"]"
      *-----------------------------------------------------------------
           EVALUATE  WK-TORI-JIYU
             WHEN  CNS-SDII
             WHEN  CNS-ODIV
             WHEN  CNS-CIQI
      *---------- 株式配当新株判定フラグ：'1'(対象)
               MOVE  CNS-KH-SKB-HT-FLG-1
                 TO  KABU-HAIT-SKB-HANTEI-FLG
                                        OF  PARM-SMZJ4012-ARG2
             WHEN  OTHER
      *---------- 株式配当新株判定フラグ：'0'(対象外)
               MOVE  CNS-KH-SKB-HT-FLG-0
                 TO  KABU-HAIT-SKB-HANTEI-FLG
                                        OF  PARM-SMZJ4012-ARG2
           END-EVALUATE
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":株式配当新株判定フラグ =["
      D             KABU-HAIT-SKB-HANTEI-FLG
      D                                 OF  PARM-SMZJ4012-ARG2"]"
      D    DISPLAY  CNS-PGM-ID":***  株式配当新株判定フラグ編集 END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       KH-SKB-HT-FLG-EXT.
           EXIT.
      ******************************************************************
      *    3.2.9 初期設定元本フラグ編集
      ******************************************************************
       SHOKI-SET-GNP-FLG-RTN  SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":***  初期設定元本フラグ編集  START  ***"
      D    DISPLAY  CNS-PGM-ID":取引事由 =["WK-TORI-JIYU"]"
      *-----------------------------------------------------------------
           EVALUATE  WK-TORI-JIYU
             WHEN  CNS-INFI
      *---------- 初期設定元本フラグ：'1'(である)
               MOVE  CNS-SHOKI-SET-GNP-FLG-1
                 TO  SHOKI-SET-GNP-FLG  OF  PARM-SMZJ4012-ARG2
             WHEN  OTHER
      *---------- 初期設定元本フラグ：'0'(でない)
               MOVE  CNS-SHOKI-SET-GNP-FLG-0
                 TO  SHOKI-SET-GNP-FLG  OF  PARM-SMZJ4012-ARG2
           END-EVALUATE
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":初期設定元本フラグ =["
      D             SHOKI-SET-GNP-FLG   OF  PARM-SMZJ4012-ARG2"]"
      D    DISPLAY  CNS-PGM-ID":***  初期設定元本フラグ編集  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       SHOKI-SET-GNP-FLG-EXT.
           EXIT.
      ******************************************************************
      *    3.2.10 CB転換・ワラント行使フラグ編集
      ******************************************************************
       CB-TENKAN-WRNT-FLG-RTN SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":***  CB転換・ワラント行使フラグ編集"
      D             "  START  ***"
      D    DISPLAY  CNS-PGM-ID":取引事由 =["WK-TORI-JIYU"]"
      *-----------------------------------------------------------------
           EVALUATE  WK-TORI-JIYU
             WHEN  CNS-CBB
             WHEN  CNS-OSIW
               MOVE  CNS-CB-TENKAN-WRNT-FLG-1
                 TO  CB-TENKAN-WRNT-FLG OF  PARM-SMZJ4012-ARG2
             WHEN  OTHER
               MOVE  CNS-CB-TENKAN-WRNT-FLG-0
                 TO  CB-TENKAN-WRNT-FLG OF  PARM-SMZJ4012-ARG2
           END-EVALUATE
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":CB転換・ワラント行使フラグ =["
      D             CB-TENKAN-WRNT-FLG  OF  PARM-SMZJ4012-ARG2"]"
      D    DISPLAY  CNS-PGM-ID":***  CB転換・ワラント行使フラグ編集"
      D             "  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       CB-TENKAN-WRNT-FLG-EXT.
           EXIT.
      ******************************************************************
      *    3.2.11 取引属性逆転フラグ編集
      ******************************************************************
       TRHK-ZKSE-GT-FLG-RTN   SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":***  取引属性逆転フラグ編集  START  ***"
      D    DISPLAY  CNS-PGM-ID":取引事由 =["WK-TORI-JIYU"]"
      *-----------------------------------------------------------------
           EVALUATE  WK-TORI-JIYU
             WHEN  CNS-GANC
             WHEN  CNS-GANI
             WHEN  CNS-LOOC
             WHEN  CNS-LOOI
             WHEN  CNS-PYIN
             WHEN  CNS-RCOT
               MOVE  CNS-TRHK-ZKSE-GT-FLG-1
                 TO  TRHK-ZKSE-GT-FLG  OF  PARM-SMZJ4012-ARG2
             WHEN  OTHER
               MOVE  CNS-TRHK-ZKSE-GT-FLG-0
                 TO  TRHK-ZKSE-GT-FLG  OF  PARM-SMZJ4012-ARG2
           END-EVALUATE
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":取引属性逆転フラグ =["
      D             TRHK-ZKSE-GT-FLG  OF  PARM-SMZJ4012-ARG2"]"
      D    DISPLAY  CNS-PGM-ID":***  取引属性逆転フラグ編集  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       TRHK-ZKSE-GT-FLG-EXT.
           EXIT.
      ******************************************************************
      *    3.3 後処理
      ******************************************************************
       END-RTN                SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID": ***   後処理  START  ***"
      *-----------------------------------------------------------------
      *----------  リターンパッケージセットルーチンをコールする
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D             ": リターンパッケージセットルーチンSCZY1170 開始"
      *-----------------------------------------------------------------
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":RTN-CD   OF  PARM-SCZY1170-ARG1 = ["
      D                         RTN-CD   OF  PARM-SCZY1170-ARG1"]"
      *-----------------------------------------------------------------
           CALL  "SCZY1170"             USING  PARM-SCZY1170-IN
                                               PARM-SCZY1170-RTNPKG
           MOVE  PARM-SCZY1170-RTNPKG   TO  PARM-SMZJ4012-ARG3
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":PARM-SCZY1170-RTNPKG   = ["
      D             PARM-SCZY1170-RTNPKG"]"
      D    DISPLAY  CNS-PGM-ID":PARM-SMZJ4012-ARG3 = ["
      D             PARM-SMZJ4012-ARG3"]"
      D    DISPLAY  CNS-PGM-ID":COUNT = ["
      D             COUNTER                    OF PARM-SMZJ4012-ARG3"]"
      D    DISPLAY  CNS-PGM-ID":エラー検知プログラム名 = ["
      D       ERR-PGM-ID
      D          OF PARM-SMZJ4012-ARG3(COUNTER OF PARM-SMZJ4012-ARG3)"]"
      D    DISPLAY  CNS-PGM-ID":ポジション = ["
      D       ERR-POS
      D          OF PARM-SMZJ4012-ARG3(COUNTER OF PARM-SMZJ4012-ARG3)"]"
      D    DISPLAY  CNS-PGM-ID":リターンコード = ["
      D       RTN-CD
      D          OF PARM-SMZJ4012-ARG3(COUNTER OF PARM-SMZJ4012-ARG3)"]"
      D    DISPLAY  CNS-PGM-ID":詳細リターンコード = ["
      D       DETAIL-RTN-CD
      D          OF PARM-SMZJ4012-ARG3(COUNTER OF PARM-SMZJ4012-ARG3)"]"
      D    DISPLAY  CNS-PGM-ID":詳細リターンコードデコードフラグ = ["
      D       DETAIL-RTN-CD-FLG
      D          OF PARM-SMZJ4012-ARG3(COUNTER OF PARM-SMZJ4012-ARG3)"]"
      D    DISPLAY  CNS-PGM-ID":フリーメッセージ = ["
      D       FREE-MSG
      D          OF PARM-SMZJ4012-ARG3(COUNTER OF PARM-SMZJ4012-ARG3)"]"
      D    DISPLAY  CNS-PGM-ID
      D           ": リターンパッケージセットルーチンSCZY1170終了"
      D    DISPLAY  CNS-PGM-ID": ***   後処理  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       END-EXT.
           EXIT.
