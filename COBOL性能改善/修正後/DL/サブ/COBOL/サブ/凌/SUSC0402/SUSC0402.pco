      ******************************************************************
      *$Header$
      ******************************************************************
      *
      *     システムＩＤ   : T-STAR
      *     システム名称   : 次期TSTARシステム
      *     プログラムＩＤ : SUSC0402
      *     プログラム名称 : 残高進捗チェック
      *     処理概要       : 残高進捗チェックを行う。
      *     更新ＤＢ       : なし
      *     参照ＤＢ       : TKDYAA40
      *     引数           :
      *     改訂履歴
      *     年月日     区分  所属       担当者  内容
      *     ---------- ----  ---------  ------  ------------------------
      *     20090211   新規  SCS        黄剣鵬  新規作成
      *
      ******************************************************************
      ******************************************************************
       IDENTIFICATION         DIVISION.
       PROGRAM-ID.    SUSC0402.
       AUTHOR.        Nomura.
       DATE-WRITTEN.  09-02-11.
       DATE-COMPILED.
      ******************************************************************
      *
      *    環  境  部
      *
      ******************************************************************
       ENVIRONMENT            DIVISION.
       CONFIGURATION          SECTION.
       SOURCE-COMPUTER.       L1000V.
      *SOURCE-COMPUTER.       L1000V   WITH DEBUGGING MODE.
       OBJECT-COMPUTER.       RP5470.
       INPUT-OUTPUT           SECTION.
       FILE-CONTROL.
      ******************************************************************
      *
      *    デ  ー  タ  部
      *
      ******************************************************************
       DATA                   DIVISION.
      ******************************************************************
      *    作  業  場  所  節
      ******************************************************************
       WORKING-STORAGE        SECTION.
       01  IDNAME                  PIC  X(80)  VALUE
       "$Id$".
      ******************************************************************
      *    コンスタント  エリア
      ******************************************************************
      *----------  共通コンスタント定義の宣言
       01  CONSTANT-AREA.
         05  CNS-USCNS.
           COPY  "USCNS.cpy".
      *----------  プログラム名
         05  CNS-PGM-ID            PIC  X(08)  VALUE  "SUSC0402".
      *----------  進捗管理
         05  CNS-TBL-TKDYAA40      PIC  X(08)  VALUE  "TKDYAA40".
      ******************************************************************
      *    ORACLE ホスト変数定義
      ******************************************************************
           EXEC  SQL  INCLUDE  SQLCA             END-EXEC.
           EXEC  SQL  INCLUDE  ORACA             END-EXEC.
           EXEC  SQL  BEGIN    DECLARE  SECTION  END-EXEC.
       01  HST-AREA.
         05  HST-KEY-TKDYAA40.
      *----------  会社コード
           10  H-CMP-CD            PIC  X(04).
      *----------  ファンドコード
           10  H-FUND-CD           PIC  X(12).
      *----------  ポートフォリオコード
           10  H-PTF-CD            PIC  X(04).
      *----------  基準日
           10  H-KJN-YMD           PIC  X(08).
         05  HST-TBL-TKDYAA40.
      *----------  進捗STS
           10  H-SINSYOU           PIC  X(03).
      *----------  動的SQL文をいれるバッファ
         05  SQLSTMT               PIC  X(500).
           EXEC  SQL  END  DECLARE  SECTION      END-EXEC.
      ******************************************************************
      *    フラグエリア
      ******************************************************************
       01  FLG-AREA.
      *----------  進捗ステータス取得CURSOR OPEN FLAG
         05  FLG-CR1-OPEN          PIC  9(01).
      ******************************************************************
      *    サブプログラム  パラメ−タ  エリア
      ******************************************************************
      *----------  テーブルID取得ルーチンパラメータ
       01  PARM-SCZY1160-INOUT.
           COPY  "SCZY1160.cpy".
      *----------  リターンパッケージセットルーチン
       01  PARM-SCZY1170-IN.
           COPY  "SCZY1170.cpy".
      *----------  RTNパッケージコピー句
       01  PARM-RTNPKG.
         05  PARM-RTNPKG-ARG.
           COPY  "CZRTNPACKAGE.cpy".
      ******************************************************************
      *    SQL  エリア
      ******************************************************************
       01  SQL-AREA.
         05  SQL-SELECT-DATA.
           10  FILLER              PIC  X(07)
               VALUE  "SELECT ".
      *----------  進捗ステータス
           10  FILLER              PIC  X(11)
               VALUE  "SHINTK_STS ".
      *----------  FROM
           10  FILLER              PIC  X(06)
               VALUE  " FROM ".
           10  SQL-TKDYAA40        PIC  X(13).
      *----------  WHERE
           10  FILLER              PIC  X(07)
               VALUE  " WHERE ".
      *----------  会社コード
           10  FILLER              PIC  X(17)
               VALUE  "RIYO_CMP_CD = :V1".
      *----------  ファンドコード
           10  FILLER              PIC  X(18)
               VALUE  " AND FUND_CD = :V2".
      *----------  ポートフォリオコード
           10  FILLER              PIC  X(17)
               VALUE  " AND PTF_CD = :V3".
      *----------  進捗タスクID
           10  FILLER              PIC  X(31)
               VALUE  " AND SHINTK_TASK_ID = 'ST00500'".
      *----------  最終処理基準日
           10  FILLER              PIC  X(23)
               VALUE  " AND SS_SYORI_YMD = :V4".
      *----------  ORDER BY
           10  FILLER              PIC  X(25)
               VALUE  " ORDER BY SHINTK_STS DESC".
      ******************************************************************
      *                ワ−ク  エリア
      ******************************************************************
       01  WORK-AREA.
      *----------  エラーフリーメッセージ
         05  WK-SQL-ERR-MSG.
      *----------  SELECTリーメッセージ
           10  WK-SELECT-FREE-MSG  PIC  X(500).
         05  WK-RUN-SDATETIME      PIC  X(14).                          GUT2013
         05  WK-RUN-EDATETIME      PIC  X(14).                          GUT2013
      ******************************************************************
      *    連  絡  節
      ******************************************************************
       LINKAGE                SECTION.
       01  PARM-SUSC0402-INOUT.
           COPY  "SUSC0402.cpy".
       01  PARM-SUSC0402-RTNPKG.
         05  PARM-SUSC0402-ARG3.
           COPY  "CZRTNPACKAGE.cpy".
      ******************************************************************
      *
      *    手 続 き 部
      *
      ******************************************************************
       PROCEDURE              DIVISION
           USING  PARM-SUSC0402-INOUT
                  PARM-SUSC0402-RTNPKG.
      ******************************************************************
      *    メイン処理
      ******************************************************************
       BASE-RTN               SECTION.
           MOVE FUNCTION CURRENT-DATE(1:14) TO WK-RUN-SDATETIME
      *--------------------<DEBUG>--------------------------------------
           DISPLAY "プログラムID:        ["CNS-PGM-ID"]"                GUT2013
           DISPLAY "セクション:          [BASE-RTN]"                    GUT2013
           DISPLAY "セクションの開始時間:["WK-RUN-SDATETIME"]"          GUT2013
      *-----------------------------------------------------------------
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** SUSC0402 残高進捗 START ***"
      *-----------------------------------------------------------------
      *----------  3.1 前処理
           PERFORM  INIT-RTN
      *----------  3.2 主処理
           PERFORM  MAIN-RTN
      *----------  3.3 後処理
           PERFORM  END-RTN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** SUSC0402  残高進捗 END ***"
      *-----------------------------------------------------------------
           MOVE FUNCTION CURRENT-DATE(1:14) TO WK-RUN-EDATETIME
      *--------------------<DEBUG>--------------------------------------
           DISPLAY "プログラムID:        ["CNS-PGM-ID"]"                GUT2013
           DISPLAY "セクション:          [BASE-RTN]"                    GUT2013
           DISPLAY "セクションの終了時間:["WK-RUN-EDATETIME"]"          GUT2013
      *-----------------------------------------------------------------
           CONTINUE.
       BASE-EXT.
           GOBACK.
      ******************************************************************
      *    3.1 前処理
      ******************************************************************
       INIT-RTN               SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  前処理  START  ***"
      *-----------------------------------------------------------------
      *----------  3.1.1 初期化処理
           PERFORM  INITIALIZE-RTN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  前処理  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       INIT-EXT.
           EXIT.
      ******************************************************************
      *    3.1.1.初期化処理
      ******************************************************************
       INITIALIZE-RTN         SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 初期化処理 START ***"
      *-----------------------------------------------------------------
           INITIALIZE  FLG-AREA
                       SQL-AREA
                       PARM-RTNPKG
                       HST-AREA
                       WORK-AREA
                       PARM-SCZY1160-INOUT
                       PARM-SCZY1170-IN
                       PARM-SUSC0402-ARG2
                       PARM-SUSC0402-RTNPKG
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":FLG-AREA =[" FLG-AREA "]"
      D    DISPLAY  CNS-PGM-ID ":SQL-AREA =[" SQL-AREA "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":HST-AREA =["
      D                         HST-AREA "]"
      D    DISPLAY  CNS-PGM-ID ":WORK-AREA =[" WORK-AREA "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY1160-INOUT =["
      D                         PARM-SCZY1160-INOUT "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY1170-IN =["
      D                         PARM-SCZY1170-IN  "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SUSC0402-ARG2 =["
      D                         PARM-SUSC0402-ARG2 "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SUSC0402-RTNPKG =["
      D                         PARM-SUSC0402-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":*** 初期化処理 END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       INITIALIZE-EXT.
           EXIT.
      ******************************************************************
      *    3.2.主処理
      ******************************************************************
       MAIN-RTN               SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  主処理  START  ***"
      *-----------------------------------------------------------------
      *----------  3.2.1.テーブルID取得処理
           PERFORM  TBL-ID-GET-RTN
      *----------  3.2.2.進捗ステータス
           PERFORM  SINSYOU-GET-RTN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  主処理  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       MAIN-EXT.
           EXIT.
      ******************************************************************
      *    3.2.1.テーブルID取得処理
      ******************************************************************
       TBL-ID-GET-RTN         SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** SCZY1160:"
      D                       "テーブルID取得処理 START *** "
      *-----------------------------------------------------------------
           INITIALIZE  PARM-RTNPKG
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      *-----------------------------------------------------------------
      *----------  利用会社コード
           MOVE  RIYO-CMP-CD           OF  PARM-SUSC0402-ARG1
             TO  RIYO-CMP-CD           OF  PARM-SCZY1160-ARG1
      *----------  テーブルID
           MOVE  CNS-TBL-TKDYAA40
             TO  TBL-ID                OF  PARM-SCZY1160-ARG1(1)
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY1160-ARG1:"
      D    DISPLAY  CNS-PGM-ID ":利用会社コード =["
      D              RIYO-CMP-CD       OF PARM-SCZY1160-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":テーブルID(1)  =["
      D              TBL-ID            OF PARM-SCZY1160-ARG1(1) "]"
      D    DISPLAY  CNS-PGM-ID ":***  CALL SCZY1160 START  ***"
      *-----------------------------------------------------------------
           CALL  "SCZY1160"            USING  PARM-SCZY1160-INOUT
                                              PARM-RTNPKG
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  CALL SCZY1160 END  ***"
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG=[" PARM-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":RTN-CD OF SCZY1160=["
      D              RTN-CD OF PARM-RTNPKG(COUNTER  OF  PARM-RTNPKG) "]"
      D    DISPLAY  CNS-PGM-ID ":COUNTER  OF  PARM-RTNPKG=["
      D                          COUNTER  OF  PARM-RTNPKG "]"
      *-----------------------------------------------------------------
           IF  RTN-CD OF PARM-RTNPKG(COUNTER    OF  PARM-RTNPKG) =
               CNS-RTN-000
           THEN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*****  テーブルID正常取得  *****"
      D    DISPLAY  CNS-PGM-ID ":TKDYAA40 ID =["
      D             HENSYUGO-TBL-ID    OF  PARM-SCZY1160-ARG2(1) "]"
      *-----------------------------------------------------------------
             MOVE  HENSYUGO-TBL-ID     OF  PARM-SCZY1160-ARG2(1)
               TO  SQL-TKDYAA40
             INITIALIZE  PARM-RTNPKG
           ELSE
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*****  テーブルID取得エラー  *****"
      *-----------------------------------------------------------------
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID          OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-001
               TO  ERR-POS             OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-901
               TO  RTN-CD              OF  PARM-SCZY1170-ARG1
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  RTN-CD  OF  PARM-RTNPKG (COUNTER OF PARM-RTNPKG)
      D             "RTN-CD  OF  PARM-RTNPKG (COUNTER OF PARM-RTNPKG)"
      *-----------------------------------------------------------------
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-0
               TO  DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
             MOVE  SPACE
               TO  FREE-MSG            OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY CNS-PGM-ID ":*** SCZY1160:テーブルID取得処理  END"
      D                       " ***"
      *-----------------------------------------------------------------
           CONTINUE.
       TBL-ID-GET-EXT.
           EXIT.
      ******************************************************************
      *    3.2.2.進捗ステータス取得処理
      ******************************************************************
       SINSYOU-GET-RTN        SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 進捗ステータス取得処理 START *** "
      *-----------------------------------------------------------------
      *----------  CURSOR PREPARE
           PERFORM  CR1-PREPARE-RTN
      *----------  CURSOR OPEN
           PERFORM  CR1-OPEN-RTN
      *----------  CURSOR FETCH
           PERFORM  CR1-FETCH-RTN
      *----------  CURSOR CLOSE
           PERFORM  CR1-CLOSE-RTN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 進捗ステータス取得処理 END *** "
      *-----------------------------------------------------------------
           CONTINUE.
       SINSYOU-GET-EXT.
           EXIT.
      ******************************************************************
      *    進捗ステータス取得CURSOR PREPARE
      ******************************************************************
       CR1-PREPARE-RTN        SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 進捗ステータス取得"
      D                       "CURSOR PREPARE START ***"
      *-----------------------------------------------------------------
      *----------  利用会社コード
           MOVE  RIYO-CMP-CD           OF  PARM-SUSC0402-ARG1
             TO  H-CMP-CD
      *----------  ファンドコード
           MOVE  FUND-CD               OF  PARM-SUSC0402-ARG1
             TO  H-FUND-CD
      *----------  ポートフォリオコード
           MOVE  PTF-CD                OF  PARM-SUSC0402-ARG1
             TO  H-PTF-CD
      *----------  基準日
           MOVE  KJN-YMD               OF  PARM-SUSC0402-ARG1
             TO  H-KJN-YMD
           STRING  SQL-SELECT-DATA
                   DELIMITED BY SIZE
                   INTO  SQLSTMT
           STRING  SQLSTMT(1:FUNCTION  LENGTH(SQL-SELECT-DATA))
                   "[:V1="
                   H-CMP-CD
                   "][:V2="
                   H-FUND-CD
                   "][:V3="
                   H-PTF-CD
                   "][:V4="
                   H-KJN-YMD
                   "]"
                DELIMITED BY SIZE
                INTO  WK-SELECT-FREE-MSG
           END-STRING
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":利用会社コード =[" H-CMP-CD"]"
      D    DISPLAY  CNS-PGM-ID ":ファンドコード=[" H-FUND-CD "]"
      D    DISPLAY  CNS-PGM-ID ":ポートフォリオコード=["H-PTF-CD"]"
      D    DISPLAY  CNS-PGM-ID ":基準日=["H-KJN-YMD "]"
      D    DISPLAY  CNS-PGM-ID ":SQLSTMT=[" SQLSTMT "]"
      D    DISPLAY  CNS-PGM-ID ":WK-SELECT-FREE-MSG=["
      D             WK-SELECT-FREE-MSG"]"
      *-----------------------------------------------------------------
      *----------  通貨全項目取得CURSOR OPEN
      *----------  PREPARE CURSOR
           EXEC  SQL
             PREPARE  S1  FROM  :SQLSTMT
           END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":進捗ステータス取得"
      D             "CURSOR PREPARE SQLCODE =[" SQLCODE "]"
      *-----------------------------------------------------------------
           IF  SQLCODE = CNS-SQL-NORMAL
           THEN
             CONTINUE
           ELSE
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID          OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-002
               TO  ERR-POS             OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-900
               TO  RTN-CD              OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  SQLCODE
               TO  DETAIL-RTN-CD       OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-1
               TO  DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
             MOVE  WK-SELECT-FREE-MSG
               TO  FREE-MSG            OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *----------DECLARE CURSOR
           EXEC  SQL
             DECLARE  CR1  CURSOR  FOR  S1
           END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":進捗ステータス取得"
      D                        "CURSOR DECLARE SQLCODE=[" SQLCODE "]"
      D    DISPLAY  CNS-PGM-ID ":*** 進捗ステータス取得"
      D                        "CURSOR PREPARE END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       CR1-PREPARE-EXT.
           EXIT.
      ******************************************************************
      *    進捗ステータス取得CURSOR OPEN
      ******************************************************************
       CR1-OPEN-RTN           SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 進捗ステータス取得"
      D                       "CURSOR OPEN START ***"
      *-----------------------------------------------------------------
      *----------OPEN    CURSOR
           EXEC  SQL
             OPEN  CR1         USING :H-CMP-CD,
                                     :H-FUND-CD,
                                     :H-PTF-CD,
                                     :H-KJN-YMD
           END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":進捗ステータス取得CURSOR"
      D                        " OPEN SQLCODE =["SQLCODE "]"
      *-----------------------------------------------------------------
           IF  SQLCODE  =  CNS-SQL-NORMAL
           THEN
             MOVE  CNS-ON              TO  FLG-CR1-OPEN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":CURSOR OPEN 正常"
      D    DISPLAY  CNS-PGM-ID ":CR1-OPEN-FLAG =[" FLG-CR1-OPEN "]"
      *-----------------------------------------------------------------
           ELSE
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID          OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-003
               TO  ERR-POS             OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-900
               TO  RTN-CD              OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  SQLCODE
               TO  DETAIL-RTN-CD       OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-1
               TO  DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
             MOVE  WK-SELECT-FREE-MSG
               TO  FREE-MSG            OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 進捗ステータス取得"
      D                        "CURSOR OPEN END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       CR1-OPEN-EXT.
           EXIT.
      ******************************************************************
      *    進捗ステータス取得CURSOR FETCH
      ******************************************************************
       CR1-FETCH-RTN          SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 進捗ステータス取得"
      D                       "CURSOR FETCH START ***"
      *-----------------------------------------------------------------
      *----------  初期化
           INITIALIZE  HST-TBL-TKDYAA40
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":HST-TBL-TKDYAA40 =["
      D                          HST-TBL-TKDYAA40 "]"
      *-----------------------------------------------------------------
      *----------  CR1 FETCH
           EXEC SQL
             FETCH CR1
             INTO  :HST-TBL-TKDYAA40.H-SINSYOU
           END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":進捗ステータス取得CURSOR "
      D                        " FETCH SQLCODE=[" SQLCODE "]"
      *-----------------------------------------------------------------
           EVALUATE  SQLCODE
      *----------  正常
             WHEN  CNS-SQL-NORMAL
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 出力データ編集 START ***"
      *-----------------------------------------------------------------
      *----------  進捗ステータス
               MOVE  H-SINSYOU
                 TO  SINSYOU-STS       OF  PARM-SUSC0402-ARG2
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":進捗ステータス=["
      D             SINSYOU-STS        OF  PARM-SUSC0402-ARG2"]"
      *-----------------------------------------------------------------
             WHEN  CNS-SQL-NOTFOUND
      *----------  進捗ステータス
               MOVE  SPACE
                 TO  SINSYOU-STS       OF  PARM-SUSC0402-ARG2
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":進捗ステータス=["
      D             SINSYOU-STS        OF  PARM-SUSC0402-ARG2"]"
      *-----------------------------------------------------------------
             WHEN  OTHER
      *----------  エラー検知プログラム名
               MOVE  CNS-PGM-ID
                 TO  ERR-PGM-ID        OF  PARM-SCZY1170-ARG1
      *----------  ポジション
               MOVE  CNS-POS-004
                 TO  ERR-POS           OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
               MOVE  CNS-RTN-900
                 TO  RTN-CD            OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
               MOVE  SQLCODE
                 TO  DETAIL-RTN-CD     OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
               MOVE  CNS-DETAIL-1
                 TO  DETAIL-RTN-CD-FLG OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
               MOVE  WK-SELECT-FREE-MSG
                 TO  FREE-MSG          OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
               PERFORM  ERR-RTN
           END-EVALUATE
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 進捗ステータス取得"
      D                        "CURSOR FETCH END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       CR1-FETCH-EXT.
           EXIT.
      ******************************************************************
      *    進捗ステータス取得CURSOR CLOSE
      ******************************************************************
       CR1-CLOSE-RTN          SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 進捗ステータス取得"
      D                       "CURSOR CLOSE START ***"
      D    DISPLAY  CNS-PGM-ID ":FLG-CR1-OPEN= [" FLG-CR1-OPEN "]"
      *-----------------------------------------------------------------
           EXEC  SQL
             CLOSE CR1
           END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":進捗ステータス CLOSE SQLCODE"
      D                          " =[" SQLCODE "]"
      *-----------------------------------------------------------------
           IF  SQLCODE = CNS-SQL-NORMAL
           THEN
             MOVE  CNS-OFF           TO  FLG-CR1-OPEN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":CR2-OPEN-FLAG =[" FLG-CR1-OPEN "]"
      *-----------------------------------------------------------------
           ELSE
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID        OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-005
               TO  ERR-POS           OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-900
               TO  RTN-CD            OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  SQLCODE
               TO  DETAIL-RTN-CD     OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-1
               TO  DETAIL-RTN-CD-FLG OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
             MOVE  WK-SELECT-FREE-MSG
               TO  FREE-MSG          OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 進捗ステータス取得"
      D                        "CURSOR CLOSE END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       CR1-CLOSE-EXT.
           EXIT.
      ******************************************************************
      *    3.3 後処理
      ******************************************************************
       END-RTN                SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 後処理 START ***"
      *-----------------------------------------------------------------
           MOVE  CNS-RTN-000
             TO  RTN-CD                OF  PARM-SCZY1170-ARG1
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":エラー検知プログラム名 =["
      D             ERR-PGM-ID         OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":ポジション =["
      D             ERR-POS            OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":リターンコード =["
      D             RTN-CD             OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":詳細リターンコード =["
      D             DETAIL-RTN-CD      OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":詳細リターンコードデコードフラグ =["
      D             DETAIL-RTN-CD-FLG  OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":フリーメッセージ =["
      D             FREE-MSG           OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":*** CALL SCZY1170 START ***"
      *-----------------------------------------------------------------
           CALL   "SCZY1170"           USING  PARM-SCZY1170-IN
                                              PARM-RTNPKG
           MOVE  PARM-RTNPKG           TO  PARM-SUSC0402-ARG3
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** CALL SCZY1170 END ***"
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SUSC0402-ARG2 =["
      D             PARM-SUSC0402-ARG2 "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SUSC0402-ARG3 =["
      D             PARM-SUSC0402-ARG3 "]"
      D    DISPLAY  CNS-PGM-ID ":*** 後処理 END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       END-EXT.
           EXIT.
      ******************************************************************
      *    3.4 エラー処理
      ******************************************************************
       ERR-RTN                SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** エラー処理 START ***"
      D    DISPLAY  CNS-PGM-ID ":FLG-CR1-OPEN =[" FLG-CR1-OPEN "]"
      *-----------------------------------------------------------------
           IF  FLG-CR1-OPEN = CNS-ON
           THEN
             EXEC  SQL
                 CLOSE CR1
             END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":SQLCODE(CLOSE) =[" SQLCODE "]"
      *-----------------------------------------------------------------
             MOVE  CNS-OFF             TO  FLG-CR1-OPEN
           ELSE
             CONTINUE
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":カーソルCLOSEなし"
      *-----------------------------------------------------------------
           END-IF
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":FLG-CR1-OPEN =[" FLG-CR1-OPEN "]"
      D    DISPLAY  CNS-PGM-ID ":エラー検知プログラム名 =["
      D             ERR-PGM-ID         OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":ポジション =["
      D             ERR-POS            OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":リターンコード =["
      D             RTN-CD             OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":詳細リターンコード =["
      D             DETAIL-RTN-CD      OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":詳細リターンコードデコードフラグ =["
      D             DETAIL-RTN-CD-FLG  OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":フリーメッセージ =["
      D             FREE-MSG           OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":*** CALL SCZY1170 START ***"
      *-----------------------------------------------------------------
           CALL   "SCZY1170"           USING  PARM-SCZY1170-IN
                                              PARM-RTNPKG
           MOVE  PARM-RTNPKG           TO  PARM-SUSC0402-ARG3
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** CALL SCZY1170 END ***"
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SUSC0402-ARG2 =["
      D             PARM-SUSC0402-ARG2 "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SUSC0402-ARG3 =["
      D             PARM-SUSC0402-ARG3 "]"
      D    DISPLAY  CNS-PGM-ID ":*** エラー処理 END ***"
      D    DISPLAY  CNS-PGM-ID ":*** SUSC0402 END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       ERR-EXT.
           GOBACK.
