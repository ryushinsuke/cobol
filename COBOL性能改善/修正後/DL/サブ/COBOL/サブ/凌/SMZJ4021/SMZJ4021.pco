      ******************************************************************
      *$Header$
      ******************************************************************
      *
      * システムＩＤ   : T-STAR
      * システム名称   : 次期TSTARシステム
      * プログラムＩＤ : SMZJ4021
      * プログラム名称 : 勘定残高バランスチェック
      * 処理概要       : 利払日を過ぎてもマイナス経過利息の仮払金が残っている場合には、残高処理でワーニングをログに出す。
      * 参照ＤＢ       : 勘定残高(TKMZKZ00)
      * 引数           : IN  利用会社コード
      *                : IN  ファンドコード
      *                : IN  ポートフォリオコード
      *                : IN  基準日
      *                : OUT 警告フラグ
      *                : OUT RTNパッケージ
      * 改訂履歴
      * 年月日     区分  所属       担当者  内容
      * ---------- ----  ---------  ------  ------------------------
      * 20090323   新規  SCS        王飛     新規作成
      ******************************************************************
       IDENTIFICATION         DIVISION.
       PROGRAM-ID.    SMZJ4021.
       AUTHOR.        Nomura.
       DATE-WRITTEN.  09-03-23.
       DATE-COMPILED.
      ******************************************************************
      *
      *                 環  境  部
      *
      ******************************************************************
       ENVIRONMENT            DIVISION.
       CONFIGURATION          SECTION.
       SOURCE-COMPUTER.       L1000V.
      *SOURCE-COMPUTER.       L1000V   WITH DEBUGGING MODE.
       OBJECT-COMPUTER.       RP5470.
       INPUT-OUTPUT           SECTION.
       FILE-CONTROL.
      ******************************************************************
      *
      *                 デ  ー  タ  部
      *
      ******************************************************************
       DATA                   DIVISION.
       WORKING-STORAGE        SECTION.
       01  IDNAME                  PIC  X(80)  VALUE
       "$Id$".
      ******************************************************************
      *                 コンスタント  エリア
      ******************************************************************
       01  CONSTANT-AREA.
         05  CNS-MZCNS.
           COPY "MZCNS.cpy".
      *----------  プログラム名
         05  CNS-PGM-ID            PIC  X(08)  VALUE  "SMZJ4021".
      *----------  勘定残高(TKMZKZ00)
         05  CNS-TBL-TKMZKZ00      PIC  X(08)  VALUE  "TKMZKZ00".
      *----------  パラメータエラーメッセージ
         05  CNS-SQL-MSG-SELECT    PIC  X(17)  VALUE
                                               " SQL-MSG(SELECT):".
      *----------  警告フラグ
         05  CNS-WARN-FLG-1        PIC  X(01)  VALUE  "1".
      ******************************************************************
      *                 フラグエリア
      ******************************************************************
       01  FLG-AREA.
      *----------  勘定残高カーソルオープンフラグ
         05  FLG-TKMZKZ00-CR-OPEN  PIC  9(01).
      ******************************************************************
      *                 ワ−クエリア
      ******************************************************************
       01  WORK-AREA.
         05  WK-RUN-SDATETIME      PIC  X(14).                          GUT2013
         05  WK-RUN-EDATETIME      PIC  X(14).                          GUT2013
         05  WK-SMZJ4021-ARG1.
      *----------  利用会社コード
           10  WK-RIYO-CMP-CD      PIC  X(04).
      *----------  ファンドコード
           10  WK-FUND-CD          PIC  X(12).
      *----------  ポートフォリオコード
           10  WK-PTF-CD           PIC  X(04).
      *----------  基準日
           10  WK-KJN-YMD          PIC  S9(08).
      ******************************************************************
      *    ＯＲＡＣＬＥホスト変数定義
      ******************************************************************
      *----------  ＳＱＬコミュニケーション領域
           EXEC  SQL  INCLUDE  SQLCA             END-EXEC.
           EXEC  SQL  INCLUDE  ORACA             END-EXEC.
           EXEC  SQL  BEGIN   DECLARE   SECTION  END-EXEC.
       01  HST-AREA.
      *----------  勘定残高(別紙1-1)
         05  H-TKMZKZ00-S.
      *----------  利用会社コード
           10  H-RIYO-CMP-CD       PIC  X(04).
      *----------  ファンドコード
           10  H-FUND-CD           PIC  X(12).
      *----------  ポートフォリオコード
           10  H-PTF-CD            PIC  X(04).
      *----------  基準日
           10  H-KJN-YMD           PIC  S9(08).
      *----------  COUNT_基準日
         05  H-COUNT-KJN-YMD       PIC  S9(08).
         05  SQLSTMT               PIC  X(3000).
           EXEC  SQL  END      DECLARE  SECTION  END-EXEC.
      ******************************************************************
      *            SQL  エリア
      ******************************************************************
       01  SQL-AREA.
      *----------  対象取引情報を取得SQL文(別紙1-1)
         05  SQL-SELECT-TKMZKZ00.
           10  FILLER              PIC  X(07)  VALUE
               "SELECT ".
      *----------  COUNT(基準日)
           10  FILLER              PIC  X(32)  VALUE
               "COUNT(KJN_YMD) AS COUNT_KJN_YMD ".
      *----------  FROM
           10  FILLER              PIC  X(06)  VALUE
               "FROM( ".
      *----------  SELECT
           10  FILLER              PIC  X(07)  VALUE
               "SELECT ".
      *----------  利用会社コード
           10  FILLER              PIC  X(12)  VALUE
               "RIYO_CMP_CD,".
      *----------  ファンドコード
           10  FILLER              PIC  X(08)  VALUE
               "FUND_CD,".
      *----------  ポートフォリオコード
           10  FILLER              PIC  X(07)  VALUE
               "PTF_CD,".
      *----------  基準日
           10  FILLER              PIC  X(08)  VALUE
               "KJN_YMD,".
      *----------  決算日断面区分
           10  FILLER              PIC  X(16)  VALUE
               "KSN_YMD_DMN_KBN,".
      *----------  SUM(DECODE(貸借区分,'1',勘定残高,勘定残高 * (-1)))
           10  FILLER              PIC  X(45)  VALUE
               "SUM(DECODE(TAISHK_KBN,'1',KNJZN,KNJZN*(-1))) ".
      *----------  AS SUM_勘定残高
           10  FILLER              PIC  X(13)  VALUE
               "AS SUM_KNJZN ".
           10  FILLER              PIC  X(05)  VALUE
               "FROM ".
           10  SQL-TBL-TKMZKZ00    PIC  X(13).
      *----------  WHERE
           10  FILLER              PIC  X(07)  VALUE
               " WHERE ".
      *----------  利用会社コード
           10  FILLER              PIC  X(29)  VALUE
               "RIYO_CMP_CD = :P_RIYO_CMP_CD ".
      *----------  ファンドコード
           10  FILLER              PIC  X(25)  VALUE
               "AND FUND_CD = :P_FUND_CD ".
      *----------  ポートフォリオコード
           10  FILLER              PIC  X(23)  VALUE
               "AND PTF_CD = :P_PTF_CD ".
      *----------  評価系列コード
           10  FILLER              PIC  X(25)  VALUE
               "AND HYK_KRTU_CD = '    ' ".
      *----------  基準日
           10  FILLER              PIC  X(25)  VALUE
               "AND KJN_YMD = :P_KJN_YMD ".
      *----------  SUBSTR(勘定科目コード,3,8)
           10  FILLER              PIC  X(39)  VALUE
               "AND SUBSTR(KNJ_KAMK_CD,3,8) = '000000' ".
      *----------  SUBSTR(勘定科目コード,1,2)
           10  FILLER              PIC  X(28)  VALUE
               "AND SUBSTR(KNJ_KAMK_CD,1,2) ".
           10  FILLER              PIC  X(30)  VALUE
               "IN ('01','02','03','04','05') ".
      *----------  GROUP BY
           10  FILLER              PIC  X(09)  VALUE
               "GROUP BY ".
      *----------  利用会社コード
           10  FILLER              PIC  X(12)  VALUE
               "RIYO_CMP_CD,".
      *----------  ファンドコード
           10  FILLER              PIC  X(08)  VALUE
               "FUND_CD,".
      *----------  ポートフォリオコード
           10  FILLER              PIC  X(07)  VALUE
               "PTF_CD,".
      *----------  基準日
           10  FILLER              PIC  X(08)  VALUE
               "KJN_YMD,".
      *----------  決算日断面区分
           10  FILLER              PIC  X(17)  VALUE
               "KSN_YMD_DMN_KBN) ".
      *----------  WHERE
           10  FILLER              PIC  X(20)  VALUE
               "WHERE SUM_KNJZN <> 0".
      ******************************************************************
      *                エラーメッセージ
      ******************************************************************
       01  E-MSG-TBL.
      *----------  SELECT SQL文(別紙1-1)
         05  WK-MSG-TKMZKZ00.
      *----------  COUNT(基準日) AS COUNT_基準日
           10  FILLER              PIC  X(38)  VALUE
               "COUNT(KJN_YMD) AS COUNT_KJN_YMD FROM( ".
           10  FILLER              PIC  X(07)  VALUE
               "SELECT ".
           10  FILLER              PIC  X(12)  VALUE
               "RIYO_CMP_CD,".
           10  FILLER              PIC  X(08)  VALUE
               "FUND_CD,".
           10  FILLER              PIC  X(07)  VALUE
               "PTF_CD,".
           10  FILLER              PIC  X(08)  VALUE
               "KJN_YMD,".
           10  FILLER              PIC  X(16)  VALUE
               "KSN_YMD_DMN_KBN,".
           10  FILLER              PIC  X(50)  VALUE
               "SUM(DECODE(TAISHK_KBN, '1', KNJZN, KNJZN * (-1))) ".
           10  FILLER              PIC  X(13)  VALUE
               "AS SUM_KNJZN ".
           10  FILLER              PIC  X(05)  VALUE
               "FROM ".
           10  WK-SQL-TBL-TKMZKZ00 PIC  X(13).
           10  FILLER              PIC  X(07)  VALUE
               " WHERE ".
      *----------  利用会社コード
           10  FILLER              PIC  X(15)  VALUE
               "RIYO_CMP_CD = '".
           10  WK-SQL-RIYO-CMP-CD  PIC  X(04).
      *----------  ファンドコード
           10  FILLER              PIC  X(17)  VALUE
               "' AND FUND_CD = '".
           10  WK-SQL-FUND-CD      PIC  X(12).
      *----------  ポートフォリオコード
           10  FILLER              PIC  X(16)  VALUE
               "' AND PTF_CD = '".
           10  WK-SQL-PTF-CD       PIC  X(04).
      *----------  評価系列コード
           10  FILLER              PIC  X(27)  VALUE
               "' AND HYK_KRTU_CD = '    ' ".
      *----------  基準日
           10  FILLER              PIC  X(14)  VALUE
               "AND KJN_YMD = ".
           10  WK-SQL-KJN-YMD      PIC  S9(08).
      *----------  SUBSTR(勘定科目コード,3,8)
           10  FILLER              PIC  X(40)  VALUE
               " AND SUBSTR(KNJ_KAMK_CD,3,8) = '000000' ".
      *----------  SUBSTR(勘定科目コード,1,2)
           10  FILLER              PIC  X(28)  VALUE
               "AND SUBSTR(KNJ_KAMK_CD,1,2) ".
           10  FILLER              PIC  X(30)  VALUE
               "IN ('01','02','03','04','05') ".
      *----------  GROUP BY
           10  FILLER              PIC  X(09)  VALUE
               "GROUP BY ".
      *----------  利用会社コード
           10  FILLER              PIC  X(12)  VALUE
               "RIYO_CMP_CD,".
      *----------  ファンドコード
           10  FILLER              PIC  X(08)  VALUE
               "FUND_CD,".
      *----------  ポートフォリオコード
           10  FILLER              PIC  X(07)  VALUE
               "PTF_CD,".
      *----------  基準日
           10  FILLER              PIC  X(08)  VALUE
               "KJN_YMD,".
      *----------  決算日断面区分
           10  FILLER              PIC  X(17)  VALUE
               "KSN_YMD_DMN_KBN) ".
      *----------  WHERE
           10  FILLER              PIC  X(20)  VALUE
               "WHERE SUM_KNJZN <> 0".
      ******************************************************************
      *                 サブプログラム  パラメ−タ  エリア
      ******************************************************************
      *----------  SCZY1170 リターンパッケージセットルーチン
       01  PARM-SCZY1170-IN.
           COPY  "SCZY1170.cpy".
       01  PARM-SCZY1170-RTNPKG.
         05  PARM-SCZY1170-ARG2.
           COPY  "CZRTNPACKAGE.cpy".
      *----------  SCZY1160 テーブルID取得ルーチン
       01  PARM-SCZY1160-INOUT.
           COPY  "SCZY1160.cpy".
      *----------  RTNパッケージコピー句
       01  PARM-RTNPKG.
         05  PARM-RTNPKG-ARG.
           COPY  "CZRTNPACKAGE.cpy".
      ******************************************************************
      *                 連絡節
      ******************************************************************
       LINKAGE                SECTION.
       01  PARM-SMZJ4021-INOUT.
           COPY  "SMZJ4021.cpy".
      *----------  RTNパッケージ
       01  PARM-SMZJ4021-RTNPKG.
         05  PARM-SMZJ4021-ARG3.
             COPY  "CZRTNPACKAGE.cpy".
      ******************************************************************
      *
      *                 手 続 き 部
      *
      ******************************************************************
       PROCEDURE              DIVISION
           USING  PARM-SMZJ4021-INOUT
                  PARM-SMZJ4021-RTNPKG.
      ******************************************************************
      *                 勘定残高バランスチェック
      ******************************************************************
       BASE-RTN               SECTION.
           MOVE FUNCTION CURRENT-DATE(1:14) TO WK-RUN-SDATETIME
      *--------------------<DEBUG>--------------------------------------
           DISPLAY "プログラムID:        ["CNS-PGM-ID"]"                GUT2013
           DISPLAY "セクション:          [BASE-RTN]"                    GUT2013
           DISPLAY "セクションの開始時間:["WK-RUN-SDATETIME"]"          GUT2013
      *-----------------------------------------------------------------
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID": ***  SMZJ4021処理  START  ***"
      *-----------------------------------------------------------------
      *----------  3.1 前処理
           PERFORM  INIT-RTN
      *----------  3.2 主処理
           PERFORM  MAIN-RTN
      *----------  3.3 後処理
           PERFORM  END-RTN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID": ***  SMZJ4021処理  END  ***"
      *-----------------------------------------------------------------
           MOVE FUNCTION CURRENT-DATE(1:14) TO WK-RUN-EDATETIME
      *--------------------<DEBUG>--------------------------------------
           DISPLAY "プログラムID:        ["CNS-PGM-ID"]"                GUT2013
           DISPLAY "セクション:          [BASE-RTN]"                    GUT2013
           DISPLAY "セクションの終了時間:["WK-RUN-EDATETIME"]"          GUT2013
      *-----------------------------------------------------------------
           CONTINUE.
       BASE-EXT.
           GOBACK.
      ******************************************************************
      *                 3.1 前処理
      ******************************************************************
       INIT-RTN               SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":***  前処理  START  ***"
      *-----------------------------------------------------------------
      *----------  3.1.1 初期化処理
           PERFORM  INITIALIZE-RTN
      *----------  3.1.2 テーブルID取得処理
           PERFORM  TBL-ID-GET-RTN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID": ***  前処理  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       INIT-EXT.
           EXIT.
      ******************************************************************
      *        3.1.1 初期化処理
      ******************************************************************
       INITIALIZE-RTN         SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  初期化処理  START  ***"
      *-----------------------------------------------------------------
      *----------  初期化
           INITIALIZE     FLG-AREA
                          WORK-AREA
                          HST-AREA
                          SQL-AREA
                          E-MSG-TBL
                          PARM-SCZY1160-INOUT
                          PARM-SCZY1170-IN
                          PARM-SCZY1170-RTNPKG
                          PARM-SMZJ4021-ARG2
                          PARM-SMZJ4021-ARG3
                          PARM-RTNPKG
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":FLG-AREA         = ["FLG-AREA"]"
      D    DISPLAY  CNS-PGM-ID":WORK-AREA        = ["WORK-AREA"]"
      D    DISPLAY  CNS-PGM-ID":HST-AREA         = ["HST-AREA"]"
      D    DISPLAY  CNS-PGM-ID":SQL-AREA         = ["SQL-AREA"]"
      D    DISPLAY  CNS-PGM-ID":E-MSG-TBL        = ["E-MSG-TBL"]"
      D    DISPLAY  CNS-PGM-ID":PARM-SCZY1160-INOUT = ["
      D             PARM-SCZY1160-INOUT"]"
      D    DISPLAY  CNS-PGM-ID":PARM-SCZY1170-IN = ["PARM-SCZY1170-IN"]"
      D    DISPLAY  CNS-PGM-ID":PARM-SCZY1170-RTNPKG= ["
      D             PARM-SCZY1170-RTNPKG"]"
      D    DISPLAY  CNS-PGM-ID"PARM-SMZJ4021-ARG2 = ["
      D             PARM-SMZJ4021-ARG2"]"
      D    DISPLAY  CNS-PGM-ID"PARM-SMZJ4021-ARG3 = ["
      D             PARM-SMZJ4021-ARG3"]"
      D    DISPLAY  CNS-PGM-ID":PARM-RTNPKG = ["PARM-RTNPKG"]"
      *-----------------------------------------------------------------
      *----------  リターンコード
           MOVE  CNS-RTN-000
             TO  RTN-CD                 OF  PARM-SCZY1170-ARG1
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":RTN-CD  OF  PARM-SCZY1170-ARG1 = ["
      D             RTN-CD OF  PARM-SCZY1170-ARG1"]"
      *-----------------------------------------------------------------
      *----------  利用会社コード
           MOVE  RIYO-CMP-CD            OF  PARM-SMZJ4021-ARG1
             TO  WK-RIYO-CMP-CD         OF  WK-SMZJ4021-ARG1
      *----------  ファンドコード
           MOVE  FUND-CD                OF  PARM-SMZJ4021-ARG1
             TO  WK-FUND-CD             OF  WK-SMZJ4021-ARG1
      *----------  ポートフォリオコード
           MOVE  PTF-CD                 OF  PARM-SMZJ4021-ARG1
             TO  WK-PTF-CD              OF  WK-SMZJ4021-ARG1
      *----------  基準日
           MOVE  KJN-YMD                OF  PARM-SMZJ4021-ARG1
             TO  WK-KJN-YMD             OF  WK-SMZJ4021-ARG1
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":ARG1.利用会社コード= ["
      D             WK-RIYO-CMP-CD      OF  WK-SMZJ4021-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":ARG1.ファンドコード= ["
      D             WK-FUND-CD          OF  WK-SMZJ4021-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":ARG1.ポートフォリオコード= ["
      D             WK-PTF-CD           OF  WK-SMZJ4021-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":ARG1.基準日= ["
      D             WK-KJN-YMD          OF  WK-SMZJ4021-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":***  初期化処理 END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       INITIALIZE-EXT.
           EXIT.
      ******************************************************************
      *        3.1.2 テーブルID取得処理
      ******************************************************************
       TBL-ID-GET-RTN         SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":***  テーブルID取得  START  ***"
      *-----------------------------------------------------------------
           INITIALIZE  PARM-SCZY1160-INOUT
                       PARM-RTNPKG
      *----------  利用会社コード
           MOVE  WK-RIYO-CMP-CD         OF  WK-SMZJ4021-ARG1
             TO  RIYO-CMP-CD            OF  PARM-SCZY1160-ARG1
      *----------  勘定残高(TKMZKZ00)
           MOVE  CNS-TBL-TKMZKZ00
             TO  TBL-ID                 OF  PARM-SCZY1160-ARG1(1)
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":利用会社コード=["
      D             RIYO-CMP-CD         OF  PARM-SCZY1160-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":勘定残高      =["
      D             TBL-ID              OF  PARM-SCZY1160-ARG1(1)"]"
      *-----------------------------------------------------------------
           CALL  "SCZY1160"             USING  PARM-SCZY1160-INOUT
                                               PARM-RTNPKG
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":RTN-CD(SCZY1160)=["
      D             RTN-CD OF PARM-RTNPKG(COUNTER OF  PARM-RTNPKG)"]"
      D    DISPLAY  CNS-PGM-ID":COUNTER OF  PARM-RTNPKG =["
      D             COUNTER             OF PARM-RTNPKG"]"
      *-----------------------------------------------------------------
           IF  RTN-CD  OF  PARM-RTNPKG(COUNTER  OF  PARM-RTNPKG)
            =  CNS-RTN-000
           THEN
      *----------  勘定残高(TKMZKZ00)
             MOVE  HENSYUGO-TBL-ID      OF  PARM-SCZY1160-ARG2(1)
               TO  SQL-TBL-TKMZKZ00,
                   WK-SQL-TBL-TKMZKZ00
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":勘定残高(別紙1-1)=["SQL-TBL-TKMZKZ00"]"
      *-----------------------------------------------------------------
           ELSE
      *----------  エラー
             MOVE  PARM-RTNPKG          TO  PARM-SCZY1170-RTNPKG
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID           OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-001
               TO  ERR-POS              OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-901
               TO  RTN-CD               OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  RTN-CD  OF  PARM-RTNPKG(COUNTER  OF  PARM-RTNPKG)
               TO  DETAIL-RTN-CD        OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-0
               TO  DETAIL-RTN-CD-FLG    OF  PARM-SCZY1170-ARG1
      *---------- フリーメッセージ
             MOVE  SPACE
               TO  FREE-MSG             OF  PARM-SCZY1170-ARG1
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":テーブルID取得処理  異常"
      D    DISPLAY  CNS-PGM-ID":エラー検知プログラム名            =["
      D             ERR-PGM-ID          OF  PARM-SCZY1170-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":ポジション                        =["
      D             ERR-POS             OF  PARM-SCZY1170-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":リターンコード                    =["
      D             RTN-CD              OF  PARM-SCZY1170-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":詳細リターンコード                =["
      D             DETAIL-RTN-CD       OF  PARM-SCZY1170-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":詳細リターンコードデコードフラグ  =["
      D             DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":フリーメッセージ                  =["
      D             FREE-MSG            OF  PARM-SCZY1170-ARG1"]"
      *-----------------------------------------------------------------
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":***  テーブルID取得  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       TBL-ID-GET-EXT.
           EXIT.
      ******************************************************************
      *                 3.2 主処理
      ******************************************************************
       MAIN-RTN               SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID": *****  主処理  START  *****"
      *-----------------------------------------------------------------
      *----------  3.2.1.バランスチェック
           PERFORM  BLCE-CHECK-RTN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":*****  主処理  END  *****"
      *-----------------------------------------------------------------
           CONTINUE.
       MAIN-EXT.
           EXIT.
      ******************************************************************
      *                 バランスチェック
      ******************************************************************
       BLCE-CHECK-RTN         SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID": *****  バランスチェック  START  *****"
      *-----------------------------------------------------------------
      *----------  勘定残高(別紙1-1)CURSOR PREPARE
           PERFORM  TKMZKZ00-PRE-RTN
      *----------  勘定残高(別紙1-1)CURSOR OPEN
           PERFORM  TKMZKZ00-OPEN-RTN
      *----------  勘定残高(別紙1-1)CURSOR FETCH
           PERFORM  TKMZKZ00-FETCH-RTN
      *----------  勘定残高(別紙1-1)CURSOR CLOSE
           PERFORM  TKMZKZ00-CLOSE-RTN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":*****  バランスチェック  END  *****"
      *-----------------------------------------------------------------
           CONTINUE.
       BLCE-CHECK-EXT.
           EXIT.
      ******************************************************************
      *    勘定残高(別紙1-1)CURSOR PREPARE
      ******************************************************************
       TKMZKZ00-PRE-RTN       SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D             ":***勘定残高(別紙1-1)CURSOR PREPARE START ***"
      *-----------------------------------------------------------------
           INITIALIZE  H-TKMZKZ00-S
      *----------  利用会社コード
           MOVE  WK-RIYO-CMP-CD         OF  WK-SMZJ4021-ARG1
             TO  H-RIYO-CMP-CD          OF  H-TKMZKZ00-S,
                 WK-SQL-RIYO-CMP-CD     OF  WK-MSG-TKMZKZ00
      *----------  ファンドコード
           MOVE  WK-FUND-CD             OF  WK-SMZJ4021-ARG1
             TO  H-FUND-CD              OF  H-TKMZKZ00-S,
                 WK-SQL-FUND-CD         OF  WK-MSG-TKMZKZ00
      *----------  ポートフォリオコード
           MOVE  WK-PTF-CD              OF  WK-SMZJ4021-ARG1
             TO  H-PTF-CD               OF  H-TKMZKZ00-S,
                 WK-SQL-PTF-CD          OF  WK-MSG-TKMZKZ00
      *----------  基準日
           MOVE  WK-KJN-YMD             OF  WK-SMZJ4021-ARG1
             TO  H-KJN-YMD              OF  H-TKMZKZ00-S,
                 WK-SQL-KJN-YMD         OF  WK-MSG-TKMZKZ00
           INITIALIZE  SQLSTMT
      *----------  ＳＱＬ文
           MOVE  SQL-SELECT-TKMZKZ00    TO  SQLSTMT
      *-----------------------DEBUG-------------------------------------
      D    DISPLAY CNS-PGM-ID":利用会社コード      =["
      D            H-RIYO-CMP-CD        OF  H-TKMZKZ00-S"]"
      D    DISPLAY CNS-PGM-ID":ファンドコード      =["
      D            H-FUND-CD            OF  H-TKMZKZ00-S"]"
      D    DISPLAY CNS-PGM-ID":ポートフォリオコード=["
      D            H-PTF-CD             OF  H-TKMZKZ00-S"]"
      D    DISPLAY CNS-PGM-ID":基準日               =["
      D            H-KJN-YMD            OF  H-TKMZKZ00-S"]"
      D    DISPLAY CNS-PGM-ID":SQLSTMT=["SQLSTMT"]"
      *-----------------------------------------------------------------
      *----------  勘定残高(別紙1-1)CURSOR PREPARE
           EXEC  SQL
             PREPARE  S1  FROM :SQLSTMT
           END-EXEC
      *-----------------------DEBUG-------------------------------------
      D    DISPLAY CNS-PGM-ID":勘定残高(別紙1-1)SQLCODE PREPARE=["
      D            SQLCODE"]"
      *-----------------------------------------------------------------
           IF  SQLCODE =  CNS-SQL-NORMAL
           THEN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":(別紙1-1) SQL処理 (PREPARE)  正常"
      *-----------------------------------------------------------------
             CONTINUE
           ELSE
      *----------------------<DEBUG>------------------------------------
      D    DISPLAY CNS-PGM-ID":(別紙1-1) SQL処理エラー  (PREPARE)"
      *-----------------------------------------------------------------
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID           OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-002
               TO  ERR-POS              OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-900
               TO  RTN-CD               OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  SQLCODE
               TO  DETAIL-RTN-CD        OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-1
               TO  DETAIL-RTN-CD-FLG    OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
             STRING  CNS-SQL-MSG-SELECT,
                     WK-MSG-TKMZKZ00    DELIMITED  BY  SIZE
               INTO  FREE-MSG           OF  PARM-SCZY1170-ARG1
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID":エラー検知プログラム名 =["
      D             ERR-PGM-ID          OF  PARM-SCZY1170-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":ポジション             =["
      D             ERR-POS             OF  PARM-SCZY1170-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":リターンコード         =["
      D             RTN-CD              OF  PARM-SCZY1170-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":詳細リターンコード     =["
      D             DETAIL-RTN-CD       OF  PARM-SCZY1170-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":詳細リターンコードデコードフラグ=["
      D             DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":フリーメッセージ       =["
      D             FREE-MSG            OF  PARM-SCZY1170-ARG1"]"
      *-----------------------------------------------------------------
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *----------  勘定残高(別紙1-1)CURSOR DECLARE
           EXEC  SQL
             DECLARE  TKMZKZ00_CUR  CURSOR FOR  S1
           END-EXEC
      *-----------------------DEBUG-------------------------------------
      D    DISPLAY CNS-PGM-ID":勘定残高(別紙1-1)SQLCODE DECLARE=["
      D            SQLCODE"]"
      D    DISPLAY  CNS-PGM-ID
      D             ":***勘定残高(別紙1-1)CURSOR PREPARE END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       TKMZKZ00-PRE-EXT.
           EXIT.
      ******************************************************************
      *    勘定残高(別紙1-1)CURSOR OPEN
      ******************************************************************
       TKMZKZ00-OPEN-RTN      SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D             ":*** 勘定残高(別紙1-1)CURSOR OPEN START ***"
      *-----------------------------------------------------------------
      *----------  勘定残高(別紙1-1) CURSOR OPEN
           EXEC  SQL
               OPEN TKMZKZ00_CUR
               USING :H-TKMZKZ00-S.H-RIYO-CMP-CD,
                     :H-TKMZKZ00-S.H-FUND-CD,
                     :H-TKMZKZ00-S.H-PTF-CD,
                     :H-TKMZKZ00-S.H-KJN-YMD
           END-EXEC
      *-----------------------DEBUG-------------------------------------
      D    DISPLAY CNS-PGM-ID":勘定残高(別紙1-1)SQLCODE OPEN=["
      D            SQLCODE"]"
      *-----------------------------------------------------------------
           IF  SQLCODE = CNS-SQL-NORMAL
           THEN
             MOVE  CNS-ON             TO  FLG-TKMZKZ00-CR-OPEN
      *-----------------------DEBUG-------------------------------------
      D    DISPLAY  CNS-PGM-ID":(別紙1-1) CURSOR OPEN  正常"
      D    DISPLAY  CNS-PGM-ID":FLG-TKMZKZ00-CR-OPEN=["
      D             FLG-TKMZKZ00-CR-OPEN"]"
      *-----------------------------------------------------------------
           ELSE
      *-----------------------DEBUG-------------------------------------
      D    DISPLAY CNS-PGM-ID":(別紙1-1) SQL処理エラー  (OPEN)"
      *-----------------------------------------------------------------
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID           OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-003
               TO  ERR-POS              OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-900
               TO  RTN-CD               OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  SQLCODE
               TO  DETAIL-RTN-CD        OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-1
               TO  DETAIL-RTN-CD-FLG    OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
             STRING  CNS-SQL-MSG-SELECT,
                     WK-MSG-TKMZKZ00    DELIMITED  BY  SIZE
               INTO  FREE-MSG           OF  PARM-SCZY1170-ARG1
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID":エラー検知プログラム名 =["
      D             ERR-PGM-ID          OF  PARM-SCZY1170-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":ポジション             =["
      D             ERR-POS             OF  PARM-SCZY1170-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":リターンコード         =["
      D             RTN-CD              OF  PARM-SCZY1170-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":詳細リターンコード     =["
      D             DETAIL-RTN-CD       OF  PARM-SCZY1170-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":詳細リターンコードデコードフラグ=["
      D             DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":フリーメッセージ       =["
      D             FREE-MSG            OF  PARM-SCZY1170-ARG1"]"
      *-----------------------------------------------------------------
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D             ":*** 勘定残高(別紙1-1)CURSOR OPEN  END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       TKMZKZ00-OPEN-EXT.
           EXIT.
      ******************************************************************
      *    勘定残高(別紙1-1)CURSOR FETCH
      ******************************************************************
       TKMZKZ00-FETCH-RTN     SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D             ":*** 勘定残高(別紙1-1)CURSOR FETCH START ***"
      *-----------------------------------------------------------------
      *----------  勘定残高(別紙1-1) CURSOR FETCH
           INITIALIZE              H-COUNT-KJN-YMD
           EXEC  SQL
               FETCH  TKMZKZ00_CUR  INTO :H-COUNT-KJN-YMD
           END-EXEC
      *-----------------------DEBUG-------------------------------------
      D    DISPLAY CNS-PGM-ID":勘定残高(別紙1-1)SQLCODE FETCH=["
      D            SQLCODE"]"
      *-----------------------------------------------------------------
           EVALUATE  SQLCODE
             WHEN  CNS-SQL-NORMAL
      *-----------------------DEBUG-------------------------------------
      D    DISPLAY  CNS-PGM-ID":(別紙1-1) CURSOR FETCH  正常"
      D    DISPLAY  CNS-PGM-ID":COUNT_基準日=["H-COUNT-KJN-YMD"]"
      *-----------------------------------------------------------------
               IF  H-COUNT-KJN-YMD > ZERO
               THEN
                 MOVE  CNS-WARN-FLG-1
                   TO  WARN-FLG         OF  PARM-SMZJ4021-ARG2
               ELSE
                 CONTINUE
               END-IF
      *-----------------------DEBUG-------------------------------------
      D    DISPLAY  CNS-PGM-ID":ARG2.警告フラグ=["
      D             WARN-FLG         OF  PARM-SMZJ4021-ARG2"]"
      *-----------------------------------------------------------------
             WHEN  CNS-SQL-NOTFOUND
               CONTINUE
             WHEN  OTHER
      *-----------------------DEBUG-------------------------------------
      D    DISPLAY CNS-PGM-ID":(別紙1-1) SQL処理エラー  (FETCH) "
      *-----------------------------------------------------------------
      *----------  エラー検知プログラム名
               MOVE  CNS-PGM-ID
                 TO  ERR-PGM-ID         OF  PARM-SCZY1170-ARG1
      *----------  ポジション
               MOVE  CNS-POS-004
                 TO  ERR-POS            OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
               MOVE  CNS-RTN-900
                 TO  RTN-CD             OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
               MOVE  SQLCODE
                 TO  DETAIL-RTN-CD      OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
               MOVE  CNS-DETAIL-1
                 TO  DETAIL-RTN-CD-FLG  OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
               STRING  CNS-SQL-MSG-SELECT,
                       WK-MSG-TKMZKZ00  DELIMITED  BY  SIZE
                INTO   FREE-MSG         OF  PARM-SCZY1170-ARG1
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID":エラー検知プログラム名 =["
      D             ERR-PGM-ID          OF  PARM-SCZY1170-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":ポジション             =["
      D             ERR-POS             OF  PARM-SCZY1170-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":リターンコード         =["
      D             RTN-CD              OF  PARM-SCZY1170-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":詳細リターンコード     =["
      D             DETAIL-RTN-CD       OF  PARM-SCZY1170-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":詳細リターンコードデコードフラグ=["
      D             DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":フリーメッセージ       =["
      D             FREE-MSG            OF  PARM-SCZY1170-ARG1"]"
      *-----------------------------------------------------------------
      *----------  エラー処理
               PERFORM  ERR-RTN
           END-EVALUATE
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D             ":*** 勘定残高(別紙1-1)CURSOR FETCH END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       TKMZKZ00-FETCH-EXT.
           EXIT.
      ******************************************************************
      *   勘定残高(別紙1-1)CURSOR CLOSE
      ******************************************************************
       TKMZKZ00-CLOSE-RTN     SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D             ":*** 勘定残高(別紙1-1)CURSOR CLOSE  START ***"
      *-----------------------------------------------------------------
      *----------  勘定残高(別紙1-1)CURSOR CLOSE
           EXEC  SQL
             CLOSE TKMZKZ00_CUR
           END-EXEC
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":勘定残高(別紙1-1)CURSOR CLOSE SQLCODE=["
      D     SQLCODE "]"
      *-------------------<DEBUG>---------------------------------------
           IF  SQLCODE = CNS-SQL-NORMAL
           THEN
      *----------  OPEN-FLG
             MOVE  CNS-OFF              TO  FLG-TKMZKZ00-CR-OPEN
      *-----------------------DEBUG-------------------------------------
      D    DISPLAY  CNS-PGM-ID":(別紙1-1) SQL処理 (CLOSE)  正常"
      D    DISPLAY  CNS-PGM-ID":FLG-TKMZKZ00-CR-OPEN=["
      D             FLG-TKMZKZ00-CR-OPEN"]"
      *-----------------------------------------------------------------
           ELSE
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":(別紙1-1) SQL処理エラー (CLOSE)"
      *-------------------<DEBUG>---------------------------------------
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID           OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-005
               TO  ERR-POS              OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-900
               TO  RTN-CD               OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  SQLCODE
               TO  DETAIL-RTN-CD        OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-1
               TO  DETAIL-RTN-CD-FLG    OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
             STRING  CNS-SQL-MSG-SELECT,
                     WK-MSG-TKMZKZ00    DELIMITED  BY  SIZE
               INTO  FREE-MSG           OF  PARM-SCZY1170-ARG1
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID":エラー検知プログラム名 =["
      D             ERR-PGM-ID          OF  PARM-SCZY1170-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":ポジション             =["
      D             ERR-POS             OF  PARM-SCZY1170-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":リターンコード         =["
      D             RTN-CD              OF  PARM-SCZY1170-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":詳細リターンコード     =["
      D             DETAIL-RTN-CD       OF  PARM-SCZY1170-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":詳細リターンコードデコードフラグ=["
      D             DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":フリーメッセージ       =["
      D             FREE-MSG            OF  PARM-SCZY1170-ARG1"]"
      *-----------------------------------------------------------------
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D             ":*** 勘定残高(別紙1-1)CURSOR CLOSE  END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       TKMZKZ00-CLOSE-EXT.
           EXIT.
      ******************************************************************
      *     3.3.1 後処理
      ******************************************************************
       END-RTN                SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":***  後処理  START  ***"
      *-----------------------------------------------------------------
      *----------  リターンパッケージの設定
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":***リターンパッケージの処理 START ***"
      *-----------------------------------------------------------------
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY CNS-PGM-ID":RTN-CD   OF  PARM-SCZY1170-ARG1=["
      D                        RTN-CD   OF  PARM-SCZY1170-ARG1"]"
      *-----------------------------------------------------------------
           CALL   "SCZY1170"            USING  PARM-SCZY1170-IN
                                               PARM-SCZY1170-RTNPKG
           MOVE  PARM-SCZY1170-RTNPKG   TO     PARM-SMZJ4021-ARG3
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY CNS-PGM-ID":PARM-SCZY1170-RTNPKG  =["
      D            PARM-SCZY1170-RTNPKG"]"
      D    DISPLAY CNS-PGM-ID":PARM-SMZJ4021-ARG3  =["
      D            PARM-SMZJ4021-ARG3"]"
      D    DISPLAY CNS-PGM-ID":COUNTER               =["
      D            COUNTER              OF PARM-SMZJ4021-ARG3"]"
      D    DISPLAY CNS-PGM-ID":エラー検知プログラム名=["
      D            ERR-PGM-ID           OF PARM-SMZJ4021-ARG3(COUNTER
      D                                 OF PARM-SMZJ4021-ARG3)"]"
      D    DISPLAY CNS-PGM-ID":ポジション            =["
      D            ERR-POS              OF PARM-SMZJ4021-ARG3(COUNTER
      D                                 OF PARM-SMZJ4021-ARG3)"]"
      D    DISPLAY CNS-PGM-ID":リターンコード        =["
      D            RTN-CD               OF PARM-SMZJ4021-ARG3(COUNTER
      D                                 OF PARM-SMZJ4021-ARG3)"]"
      D    DISPLAY CNS-PGM-ID":詳細リターンコード    =["
      D            DETAIL-RTN-CD        OF PARM-SMZJ4021-ARG3(COUNTER
      D                                 OF PARM-SMZJ4021-ARG3)"]"
      D    DISPLAY CNS-PGM-ID":詳細リターンコードデコードフラグ =["
      D            DETAIL-RTN-CD-FLG    OF PARM-SMZJ4021-ARG3(COUNTER
      D                                 OF PARM-SMZJ4021-ARG3)"]"
      D    DISPLAY CNS-PGM-ID":フリーメッセージ      =["
      D            FREE-MSG             OF PARM-SMZJ4021-ARG3(COUNTER
      D                                 OF PARM-SMZJ4021-ARG3)"]"
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY CNS-PGM-ID":***リターンパッケージの処理  END  ***"
      D    DISPLAY  CNS-PGM-ID":***   後処理  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       END-EXT.
           EXIT.
      ******************************************************************
      *     3.4.1   エラー処理
      ******************************************************************
       ERR-RTN                SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":***  エラー処理  START  ***"
      *-----------------------------------------------------------------
           INITIALIZE     PARM-SMZJ4021-ARG2
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D    ":PARM-SMZJ4021-ARG2   = ["PARM-SMZJ4021-ARG2"]"
      D    DISPLAY  CNS-PGM-ID
      D    ":FLG-TKMZKZ00-CR-OPEN = ["FLG-TKMZKZ00-CR-OPEN"]"
      *-----------------------------------------------------------------
      *----------  勘定残高
           IF  FLG-TKMZKZ00-CR-OPEN = CNS-ON
           THEN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":ERROR処理 CLOSE TKMZKZ00_CUR"
      *-----------------------------------------------------------------
             EXEC  SQL
                 CLOSE TKMZKZ00_CUR
             END-EXEC
             MOVE  CNS-OFF              TO  FLG-TKMZKZ00-CR-OPEN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D    ":FLG-TKMZKZ00-CR-OPEN = ["FLG-TKMZKZ00-CR-OPEN"]"
      *-----------------------------------------------------------------
           ELSE
             CONTINUE
           END-IF
      *----------  リターンパッケージの設定
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":***リターンパッケージの処理2 START ***"
      *-----------------------------------------------------------------
           CALL   "SCZY1170"             USING  PARM-SCZY1170-IN
                                                PARM-SCZY1170-RTNPKG
           MOVE  PARM-SCZY1170-RTNPKG    TO     PARM-SMZJ4021-RTNPKG
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY CNS-PGM-ID":PARM-SCZY1170-RTNPKG=["
      D            PARM-SCZY1170-RTNPKG    "]"
      D    DISPLAY CNS-PGM-ID":PARM-SMZJ4021-ARG3=["
      D            PARM-SMZJ4021-ARG3    "]"
      D    DISPLAY CNS-PGM-ID":  COUNTER  = ["
      D            COUNTER               OF PARM-SMZJ4021-RTNPKG"]"
      D    DISPLAY CNS-PGM-ID":  エラー検知プログラム名= ["
      D            ERR-PGM-ID            OF PARM-SMZJ4021-RTNPKG(COUNTER
      D                                  OF PARM-SMZJ4021-RTNPKG)"]"
      D    DISPLAY CNS-PGM-ID":  ポジション = ["
      D            ERR-POS               OF PARM-SMZJ4021-RTNPKG(COUNTER
      D                                  OF PARM-SMZJ4021-RTNPKG)"]"
      D    DISPLAY CNS-PGM-ID":  リターンコード = ["
      D            RTN-CD                OF PARM-SMZJ4021-RTNPKG(COUNTER
      D                                  OF PARM-SMZJ4021-RTNPKG)"]"
      D    DISPLAY CNS-PGM-ID":  詳細リターンコード = ["
      D            DETAIL-RTN-CD         OF PARM-SMZJ4021-RTNPKG(COUNTER
      D                                  OF PARM-SMZJ4021-RTNPKG)"]"
      D    DISPLAY CNS-PGM-ID":  詳細リターンコードデコードフラグ = ["
      D            DETAIL-RTN-CD-FLG     OF PARM-SMZJ4021-RTNPKG(COUNTER
      D                                  OF PARM-SMZJ4021-RTNPKG)"]"
      D    DISPLAY CNS-PGM-ID":  フリーメッセージ = ["
      D            FREE-MSG              OF PARM-SMZJ4021-RTNPKG(COUNTER
      D                                  OF PARM-SMZJ4021-RTNPKG)"]"
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":***リターンパッケージの処理2  END  ***"
      D    DISPLAY  CNS-PGM-ID":***  エラー処理  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       ERR-EXT.
           GOBACK.
