      ******************************************************************
      *$Header$
      ******************************************************************
      *
      * システムＩＤ   : T-STAR
      * システム名称   : 次期TSTARシステム
      * プログラムＩＤ : SMZJ4022
      * プログラム名称 : 数量単位再計算
      * 処理概要       : 数量単位再計算
      * 参照ＤＢ       :
      * 引数           : IN  銘柄属性取得フラグ
      *                : IN  管理数量単位
      *                : IN  銘柄取得用データ
      *                : IN  銘柄属性データ
      *                : OUT 再計算数量単位
      *                : OUT RTNパッケージ
      * VERSION  1.00
      * 改訂履歴
      * 年月日     区分  所属       担当者    内容
      * ---------- ----  ---------  ------  ------------------------
      * 20090619   新規  SCS        王飛     新規作成
      * 20090624   修正  SCS        李菁
      ******************************************************************
       IDENTIFICATION         DIVISION.
       PROGRAM-ID.    SMZJ4022.
       AUTHOR.        Nomura.
       DATE-WRITTEN.  09-06-19.
       DATE-COMPILED.
      ******************************************************************
      *
      *                 環  境  部
      *
      ******************************************************************
       ENVIRONMENT            DIVISION.
       CONFIGURATION          SECTION.
       SOURCE-COMPUTER.       L1000V.
      *SOURCE-COMPUTER.       L1000V   WITH DEBUGGING MODE.
       OBJECT-COMPUTER.       RP5470.
       INPUT-OUTPUT           SECTION.
       FILE-CONTROL.
      ******************************************************************
      *
      *                 デ  ー  タ  部
      *
      ******************************************************************
       DATA                   DIVISION.
       WORKING-STORAGE        SECTION.
       01  IDNAME                  PIC  X(80)  VALUE
       "$Id$".
      ******************************************************************
      *                 コンスタント  エリア
      ******************************************************************
       01  CONSTANT-AREA.
         05  CNS-MZCNS.
           COPY "MZCNS.cpy".
         05  CNS-RTN-111           PIC  X(03)  VALUE  "111".
         05  CNS-RTN-112           PIC  X(03)  VALUE  "112".
         05  CNS-PGM-ID            PIC  X(08)  VALUE  "SMZJ4022".
      ******************************************************************
      *                 ワ−クエリア
      ******************************************************************
       01  WORK-AREA.
         05  WK-PARM-SMZJ4022-ARG1.
      *----------  銘柄属性取得フラグ
           10  WK-MEG-ZOK-FLG      PIC  X(01).
      *----------  管理数量単位
           10  WK-KANRI-SURYO-TANI PIC  9(12)V9(06).
      *----------  銘柄取得用データ
           10  WK-MEG-GET-DATA.
      *----------  利用会社コード
             15  WK-RIYO-CMP-CD    PIC  X(04).
      *----------  内部銘柄コード
             15  WK-NAIBU-MEG-CD   PIC  X(15).
      *----------  証券種別コード
             15  WK-SEC-SBT-CD     PIC  X(04).
      *----------  銘柄通貨コード
             15  WK-MEG-TUKA-CD    PIC  X(04).
      *----------  基本属性取得用基準日
             15  WK-KIHON-KIJYUN-BI
                                   PIC  9(08).
      *----------  銘柄属性データ
           10  WK-MEG-ZOK-DATA.
      *---------- 乗数（分子）
             15  WK-JYOUSUU-BUNSI  PIC  S9(08)V9(10).
      *---------- 乗数（分母）
             15  WK-JYOUSUU-BUNBO  PIC  S9(08)V9(10).
      ******************************************************************
      *                 サブプログラム  パラメ−タ  エリア
      ******************************************************************
      *----------  SCZY1170 リターンパッケージセットルーチン
       01  PARM-SCZY1170-IN.
      *----------  リターンパッケージセットルーチンコピー句
           COPY  "SCZY1170.cpy".
       01  PARM-SCZY1170-RTNPKG.
         05  PARM-SCZY1170-ARG2.
      *----------  RTNパッケージ
           COPY  "CZRTNPACKAGE.cpy".
      *----------  銘柄属性取得ルーチン
       01  PARM-SCZY3004-INOUT.
           COPY  "SCZY3004.cpy".
       01  PARM-RTNPKG.
         05 PARM-RTNPKG-ARG.
           COPY  "CZRTNPACKAGE.cpy".
      ******************************************************************
      *                 連絡節
      ******************************************************************
       LINKAGE                SECTION.
       01  PARM-SMZJ4022-INOUT.
           COPY  "SMZJ4022.cpy".
       01  PARM-SMZJ4022-RTNPKG.
         05  PARM-SMZJ4022-ARG3.
           COPY  "CZRTNPACKAGE.cpy".
      ******************************************************************
      *
      *                 手 続 き 部
      *
      ******************************************************************
       PROCEDURE              DIVISION
           USING  PARM-SMZJ4022-INOUT
                  PARM-SMZJ4022-RTNPKG.
      ******************************************************************
      *                 数量単位再計算
      ******************************************************************
       BASE-RTN               SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":***  SMZJ4022処理  START  ***"
      *-----------------------------------------------------------------
      *----------  3.1 前処理
           PERFORM  INIT-RTN
      *----------  3.2 主処理
           PERFORM  MAIN-RTN
      *----------  3.3 後処理
           PERFORM  END-RTN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":***  SMZJ4022処理  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       BASE-EXT.
           GOBACK.
      ******************************************************************
      *        3.1 前処理
      ******************************************************************
       INIT-RTN               SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":***  前処理  START  ***"
      *-----------------------------------------------------------------
      *----------  3.1.1 初期化処理
           PERFORM  INITIALIZE-RTN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":***  前処理   END   ***"
      *-----------------------------------------------------------------
           CONTINUE.
       INIT-EXT.
           EXIT.
      ******************************************************************
      *        3.1.1 初期化処理
      ******************************************************************
       INITIALIZE-RTN         SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  初期化処理  START  ***"
      *-----------------------------------------------------------------
      *----------  初期化
           INITIALIZE     WORK-AREA
                          PARM-SCZY1170-IN
                          PARM-SCZY1170-RTNPKG
                          PARM-SMZJ4022-ARG2
                          PARM-SMZJ4022-ARG3
                          PARM-SCZY3004-INOUT
                          PARM-RTNPKG
           MOVE  CNS-RTN-000
             TO  RTN-CD                 OF  PARM-SCZY1170-ARG1
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":ARG1.管理数量単位=["
      D             KANRI-SURYO-TANI    OF  PARM-SMZJ4022-ARG1"]"
      *-----------------------------------------------------------------
      *---------- 再計算数量単位
           MOVE  KANRI-SURYO-TANI       OF  PARM-SMZJ4022-ARG1
             TO  SAIKS-SURYO-TANI       OF  PARM-SMZJ4022-ARG2
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":WORK-AREA        = ["WORK-AREA"]"
      D    DISPLAY  CNS-PGM-ID":PARM-SCZY1170-IN = ["PARM-SCZY1170-IN"]"
      D    DISPLAY  CNS-PGM-ID":PARM-SCZY1170-RTNPKG= ["
      D             PARM-SCZY1170-RTNPKG"]"
      D    DISPLAY  CNS-PGM-ID":PARM-SMZJ4022-ARG2  = ["
      D             PARM-SMZJ4022-ARG2 "]"
      D    DISPLAY  CNS-PGM-ID":PARM-SMZJ4022-ARG3  = ["
      D             PARM-SMZJ4022-ARG3 "]"
      D    DISPLAY  CNS-PGM-ID":PARM-SCZY3004-INOUT = ["
      D             PARM-SCZY3004-INOUT  "]"
      D    DISPLAY  CNS-PGM-ID":PARM-RTNPKG= ["
      D             PARM-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID":RTN-CD  OF  PARM-SCZY1170-ARG1=["
      D             RTN-CD              OF  PARM-SCZY1170-ARG1"]"
      D    DISPLAY  CNS-PGM-ID":ARG2.再計算数量単位=["
      D             SAIKS-SURYO-TANI    OF  PARM-SMZJ4022-ARG2"]"
      *-----------------------------------------------------------------
      *----------  銘柄属性取得フラグ
           MOVE  MEG-ZOK-FLG            OF  PARM-SMZJ4022-ARG1
             TO  WK-MEG-ZOK-FLG
      *----------  管理数量単位
           MOVE  KANRI-SURYO-TANI       OF  PARM-SMZJ4022-ARG1
             TO  WK-KANRI-SURYO-TANI
      *----------  利用会社コード
           MOVE  RIYO-CMP-CD            OF  PARM-SMZJ4022-ARG1
             TO  WK-RIYO-CMP-CD
      *----------  内部銘柄コード
           MOVE  NAIBU-MEG-CD           OF  PARM-SMZJ4022-ARG1
             TO  WK-NAIBU-MEG-CD
      *----------  証券種別コード
           MOVE  SEC-SBT-CD             OF  PARM-SMZJ4022-ARG1
             TO  WK-SEC-SBT-CD
      *----------  銘柄通貨コード
           MOVE  MEG-TUKA-CD            OF  PARM-SMZJ4022-ARG1
             TO  WK-MEG-TUKA-CD
      *----------  基本属性取得用基準日
           MOVE  KIHON-KIJYUN-BI        OF  PARM-SMZJ4022-ARG1
             TO  WK-KIHON-KIJYUN-BI
      *---------- 乗数（分子）
           MOVE  JYOUSUU-BUNSI          OF  PARM-SMZJ4022-ARG1
             TO  WK-JYOUSUU-BUNSI
      *---------- 乗数（分母）
           MOVE  JYOUSUU-BUNBO          OF  PARM-SMZJ4022-ARG1
             TO  WK-JYOUSUU-BUNBO
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":銘柄属性取得フラグ=["WK-MEG-ZOK-FLG"]"
      D    DISPLAY  CNS-PGM-ID":管理数量単位      =["
      D             WK-KANRI-SURYO-TANI"]"
      D    DISPLAY  CNS-PGM-ID":銘柄取得用データ"
      D    DISPLAY  CNS-PGM-ID":利用会社コード    =["WK-RIYO-CMP-CD"]"
      D    DISPLAY  CNS-PGM-ID":内部銘柄コード    =["WK-NAIBU-MEG-CD"]"
      D    DISPLAY  CNS-PGM-ID":証券種別コード    =["WK-SEC-SBT-CD"]"
      D    DISPLAY  CNS-PGM-ID":銘柄通貨コード    =["WK-MEG-TUKA-CD"]"
      D    DISPLAY  CNS-PGM-ID":基本属性取得用基準日=["
      D             WK-KIHON-KIJYUN-BI"]"
      D    DISPLAY  CNS-PGM-ID":銘柄属性データ"
      D    DISPLAY  CNS-PGM-ID":乗数（分子）      =["WK-JYOUSUU-BUNSI"]"
      D    DISPLAY  CNS-PGM-ID":乗数（分母）      =["WK-JYOUSUU-BUNBO"]"
      D    DISPLAY  CNS-PGM-ID":***  初期化処理 END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       INITIALIZE-EXT.
           EXIT.
      ******************************************************************
      *        3.2 主処理
      ******************************************************************
       MAIN-RTN               SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID": *****  主処理  START  *****"
      D    DISPLAY  CNS-PGM-ID":ARG1.銘柄属性取得フラグ=["
      D             WK-MEG-ZOK-FLG"]"
      *-----------------------------------------------------------------
           IF  WK-MEG-ZOK-FLG   =   "1"
           THEN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":ARG1.銘柄属性取得フラグ＝'1'の場合"
      *-----------------------------------------------------------------
             IF  WK-JYOUSUU-BUNSI  NOT  =  ZERO  AND
                 WK-JYOUSUU-BUNBO  NOT  =  ZERO
             THEN
      *---------- ARG2.再計算数量単位
               COMPUTE  SAIKS-SURYO-TANI  OF  PARM-SMZJ4022-ARG2
                     =  WK-KANRI-SURYO-TANI
                     *  WK-JYOUSUU-BUNSI
                     /  WK-JYOUSUU-BUNBO
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":ARG2.再計算数量単位=["
      D             SAIKS-SURYO-TANI  OF  PARM-SMZJ4022-ARG2"]"
      *-----------------------------------------------------------------
             ELSE
               CONTINUE
             END-IF
           ELSE
             IF  WK-NAIBU-MEG-CD  NOT =  SPACE
             THEN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D             ":ARG1.銘柄取得用データ.内部銘柄コード≠spaceの場合"
      *-----------------------------------------------------------------
               PERFORM  CALL-SCZY3004-RTN
             ELSE
               CONTINUE
             END-IF
           END-IF
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID": *****  主処理  END  *****"
      *-----------------------------------------------------------------
           CONTINUE.
       MAIN-EXT.
           EXIT.
      ******************************************************************
      *        CALL  SCZY3004
      ******************************************************************
       CALL-SCZY3004-RTN      SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  CALL  SCZY3004  START  ***"
      *-----------------------------------------------------------------
           INITIALIZE   PARM-SCZY3004-INOUT
                        PARM-RTNPKG
      *---------- 利用会社コード
           MOVE  WK-RIYO-CMP-CD
             TO  RIYO-CMP-CD            OF  PARM-SCZY3004-ARG1
      *---------- 内部銘柄コード
           MOVE  WK-NAIBU-MEG-CD
             TO  NAIBU-MEG-CD           OF  PARM-SCZY3004-ARG1
      *---------- 証券種別コード
           MOVE  WK-SEC-SBT-CD
             TO  SYOUKEN-CD             OF  PARM-SCZY3004-ARG1
      *---------- 銘柄通貨コード
           MOVE  WK-MEG-TUKA-CD
             TO  MEIGAR-TUKA-CD         OF  PARM-SCZY3004-ARG1
      *---------- 基本属性取得用基準日
           MOVE  WK-KIHON-KIJYUN-BI
             TO  KIHON-KIJYUN-BI        OF  PARM-SCZY3004-ARG1
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":利用会社コード       =["
      D             RIYO-CMP-CD         OF  PARM-SCZY3004-ARG1"]"
      D    DISPLAY  CNS-PGM-ID ":内部銘柄コード       =["
      D             NAIBU-MEG-CD        OF  PARM-SCZY3004-ARG1"]"
      D    DISPLAY  CNS-PGM-ID ":証券種別コード       =["
      D             SYOUKEN-CD          OF  PARM-SCZY3004-ARG1"]"
      D    DISPLAY  CNS-PGM-ID ":銘柄通貨コード       =["
      D             MEIGAR-TUKA-CD      OF  PARM-SCZY3004-ARG1"]"
      D    DISPLAY  CNS-PGM-ID ":基本属性取得用基準日 =["
      D             KIHON-KIJYUN-BI     OF  PARM-SCZY3004-ARG1"]"
      *-----------------------------------------------------------------
           CALL   "SCZY3004"             USING  PARM-SCZY3004-INOUT
                                                PARM-RTNPKG
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":PARM-RTNPKG=["
      D             PARM-RTNPKG"]"
      D    DISPLAY  CNS-PGM-ID":RTN-CD(SCZY3004)=["
      D             RTN-CD  OF  PARM-RTNPKG(COUNTER
      D             OF  PARM-RTNPKG)"]"
      D    DISPLAY  CNS-PGM-ID":COUNTER  OF  PARM-RTNPKG=["
      D             COUNTER              OF  PARM-RTNPKG"]"
      *-----------------------------------------------------------------
           EVALUATE  RTN-CD  OF  PARM-RTNPKG(COUNTER  OF  PARM-RTNPKG)
      *----------  正常
             WHEN  CNS-RTN-000
      *----------  一部該当データなし警告
             WHEN  CNS-RTN-111
      *----------  利率取得失敗
             WHEN  CNS-RTN-112
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":リターンパッケージ.乗数（分子）=["
      D             JYOUSUU-BUNSI     OF  PARM-SCZY3004-ARG2"]"
      D    DISPLAY  CNS-PGM-ID":リターンパッケージ.乗数（分母）=["
      D             JYOUSUU-BUNBO     OF  PARM-SCZY3004-ARG2"]"
      *-----------------------------------------------------------------
               IF  JYOUSUU-BUNSI     OF  PARM-SCZY3004-ARG2  NOT  = ZERO
               AND JYOUSUU-BUNBO     OF  PARM-SCZY3004-ARG2  NOT  = ZERO
               THEN
      *---------- ARG2.再計算数量単位
                 COMPUTE  SAIKS-SURYO-TANI  OF  PARM-SMZJ4022-ARG2
                       =  WK-KANRI-SURYO-TANI
                       *  JYOUSUU-BUNSI     OF  PARM-SCZY3004-ARG2
                       /  JYOUSUU-BUNBO     OF  PARM-SCZY3004-ARG2
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":ARG2.再計算数量単位=["
      D             SAIKS-SURYO-TANI  OF  PARM-SMZJ4022-ARG2"]"
      *-----------------------------------------------------------------
               ELSE
                 CONTINUE
               END-IF
             WHEN  OTHER
               CONTINUE
           END-EVALUATE
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  CALL  SCZY3004  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       CALL-SCZY3004-EXT.
           EXIT.
      ******************************************************************
      *     3.3.1 後処理
      ******************************************************************
       END-RTN                SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":***  後処理  START  ***"
      *-----------------------------------------------------------------
      *----------  リターンパッケージの設定
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":***リターンパッケージの処理1 START ***"
      *-----------------------------------------------------------------
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY CNS-PGM-ID":RTN-CD    OF  PARM-SCZY1170-ARG1=["
      D                        RTN-CD    OF  PARM-SCZY1170-ARG1"]"
      *-----------------------------------------------------------------
           CALL   "SCZY1170"             USING  PARM-SCZY1170-IN
                                                PARM-SCZY1170-RTNPKG
           MOVE  PARM-SCZY1170-RTNPKG    TO     PARM-SMZJ4022-ARG3
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY CNS-PGM-ID":PARM-SCZY1170-RTNPKG=["
      D            PARM-SCZY1170-RTNPKG"]"
      D    DISPLAY  CNS-PGM-ID":PARM-SMZJ4022-ARG3 = ["
      D             PARM-SMZJ4022-ARG3"]"
      D    DISPLAY  CNS-PGM-ID":COUNTER = ["
      D             COUNTER                    OF PARM-SMZJ4022-ARG3"]"
      D    DISPLAY  CNS-PGM-ID":エラー検知プログラム名 = ["
      D             ERR-PGM-ID
      D          OF PARM-SMZJ4022-ARG3(COUNTER OF PARM-SMZJ4022-ARG3)"]"
      D    DISPLAY  CNS-PGM-ID":ポジション = ["
      D             ERR-POS
      D          OF PARM-SMZJ4022-ARG3(COUNTER OF PARM-SMZJ4022-ARG3)"]"
      D    DISPLAY  CNS-PGM-ID":リターンコード = ["
      D             RTN-CD
      D          OF PARM-SMZJ4022-ARG3(COUNTER OF PARM-SMZJ4022-ARG3)"]"
      D    DISPLAY  CNS-PGM-ID":詳細リターンコード = ["
      D             DETAIL-RTN-CD
      D          OF PARM-SMZJ4022-ARG3(COUNTER OF PARM-SMZJ4022-ARG3)"]"
      D    DISPLAY  CNS-PGM-ID":詳細リターンコードデコードフラグ = ["
      D             DETAIL-RTN-CD-FLG
      D          OF PARM-SMZJ4022-ARG3(COUNTER OF PARM-SMZJ4022-ARG3)"]"
      D    DISPLAY  CNS-PGM-ID":フリーメッセージ = ["
      D             FREE-MSG
      D          OF PARM-SMZJ4022-ARG3(COUNTER OF PARM-SMZJ4022-ARG3)"]"
      D    DISPLAY  CNS-PGM-ID
      D             ": リターンパッケージセットルーチンSCZY1170終了"
      D    DISPLAY  CNS-PGM-ID": ***   後処理  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       END-EXT.
           EXIT.
