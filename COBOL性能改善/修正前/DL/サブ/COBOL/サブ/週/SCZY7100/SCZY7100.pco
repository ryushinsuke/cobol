      ******************************************************************
      *$Header:$
      ******************************************************************
      *
      * システムＩＤ   : TSTAR
      * システム名称   : 次期TSTARシステム
      * プログラム名称 : バッチＤＢ接続情報取得ルーチン
      * 処理概要       : 環境変数より、バッチにおけるDB接続のユｰザｰID、
      *                  パスワード、インスタンスIDを取得する
      * 引数           : IN  利用会社コード
      *                : OUT DB接続情報
      *                : OUT RTNパッケージ
      *
      * 改訂履歴
      * 年月日   区分 所属     担当者  内容
      * -------- ---- -------- ------- --------------------------------
      * 20070920 新規 SCS      薛銀峰  新規作成
      * 20080201 修正 SCS      薛銀峰  修正
      ******************************************************************
       IDENTIFICATION         DIVISION.
       PROGRAM-ID.    SCZY7100.
       AUTHOR.        Nomura.
       DATE-WRITTEN.  08-02-01.
       DATE-COMPILED.
      ******************************************************************
      *
      *                環  境  部
      *
      ******************************************************************
       ENVIRONMENT            DIVISION.
       CONFIGURATION          SECTION.
       SOURCE-COMPUTER.       L1000V.
      * SOURCE-COMPUTER.       L1000V   WITH DEBUGGING MODE.
       OBJECT-COMPUTER.       RP5470.
       SPECIAL-NAMES.
           ENVIRONMENT-NAME               ENV-NAME
           ENVIRONMENT-VALUE              ENV-VALUE.
       INPUT-OUTPUT           SECTION.
       FILE-CONTROL.
      ******************************************************************
      *
      *                デ  ー  タ  部
      *
      ******************************************************************
       DATA                        DIVISION.
       WORKING-STORAGE             SECTION.
       01  IDNAME                  PIC  X(80)  VALUE
       "$Id: $".
      ******************************************************************
      *                コンスタント  エリア
      ******************************************************************
       01  CONSTANT-AREA.
         05  CNS-PGM-ID            PIC  X(16)  VALUE  "SCZY7100".
         05  CNS-RTN-CD-NORMAL     PIC  X(03)  VALUE  "000".
         05  CNS-POS-1             PIC  X(04)  VALUE  "0001".
         05  CNS-POS-2             PIC  X(04)  VALUE  "0002".
      ******************************************************************
      *                ワ−クエリア
      ******************************************************************
       01  WORK-AREA.
         05  WK-ERR-MSG-NO         PIC  X(04).
      *----------  ユーザーID
         05  WK-ORA-USER-ID        PIC  X(40).
      *----------  パスワード
         05  WK-ORA-PASSWORD       PIC  X(40).
      *----------  インスタンスID
         05  WK-ORA-INS-ID         PIC  X(40).
      *----------  インスタンスの環境変数名
         05  WK-INS-HUNSUU-NA      PIC  X(12).
      ******************************************************************
      *                サブプログラム  パラメ−タ  エリア
      ******************************************************************
       01  PARM-SCZY1170-INOUT.
      *----------  リターンパッケージセットルーチンコピー句
         COPY  "SCZY1170.cpy".
       01  PARM-SCZY1170-RTNPKG.
         05  PARM-SCZY1170-ARG2.
      *----------  RTNパッケージ
           10  RTN-PAKEGE.
             COPY  "CZRTNPACKAGE.cpy".
      ******************************************************************
      *                連絡節
      ******************************************************************
       LINKAGE                SECTION.
       01  PARM-SCZY7100-INOUT.
      *----------  バッチＤＢ接続情報取得ルーチンのコピー句
         COPY  "SCZY7100.cpy".
       01  PARM-SCZY7100-RTNPKG.
         05  PARM-SCZY7100-ARG3.
      *----------  RTNパッケージ
           10  RTN-PAKEGE.
             COPY  "CZRTNPACKAGE.cpy".
      ******************************************************************
      *
      *                PROCEDURE      DIVISION
      *
      ******************************************************************
       PROCEDURE              DIVISION
           USING  PARM-SCZY7100-INOUT
                  PARM-SCZY7100-RTNPKG.
      ******************************************************************
      *           SCZY7100処理
      ******************************************************************
       SCZY7100-RTN            SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "****  SCZY7100-RTN  START  ****"
      *-----------------------------------------------------------------
      *----------  初期処理
           PERFORM  INIT-RTN
      *----------  メイン処理
           PERFORM  MAIN-RTN
      *----------  終了処理
           PERFORM  END-RTN.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "****  SCZY7100-RTN  END  ****".
      *-----------------------------------------------------------------
       SCZY7100-EXT.
           GOBACK.
      ******************************************************************
      *           初期処理
      ******************************************************************
       INIT-RTN               SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "****  INIT-RTN  START  ****"
      *-----------------------------------------------------------------
      *----------  初期化
           INITIALIZE  WORK-AREA
                       PARM-SCZY7100-ARG2
                       PARM-SCZY7100-ARG3
                       PARM-SCZY1170-INOUT
                       PARM-SCZY1170-RTNPKG.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "WORK-AREA           :[" WORK-AREA              "]"
      D    DISPLAY  "PARM-SCZY7100-ARG2  :[" PARM-SCZY7100-ARG2     "]"
      D    DISPLAY  "PARM-SCZY7100-ARG3  :[" PARM-SCZY7100-ARG3     "]"
      D    DISPLAY  "PARM-SCZY1170-INOUT :[" PARM-SCZY1170-INOUT    "]"
      D    DISPLAY  "PARM-SCZY1170-RTNPKG:[" PARM-SCZY1170-RTNPKG   "]"
      D    DISPLAY  "****  INIT-RTN  END  ****".
      *-----------------------------------------------------------------
       INIT-EXT.
           EXIT.
      ******************************************************************
      *           メイン処理
      ******************************************************************
       MAIN-RTN               SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "****  MAIN-RTN  START  ****"
      D    DISPLAY  "利用会社コード      :["
      D              RIYO-CMP-CD  OF  PARM-SCZY7100-ARG1 "]"
      *-----------------------------------------------------------------
           IF  RIYO-CMP-CD  OF  PARM-SCZY7100-ARG1 = SPACE  THEN
               MOVE  CNS-POS-1      TO    WK-ERR-MSG-NO
               PERFORM  ERR-RTN
           END-IF
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "**** バッチＤＢ接続情報取得処理 START ****"
      *-----------------------------------------------------------------
      *----------  環境変数からユーザーIDを取得する
           DISPLAY  "ora_userid"      UPON  ENV-NAME
           ACCEPT   WK-ORA-USER-ID    FROM  ENV-VALUE
           ON EXCEPTION
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "環境変数からユーザーIDを取得できない."
      *-----------------------------------------------------------------
           CONTINUE
           END-ACCEPT
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "ユーザーID          :[" WK-ORA-USER-ID  "]"
      *-----------------------------------------------------------------
      *----------  環境変数からパスワードを取得する
           DISPLAY  "ora_password"    UPON  ENV-NAME
           ACCEPT   WK-ORA-PASSWORD   FROM  ENV-VALUE
           ON EXCEPTION
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "環境変数からパスワードを取得できない."
      *-----------------------------------------------------------------
           CONTINUE
           END-ACCEPT
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "パスワード          :[" WK-ORA-PASSWORD "]"
      *-----------------------------------------------------------------
      *----------  環境変数からインスタンスIDを取得する
           IF  RIYO-CMP-CD  OF  PARM-SCZY7100-ARG1 = "C000"  THEN
               MOVE  "ora_ins_4999"   TO    WK-INS-HUNSUU-NA
           ELSE
               STRING  "ora_ins_",
                       RIYO-CMP-CD    OF  PARM-SCZY7100-ARG1
               DELIMITED BY SIZE      INTO  WK-INS-HUNSUU-NA
           END-IF
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "ｲﾝｽﾀﾝｽの環境変数名  :[" WK-INS-HUNSUU-NA "]"
      *-----------------------------------------------------------------
           DISPLAY  WK-INS-HUNSUU-NA  UPON  ENV-NAME
           ACCEPT   WK-ORA-INS-ID     FROM  ENV-VALUE
           ON EXCEPTION
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "環境変数からインスタンスIDを取得できない."
      *-----------------------------------------------------------------
               MOVE  CNS-POS-2      TO    WK-ERR-MSG-NO
               PERFORM  ERR-RTN
           END-ACCEPT
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "インスタンスID      :[" WK-ORA-INS-ID  "]"
      *-----------------------------------------------------------------
      *----------  DB接続情報をセットする
           STRING  WK-ORA-USER-ID,
                   "/",
                   WK-ORA-PASSWORD,
                   "@",
                   WK-ORA-INS-ID
           DELIMITED BY SPACE   INTO  DB-SETUZOKU-JYOUHOU
                                  OF  PARM-SCZY7100-ARG2.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "DB接続情報          :["
      D              DB-SETUZOKU-JYOUHOU  OF  PARM-SCZY7100-ARG2 "]"
      D    DISPLAY  "****  MAIN-RTN  END  ****".
      *-----------------------------------------------------------------
       MAIN-EXT.
           EXIT.
      ******************************************************************
      *           後処理
      ******************************************************************
       END-RTN                SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "****  END-RTN  START  ****"
      *-----------------------------------------------------------------
      *----------  リターンコード
           MOVE  CNS-RTN-CD-NORMAL           TO  RTN-CD
                                             OF  PARM-SCZY1170-ARG1
           CALL  "SCZY1170"   USING  PARM-SCZY1170-INOUT
                                     PARM-SCZY1170-RTNPKG
           MOVE  RTN-PAKEGE                  OF  PARM-SCZY1170-ARG2
             TO  RTN-PAKEGE                  OF  PARM-SCZY7100-ARG3.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "DB接続情報          :["
      D              DB-SETUZOKU-JYOUHOU  OF  PARM-SCZY7100-ARG2 "]"
      D    DISPLAY  "RTN-PAKEGE          :["
      D              RTN-PAKEGE  OF  PARM-SCZY7100-ARG3 "]"
      D    DISPLAY  "****  END-RTN  END  ****".
      *-----------------------------------------------------------------
       END-EXT.
           EXIT.
      ******************************************************************
      *           エラー処理
      ******************************************************************
       ERR-RTN                SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "****  ERR-RTN  START  ****"
      *-----------------------------------------------------------------
           EVALUATE  WK-ERR-MSG-NO
               WHEN  CNS-POS-1
                   MOVE  CNS-PGM-ID          TO  ERR-PGM-ID
                                             OF  PARM-SCZY1170-ARG1
                   MOVE  CNS-POS-1           TO  ERR-POS
                                             OF  PARM-SCZY1170-ARG1
                   MOVE  "800"               TO  RTN-CD
                                             OF  PARM-SCZY1170-ARG1
                   MOVE  ZERO                TO  DETAIL-RTN-CD
                                             OF  PARM-SCZY1170-ARG1
                   MOVE  "0"                 TO  DETAIL-RTN-CD-FLG
                                             OF  PARM-SCZY1170-ARG1
               WHEN  CNS-POS-2
                   MOVE  CNS-PGM-ID          TO  ERR-PGM-ID
                                             OF  PARM-SCZY1170-ARG1
                   MOVE  CNS-POS-2           TO  ERR-POS
                                             OF  PARM-SCZY1170-ARG1
                   MOVE  "910"               TO  RTN-CD
                                             OF  PARM-SCZY1170-ARG1
                   MOVE  ZERO                TO  DETAIL-RTN-CD
                                             OF  PARM-SCZY1170-ARG1
                   MOVE  "0"                 TO  DETAIL-RTN-CD-FLG
                                             OF  PARM-SCZY1170-ARG1
               WHEN  OTHER
                   CONTINUE
           END-EVALUATE
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "ERR-PGM-ID          :["
      D              ERR-PGM-ID  OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  "ERR-POS             :["
      D              ERR-POS  OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  "RTN-CD              :["
      D              RTN-CD  OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  "DETAIL-RTN-CD       :["
      D              DETAIL-RTN-CD  OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  "DETAIL-RTN-CD-FLG   :["
      D              DETAIL-RTN-CD-FLG  OF  PARM-SCZY1170-ARG1 "]"
      *-----------------------------------------------------------------
           CALL  "SCZY1170"   USING  PARM-SCZY1170-INOUT
                                     PARM-SCZY1170-RTNPKG
           MOVE  RTN-PAKEGE                  OF  PARM-SCZY1170-ARG2
             TO  RTN-PAKEGE                  OF  PARM-SCZY7100-ARG3.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "RTN-PAKEGE          :["
      D              RTN-PAKEGE  OF  PARM-SCZY7100-ARG3 "]"
      D    DISPLAY  "****  ERR-RTN  END  ****".
      *-----------------------------------------------------------------
       ERR-EXT.
           GOBACK.
      ******************************************************************