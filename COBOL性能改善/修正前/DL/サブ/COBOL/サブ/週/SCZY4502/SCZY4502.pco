      ******************************************************************
      *$Header$
      ******************************************************************
      *
      *     システムＩＤ   : T-STAR
      *     システム名称   : 次期TSTARシステム
      *     プログラムＩＤ : SCZY4502
      *     プログラム名称 : 為替換算情報取得処理
      *     処理概要       : アーギュメントINより、
      *                      メモリから対象の為替換算情報を一件取得する。
      *     更新ＤＢ       : なし
      *     参照ＤＢ       : なし
      *     引数           : IN  利用会社コード
      *                    : OUT  媒介通貨
      *                    : OUT  乗算/除算区分
      *     改訂履歴
      *     年月日     区分  所属       担当者  内容
      *     ---------- ----  ---------  ------  ------------------------
      *     20100108   新規   SCS       張香桃  新規作成
      *     20100318   修正  SCS        張香桃  連結障害2183
      ******************************************************************
      ******************************************************************
       IDENTIFICATION         DIVISION.
       PROGRAM-ID.    SCZY4502.
       AUTHOR.        Nomura.
       DATE-WRITTEN.  10-01-06.
       DATE-COMPILED.
      ******************************************************************
      *
      *    環  境  部
      *
      ******************************************************************
       ENVIRONMENT            DIVISION.
       CONFIGURATION          SECTION.
       SOURCE-COMPUTER.       L1000V.
      *SOURCE-COMPUTER.       L1000V   WITH DEBUGGING MODE.
       OBJECT-COMPUTER.       RP5470.
       INPUT-OUTPUT           SECTION.
       FILE-CONTROL.
      ******************************************************************
      *
      *    デ  ー  タ  部
      *
      ******************************************************************
       DATA                   DIVISION.
      ******************************************************************
      *    作  業  場  所  節
      ******************************************************************
       WORKING-STORAGE        SECTION.
       01  IDNAME                  PIC  X(80)  VALUE
       "$Id$".
      ******************************************************************
      *    コンスタント  エリア
      ******************************************************************
      *----------  共通コンスタント定義の宣言
       01  CONSTANT-AREA.
         05  CNS-SCZYCNS.
           COPY  "KOMONCNS.cpy".
           COPY  "SCZY_PFCM.cpy".
      *----------  プログラム名
         05  CNS-PGM-ID            PIC  X(08)  VALUE  "SCZY4502".
      *----------  件数
         05  CNS-0                 PIC  9(01)  VALUE  0.
         05  CNS-1                 PIC  9(01)  VALUE  1.
         05  CNS-2                 PIC  9(01)  VALUE  2.
      ******************************************************************
      *    サブプログラム  パラメ−タ  エリア
      ******************************************************************
      *----------  リターンパッケージセットルーチン
       01  PARM-SCZY1170-IN.
           COPY  "SCZY1170.cpy".
      *----------  RTNケィケージコゴー句
       01  PARM-RTNPKG.
         05  PARM-RTNPKG-ARG.
           COPY  "CZRTNPACKAGE.cpy".
      ******************************************************************
      *                ワ−ク  エリア
      ******************************************************************
       01  WORK-AREA.
      *----------  ワーク⇔サリーメィセージ
         05  WK-IDX-CNT            PIC  9(04).
         05  WK-DATA-FLG           PIC  9(01).
         05  WK-SEC-POS            PIC  9(04).
         05  WK-IN.
           10  WK-TKJCKK00.
      *----------  商品属性データ配列
             15  WK-SYOHIN-SOKUSEI  OCCURS  10001.
      *----------  為替系列コード
               20  WK-KWS-KRTU-CD  PIC  X(04).
      *----------  ベース通貨コード
               20  WK-BASE-TUKA-CD
                                   PIC  X(04).
      *----------  目的通貨コード
               20  WK-MOKUTEKI-TUKA-CD
                                   PIC  X(04).
      *----------  媒介通貨
               20  WK-BAYIKAYI-TUKA
                                   PIC  X(04).
      *----------  乗算/除算区分
               20  WK-JZ-JS-KBN    PIC  X(01).
      *----------  有効件数
           10  WK-YUUKOU-KENSUU    PIC  9(05).
      ******************************************************************
      *    連  絡  節
      ******************************************************************
       LINKAGE                SECTION.
       01  PARM-SCZY4502-INOUT.
           COPY  "SCZY4502.cpy".
       01  PARM-SCZY4502-RTNPKG.
         05  PARM-SCZY4502-ARG4.
           COPY  "CZRTNPACKAGE.cpy".
      ******************************************************************
      *
      *    手 続 き 部
      *
      ******************************************************************
       PROCEDURE              DIVISION
           USING  PARM-SCZY4502-INOUT
                  PARM-SCZY4502-RTNPKG.
      ******************************************************************
      *    メイン処理
      ******************************************************************
       BASE-RTN               SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** SCZY4502 "
      D                      "為替換算情報取得処理 START ***"
      *-----------------------------------------------------------------
      *----------  3.1 前処理
           PERFORM  INIT-RTN
      *----------  3.2 主処理
           PERFORM  MAIN-RTN
      *----------  3.3 後処理
           PERFORM  END-RTN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** SCZY4502 "
      D                      "為替換算情報取得処理 END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       BASE-EXT.
           GOBACK.
      ******************************************************************
      *    3.1 前処理
      ******************************************************************
       INIT-RTN               SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  前処理  START  ***"
      *-----------------------------------------------------------------
      *----------  3.1.1 初期化処理
           PERFORM  INITIALIZE-RTN
      *----------  3.1.2 パラメータチェック
           PERFORM  SEC-SBT-CHECK-RTN
      *----------  通貨コード判定
           PERFORM  APL-TUKA-CD-RTN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  前処理  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       INIT-EXT.
           EXIT.
      ******************************************************************
      *    3.1.1.初期化処理
      ******************************************************************
       INITIALIZE-RTN         SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 初期化処理 START ***"
      *-----------------------------------------------------------------
           INITIALIZE  PARM-RTNPKG
                       WORK-AREA
                       PARM-SCZY1170-IN
                       PARM-SCZY4502-ARG3
                       PARM-SCZY4502-RTNPKG
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":WORK-AREA =[" WORK-AREA "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY1170-IN =["
      D                         PARM-SCZY1170-IN  "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4502-ARG3 =["
      D                         PARM-SCZY4502-ARG3 "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4502-RTNPKG =["
      D                         PARM-SCZY4502-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":*** 初期化処理 END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       INITIALIZE-EXT.
           EXIT.
      ******************************************************************
      *    3.1.2.パラメータチェック
      ******************************************************************
       SEC-SBT-CHECK-RTN      SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** パラメータチェック START ***"
      D    DISPLAY  CNS-PGM-ID "為替系列コード1:["
      D             KWS-KRTU-CD         OF   PARM-SCZY4502-ARG1"]"
      D    DISPLAY  CNS-PGM-ID "ベース通貨コード1:["
      D             BASE-TUKA-CD        OF   PARM-SCZY4502-ARG1"]"
      D    DISPLAY  CNS-PGM-ID "目的通貨コード1:["
      D             MOKUTEKI-TUKA-CD    OF   PARM-SCZY4502-ARG1"]"
      *-----------------------------------------------------------------
           IF    BASE-TUKA-CD       OF   PARM-SCZY4502-ARG1 NOT = SPACE
           AND   MOKUTEKI-TUKA-CD   OF   PARM-SCZY4502-ARG1 NOT = SPACE
           THEN
               CONTINUE
           ELSE
      *----------  エラー検知プログラム名
               MOVE  CNS-PGM-ID
                 TO  ERR-PGM-ID        OF  PARM-SCZY1170-ARG1
      *----------  ポジション
               MOVE  CNS-POS-001
                 TO  ERR-POS           OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
               MOVE  CNS-RTN-800
                 TO  RTN-CD            OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
               MOVE  CNS-0
                 TO  DETAIL-RTN-CD     OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
               MOVE  CNS-DETAIL-0
                 TO  DETAIL-RTN-CD-FLG OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
               MOVE  "パラメータエラー"
                 TO  FREE-MSG          OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
               PERFORM  ERR-RTN
           END-IF
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** パラメータチェック END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       SEC-SBT-CHECK-EXT.
           EXIT.
      ******************************************************************
      *                 通貨コード判定
      ******************************************************************
       APL-TUKA-CD-RTN        SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":通貨コード判定処理開始"
      D    DISPLAY  CNS-PGM-ID ":通貨コード判定"
      *-----------------------------------------------------------------
           IF  (
      *----------  ARG1.ベース通貨コード = ARG1.目的通貨コードの場合
                 BASE-TUKA-CD         OF  PARM-SCZY4502-ARG1
                                      =   MOKUTEKI-TUKA-CD
                                      OF  PARM-SCZY4502-ARG1)
           THEN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY CNS-PGM-ID ":ベース通貨コード = 目的通貨コードの場合"
      *-----------------------------------------------------------------
      *----------  媒介通貨 ← スペース
               MOVE  SPACE            TO  BAYIKAYI-TUKA
                                      OF  PARM-SCZY4502-ARG3
      *----------  乗算/除算区分 ← '1'(乗算)
               MOVE  "1"               TO  JZ-JS-KBN
                                      OF  PARM-SCZY4502-ARG3
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID":媒介通貨     =[" BAYIKAYI-TUKA
      D                               OF  PARM-SCZY4502-ARG3      "]"
      D    DISPLAY  CNS-PGM-ID":乗算/除算区分=[" JZ-JS-KBN
      D                               OF  PARM-SCZY4502-ARG3      "]"
      D    DISPLAY  CNS-PGM-ID":後処理へ"
      *-----------------------------------------------------------------
      *----------  後処理 へ
               PERFORM  END-RTN
      *----------  上記以外の場合
           ELSE
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":ベース通貨コード≠目的通貨コードの場合"
      *-----------------------------------------------------------------
               CONTINUE
           END-IF
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":通貨コード判定処理終了"
      *-----------------------------------------------------------------
           CONTINUE.
       APL-TUKA-CD-EXT.
           EXIT.
      ******************************************************************
      *    3.2.主処理
      ******************************************************************
       MAIN-RTN               SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  主処理  START  ***"
      *-----------------------------------------------------------------
      *----------  3.2.2.為替換算情報取得処理
           PERFORM  KWS-GET-RTN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  主処理  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       MAIN-EXT.
           EXIT.
      ******************************************************************
      *    3.2.2.為替換算情報取得処理
      ******************************************************************
       KWS-GET-RTN            SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D                  ":*** 為替換算情報取得処理 START *** "
      D    DISPLAY  CNS-PGM-ID ":為替系列コード1:["
      D             KWS-KRTU-CD         OF   PARM-SCZY4502-ARG1"]"
      D    DISPLAY  CNS-PGM-ID ":ベース通貨コード1:["
      D             BASE-TUKA-CD        OF   PARM-SCZY4502-ARG1"]"
      D    DISPLAY  CNS-PGM-ID ":目的通貨コード1:["
      D             MOKUTEKI-TUKA-CD    OF   PARM-SCZY4502-ARG1"]"
      D    DISPLAY  CNS-PGM-ID ":有効件数:["
      D             WK-YUUKOU-KENSUU  OF  WK-IN"]"
      *-----------------------------------------------------------------
           MOVE  PARM-SCZY4502-ARG2     TO   WK-IN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":有効件数:["
      D             WK-YUUKOU-KENSUU  OF  WK-IN"]"
      *-----------------------------------------------------------------
           PERFORM             VARYING  WK-IDX-CNT
             FROM CNS-1 BY CNS-1  UNTIL WK-IDX-CNT >
                                  WK-YUUKOU-KENSUU  OF  WK-IN
             OR   WK-DATA-FLG = CNS-1
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":WK-IDX-CNT:[" WK-IDX-CNT "]"
      D    DISPLAY  CNS-PGM-ID ":為替系列コード:["
      D             WK-KWS-KRTU-CD OF   WK-SYOHIN-SOKUSEI(WK-IDX-CNT)"]"
      D    DISPLAY  CNS-PGM-ID ":ベース通貨コード:["
      D            WK-BASE-TUKA-CD OF   WK-SYOHIN-SOKUSEI(WK-IDX-CNT)"]"
      D    DISPLAY  CNS-PGM-ID ":目的通貨コード:["
      D        WK-MOKUTEKI-TUKA-CD OF   WK-SYOHIN-SOKUSEI(WK-IDX-CNT)"]"
      *-----------------------------------------------------------------
             IF  WK-KWS-KRTU-CD       OF   WK-SYOHIN-SOKUSEI(WK-IDX-CNT)
              =  KWS-KRTU-CD          OF   PARM-SCZY4502-ARG1
             AND WK-BASE-TUKA-CD      OF   WK-SYOHIN-SOKUSEI(WK-IDX-CNT)
              =  BASE-TUKA-CD         OF   PARM-SCZY4502-ARG1
             AND WK-MOKUTEKI-TUKA-CD  OF   WK-SYOHIN-SOKUSEI(WK-IDX-CNT)
              =   MOKUTEKI-TUKA-CD    OF   PARM-SCZY4502-ARG1
             THEN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":DATA-FLG＝1(ベース／目的逆転)ない場合)"
      *-----------------------------------------------------------------
               MOVE  CNS-1            TO   WK-DATA-FLG
             ELSE
               CONTINUE
             END-IF
             IF  WK-KWS-KRTU-CD       OF   WK-SYOHIN-SOKUSEI(WK-IDX-CNT)
              =  KWS-KRTU-CD          OF   PARM-SCZY4502-ARG1
             AND WK-BASE-TUKA-CD      OF   WK-SYOHIN-SOKUSEI(WK-IDX-CNT)
              =  MOKUTEKI-TUKA-CD     OF   PARM-SCZY4502-ARG1
             AND WK-MOKUTEKI-TUKA-CD  OF   WK-SYOHIN-SOKUSEI(WK-IDX-CNT)
              =  BASE-TUKA-CD         OF   PARM-SCZY4502-ARG1
             THEN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":DATA-FLG＝2(ベース／目的逆転)の場合"
      *-----------------------------------------------------------------
               MOVE  CNS-2             TO   WK-DATA-FLG
               MOVE  WK-IDX-CNT        TO   WK-SEC-POS
             ELSE
               CONTINUE
             END-IF
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":WK-DATA-FLG=["WK-DATA-FLG  "]"
      D    DISPLAY  CNS-PGM-ID ":WK-SEC-POS =["WK-SEC-POS  "]"
      D    DISPLAY  CNS-PGM-ID ":元乗算/除算区分=["
      D    WK-JZ-JS-KBN    OF  WK-SYOHIN-SOKUSEI(WK-IDX-CNT)"]"
      *-----------------------------------------------------------------
           END-PERFORM
             EVALUATE  WK-DATA-FLG
      *----------  データなし
               WHEN  CNS-0
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "データなし"
      *-----------------------------------------------------------------
      *----------  エラー検知プログラム名
               MOVE  CNS-PGM-ID
                 TO  ERR-PGM-ID        OF  PARM-SCZY1170-ARG1
      *----------  ポジション
               MOVE  CNS-POS-002
                 TO  ERR-POS           OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
               MOVE  CNS-RTN-110
                 TO  RTN-CD            OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
               MOVE  CNS-0
                 TO  DETAIL-RTN-CD     OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
               MOVE  CNS-DETAIL-1
                 TO  DETAIL-RTN-CD-FLG OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
               MOVE  "該当為替換算情報なし"
                 TO  FREE-MSG          OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
               PERFORM  ERR-RTN
               WHEN  CNS-1
      *---------- 媒介通貨
               COMPUTE  WK-IDX-CNT     =    WK-IDX-CNT - CNS-1
               MOVE  WK-BAYIKAYI-TUKA  OF  WK-SYOHIN-SOKUSEI(WK-IDX-CNT)
                 TO  BAYIKAYI-TUKA     OF  PARM-SCZY4502-ARG3
      *---------- 乗算/除算区分
               IF  WK-JZ-JS-KBN        OF  WK-SYOHIN-SOKUSEI(WK-IDX-CNT)
                = "0"
               THEN
                  MOVE  "1"
                    TO  JZ-JS-KBN      OF  PARM-SCZY4502-ARG3
               ELSE
                 CONTINUE
               END-IF
               IF  WK-JZ-JS-KBN        OF  WK-SYOHIN-SOKUSEI(WK-IDX-CNT)
                = "1"
               THEN
                  MOVE  "2"
                    TO  JZ-JS-KBN      OF  PARM-SCZY4502-ARG3
               ELSE
                 CONTINUE
               END-IF
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":媒介通貨["
      D             BAYIKAYI-TUKA   OF  PARM-SCZY4502-ARG3"]"
      D    DISPLAY  CNS-PGM-ID ":乗算/除算区分["
      D              JZ-JS-KBN  OF  PARM-SCZY4502-ARG3"]"
      *-----------------------------------------------------------------
               WHEN  CNS-2
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":ベース／目的逆転の場合"
      *-----------------------------------------------------------------
      *---------- 媒介通貨
               MOVE  WK-BAYIKAYI-TUKA  OF  WK-SYOHIN-SOKUSEI(WK-SEC-POS)
                 TO  BAYIKAYI-TUKA     OF  PARM-SCZY4502-ARG3
      *---------- 乗算/除算区分
               IF  WK-JZ-JS-KBN        OF  WK-SYOHIN-SOKUSEI(WK-SEC-POS)
                = "0"
               THEN
                  MOVE  "2"
                    TO  JZ-JS-KBN      OF  PARM-SCZY4502-ARG3
               ELSE
                 CONTINUE
               END-IF
               IF  WK-JZ-JS-KBN        OF  WK-SYOHIN-SOKUSEI(WK-SEC-POS)
                = "1"
               THEN
                  MOVE  "1"
                    TO  JZ-JS-KBN      OF  PARM-SCZY4502-ARG3
               ELSE
                 CONTINUE
               END-IF
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":媒介通貨["
      D             BAYIKAYI-TUKA   OF  PARM-SCZY4502-ARG3"]"
      D    DISPLAY  CNS-PGM-ID ":乗算/除算区分["
      D             JZ-JS-KBN  OF  PARM-SCZY4502-ARG3"]"
      *-----------------------------------------------------------------
               WHEN  OTHER
               CONTINUE
             END-EVALUATE
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":WK-IDX-CNT=["WK-IDX-CNT "]"
      D    DISPLAY  CNS-PGM-ID ":WK-SEC-POS=["WK-SEC-POS "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4502-ARG2(WK-IDX-CNT)=["
      D    WK-TKJCKK00"]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4502-ARG3=["
      D    PARM-SCZY4502-ARG3 "]"
      D    DISPLAY  CNS-PGM-ID ":*** 為替換算情報取得処理 END *** "
      *-----------------------------------------------------------------
           CONTINUE.
       KWS-GET-EXT.
           EXIT.
      ******************************************************************
      *    3.3 後処理
      ******************************************************************
       END-RTN                SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 後処理 START ***"
      *-----------------------------------------------------------------
           MOVE  CNS-RTN-000
             TO  RTN-CD                OF  PARM-SCZY1170-ARG1
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** CALL SCZY1170 START ***"
      *-----------------------------------------------------------------
           CALL   "SCZY1170"           USING  PARM-SCZY1170-IN
                                              PARM-RTNPKG
           MOVE  PARM-RTNPKG           TO     PARM-SCZY4502-ARG4
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** CALL SCZY1170 END ***"
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4502-ARG2 =["
      D             PARM-SCZY4502-ARG2 "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4502-ARG3 =["
      D             PARM-SCZY4502-ARG3 "]"
      D    DISPLAY  CNS-PGM-ID ":*** 後処理 END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       END-EXT.
           GOBACK.
      ******************************************************************
      *    3.4 エラー処理
      ******************************************************************
       ERR-RTN                SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** エラー処理 START ***"
      D    DISPLAY  CNS-PGM-ID ":エラー検知プログラム名 =["
      D             ERR-PGM-ID         OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":ポジション =["
      D             ERR-POS            OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":リターンコード =["
      D             RTN-CD             OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":詳細リターンコード =["
      D             DETAIL-RTN-CD      OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":詳細リターンコードデコードフラグ =["
      D             DETAIL-RTN-CD-FLG  OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":フリーメッセージ =["
      D             FREE-MSG           OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":*** CALL SCZY1170 START ***"
      *-----------------------------------------------------------------
           CALL   "SCZY1170"           USING  PARM-SCZY1170-IN
                                              PARM-RTNPKG
           MOVE  PARM-RTNPKG           TO  PARM-SCZY4502-ARG4
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** CALL SCZY1170 END ***"
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4502-ARG2 =["
      D             PARM-SCZY4502-ARG2 "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4502-ARG3 =["
      D             PARM-SCZY4502-ARG3 "]"
      D    DISPLAY  CNS-PGM-ID ":*** エラー処理 END ***"
      D    DISPLAY  CNS-PGM-ID ":*** SCZY4502 END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       ERR-EXT.
           GOBACK.
