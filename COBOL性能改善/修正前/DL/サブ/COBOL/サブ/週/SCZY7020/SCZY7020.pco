      ******************************************************************
      *$Header: $
      ******************************************************************
      *
      *     システムＩＤ   : TSTAR
      *     システム名称   : 次期TSTARシステム
      *     プログラム名称 : バッチ終了メッセージ出力ルーチン
      *     処理概要       : バッチ終了メッセージ出力ルーチン
      *     引数           : IN  エラー種別
      *                    : IN  プログラム名
      *                    : IN  利用会社コード
      *                    : IN  基準日
      *                    : IN  実行処理ID
      *                    : IN  起動ID
      *                    : IN  RTNパッケ
      *     改訂履歴
      *     年月日     区分  所属       担当者  内容
      *     ---------- ----  ---------  ------  ------------------------
      *     20070924   新規  SCS        劉鳳麗  新規作成
      ******************************************************************
      ******************************************************************
       IDENTIFICATION         DIVISION.
       PROGRAM-ID.    SCZY7020.
       AUTHOR.        Nomura.
       DATE-WRITTEN.  07-09-24.
       DATE-COMPILED.
      ******************************************************************
      *
      *                 環  境  部
      *
      ******************************************************************
       ENVIRONMENT            DIVISION.
       CONFIGURATION          SECTION.
       SOURCE-COMPUTER.       L1000V.
      *SOURCE-COMPUTER.       L1000V  WITH  DEBUGGING  MODE.
       OBJECT-COMPUTER.       RP5470.
       INPUT-OUTPUT           SECTION.
       FILE-CONTROL.
      ******************************************************************
      *
      *                 デ  ー  タ  部
      *
      ******************************************************************
       DATA                   DIVISION.
       WORKING-STORAGE        SECTION.
       01  IDNAME                  PIC  X(80)  VALUE
       "$Id: $".
      ******************************************************************
      *                コンスタント エリア
      ******************************************************************
       01  CONS-PROG-AREA.
      *----------  正常終了
         05  CNS-N                      PIC  X(01)  VALUE  "N".
      *----------  業務エラー
         05  CNS-U                      PIC  X(01)  VALUE  "U".
      *----------  システムエラー
         05  CNS-S                      PIC  X(01)  VALUE  "S".
      ******************************************************************
      *                   ワーク  エリア
      ******************************************************************
       01  WK-AREA.
      *---------- 詳細リターンコードデコードフラグ
           10  WK-DETAIL-RTN-CD-FLG     PIC  X(01).
      *----------  BLのマシン日付時刻
           10  WK-MAC-DATE-TIME         PIC  X(14).
      *---------- エラー種別
           10  WK-ERR-SYUBETU           PIC  X(01).
           10  WK-COUNTER               PIC  9(02).
      *---------- エラー種別-N(メッセージ)
           10  WK-MSG-N.
      *----------  メッセージ1行目
             15  WK-MSG-N-1.
               20 WK-MAC-DATE-TIME-N.
                 25  WK-YY-N            PIC  X(04).
                 25  FILLER             PIC  X(01)
                   VALUE  "/".
                 25  WK-MM-N            PIC  X(02).
                 25  FILLER             PIC  X(01)
                   VALUE  "/".
                 25  WK-DD-N            PIC  X(02).
                 25  FILLER             PIC  X(01)
                   VALUE  " ".
                 25  WK-HH-N            PIC  X(02).
                 25  FILLER             PIC  X(01)
                   VALUE  ":".
                 25  WK-MI-N            PIC  X(02).
                 25  FILLER             PIC  X(01)
                   VALUE  ":".
                 25  WK-SS-N            PIC  X(02).
               20  FILLER               PIC  X(01)
                 VALUE  " ".
               20  WK-PGM-ID-N          PIC  X(16).
               20  FILLER               PIC  X(19)
                 VALUE  " Is Ended Normally.".
               20  FILLER               PIC  X(01)
                 VALUE  X"0A".
      *----------  メッセージ2行目
             15  WK-MSG-N-2.
               20  FILLER               PIC  X(11)
                 VALUE  "RIYO_CMPCD=".
               20  WK-RIYO-CMP-CD-N     PIC  X(04).
               20  FILLER               PIC  X(12)
                 VALUE  " KIJYUN_YMD=".
               20  WK-KJN-YMD-N         PIC  X(08).
               20  FILLER               PIC  X(09)
                 VALUE  " EXEC_ID=".
               20  WK-JIKOU-SYORI-ID-N  PIC  X(07).
               20  FILLER               PIC  X(09)
                 VALUE  " KIDO_ID=".
               20  WK-KIDOU-ID-N        PIC  X(04).
      *---------- エラー種別-U(メッセージ)
           10  WK-MSG-U.
      *----------  メッセージ1行目
             15  WK-MSG-U-1.
               20 WK-MAC-DATE-TIME-U.
                 25  WK-YY-U            PIC  X(04).
                 25  FILLER             PIC  X(01)
                   VALUE  "/".
                 25  WK-MM-U            PIC  X(02).
                 25  FILLER             PIC  X(01)
                   VALUE  "/".
                 25  WK-DD-U            PIC  X(02).
                 25  FILLER             PIC  X(01)
                   VALUE  " ".
                 25  WK-HH-U            PIC  X(02).
                 25  FILLER             PIC  X(01)
                   VALUE  ":".
                 25  WK-MI-U            PIC  X(02).
                 25  FILLER             PIC  X(01)
                   VALUE  ":".
                 25  WK-SS-U            PIC  X(02).
               20  FILLER               PIC  X(01)
                 VALUE  " ".
               20  WK-PGM-ID-U          PIC  X(16).
               20  FILLER               PIC  X(27)
                 VALUE  " Is Ended Abnormally(User).".
               20  FILLER               PIC  X(01)
                 VALUE  X"0A".
      *----------  メッセージ2行目
             15  WK-MSG-U-2.
               20  FILLER               PIC  X(11)
                 VALUE  "RIYO_CMPCD=".
               20  WK-RIYO-CMP-CD-U-2   PIC  X(04).
               20  FILLER               PIC  X(12)
                 VALUE  " KIJYUN_YMD=".
               20  WK-KJN-YMD-U-2       PIC  X(08).
               20  FILLER               PIC  X(09)
                 VALUE  " EXEC_ID=".
               20  WK-JIKOU-SYORI-ID-U-2
                                        PIC  X(07).
               20  FILLER               PIC  X(09)
                 VALUE  " KIDO_ID=".
               20  WK-KIDOU-ID-U-2      PIC  X(04).
               20  FILLER               PIC  X(01)
                 VALUE  X"0A".
      *----------  メッセージ3行目
             15  WK-MSG-U-3.
               20  FILLER               PIC  X(26)
                 VALUE  "業務エラーが発生しました。".
               20  FILLER               PIC  X(44)
                 VALUE  "詳細は実行状況照会画面で参照してください".
      *---------- エラー種別-S(メッセージ)
           10  WK-MSG-S.
      *----------  メッセージ1行目
             15  WK-MSG-S-1.
               20 WK-MAC-DATE-TIME-S.
                 25  WK-YY-S            PIC  X(04).
                 25  FILLER             PIC  X(01)
                   VALUE  "/".
                 25  WK-MM-S            PIC  X(02).
                 25  FILLER             PIC  X(01)
                   VALUE  "/".
                 25  WK-DD-S            PIC  X(02).
                 25  FILLER             PIC  X(01)
                   VALUE  " ".
                 25  WK-HH-S            PIC  X(02).
                 25  FILLER             PIC  X(01)
                   VALUE  ":".
                 25  WK-MI-S            PIC  X(02).
                 25  FILLER             PIC  X(01)
                   VALUE  ":".
                 25  WK-SS-S            PIC  X(02).
               20  FILLER               PIC  X(01)
                 VALUE  " ".
               20  WK-PGM-ID-S          PIC  X(16).
               20  FILLER               PIC  X(29)
                 VALUE  " Is Ended Abnormally(System).".
               20  FILLER               PIC  X(01)
                 VALUE  X"0A".
      *----------  メッセージ2行目
             15  WK-MSG-S-2.
               20  FILLER               PIC  X(11)
                 VALUE  "RIYO_CMPCD=".
               20  WK-RIYO-CMP-CD-S     PIC  X(04).
               20  FILLER               PIC  X(12)
                 VALUE  " KIJYUN_YMD=".
               20  WK-KJN-YMD-S         PIC  X(08).
               20  FILLER               PIC  X(09)
                 VALUE  " EXEC_ID=".
               20  WK-JIKOU-SYORI-ID-S  PIC  X(07).
               20  FILLER               PIC  X(09)
                 VALUE  " KIDO_ID=".
               20  WK-KIDOU-ID-S        PIC  X(04).
      *---------- 1行目
           10  WK-LINE-1.
               20  FILLER               PIC  X(04)
                 VALUE  "PGM=".
               20  WK-PGM-ID            PIC  X(16).
               20  FILLER               PIC  X(06)
                 VALUE  " PSTN=".
               20  WK-ERR-POS           PIC  X(04).
               20  FILLER               PIC  X(07)
                 VALUE  " RTNCD=".
               20  WK-RTN-CD            PIC  X(03).
      *---------- 2行目
           10  WK-LINE-2.
      *---------- 詳細リターンコードデコードフラグ＝'0'の場合
             15  WK-LINE-2-0.
               20  FILLER               PIC  X(10)
                 VALUE  "DTL_RTNCD=".
               20  WK-DTL-RTNCD-0       PIC  -9(09).
      *---------- 詳細リターンコードデコードフラグ＝'0'以外の場合
             15  WK-LINE-2-1.
               20  FILLER               PIC  X(10)
                 VALUE  "DTL_RTNCD=".
               20  WK-DTL-RTNCD-1       PIC  -9(09).
               20  FILLER               PIC  X(02)
                 VALUE  "（".
               20  WK-FREE-MSG-1        PIC  X(100).
               20  FILLER               PIC  X(02)
                 VALUE  "）".
      *---------- 3行目
           10  WK-LINE-3.
               20  WK-FREE-MSG-3        PIC  X(1900).
      ******************************************************************
      *                サブプログラム  パラメ−タ  エリア
      ******************************************************************
      ******************************************************************
      *                SQL通信, ORACLE通信,ホスト変数定義 エリア
      ******************************************************************
      ******************************************************************
      *                  連絡節
      ******************************************************************
       LINKAGE                SECTION.
       01  PARM-SCZY7020-INOUT.
      *----------  バッチ終了メッセージ出力ルーチンコピー句
         COPY "SCZY7020.cpy".
       01  PARM-SCZY7020-RTNPKG.
         05  PARM-SCZY7020-ARG3.
      *----------  RTNパッケージ
           COPY "CZRTNPACKAGE.cpy".
      ******************************************************************
      *
      *        PROCEDURE      DIVISION
      *
      ******************************************************************
       PROCEDURE              DIVISION
           USING  PARM-SCZY7020-INOUT
                  PARM-SCZY7020-RTNPKG.
      ******************************************************************
      *                      SCZY7020処理
      ******************************************************************
       BASE-RTN               SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  SCZY7020処理  START  *****"
      *-----------------------------------------------------------------
      *---------- メイン処理
           PERFORM  MAIN-RTN
      *---------- 終了処理
           PERFORM  END-RTN.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  SCZY7020処理  END  *****".
      *-----------------------------------------------------------------
       BASE-EXT.
           GOBACK.
      ******************************************************************
      *                      メイン処理
      ******************************************************************
       MAIN-RTN               SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  メイン処理  START  *****"
      *-----------------------------------------------------------------
           INITIALIZE             WK-AREA
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "WK-AREA = [" WK-AREA "]"
      *-----------------------------------------------------------------
           MOVE  ERR-SYUBETU                 TO  WK-ERR-SYUBETU
           MOVE  FUNCTION CURRENT-DATE(1:14) TO  WK-MAC-DATE-TIME
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "WK-ERR-SYUBETU  =["WK-ERR-SYUBETU"]"
      D    DISPLAY  "WK-MAC-DATE-TIME=["WK-MAC-DATE-TIME"]"
      *-----------------------------------------------------------------
           EVALUATE  WK-ERR-SYUBETU
               WHEN  CNS-N
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  ERR-SYUBETU-N処理  START  *****"
      *-----------------------------------------------------------------
               MOVE  WK-MAC-DATE-TIME(1:4)   TO  WK-YY-N
               MOVE  WK-MAC-DATE-TIME(5:2)   TO  WK-MM-N
               MOVE  WK-MAC-DATE-TIME(7:2)   TO  WK-DD-N
               MOVE  WK-MAC-DATE-TIME(9:2)   TO  WK-HH-N
               MOVE  WK-MAC-DATE-TIME(11:2)  TO  WK-MI-N
               MOVE  WK-MAC-DATE-TIME(13:2)  TO  WK-SS-N
               MOVE  PGM-ID                  TO  WK-PGM-ID-N
               MOVE  RIYO-CMP-CD             TO  WK-RIYO-CMP-CD-N
               MOVE  KJN-YMD                 TO  WK-KJN-YMD-N
               MOVE  JIKOU-SYORI-ID          TO  WK-JIKOU-SYORI-ID-N
               MOVE  KIDOU-ID                TO  WK-KIDOU-ID-N
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "WK-MAC-DATE-TIME-N =["WK-MAC-DATE-TIME-N"]"
      D    DISPLAY  "WK-PGM-ID-N        =["WK-PGM-ID-N"]"
      D    DISPLAY  "WK-RIYO-CMP-CD-N   =["WK-RIYO-CMP-CD-N"]"
      D    DISPLAY  "WK-KJN-YMD-N       =["WK-KJN-YMD-N"]"
      D    DISPLAY  "WK-JIKOU-SYORI-ID-N=["WK-JIKOU-SYORI-ID-N"]"
      D    DISPLAY  "WK-KIDOU-ID-N      =["WK-KIDOU-ID-N"]"
      D    DISPLAY  "WK-MSG-N           =["WK-MSG-N"]"
      *-----------------------------------------------------------------
               DISPLAY  WK-MSG-N
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  ERR-SYUBETU-N処理  END  *****"
      *-----------------------------------------------------------------
               WHEN  CNS-U
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  ERR-SYUBETU-U処理  START  *****"
      *-----------------------------------------------------------------
               MOVE  WK-MAC-DATE-TIME(1:4)   TO  WK-YY-U
               MOVE  WK-MAC-DATE-TIME(5:2)   TO  WK-MM-U
               MOVE  WK-MAC-DATE-TIME(7:2)   TO  WK-DD-U
               MOVE  WK-MAC-DATE-TIME(9:2)   TO  WK-HH-U
               MOVE  WK-MAC-DATE-TIME(11:2)  TO  WK-MI-U
               MOVE  WK-MAC-DATE-TIME(13:2)  TO  WK-SS-U
               MOVE  PGM-ID                  TO  WK-PGM-ID-U
               MOVE  RIYO-CMP-CD             TO  WK-RIYO-CMP-CD-U-2
               MOVE  KJN-YMD                 TO  WK-KJN-YMD-U-2
               MOVE  JIKOU-SYORI-ID          TO  WK-JIKOU-SYORI-ID-U-2
               MOVE  KIDOU-ID                TO  WK-KIDOU-ID-U-2
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "WK-MAC-DATE-TIME-N =["WK-MAC-DATE-TIME-U"]"
      D    DISPLAY  "WK-PGM-ID-N        =["WK-PGM-ID-U"]"
      D    DISPLAY  "WK-RIYO-CMP-CD-U-2 =["WK-RIYO-CMP-CD-U-2"]"
      D    DISPLAY  "WK-KJN-YMD-U-2     =["WK-KJN-YMD-U-2"]"
      D    DISPLAY  "WK-JIKOU-SYORI-ID-U-2=["WK-JIKOU-SYORI-ID-U-2"]"
      D    DISPLAY  "WK-KIDOU-ID-U-2    =["WK-KIDOU-ID-U-2"]"
      D    DISPLAY  "WK-MSG-U           =["WK-MSG-U"]"
      *-----------------------------------------------------------------
               DISPLAY  WK-MSG-U
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  ERR-SYUBETU-U処理  END  *****"
      *-----------------------------------------------------------------
               WHEN  CNS-S
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  ERR-SYUBETU-S処理  START  *****"
      *-----------------------------------------------------------------
               MOVE  WK-MAC-DATE-TIME(1:4)   TO  WK-YY-S
               MOVE  WK-MAC-DATE-TIME(5:2)   TO  WK-MM-S
               MOVE  WK-MAC-DATE-TIME(7:2)   TO  WK-DD-S
               MOVE  WK-MAC-DATE-TIME(9:2)   TO  WK-HH-S
               MOVE  WK-MAC-DATE-TIME(11:2)  TO  WK-MI-S
               MOVE  WK-MAC-DATE-TIME(13:2)  TO  WK-SS-S
               MOVE  PGM-ID                  TO  WK-PGM-ID-S
               MOVE  RIYO-CMP-CD             TO  WK-RIYO-CMP-CD-S
               MOVE  KJN-YMD                 TO  WK-KJN-YMD-S
               MOVE  JIKOU-SYORI-ID          TO  WK-JIKOU-SYORI-ID-S
               MOVE  KIDOU-ID                TO  WK-KIDOU-ID-S
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "WK-MAC-DATE-TIME-S =["WK-MAC-DATE-TIME-S"]"
      D    DISPLAY  "WK-PGM-ID-S        =["WK-PGM-ID-S"]"
      D    DISPLAY  "WK-RIYO-CMP-CD-S   =["WK-RIYO-CMP-CD-S"]"
      D    DISPLAY  "WK-KJN-YMD-S       =["WK-KJN-YMD-S"]"
      D    DISPLAY  "WK-JIKOU-SYORI-ID-S=["WK-JIKOU-SYORI-ID-S"]"
      D    DISPLAY  "WK-KIDOU-ID-S      =["WK-KIDOU-ID-S"]"
      D    DISPLAY  "WK-MSG-S           =["WK-MSG-S"]"
      *-----------------------------------------------------------------
               DISPLAY  WK-MSG-S
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  ERR-SYUBETU-S 処理  END  *****"
      *-----------------------------------------------------------------
               MOVE  COUNTER            OF  PARM-SCZY7020-RTNPKG
                 TO  WK-COUNTER
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "COUNTER=["WK-COUNTER"]"
      *-----------------------------------------------------------------
               PERFORM  UNTIL WK-COUNTER = 0
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  1行目処理  START  *****"
      *-----------------------------------------------------------------
               MOVE  ERR-PGM-ID
                 OF  PARM-SCZY7020-ARG3(WK-COUNTER)
                 TO  WK-PGM-ID
               MOVE  ERR-POS
                 OF  PARM-SCZY7020-ARG3(WK-COUNTER)
                 TO  WK-ERR-POS
               MOVE  RTN-CD
                 OF  PARM-SCZY7020-ARG3(WK-COUNTER)
                 TO  WK-RTN-CD
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "WK-PGM-ID  =["WK-PGM-ID"]"
      D    DISPLAY  "WK-ERR-POS =["WK-ERR-POS"]"
      D    DISPLAY  "WK-RTN-CD  =["WK-RTN-CD"]"
      D    DISPLAY  "WK-LINE-1  =["WK-LINE-1"]"
      *-----------------------------------------------------------------
               DISPLAY  WK-LINE-1
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  1行目処理  END  *****"
      D    DISPLAY  "*****  2行目処理  START  *****"
      *-----------------------------------------------------------------
                 MOVE  DETAIL-RTN-CD-FLG
                   OF  PARM-SCZY7020-ARG3(WK-COUNTER)
                   TO  WK-DETAIL-RTN-CD-FLG
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "WK-DETAIL-RTN-CD-FLG=["WK-DETAIL-RTN-CD-FLG"]"
      D    DISPLAY  "DETAIL-RTN-CD       =["DETAIL-RTN-CD
      D                          OF  PARM-SCZY7020-ARG3(WK-COUNTER)"]"
      *-----------------------------------------------------------------
           IF  DETAIL-RTN-CD          OF  PARM-SCZY7020-ARG3(WK-COUNTER)
               NOT = 0
           THEN
             IF  WK-DETAIL-RTN-CD-FLG = 1
             THEN
                 MOVE  DETAIL-RTN-CD
                   OF  PARM-SCZY7020-ARG3(WK-COUNTER)
                   TO  WK-DTL-RTNCD-1
                 MOVE  FREE-MSG
                   OF  PARM-SCZY7020-ARG3(WK-COUNTER)(1901:100)
                   TO  WK-FREE-MSG-1
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "WK-FREE-MSG-1=["WK-FREE-MSG-1"]"
      D    DISPLAY  "WK-DTL-RTNCD =["WK-DTL-RTNCD-1"]"
      D    DISPLAY  "WK-LINE-2    =["WK-LINE-2-1"]"
      *-----------------------------------------------------------------
                 DISPLAY  WK-LINE-2-1
             ELSE
                 MOVE  DETAIL-RTN-CD
                   OF  PARM-SCZY7020-ARG3(WK-COUNTER)
                   TO  WK-DTL-RTNCD-0
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "WK-DTL-RTNCD =["WK-DTL-RTNCD-0"]"
      D    DISPLAY  "WK-LINE-2    =["WK-LINE-2-0"]"
      *-----------------------------------------------------------------
                 DISPLAY  WK-LINE-2-0
             END-IF
           ELSE
               CONTINUE
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  2行目出力  しない  *****"
      *-----------------------------------------------------------------
           END-IF
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  2行目処理  END  *****"
      D    DISPLAY  "*****  3行目処理  START  *****"
      *-----------------------------------------------------------------
               MOVE  FREE-MSG
                 OF  PARM-SCZY7020-ARG3(WK-COUNTER)(1:1900)
                 TO  WK-FREE-MSG-3
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "WK-FREE-MSG=["WK-FREE-MSG-3"]"
      *-----------------------------------------------------------------
                 IF  WK-FREE-MSG-3 NOT  =  SPACE  THEN
                     DISPLAY  WK-LINE-3
                 ELSE
                   CONTINUE
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  3行目出力  しない  *****"
      *-----------------------------------------------------------------
                 END-IF
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  3行目処理  END  *****"
      *-----------------------------------------------------------------
                   COMPUTE  WK-COUNTER = WK-COUNTER - 1
                   END-PERFORM
               WHEN  OTHER
                   CONTINUE
           END-EVALUATE.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  MAIN-RTN  END  *****".
      *-----------------------------------------------------------------
       MAIN-EXT.
           EXIT.
      ******************************************************************
      *        後処理
      ******************************************************************
       END-RTN                SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****   後処理  START  *****"
      *-----------------------------------------------------------------
           CONTINUE.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****   後処理  END  *****".
      *-----------------------------------------------------------------
       END-EXT.
           EXIT.
