      ******************************************************************
      * $Header: $
      ******************************************************************
      *
      *  システムＩＤ   : T-STAR
      *  システム名称   : 次期TSTARシステム
      *  ＩＤ           : SCZY7090.pco
      *  名称           : バッチ引数一括取得ルーチン
      *  処理概要       : バッチの引数を一括取得処理を行う。
      *  引数           :IN   ARG1.引数個数
      *                  OUT  ARG2.引数内容
      *                  OUT  ARG3.RTNパッケージ
      *  改訂履歴
      *  年月日     区分 所属       担当者  内容
      *  ---------- ---- ---------- ------  ----------------------------
      *  20070911   新規 SCS        李傑    新規作成
      ******************************************************************
       IDENTIFICATION         DIVISION.
       PROGRAM-ID.    SCZY7090.
       AUTHOR.        Nomura.
       DATE-WRITTEN.  07-09-11.
       DATE-COMPILED.
      ******************************************************************
      *
      *                 環  境  部
      *
      ******************************************************************
       ENVIRONMENT            DIVISION.
       CONFIGURATION          SECTION.
      *SOURCE-COMPUTER.       L1000  WITH DEBUGGING MODE.
       SOURCE-COMPUTER.       L1000.
       OBJECT-COMPUTER.       H9000V.
      ******************************************************************
      *
      *                 デ  ー  タ  部
      *
      ******************************************************************
       DATA                   DIVISION.
      ******************************************************************
      *                 作業場所節
      ******************************************************************
       WORKING-STORAGE        SECTION.
       01  IDNAME                  PIC  X(80)  VALUE
       "$Id: $".
      ******************************************************************
      *                コンスタント  エリア
      ******************************************************************
       01  CONSTANT-AREA.
         05  CNS-PGM-ID            PIC  X(08)  VALUE  "SCZY7090".
      *----------  リターンコード: 正常終了
         05  CNS-RET-CD-NORMAL     PIC  X(03)  VALUE  "000".
      *----------  リターンコード: パラメータエラー
         05  CNS-PARAMETER-ERR     PIC  X(03)  VALUE  "800".
      *----------  リターンコード: 起動引数取得エラー
         05  CNS-KIDO-PARA-ERR     PIC  X(03)  VALUE  "810".
      ******************************************************************
      *                ワ−クエリア
      ******************************************************************
       01  WORK-AREA.
      *----------  引数個数
         05  WK-ARG-NUM            PIC  9(02).
      *----------  引数内容
         05  WK-ARG-STRING         PIC  X(512).
      *----------  引数内容
         05  WK-COUNTER            PIC  9(02).
      ******************************************************************
      *                サブプログラム  パラメ−タ  エリア
      ******************************************************************
      *----------  SCZY1170 リターンパッケージセットルーチン
       01  PARM-SCZY1170-INOUT.
      *----------  リターンパッケージセットルーチンコピー句
           COPY  "SCZY1170.cpy".
       01  PARM-SCZY1170-RTNPKG.
         05  PARM-SCZY1170-ARG2.
      *----------  RTNパッケージ
           COPY  "CZRTNPACKAGE.cpy".
      ******************************************************************
      *                連絡節
      ******************************************************************
       LINKAGE                SECTION.
       01  PARM-SCZY7090-INOUT.
      *----------  バッチ引数一括取得ルーチンコピー句
           COPY  "SCZY7090.cpy".
       01  PARM-SCZY7090-RTNPKG.
         05  PARM-SCZY7090-ARG3.
      *----------  RTNパッケージ
           COPY "CZRTNPACKAGE.cpy".
      ******************************************************************
      *
      *                 手続き部
      *
      ******************************************************************
       PROCEDURE              DIVISION
           USING  PARM-SCZY7090-INOUT
                  PARM-SCZY7090-RTNPKG.
      ******************************************************************
      *    バッチ引数一括取得ルーチン
      ******************************************************************
       SCZY7090-RTN           SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  SCZY7090処理  START  *****"
      *-----------------------------------------------------------------
      *----------  初期処理
           PERFORM  INIT-RTN
      *----------  メイン処理
           PERFORM  MAIN-RTN
      *----------  終了処理
           PERFORM  END-RTN.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  SCZY7090処理  END  *****".
      *-----------------------------------------------------------------
       SCZY7090-EXT.
           GOBACK.
      ******************************************************************
      *    前処理
      ******************************************************************
       INIT-RTN               SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  前処理  START  ***"
      *-----------------------------------------------------------------
      *----------  初期化
           INITIALIZE  WORK-AREA
                       PARM-SCZY7090-ARG2
                       PARM-SCZY7090-ARG3
                       PARM-SCZY1170-INOUT
                       PARM-SCZY1170-RTNPKG
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "WORK-AREA           = [" WORK-AREA          "]"
      D    DISPLAY  "PARM-SCZY7090-ARG2  = [" PARM-SCZY7090-ARG2 "]"
      D    DISPLAY  "PARM-SCZY7090-ARG3  = [" PARM-SCZY7090-ARG3 "]"
      D    DISPLAY  "PARM-SCZY1170-RTNPKG = [" PARM-SCZY1170-RTNPKG "]"
      D    DISPLAY  "ARG1.引数個数 =["ARG-NUM
      D                                 OF  PARM-SCZY7090-INOUT  "]"
      *-----------------------------------------------------------------
      *----------  引数個数チェック
           MOVE  ARG-NUM                OF  PARM-SCZY7090-ARG1
                                        TO  WK-ARG-NUM
           IF   WK-ARG-NUM  >  20
           THEN
      *---------- RTNパッケージ詳細
               MOVE  CNS-PGM-ID         TO  ERR-PGM-ID
                                        OF  PARM-SCZY1170-INOUT
               MOVE  "0001"             TO  ERR-POS
                                        OF  PARM-SCZY1170-INOUT
               MOVE  CNS-PARAMETER-ERR  TO  RTN-CD
                                        OF  PARM-SCZY1170-INOUT
               MOVE  0                  TO  DETAIL-RTN-CD
                                        OF  PARM-SCZY1170-INOUT
               MOVE  "0"                TO  DETAIL-RTN-CD-FLG
                                        OF  PARM-SCZY1170-INOUT
               MOVE  SPACE              TO  FREE-MSG
                                        OF  PARM-SCZY1170-INOUT
               PERFORM  ERR-RTN
           END-IF.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  前処理  END  ***".
      *-----------------------------------------------------------------
       INIT-EXT.
           EXIT.
      ******************************************************************
      *    主処理
      ******************************************************************
       MAIN-RTN               SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  主処理  START  *****"
      *-----------------------------------------------------------------
           MOVE  1  TO  WK-COUNTER
           PERFORM  PARM-GET-RTN  UNTIL  WK-COUNTER  >  WK-ARG-NUM.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  主処理  END  *****".
      *-----------------------------------------------------------------
       MAIN-EXT.
           EXIT.
      ******************************************************************
      *    バッチ引数取得処理
      ******************************************************************
       PARM-GET-RTN           SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****   バッチ引数取得処理  START  *****"
      *-----------------------------------------------------------------
           MOVE  SPACE  TO  WK-ARG-STRING
           DISPLAY WK-COUNTER
             UPON  ARGUMENT-NUMBER
           ACCEPT  WK-ARG-STRING
             FROM  ARGUMENT-VALUE
           MOVE  WK-ARG-STRING     TO  ARG-STRING
             OF  PARM-SCZY7090-ARG2(WK-COUNTER)
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "バッチ引数("WK-COUNTER") = ["  ARG-STRING
      D          OF  PARM-SCZY7090-ARG2(WK-COUNTER) "]"
      *-----------------------------------------------------------------
           COMPUTE  WK-COUNTER  =  WK-COUNTER  +  1 .
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****   バッチ引数取得処理  END  *****".
      *-----------------------------------------------------------------
       PARM-GET-EXT.
           EXIT.
      ******************************************************************
      *    後処理
      ******************************************************************
       END-RTN                SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  後処理  START  *****"
      *D    DISPLAY  "引数内容 = ["PARM-SCZY7090-ARG2"]"
      *-----------------------------------------------------------------
           MOVE  "000"                  TO  RTN-CD
                                        OF  PARM-SCZY1170-INOUT
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "RTN-CD OF  PARM-SCZY1170-INOUT = [" RTN-CD
      D                                 OF  PARM-SCZY1170-INOUT "]"
      D    DISPLAY  "*****  CALL  SCZY1170  START  *****"
      *-----------------------------------------------------------------
           CALL  "SCZY1170"             USING  PARM-SCZY1170-INOUT
                                               PARM-SCZY1170-RTNPKG
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  CALL  SCZY1170  END  *****"
      *-----------------------------------------------------------------
           MOVE  PARM-SCZY1170-RTNPKG   TO  PARM-SCZY7090-ARG3.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "PARM-SCZY7090-ARG3=["PARM-SCZY7090-ARG3"]"
      D    DISPLAY  "*****  後処理  END  *****".
      *-----------------------------------------------------------------
       END-EXT.
           EXIT.
      ******************************************************************
      *    エラー処理
      ******************************************************************
       ERR-RTN                SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  ERR-RTN  START  *****"
      D    DISPLAY  "ERR-PGM-ID        =["
      D                     ERR-PGM-ID  OF  PARM-SCZY1170-INOUT"]"
      D    DISPLAY  "ERR-POS           =["
      D                     ERR-POS     OF  PARM-SCZY1170-INOUT"]"
      D    DISPLAY  "RTN-CD            =["
      D                     RTN-CD      OF  PARM-SCZY1170-INOUT"]"
      D    DISPLAY  "DETAIL-RTN-CD     =["DETAIL-RTN-CD
      D                                 OF  PARM-SCZY1170-INOUT"]"
      D    DISPLAY  "DETAIL-RTN-CD-FLG =["
      D              DETAIL-RTN-CD-FLG  OF  PARM-SCZY1170-INOUT"]"
      D    DISPLAY  "FREE-MSG          =["
      D                     FREE-MSG    OF  PARM-SCZY1170-INOUT"]"
      D    DISPLAY  "*****  CALL  SCZY1170  START  *****"
      *-----------------------------------------------------------------
           CALL  "SCZY1170"             USING  PARM-SCZY1170-INOUT
                                               PARM-SCZY1170-RTNPKG
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  CALL  SCZY1170  END  *****"
      *-----------------------------------------------------------------
           MOVE  PARM-SCZY1170-RTNPKG   TO  PARM-SCZY7090-ARG3.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "PARM-SCZY7090-ARG3=["PARM-SCZY7090-ARG3"]"
      D    DISPLAY  "*****  ERR-RTN  END  *****".
      *-----------------------------------------------------------------
       ERR-EXT.
           GOBACK.
      ******************************************************************
