      ******************************************************************
      * $Header: $
      ******************************************************************
      *
      *  システムＩＤ   : T-STAR
      *  システム名称   : 次期TSTARシステム
      *  ＩＤ           : SCZY7110.pco
      *  名称           : バッチ用監査ログＩＮＳルーチン
      *  処理概要       : 監査ログINSルーチンをCALLして
      *                   監査ログの登録を行って
      *                   共通系DBコミットする
      *  引数           :IN   ARG1.AIFA・共通ヘッダ項目
      *                  IN   ARG2
      *                  IN   ARG3
      *                  OUT  ARG4.RTNパッケージ
      *  改訂履歴
      *  年月日     区分 所属       担当者  内容
      *  ---------- ---- ---------- ------  ----------------------------
      *  20070820   新規 SCS        蒋  磊  新規作成
      *  20101108   修正 SCS        胡  偉  テーマ3079対応
      ******************************************************************
       IDENTIFICATION         DIVISION.
       PROGRAM-ID.    SCZY7110.
       AUTHOR.        Nomura.
       DATE-WRITTEN.  08-10-14.
       DATE-COMPILED.
      ******************************************************************
      *
      *        環  境  部
      *
      ******************************************************************
       ENVIRONMENT            DIVISION.
       CONFIGURATION          SECTION.
       SOURCE-COMPUTER.       L1000V.
      *SOURCE-COMPUTER.       L1000V  WITH  DEBUGGING  MODE.
       OBJECT-COMPUTER.       RP5470.
      ******************************************************************
      *
      *        デ  ー  タ  部
      *
      ******************************************************************
       DATA                   DIVISION.
      ******************************************************************
      *        作業場所節
      ******************************************************************
       WORKING-STORAGE        SECTION.
       01  IDNAME                  PIC  X(80)  VALUE
       "$Id: $".
      ******************************************************************
      *                 コンスタント  エリア
      ******************************************************************
       01  CONST-AREA.
         05  CNS-PGM-ID            PIC  X(08)  VALUE  "SCZY7110".
      *----------  リターンコード: システムエラー（Oracle）
         05  CNS-RET-CD-ORAERR     PIC  X(03)  VALUE "900".
      ******************************************************************
      *                ＳＱＬ通信領域
      ******************************************************************
       01  PARM-SCZY1100-INOUT1.
         05  PARM-SCZY1100-ARG1.
           COPY "WOMCMAIFA.CPY".
           COPY "CZCOMMONHEADER.cpy".
       01  PARM-SCZY1100-INOUT2.
           COPY "SCZY1100.cpy".
       01  PARM-SCZY1100-RTNPKG.
      *----------  RTNパッケージ
         05  PARM-SCZY1100-ARG4.
           COPY "CZRTNPACKAGE.cpy".
      *----------  SCZY1170 リターンパッケージセットルーチン
       01  PARM-SCZY1170-INOUT.
      *----------  リターンパッケージセットルーチンコピー句
           COPY  "SCZY1170.cpy".
       01  PARM-SCZY1170-RTNPKG.
         05  PARM-SCZY1170-ARG2.
      *----------  RTNパッケージ
           COPY  "CZRTNPACKAGE.cpy".
      ******************************************************************
      *                 連絡節
      ******************************************************************
       LINKAGE                SECTION.
       01  PARM-SCZY7110-AIFA-HEADER.
         05  PARM-SCZY7110-ARG1.
           COPY "WOMCMAIFA.CPY".
           COPY "CZCOMMONHEADER.cpy".
       01  PARM-SCZY7110-INOUT.
      *----------  監査ログＩＮＳルーチンコピー句
           COPY "SCZY7110.cpy".
       01  PARM-SCZY7110-RTNPKG.
      *----------  RTNパッケージ
         05  PARM-SCZY7110-ARG4.
           COPY "CZRTNPACKAGE.cpy".
      ******************************************************************
      *
      *        PROCEDURE      DIVISION
      *
      ******************************************************************
       PROCEDURE              DIVISION
            USING   PARM-SCZY7110-AIFA-HEADER
                    PARM-SCZY7110-INOUT
                    PARM-SCZY7110-RTNPKG.
      ******************************************************************
      *             SCZY1100 監査ログＩＮＳルーチン
      ******************************************************************
       BASE-RTN               SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  バッチ用監査ログＩＮＳルーチン  START  ***"
      *-----------------------------------------------------------------
      *----------  初期化処理
           PERFORM  INIT-RTN
      *----------  主処理
           PERFORM  MAIN-RTN
      *----------  後処理
           PERFORM  END-RTN.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  バッチ用監査ログＩＮＳルーチン  END  ***".
      *-----------------------------------------------------------------
       BASE-EXT.
           GOBACK.
      ******************************************************************
      *                        初期化処理
      ******************************************************************
       INIT-RTN               SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  初期化処理  START  ***"
      *-----------------------------------------------------------------
      *----------  初期化
           INITIALIZE  PARM-SCZY1100-INOUT1
                       PARM-SCZY1100-INOUT2
                       PARM-SCZY1100-RTNPKG
                       PARM-SCZY7110-RTNPKG
                       PARM-SCZY1170-INOUT.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "初期化処理  NORMAL"
      D    DISPLAY  "***  初期化処理  END  ***".
      *-----------------------------------------------------------------
       INIT-EXT.
           EXIT.
      ******************************************************************
      *                          主処理
      ******************************************************************
       MAIN-RTN               SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  主処理  START  ***"
      *-----------------------------------------------------------------
           MOVE  PARM-SCZY7110-AIFA-HEADER   TO  PARM-SCZY1100-INOUT1
           MOVE  PARM-SCZY7110-INOUT         TO  PARM-SCZY1100-INOUT2
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "CALL  SCZY1100 ..."
      *-----------------------------------------------------------------
           CALL  "SCZY1100"        USING  PARM-SCZY1100-INOUT1
                                          PARM-SCZY1100-INOUT2
                                          PARM-SCZY1100-RTNPKG
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "RTN-CD("  COUNTER       OF PARM-SCZY1100-RTNPKG
      D      ") OF SCZY1100=["  RTN-CD       OF
      D      PARM-SCZY1100-RTNPKG(COUNTER    OF PARM-SCZY1100-RTNPKG)"]"
      *-----------------------------------------------------------------
           EVALUATE  RTN-CD   OF  PARM-SCZY1100-RTNPKG
                                   (COUNTER  OF  PARM-SCZY1100-RTNPKG)
             WHEN  "000"
               CONTINUE
             WHEN  "901"
               MOVE  CNS-PGM-ID
                 TO  ERR-PGM-ID         OF  PARM-SCZY1170-ARG1
               MOVE  "0003"             TO  ERR-POS
                                        OF  PARM-SCZY1170-ARG1
               MOVE  "901"              TO  RTN-CD
                                        OF  PARM-SCZY1170-ARG1
               MOVE  "0"                TO  DETAIL-RTN-CD-FLG
                                        OF  PARM-SCZY1170-ARG1
               MOVE  PARM-SCZY1100-RTNPKG
                                        TO  PARM-SCZY1170-RTNPKG
               PERFORM  ERR-RTN
             WHEN  OTHER
               MOVE  CNS-PGM-ID
                 TO  ERR-PGM-ID         OF  PARM-SCZY1170-ARG1
               MOVE  "0004"             TO  ERR-POS
                                        OF  PARM-SCZY1170-ARG1
               MOVE  CNS-RET-CD-ORAERR  TO  RTN-CD
                                        OF  PARM-SCZY1170-ARG1
               MOVE  "0"                TO  DETAIL-RTN-CD-FLG
                                        OF  PARM-SCZY1170-ARG1
               MOVE  PARM-SCZY1100-RTNPKG
                                        TO  PARM-SCZY1170-RTNPKG
               PERFORM  ERR-RTN
           END-EVALUATE
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  主処理  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       MAIN-EXT.
           EXIT.
      ******************************************************************
      *                        後処理
      ******************************************************************
       END-RTN                SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***   後処理  START  ***"
      *-----------------------------------------------------------------
           MOVE  "000"                       TO  RTN-CD
                                             OF  PARM-SCZY1170-ARG1
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "RTN-CD                  OF  PARM-SCZY1170-ARG1:["
      D              RTN-CD                  OF  PARM-SCZY1170-ARG1"]"
      *-----------------------------------------------------------------
           CALL     "SCZY1170"            USING  PARM-SCZY1170-INOUT
                                                 PARM-SCZY1170-RTNPKG
             MOVE   PARM-SCZY1170-ARG2       TO  PARM-SCZY7110-ARG4.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "PARM-SCZY7110-ARG4:["PARM-SCZY7110-ARG4"]"
      D    DISPLAY  "***   後処理  END  ***".
      *-----------------------------------------------------------------
       END-EXT.
           EXIT.
      ******************************************************************
      *                 エラー処理
      ******************************************************************
       ERR-RTN                SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  ERR-RTN  START  ***"
      *-----------------------------------------------------------------
           CALL     "SCZY1170"            USING  PARM-SCZY1170-INOUT
                                                 PARM-SCZY1170-RTNPKG
           MOVE   PARM-SCZY1170-ARG2         TO  PARM-SCZY7110-ARG4.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "PARM-SCZY7110-ARG4:["PARM-SCZY7110-ARG4"]"
      D    DISPLAY  "***  ERR-RTN  END  ***".
      *-----------------------------------------------------------------
       ERR-EXT.
           GOBACK.
