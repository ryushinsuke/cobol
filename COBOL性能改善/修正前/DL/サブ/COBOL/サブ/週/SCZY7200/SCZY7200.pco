      ******************************************************************
      * $Header: $
      ******************************************************************
      *
      *  システムＩＤ   : T-STAR
      *  システム名称   : 次期TSTARシステム
      *  ＩＤ           : SCZY7200.pco
      *  名称           : 処理対象実行状況管理明細ＧＥＴルーチン
      *  処理概要       : 実行待ちの実行状況管理明細データを取得して、
      *                   返す。
      *  引数           :IN   ARG1.利用会社コード
      *                  IN   ARG1.処理実行日
      *                  IN   ARG1.起動ID
      *                  IN   ARG1.帳票ID
      *                  OUT  ARG2.取得件数
      *                  OUT  ARG2.実行状況管理明細データ
      *                  OUT  ARG3.RTNパッケージ
      *  改訂履歴
      *  年月日     区分 所属       担当者  内容
      *  ---------- ---- ---------- ------  ----------------------------
      *  20070828   新規 SCS        李傑    新規作成
      ******************************************************************
       IDENTIFICATION         DIVISION.
       PROGRAM-ID.    SCZY7200.
       AUTHOR.        Nomura.
       DATE-WRITTEN.  07-08-28.
       DATE-COMPILED.
      ******************************************************************
      *
      *                 環  境  部
      *
      ******************************************************************
       ENVIRONMENT            DIVISION.
       CONFIGURATION          SECTION.
      *SOURCE-COMPUTER.       L1000  WITH DEBUGGING MODE.
       SOURCE-COMPUTER.       L1000.
       OBJECT-COMPUTER.       H9000V.
      ******************************************************************
      *
      *                 デ  ー  タ  部
      *
      ******************************************************************
       DATA                   DIVISION.
      ******************************************************************
      *                 作業場所節
      ******************************************************************
       WORKING-STORAGE        SECTION.
       01  IDNAME                  PIC  X(80)  VALUE
       "$Id: $".
      ******************************************************************
      *                コンスタント  エリア
      ******************************************************************
       01  CONSTANT-AREA.
         05  CNS-PGM-ID            PIC  X(08)  VALUE  "SCZY7200".
         05  CNS-TBL-TKDYAA35      PIC  X(08)  VALUE  "TKDYAA35".
      *----------  リターンコード: 正常終了
         05  CNS-RET-CD-NORMAL     PIC  X(03)  VALUE  "000".
      *----------  リターンコード: システムエラー（Oracle）
         05  CNS-ORACLE-ERR        PIC  X(03)  VALUE  "900".
      *----------  リターンコード: システムエラー（Oracle以外）
         05  CNS-SYSTEM-ERR        PIC  X(03)  VALUE  "901".
      *----------  リターンコード: パラメータエラー
         05  CNS-PARAMETER-ERR     PIC  X(03)  VALUE  "800".
         05  CNS-SQL-NORMAL        PIC  S9(04)  COMP-3 VALUE +0000.
         05  CNS-SQL-NOTFOUND      PIC  S9(04)  COMP-3 VALUE +1403.
      ******************************************************************
      *                フラグエリア
      ******************************************************************
       01  FLAG-AREA.
      *----------  カーソルオープンフラグ
         05  FLG-CR1               PIC  9(01).
      ******************************************************************
      *                ワ−クエリア
      ******************************************************************
       01  WORK-AREA.
      *----------  利用会社コード
         05  WK-RIYO-CMP-CD        PIC  X(04).
      *----------  処理実行日
         05  WK-SYORI-YMD          PIC  X(08).
      *----------  起動ID
         05  WK-KIDOU-ID           PIC  X(04).
      *----------  帳票ID
         05  WK-REP-ID             PIC  X(08).
      *---------- 件数COUNT
         05  WK-KENSU-CNT          PIC  9(05).
      *----------  編集後テーブルID
         05  WK-HENSYUGO-TBL-ID    PIC  X(13).
      *----------  SQL条件
         05  WK-SQLSTMT            PIC  X(500).
      *----------  ワークカーソルデータ終了フラグ
         05  WK-FETCH-END-FLG      PIC  X(01).
      *----------  SQL文字POINT
         05  WK-SQL-POINTER        PIC  9(09)  COMP.
      ******************************************************************
      *                サブプログラム  パラメ−タ  エリア
      ******************************************************************
      *----------  共通RTNパッケージ
       01  PARM-KYOTU-RTNPKG.
         05  PARM-RTNPKG.
           COPY "CZRTNPACKAGE.cpy".
      *
      *----------  SCZY1160 テーブルID取得ルーチン
       01  PARM-SCZY1160-INOUT.
      *----------  テーブルID取得ルーチンコピー句
           COPY "SCZY1160.cpy".
      *
      *----------  SCZY1170 リターンパッケージセットルーチン
       01  PARM-SCZY1170-INOUT.
      *----------  リターンパッケージセットルーチンコピー句
           COPY  "SCZY1170.cpy".
       01  PARM-SCZY1170-RTNPKG.
         05  PARM-SCZY1170-ARG2.
      *----------  RTNパッケージ
           COPY  "CZRTNPACKAGE.cpy".
      ******************************************************************
      *                SQL通信, ORACLE通信,ホスト変数定義 エリア
      ******************************************************************
           EXEC SQL INCLUDE  SQLCA             END-EXEC.
           EXEC SQL BEGIN DECLARE  SECTION     END-EXEC.
       01  ORA-WORKING-ITEMS.
      *----------  SQL条件ワーク
         05  SQLSTMT1              PIC  X(400)  VARYING.
      *----------  実行状況管理明細データ
         05  HST-JIKO-DATA.
      *----------  ファンドコード
           10  HST-FUND-CD         PIC  X(12).
      *----------  ポートフォリオコード
           10  HST-PTF-CD          PIC  X(04).
      *----------  評価系列コード
           10  HST-HYK-KRTU-CD     PIC  X(04).
      *----------  帳票ID
           10  HST-REP-ID          PIC  X(08).
      *----------  帳票任意コード
           10  HST-REP-ANY-CD      PIC  X(50).
      *----------  基準日From
           10  HST-KJN-YMD-FROM    PIC  9(08).
      *----------  基準日To
           10  HST-KJN-YMD-TO      PIC  9(08).
      *----------  業務タスクID
           10  HST-GYOMU-TASK-ID   PIC  X(07).
      *----------  明細単位実行ステータス
           10  HST-JIKOU-STS       PIC  X(03).
      *----------  datファイル名
           10  HST-DAT-FILE-NAME   PIC  X(100).
           EXEC SQL END  DECLARE   SECTION     END-EXEC.
      ******************************************************************
      *                連絡節
      ******************************************************************
       LINKAGE                SECTION.
       01  PARM-SCZY7200-INOUT.
           COPY  "SCZY7200.cpy".
       01  PARM-SCZY7200-RTNPKG.
         05  PARM-SCZY7200-ARG3.
      *----------  RTNパッケージ
           COPY "CZRTNPACKAGE.cpy".
      ******************************************************************
      *
      *                 手続き部
      *
      ******************************************************************
       PROCEDURE              DIVISION
           USING  PARM-SCZY7200-INOUT
                  PARM-SCZY7200-RTNPKG.
      ******************************************************************
      *    処理対象実行状況管理明細ＧＥＴルーチン
      ******************************************************************
       SCZY7200-RTN           SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  SCZY7200処理  START  *****"
      *-----------------------------------------------------------------
      *----------  初期処理
           PERFORM  INIT-RTN
      *----------  メイン処理
           PERFORM  MAIN-RTN
      *----------  終了処理
           PERFORM  END-RTN.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  SCZY7200処理  END  *****".
      *-----------------------------------------------------------------
       SCZY7200-EXT.
           GOBACK.
      ******************************************************************
      *    前処理
      ******************************************************************
       INIT-RTN               SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  前処理  START  ***"
      *-----------------------------------------------------------------
      *----------  初期化
           INITIALIZE  WORK-AREA
                       FLAG-AREA
                       PARM-SCZY7200-ARG2
                       PARM-SCZY7200-ARG3
                       ORA-WORKING-ITEMS
                       PARM-SCZY1160-INOUT
                       PARM-SCZY1170-INOUT
                       PARM-SCZY1170-RTNPKG
                       PARM-KYOTU-RTNPKG
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "WORK-AREA           = [" WORK-AREA            "]"
      *D    DISPLAY  "PARM-SCZY7200-ARG2  = [" PARM-SCZY7200-ARG2   "]"
      D    DISPLAY  "PARM-SCZY7200-ARG3  = [" PARM-SCZY7200-ARG3   "]"
      D    DISPLAY  "ARG1.利用会社コード =["RIYO-CMP-CD
      D                                     OF  PARM-SCZY7200-INOUT"]"
      *-----------------------------------------------------------------
           MOVE  0  TO  WK-KENSU-CNT
      *----------  利用会社コード
           IF  RIYO-CMP-CD  OF  PARM-SCZY7200-ARG1  =  SPACE
           THEN
      *---------- RTNパッケージ詳細
               MOVE  CNS-PGM-ID         TO  ERR-PGM-ID
                                        OF  PARM-SCZY1170-INOUT
               MOVE  "0001"             TO  ERR-POS
                                        OF  PARM-SCZY1170-INOUT
               MOVE  CNS-PARAMETER-ERR  TO  RTN-CD
                                        OF  PARM-SCZY1170-INOUT
               MOVE  0                  TO  DETAIL-RTN-CD
                                        OF  PARM-SCZY1170-INOUT
               MOVE  "0"                TO  DETAIL-RTN-CD-FLG
                                        OF  PARM-SCZY1170-INOUT
               MOVE  SPACE              TO  FREE-MSG
                                        OF  PARM-SCZY1170-INOUT
               PERFORM  ERR-RTN
           END-IF
           MOVE  RIYO-CMP-CD            OF  PARM-SCZY7200-ARG1
                                        TO  WK-RIYO-CMP-CD
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "ARG1. 処理実行日 =["SYORI-YMD
      D                                 OF  PARM-SCZY7200-INOUT"]"
      *-----------------------------------------------------------------
      *----------  処理実行日
           IF  SYORI-YMD  OF  PARM-SCZY7200-ARG1  =  SPACE
           THEN
      *---------- RTNパッケージ詳細
               MOVE  CNS-PGM-ID         TO  ERR-PGM-ID
                                        OF  PARM-SCZY1170-INOUT
               MOVE  "0002"             TO  ERR-POS
                                        OF  PARM-SCZY1170-INOUT
               MOVE  CNS-PARAMETER-ERR  TO  RTN-CD
                                        OF  PARM-SCZY1170-INOUT
               MOVE  0                  TO  DETAIL-RTN-CD
                                        OF  PARM-SCZY1170-INOUT
               MOVE  "0"                TO  DETAIL-RTN-CD-FLG
                                        OF  PARM-SCZY1170-INOUT
               MOVE  SPACE              TO  FREE-MSG
                                        OF  PARM-SCZY1170-INOUT
               PERFORM  ERR-RTN
           END-IF
           MOVE  SYORI-YMD              OF  PARM-SCZY7200-ARG1
                                        TO  WK-SYORI-YMD
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "ARG1.起動ID =["KIDOU-ID
      D                                 OF  PARM-SCZY7200-INOUT"]"
      *-----------------------------------------------------------------
      *----------  起動ID
           IF  KIDOU-ID  OF  PARM-SCZY7200-ARG1  =  SPACE
           THEN
      *---------- RTNパッケージ詳細
               MOVE  CNS-PGM-ID         TO  ERR-PGM-ID
                                        OF  PARM-SCZY1170-INOUT
               MOVE  "0003"             TO  ERR-POS
                                        OF  PARM-SCZY1170-INOUT
               MOVE  CNS-PARAMETER-ERR  TO  RTN-CD
                                        OF  PARM-SCZY1170-INOUT
               MOVE  0                  TO  DETAIL-RTN-CD
                                        OF  PARM-SCZY1170-INOUT
               MOVE  "0"                TO  DETAIL-RTN-CD-FLG
                                        OF  PARM-SCZY1170-INOUT
               MOVE  SPACE              TO  FREE-MSG
                                        OF  PARM-SCZY1170-INOUT
               PERFORM  ERR-RTN
           END-IF
           MOVE  KIDOU-ID               OF  PARM-SCZY7200-ARG1
                                        TO  WK-KIDOU-ID
      *----------  帳票ID
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "ARG1.帳票ID =["REP-ID
      D                                 OF  PARM-SCZY7200-ARG1"]"
      *-----------------------------------------------------------------
           MOVE  REP-ID                 OF  PARM-SCZY7200-ARG1
                                        TO  WK-REP-ID
      *----------  テーブルID取得処理
           PERFORM  SCZY1160-RTN.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  前処理  END  ***".
      *-----------------------------------------------------------------
       INIT-EXT.
           EXIT.
      ******************************************************************
      *    テーブルID取得処理
      ******************************************************************
       SCZY1160-RTN           SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  テーブルID取得処理  START  ***"
      *-----------------------------------------------------------------
      *----------  テーブルID取得ルーチンをコールする
      *----------  利用会社コード
           MOVE  WK-RIYO-CMP-CD
             TO  RIYO-CMP-CD                 OF  PARM-SCZY1160-ARG1
      *----------  テーブルID
           MOVE  CNS-TBL-TKDYAA35
             TO  TBL-ID                      OF  PARM-SCZY1160-ARG1(1)
           INITIALIZE  PARM-KYOTU-RTNPKG
           CALL  "SCZY1160"                  USING  PARM-SCZY1160-INOUT
                                                    PARM-KYOTU-RTNPKG
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "RTN-CD("  COUNTER       OF  PARM-KYOTU-RTNPKG
      D     ") OF SCZY1160=["  RTN-CD        OF
      D    PARM-KYOTU-RTNPKG(COUNTER         OF PARM-KYOTU-RTNPKG)"]"
      *-----------------------------------------------------------------
           IF  RTN-CD  OF  PARM-KYOTU-RTNPKG
              (COUNTER  OF  PARM-KYOTU-RTNPKG)
              NOT =  CNS-RET-CD-NORMAL
           THEN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "テーブルID取得エラー"
      *-----------------------------------------------------------------
      *---------- RTNパッケージ詳細
               MOVE  CNS-PGM-ID         TO  ERR-PGM-ID
                                        OF  PARM-SCZY1170-INOUT
               MOVE  "0004"             TO  ERR-POS
                                        OF  PARM-SCZY1170-INOUT
               MOVE  CNS-SYSTEM-ERR     TO  RTN-CD
                                        OF  PARM-SCZY1170-INOUT
               MOVE  0                  TO  DETAIL-RTN-CD
                                        OF  PARM-SCZY1170-INOUT
               MOVE  "0"                TO  DETAIL-RTN-CD-FLG
                                        OF  PARM-SCZY1170-INOUT
               MOVE  SPACE              TO  FREE-MSG
                                        OF  PARM-SCZY1170-INOUT
               MOVE  PARM-KYOTU-RTNPKG  TO  PARM-SCZY1170-RTNPKG
               PERFORM  ERR-RTN
           ELSE
               MOVE  HENSYUGO-TBL-ID    OF PARM-SCZY1160-ARG2(1)
                 TO  WK-HENSYUGO-TBL-ID
           END-IF.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "編集後テーブルID     =[" WK-HENSYUGO-TBL-ID "]"
      D    DISPLAY  "***  テーブルID取得処理  END  ***".
      *-----------------------------------------------------------------
       SCZY1160-EXT.
           EXIT.
      ******************************************************************
      *    主処理
      ******************************************************************
       MAIN-RTN               SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  主処理  START  *****"
      *-----------------------------------------------------------------
           MOVE  1  TO  WK-SQL-POINTER
      *----------  ＳＱＬ文作成
      *----------  電文情報
           STRING  "SELECT FUND_CD,"
                   " PTF_CD,"
                   " HYK_KRTU_CD,"
                   " REP_ID,"
                   " REP_ANY_CD,"
                   " KJN_YMD_FROM,"
                   " KJN_YMD_TO,"
                   " GYOMU_TASK_ID,"
                   " JIKOU_STS,"
                   " DAT_FILE_NAME"
                   " FROM "
                   WK-HENSYUGO-TBL-ID
      *----------  利用会社コード
                   " WHERE  RIYO_CMP_CD = :V1 "
      *----------  処理実行日
                   " AND SYORI_YMD = :V2 "
      *----------  起動ID
                   " AND KIDOU_ID = :V3 "
      *----------  明細単位実行ステータス ＝ "650"（実行待ち）
                   " AND JIKOU_STS = '650'"
               DELIMITED  BY  SIZE
               INTO  SQLSTMT1-ARR
               POINTER  WK-SQL-POINTER
      *----------  帳票ID ＝ ARG1．帳票ID
           IF  WK-REP-ID  NOT = SPACE
           THEN
               STRING  " AND REP_ID = :V4"
                   DELIMITED BY SIZE
                   INTO  SQLSTMT1-ARR
                   POINTER  WK-SQL-POINTER
           ELSE
               STRING  " AND ( REP_ID = :V4 OR 0=0 )"
                   DELIMITED BY SIZE
                   INTO  SQLSTMT1-ARR
                   POINTER  WK-SQL-POINTER
           END-IF
           STRING  " ORDER BY SYORI_BANGO "
               DELIMITED BY SIZE
               INTO  SQLSTMT1-ARR
               POINTER  WK-SQL-POINTER
           MOVE  WK-SQL-POINTER  TO  SQLSTMT1-LEN
      *
           STRING  SQLSTMT1-ARR
                   "[V1:"  WK-RIYO-CMP-CD "]"
                   "[V2:"  WK-SYORI-YMD   "]"
                   "[V3:"  WK-KIDOU-ID    "]"
                   "[V4:"  WK-REP-ID      "]"
               DELIMITED  BY  SIZE
               INTO  WK-SQLSTMT
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "SQL文         =["SQLSTMT1-ARR"]"
      D    DISPLAY  "SQL文のLENGTH =["SQLSTMT1-LEN"]"
      D    DISPLAY  "WK-SQLSTMT    =["WK-SQLSTMT  "]"
      *-----------------------------------------------------------------
      *----------  ＳＱＬ文 PREPARE
           EXEC SQL
               PREPARE STMT_RECORD1     FROM :SQLSTMT1
           END-EXEC
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "SQLCODE(PREPARE)   =["  SQLCODE  "]"
      *-----------------------------------------------------------------
           IF  SQLCODE  NOT  = CNS-SQL-NORMAL
           THEN
      *---------- RTNパッケージ詳細
               MOVE  CNS-PGM-ID         TO  ERR-PGM-ID
                                        OF  PARM-SCZY1170-INOUT
               MOVE  "0005"             TO  ERR-POS
                                        OF  PARM-SCZY1170-INOUT
               MOVE  CNS-ORACLE-ERR     TO  RTN-CD
                                        OF  PARM-SCZY1170-INOUT
               MOVE  SQLCODE            TO  DETAIL-RTN-CD
                                        OF  PARM-SCZY1170-INOUT
               MOVE  "1"                TO  DETAIL-RTN-CD-FLG
                                        OF  PARM-SCZY1170-INOUT
               MOVE  WK-SQLSTMT         TO  FREE-MSG
                                        OF  PARM-SCZY1170-INOUT
               PERFORM  ERR-RTN
           END-IF
      *----------  カーソル DECLARE
           EXEC SQL
               DECLARE CR1 CURSOR  FOR STMT_RECORD1
           END-EXEC
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "SQLCODE(DECLARE)   =["  SQLCODE  "]"
      *-----------------------------------------------------------------
           IF  SQLCODE  NOT  = CNS-SQL-NORMAL
           THEN
      *---------- RTNパッケージ詳細
               MOVE  CNS-PGM-ID         TO  ERR-PGM-ID
                                        OF  PARM-SCZY1170-INOUT
               MOVE  "0006"             TO  ERR-POS
                                        OF  PARM-SCZY1170-INOUT
               MOVE  CNS-ORACLE-ERR     TO  RTN-CD
                                        OF  PARM-SCZY1170-INOUT
               MOVE  SQLCODE            TO  DETAIL-RTN-CD
                                        OF  PARM-SCZY1170-INOUT
               MOVE  "1"                TO  DETAIL-RTN-CD-FLG
                                        OF  PARM-SCZY1170-INOUT
               MOVE  WK-SQLSTMT         TO  FREE-MSG
                                        OF  PARM-SCZY1170-INOUT
               PERFORM  ERR-RTN
           END-IF
      *----------  カーソル OPEN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "FLG-CR1=["FLG-CR1"]"
      *-----------------------------------------------------------------
           EXEC SQL
               OPEN  CR1  USING  :WK-RIYO-CMP-CD,
                                 :WK-SYORI-YMD,
                                 :WK-KIDOU-ID,
                                 :WK-REP-ID
           END-EXEC
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "SQLCODE(OPEN)      =["  SQLCODE  "]"
      *-----------------------------------------------------------------
           IF  SQLCODE  NOT  = CNS-SQL-NORMAL
           THEN
      *---------- RTNパッケージ詳細
               MOVE  CNS-PGM-ID         TO  ERR-PGM-ID
                                        OF  PARM-SCZY1170-INOUT
               MOVE  "0007"             TO  ERR-POS
                                        OF  PARM-SCZY1170-INOUT
               MOVE  CNS-ORACLE-ERR     TO  RTN-CD
                                        OF  PARM-SCZY1170-INOUT
               MOVE  SQLCODE            TO  DETAIL-RTN-CD
                                        OF  PARM-SCZY1170-INOUT
               MOVE  "1"                TO  DETAIL-RTN-CD-FLG
                                        OF  PARM-SCZY1170-INOUT
               MOVE  WK-SQLSTMT         TO  FREE-MSG
                                        OF  PARM-SCZY1170-INOUT
               PERFORM  ERR-RTN
           ELSE
               MOVE  1                  TO  FLG-CR1
           END-IF
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "FLG-CR1=["FLG-CR1"]"
      *-----------------------------------------------------------------
      *----------  カーソル FETCH
           MOVE  SPACE  TO  WK-FETCH-END-FLG
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "WK-FETCH-END-FLG=["WK-FETCH-END-FLG"]"
      *-----------------------------------------------------------------
           PERFORM  UNTIL  WK-FETCH-END-FLG  NOT =  SPACE
               PERFORM  CURSOR-FETCH-RTN
           END-PERFORM
           MOVE  WK-KENSU-CNT           TO  GET-KENSU
                                        OF  PARM-SCZY7200-ARG2
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "取得件数=["GET-KENSU  OF  PARM-SCZY7200-ARG2 "]"
      D    DISPLAY  "WK-FETCH-END-FLG=["WK-FETCH-END-FLG"]"
      *-----------------------------------------------------------------
      *----------  カーソル CLOSE
           EXEC SQL
               CLOSE CR1
           END-EXEC
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "SQLCODE(CLOSE)     =["  SQLCODE  "]"
      *-----------------------------------------------------------------
           IF  SQLCODE  NOT = CNS-SQL-NORMAL
           THEN
      *---------- RTNパッケージ詳細
               MOVE  CNS-PGM-ID         TO  ERR-PGM-ID
                                        OF  PARM-SCZY1170-INOUT
               MOVE  "0009"             TO  ERR-POS
                                        OF  PARM-SCZY1170-INOUT
               MOVE  CNS-ORACLE-ERR     TO  RTN-CD
                                        OF  PARM-SCZY1170-INOUT
               MOVE  SQLCODE            TO  DETAIL-RTN-CD
                                        OF  PARM-SCZY1170-INOUT
               MOVE  "1"                TO  DETAIL-RTN-CD-FLG
                                        OF  PARM-SCZY1170-INOUT
               MOVE  WK-SQLSTMT         TO  FREE-MSG
                                        OF  PARM-SCZY1170-INOUT
               PERFORM  ERR-RTN
           ELSE
               MOVE  0                  TO  FLG-CR1
           END-IF.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "FLG-CR1=["FLG-CR1"]"
      D    DISPLAY  "*****  主処理  END  *****".
      *-----------------------------------------------------------------
       MAIN-EXT.
           EXIT.
      ******************************************************************
      *    カーソルをフェッチ処理
      ******************************************************************
       CURSOR-FETCH-RTN       SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****   カーソルをフェッチ処理  START  *****"
      *-----------------------------------------------------------------
           INITIALIZE   HST-JIKO-DATA
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "HST-JIKO-DATA = [" HST-JIKO-DATA "]"
      *-----------------------------------------------------------------
           EXEC SQL
               FETCH  CR1
               INTO :HST-JIKO-DATA
           END-EXEC
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "SQLCODE(FETCH) =["  SQLCODE  "]"
      *-----------------------------------------------------------------
           EVALUATE  SQLCODE
           WHEN  CNS-SQL-NORMAL
               IF  WK-KENSU-CNT >= 50000
               THEN
                   MOVE  "1"            TO  WK-FETCH-END-FLG
               ELSE
                   COMPUTE  WK-KENSU-CNT = WK-KENSU-CNT + 1
                   MOVE  HST-JIKO-DATA  TO  JIKO-DATA
                              OF  PARM-SCZY7200-ARG2(WK-KENSU-CNT)
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "実行状況管理明細データ("WK-KENSU-CNT")=["
      D           JIKO-DATA   OF  PARM-SCZY7200-ARG2(WK-KENSU-CNT)"]"
      *-----------------------------------------------------------------
               END-IF
           WHEN  CNS-SQL-NOTFOUND
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "SQLCODE＝1403（データなし）の場合"
      *-----------------------------------------------------------------
               MOVE  "1"                TO  WK-FETCH-END-FLG
           WHEN  OTHER
      *---------- RTNパッケージ詳細
               MOVE  CNS-PGM-ID         TO  ERR-PGM-ID
                                        OF  PARM-SCZY1170-INOUT
               MOVE  "0008"             TO  ERR-POS
                                        OF  PARM-SCZY1170-INOUT
               MOVE  CNS-ORACLE-ERR     TO  RTN-CD
                                        OF  PARM-SCZY1170-INOUT
               MOVE  SQLCODE            TO  DETAIL-RTN-CD
                                        OF  PARM-SCZY1170-INOUT
               MOVE  "1"                TO  DETAIL-RTN-CD-FLG
                                        OF  PARM-SCZY1170-INOUT
               MOVE  WK-SQLSTMT         TO  FREE-MSG
                                        OF  PARM-SCZY1170-INOUT
               PERFORM  ERR-RTN
           END-EVALUATE.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****   カーソルをフェッチ処理  END  *****".
      *-----------------------------------------------------------------
       CURSOR-FETCH-EXT.
           EXIT.
      ******************************************************************
      *                 後処理
      ******************************************************************
       END-RTN                SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  後処理  START  *****"
      *-----------------------------------------------------------------
           MOVE  "000"                  TO  RTN-CD
                                        OF  PARM-SCZY1170-INOUT
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "RTN-CD OF  PARM-SCZY1170-INOUT = [" RTN-CD
      D                                 OF  PARM-SCZY1170-INOUT "]"
      D    DISPLAY  "*****  CALL  SCZY1170  START  *****"
      *-----------------------------------------------------------------
           CALL  "SCZY1170"             USING  PARM-SCZY1170-INOUT
                                               PARM-SCZY1170-RTNPKG
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  CALL  SCZY1170  END  *****"
      *-----------------------------------------------------------------
           MOVE  PARM-SCZY1170-RTNPKG   TO  PARM-SCZY7200-ARG3.
      *-------------------<DEBUG>---------------------------------------
      *D    DISPLAY  "実行状況管理明細データ=["PARM-SCZY7200-ARG2"]"
      D    DISPLAY  "PARM-SCZY7200-ARG3=["PARM-SCZY7200-ARG3"]"
      D    DISPLAY  "*****  後処理  END  *****".
      *-----------------------------------------------------------------
       END-EXT.
           EXIT.
      ******************************************************************
      *                 エラー処理
      ******************************************************************
       ERR-RTN                SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  ERR-RTN  START  *****"
      D    DISPLAY  "ERR-PGM-ID        =["
      D                     ERR-PGM-ID  OF  PARM-SCZY1170-INOUT"]"
      D    DISPLAY  "ERR-POS           =["
      D                     ERR-POS     OF  PARM-SCZY1170-INOUT"]"
      D    DISPLAY  "RTN-CD            =["
      D                     RTN-CD      OF  PARM-SCZY1170-INOUT"]"
      D    DISPLAY  "DETAIL-RTN-CD     =["DETAIL-RTN-CD
      D                                 OF  PARM-SCZY1170-INOUT"]"
      D    DISPLAY  "DETAIL-RTN-CD-FLG =["
      D              DETAIL-RTN-CD-FLG  OF  PARM-SCZY1170-INOUT"]"
      D    DISPLAY  "FREE-MSG          =["
      D                     FREE-MSG    OF  PARM-SCZY1170-INOUT"]"
      D    DISPLAY  "FLG-CR1=["FLG-CR1"]"
      *-----------------------------------------------------------------
           IF  FLG-CR1 = 1
           THEN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "エラー処理 CLOSE CR1"
      *-----------------------------------------------------------------
               EXEC SQL
                   CLOSE CR1
               END-EXEC
               MOVE  0  TO  FLG-CR1
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "SQLCODE=["SQLCODE"]"
      *-----------------------------------------------------------------
           END-IF
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "FLG-CR1=["FLG-CR1"]"
      D    DISPLAY  "*****  CALL  SCZY1170  START  *****"
      *-----------------------------------------------------------------
           CALL  "SCZY1170"             USING  PARM-SCZY1170-INOUT
                                               PARM-SCZY1170-RTNPKG
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  CALL  SCZY1170  END  *****"
      *-----------------------------------------------------------------
           MOVE  PARM-SCZY1170-RTNPKG   TO  PARM-SCZY7200-ARG3.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "PARM-SCZY7200-ARG3=["PARM-SCZY7200-ARG3"]"
      D    DISPLAY  "*****  ERR-RTN  END  *****".
      *-----------------------------------------------------------------
       ERR-EXT.
           GOBACK.
      ******************************************************************
