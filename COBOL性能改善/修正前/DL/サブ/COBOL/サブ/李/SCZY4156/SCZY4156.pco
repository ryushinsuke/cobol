      ******************************************************************
      *$Header$
      ******************************************************************
      *
      *     システムＩＤ   : T-STAR
      *     システム名称   : 次期TSTARシステム
      *     プログラムＩＤ : SCZY4156
      *     プログラム名称 : 商品属性全項目取得ルーチン
      *     処理概要       : アーギュメントINより、商品属性
      *                      テーブルからデータを１件抽出（SELECT）する
      *     更新ＤＢ       : なし
      *     参照ＤＢ       : 商品属性(TKCMSZ00)
      *     引数           : IN  利用会社コード
      *                      IN  証券種別コード
      *                      OUT 商品分類コード
      *                      OUT 証券種別コード
      *                      OUT 商品名称（日）
      *                      OUT 商品名称（英）
      *                      OUT 商品名称_略称（日）
      *                      OUT 商品名称_略称（英）
      *                      OUT 商品区分
      *                      OUT 管理数量単位
      *                      OUT 属性管理区分
      *                      OUT 時価等取得区分
      *                      OUT 評価パターンコード
      *                      OUT 商品属性用途
      *                      OUT 属性参照先１
      *                      OUT 属性参照先２
      *                      OUT 属性参照先３
      *                      OUT 属性参照先４
      *                      OUT 属性参照先５
      *                      OUT リファレンス項目管理可否
      *                      OUT 帳票出力分類
      *                      OUT コメント
      *                      OUT RTNパッケージ
      *     改訂履歴
      *     年月日     区分  所属       担当者  内容
      *     ---------- ----  ---------  ------  ------------------------
      *     20080415  新規  SCS         李海中  新規作成
      *     20090806  修正  SCS         李峰    テーマNo.834
      ******************************************************************
      ******************************************************************
      ******************************************************************
       IDENTIFICATION         DIVISION.
       PROGRAM-ID.    SCZY4156.
       AUTHOR.        Nomura.
       DATE-WRITTEN.  08-04-15.
       DATE-COMPILED.
      ******************************************************************
      *
      *    環  境  部
      *
      ******************************************************************
       ENVIRONMENT            DIVISION.
       CONFIGURATION          SECTION.
       SOURCE-COMPUTER.       L1000V.
      *SOURCE-COMPUTER.       L1000V   WITH DEBUGGING MODE.
       OBJECT-COMPUTER.       RP5470.
       INPUT-OUTPUT           SECTION.
       FILE-CONTROL.
      ******************************************************************
      *
      *    デ  ー  タ  部
      *
      ******************************************************************
       DATA                   DIVISION.
      ******************************************************************
      *    作  業  場  所  節
      ******************************************************************
       WORKING-STORAGE        SECTION.
       01  IDNAME                  PIC  X(80)  VALUE
       "$Id$".
      ******************************************************************
      *    コンスタント  エリア
      ******************************************************************
      *----------  共通コンスタント定義の宣言
       01  CONSTANT-AREA.
         05  CNS-SCZYCNS.
           COPY  "KOMONCNS.cpy".
           COPY  "SCZY_PFCM.cpy".
      *----------  プログラム名
         05  CNS-PGM-ID            PIC  X(08)  VALUE  "SCZY4156".
      *----------  通貨テーブル名
         05  CNS-TBL-TKCMSZ00      PIC  X(08)  VALUE  "TKCMSZ00".
      ******************************************************************
      *    ORACLE ホスト変数定義
      ******************************************************************
           EXEC  SQL  INCLUDE  SQLCA             END-EXEC.
           EXEC  SQL  INCLUDE  ORACA             END-EXEC.
           EXEC  SQL  BEGIN    DECLARE  SECTION  END-EXEC.
       01  HST-AREA.
         05  HST-TBL-TKCMSZ00.
      *----------  商品分類コード
           10  HST-SYOHIN-BUNRUI-CD
                                   PIC  X(04).
      *----------  証券種別コード
           10  HST-SEC-SBT-CD      PIC  X(04).
      *----------  商品名称（日）
           10  HST-SHOHIN-NAME-J   PIC  X(60).
      *----------  商品名称（英）
           10  HST-SHOHIN-NAME-E   PIC  X(60).
      *----------  商品名称_略称（日）
           10  HST-SHOHIN-AB-NAME-J
                                   PIC  X(30).
      *----------  商品名称_略称（英）
           10  HST-SHOHIN-AB-NAME-E
                                   PIC  X(30).
      *----------  商品区分
           10  HST-SYOHIN-KBN      PIC  X(02).
      *----------  管理数量単位
           10  HST-KANRI-SURYO-TANI
                                   PIC  S9(12)V9(06).
      *----------  属性管理区分
           10  HST-ZOUKUSEI-KANRI-KBN
                                   PIC  X(02).
      *----------  時価等取得区分
           10  HST-JIKA-SHTK-KBN   PIC  X(06).
      *----------  評価パターンコード
           10  HST-HYK-PATTERN-CD  PIC  X(04).
      *----------  商品属性用途
           10  HST-SYOHIN-ZOK-YOUTO
                                   PIC  X(01).
      *----------  属性参照先１
           10  HST-ZOK-SANS1       PIC  X(04).
      *----------  属性参照先２
           10  HST-ZOK-SANS2       PIC  X(04).
      *----------  属性参照先３
           10  HST-ZOK-SANS3       PIC  X(04).
      *----------  属性参照先４
           10  HST-ZOK-SANS4       PIC  X(04).
      *----------  属性参照先５
           10  HST-ZOK-SANS5       PIC  X(04).
      *----------  リファレンス項目管理可否
           10  HST-REF-MANA-KAHI   PIC  X(09).
      *----------  帳票出力分類
           10  HST-REP-OUT-BUNRUI  PIC  X(08).
      *----------  コメント
           10  HST-COMENT          PIC  X(20).
      *******    テーマNo.834  20090806 修正  開始    ******************
      *----------  出力順
           10  HST-OUT-JUN         PIC  S9(04).
      *******    テーマNo.834  20090806 修正  終了    ******************
         05  HST-KEY-TKCMSZ00.
      *----------  利用会社コード
           10  H-RIYO-CMP-CD       PIC  X(04).
      *----------  証券種別コード
           10  H-SEC-SBT-CD        PIC  X(04).
      *----------  削除区分
           10  H-SAKUJO-KBN        PIC  X(01).
      *----------  動的SQL文をいれるバッファ
         05  SQLSTMT               PIC  X(2500).
           EXEC  SQL  END  DECLARE  SECTION      END-EXEC.
      ******************************************************************
      *    フラグエリア
      ******************************************************************
       01  FLG-AREA.
      *----------  商品属性全項目取得CURSOR OPEN FLAG
         05  FLG-CR1-OPEN          PIC  9(01).
      ******************************************************************
      *    サブプログラム  パラメ−タ  エリア
      ******************************************************************
      *----------  テーブルID取得ルーチンパラメータ
       01  PARM-SCZY1160-INOUT.
           COPY  "SCZY1160.cpy".
      *----------  リターンパッケージセットルーチン
       01  PARM-SCZY1170-IN.
           COPY  "SCZY1170.cpy".
      *----------  RTNパッケージコピー句
       01  PARM-RTNPKG.
         05  PARM-RTNPKG-ARG.
           COPY  "CZRTNPACKAGE.cpy".
      ******************************************************************
      *    SQL  エリア
      ******************************************************************
       01  SQL-AREA.
         05  SQL-SELECT-DATA.
           10  FILLER              PIC  X(07)
               VALUE  "SELECT ".
      *----------  商品分類コード
           10  FILLER              PIC  X(17)
               VALUE  "SYOHIN_BUNRUI_CD,".
      *----------  証券種別コード
           10  FILLER              PIC  X(11)
               VALUE  "SEC_SBT_CD,".
      *----------  商品名称（日）
           10  FILLER              PIC  X(14)
               VALUE  "SHOHIN_NAME_J,".
      *----------  商品名称（英）
           10  FILLER              PIC  X(14)
               VALUE  "SHOHIN_NAME_E,".
      *----------  商品名称_略称（日）
           10  FILLER              PIC  X(17)
               VALUE  "SHOHIN_AB_NAME_J,".
      *----------  商品名称_略称（英）
           10  FILLER              PIC  X(17)
               VALUE  "SHOHIN_AB_NAME_E,".
      *----------  商品区分
           10  FILLER              PIC  X(11)
               VALUE  "SYOHIN_KBN,".
      *----------  管理数量単位
           10  FILLER              PIC  X(17)
               VALUE  "KANRI_SURYO_TANI,".
      *----------  属性管理区分
           10  FILLER              PIC  X(19)
               VALUE  "ZOUKUSEI_KANRI_KBN,".
      *----------  時価等取得区分
           10  FILLER              PIC  X(14)
               VALUE  "JIKA_SHTK_KBN,".
      *----------  評価パターンコード
           10  FILLER              PIC  X(15)
               VALUE  "HYK_PATTERN_CD,".
      *----------  商品属性用途
           10  FILLER              PIC  X(17)
               VALUE  "SYOHIN_ZOK_YOUTO,".
      *----------  属性参照先１
           10  FILLER              PIC  X(10)
               VALUE  "ZOK_SANS1,".
      *----------  属性参照先２
           10  FILLER              PIC  X(10)
               VALUE  "ZOK_SANS2,".
      *----------  属性参照先３
           10  FILLER              PIC  X(10)
               VALUE  "ZOK_SANS3,".
      *----------  属性参照先４
           10  FILLER              PIC  X(10)
               VALUE  "ZOK_SANS4,".
      *----------  属性参照先５
           10  FILLER              PIC  X(10)
               VALUE  "ZOK_SANS5,".
      *----------  リファレンス項目管理可否
           10  FILLER              PIC  X(14)
               VALUE  "REF_MANA_KAHI,".
      *----------  帳票出力分類
           10  FILLER              PIC  X(15)
               VALUE  "REP_OUT_BUNRUI,".
      *----------  コメント
           10  FILLER              PIC  X(07)
               VALUE  "COMENT,".
      *******    テーマNo.834  20090806 修正  開始    ******************
      *----------  出力順
           10  FILLER              PIC  X(07)
               VALUE  "OUT_JUN".
      *******    テーマNo.834  20090806 修正  終了    ******************
      *----------  FROM
           10  FILLER              PIC  X(06)
               VALUE  " FROM ".
           10  SQL-TKCMSZ00        PIC  X(13).
      *----------  WHERE
           10  FILLER              PIC  X(07)
               VALUE  " WHERE ".
      *----------  利用会社コード
           10  FILLER              PIC  X(17)
               VALUE  "RIYO_CMP_CD = :V1".
      *----------  証券種別コード
           10  FILLER              PIC  X(20)
               VALUE  " AND SEC_SBT_CD =:V2".
      *----------  削除区分
           10  FILLER              PIC  X(20)
               VALUE  " AND SAKUJO_KBN =:V3".
      ******************************************************************
      *                ワ−ク  エリア
      ******************************************************************
       01  WORK-AREA.
      *----------  ワークのフリーメッセージ
         05  WK-SQLSTMT            PIC  X(2500).
      ******************************************************************
      *    連  絡  節
      ******************************************************************
       LINKAGE                SECTION.
       01  PARM-SCZY4156-INOUT.
           COPY  "SCZY4156.cpy".
       01  PARM-SCZY4156-RTNPKG.
         05  PARM-SCZY4156-ARG3.
           COPY  "CZRTNPACKAGE.cpy".
      ******************************************************************
      *
      *    手 続 き 部
      *
      ******************************************************************
       PROCEDURE              DIVISION
           USING  PARM-SCZY4156-INOUT
                  PARM-SCZY4156-RTNPKG.
      ******************************************************************
      *    メイン処理
      ******************************************************************
       BASE-RTN               SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** SCZY4156 "
      D                      "商品属性全項目取得ルーチン START ***"
      *-----------------------------------------------------------------
      *----------  3.1 前処理
           PERFORM  INIT-RTN
      *----------  3.2 主処理
           PERFORM  MAIN-RTN
      *----------  3.3 後処理
           PERFORM  END-RTN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** SCZY4156 "
      D                      "商品属性全項目取得ルーチン END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       BASE-EXT.
           GOBACK.
      ******************************************************************
      *    3.1 前処理
      ******************************************************************
       INIT-RTN               SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  前処理  START  ***"
      *-----------------------------------------------------------------
      *----------  3.1.1 初期化処理
           PERFORM  INITIALIZE-RTN
      *----------  3.1.2 パラメータチェック
           PERFORM  PARM-CHK-RTN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  前処理  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       INIT-EXT.
           EXIT.
      ******************************************************************
      *    3.1.1.初期化処理
      ******************************************************************
       INITIALIZE-RTN         SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 初期化処理 START ***"
      *-----------------------------------------------------------------
           INITIALIZE  FLG-AREA
                       SQL-AREA
                       PARM-RTNPKG
                       HST-AREA
                       WORK-AREA
                       PARM-SCZY1160-INOUT
                       PARM-SCZY1170-IN
                       PARM-SCZY4156-ARG2
                       PARM-SCZY4156-RTNPKG
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":FLG-AREA =[" FLG-AREA "]"
      D    DISPLAY  CNS-PGM-ID ":SQL-AREA =[" SQL-AREA "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":HST-AREA =["
      D                         HST-AREA "]"
      D    DISPLAY  CNS-PGM-ID ":WORK-AREA =[" WORK-AREA "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY1160-INOUT =["
      D                         PARM-SCZY1160-INOUT "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY1170-IN =["
      D                         PARM-SCZY1170-IN  "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4156-ARG2 =["
      D                         PARM-SCZY4156-ARG2 "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4156-RTNPKG =["
      D                         PARM-SCZY4156-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":*** 初期化処理 END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       INITIALIZE-EXT.
           EXIT.
      ******************************************************************
      *    3.1.2.パラメータチェック
      ******************************************************************
       PARM-CHK-RTN           SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** パラメータチェック START ***"
      D    DISPLAY  CNS-PGM-ID ":利用会社コード =["
      D            RIYO-CMP-CD   OF  PARM-SCZY4156-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":証券種別コード =["
      D            SEC-SBT-CD    OF  PARM-SCZY4156-ARG1 "]"
      *-----------------------------------------------------------------
           IF  RIYO-CMP-CD      OF  PARM-SCZY4156-ARG1 <> SPACE
           AND SEC-SBT-CD       OF  PARM-SCZY4156-ARG1 <> SPACE
           THEN
             CONTINUE
           ELSE
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*****  パラメータ  エラー  *****"
      *-----------------------------------------------------------------
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID          OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-001
               TO  ERR-POS             OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-800
               TO  RTN-CD              OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  ZERO
               TO  DETAIL-RTN-CD       OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-0
               TO  DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
             MOVE  CNS-PARM-ERR
               TO  FREE-MSG            OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** パラメータチェック END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       PARM-CHK-EXT.
           EXIT.
      ******************************************************************
      *    3.2.主処理
      ******************************************************************
       MAIN-RTN               SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  主処理  START  ***"
      *-----------------------------------------------------------------
      *----------  3.2.1.テーブルID取得処理
           PERFORM  TBL-ID-GET-RTN
      *----------  3.2.2.商品属性全項目取得処理
           PERFORM  TUKA-MEISYO-GET-RTN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  主処理  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       MAIN-EXT.
           EXIT.
      ******************************************************************
      *    3.2.1.テーブルID取得処理
      ******************************************************************
       TBL-ID-GET-RTN         SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** SCZY1160:"
      D                       "テーブルID取得処理 START *** "
      *-----------------------------------------------------------------
           INITIALIZE  PARM-RTNPKG
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      *-----------------------------------------------------------------
      *----------  利用会社コード
           MOVE  RIYO-CMP-CD           OF  PARM-SCZY4156-ARG1
             TO  RIYO-CMP-CD           OF  PARM-SCZY1160-ARG1
      *----------  テーブルID
           MOVE  CNS-TBL-TKCMSZ00
             TO  TBL-ID                OF  PARM-SCZY1160-ARG1(1)
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY1160-ARG1:"
      D    DISPLAY  CNS-PGM-ID ":利用会社コード =["
      D              RIYO-CMP-CD       OF PARM-SCZY1160-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":テーブルID(1)  =["
      D              TBL-ID            OF PARM-SCZY1160-ARG1(1) "]"
      D    DISPLAY  CNS-PGM-ID ":***  CALL SCZY1160 START  ***"
      *-----------------------------------------------------------------
           CALL  "SCZY1160"            USING  PARM-SCZY1160-INOUT
                                              PARM-RTNPKG
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  CALL SCZY1160 END  ***"
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG=[" PARM-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":RTN-CD OF SCZY1160=["
      D              RTN-CD OF PARM-RTNPKG(COUNTER  OF  PARM-RTNPKG) "]"
      D    DISPLAY  CNS-PGM-ID ":COUNTER  OF  PARM-RTNPKG=["
      D                          COUNTER  OF  PARM-RTNPKG "]"
      *-----------------------------------------------------------------
           IF  RTN-CD OF PARM-RTNPKG(COUNTER  OF  PARM-RTNPKG) =
               CNS-RTN-000
           THEN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*****  テーブルID正常取得  *****"
      D    DISPLAY  CNS-PGM-ID ":TKCMSZ00 ID =["
      D             HENSYUGO-TBL-ID    OF  PARM-SCZY1160-ARG2(1) "]"
      *-----------------------------------------------------------------
             MOVE  HENSYUGO-TBL-ID     OF  PARM-SCZY1160-ARG2(1)
               TO  SQL-TKCMSZ00
             INITIALIZE  PARM-RTNPKG
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      *-----------------------------------------------------------------
           ELSE
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*****  テーブルID取得エラー  *****"
      *-----------------------------------------------------------------
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID          OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-002
               TO  ERR-POS             OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-901
               TO  RTN-CD              OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  ZERO
               TO  DETAIL-RTN-CD       OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-0
               TO  DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
             MOVE  SPACE
               TO  FREE-MSG            OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY CNS-PGM-ID ":*** SCZY1160:テーブルID取得処理  END"
      D                       " ***"
      *-----------------------------------------------------------------
           CONTINUE.
       TBL-ID-GET-EXT.
           EXIT.
      ******************************************************************
      *    3.2.2.商品属性全項目取得処理
      ******************************************************************
       TUKA-MEISYO-GET-RTN    SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D                  ":*** 商品属性全項目取得処理 START *** "
      *-----------------------------------------------------------------
      *----------  商品属性全項目取得CURSOR PREPARE
           PERFORM  CR1-PREPARE-RTN
      *----------  商品属性全項目目取得CURSOR OPEN
           PERFORM  CR1-OPEN-RTN
      *----------  商品属性全項目取得CURSOR FETCH
           PERFORM  CR1-FETCH-RTN
      *----------  商品属性全項目取得CURSOR CLOSE
           PERFORM  CR1-CLOSE-RTN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 商品属性全項目取得処理 END *** "
      *-----------------------------------------------------------------
           CONTINUE.
       TUKA-MEISYO-GET-EXT.
           EXIT.
      ******************************************************************
      *    商品属性全項目取得CURSOR PREPARE
      ******************************************************************
       CR1-PREPARE-RTN        SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 商品属性全項目取得"
      D                       "CURSOR PREPARE START ***"
      D    DISPLAY  CNS-PGM-ID
      D                   ":*** 商品属性全項目取得SQL文作成 START***"
      *-----------------------------------------------------------------
      *----------  商品属性全項目取得動的SQL文作成
           MOVE  CNS-COMP-NRI
             TO  H-RIYO-CMP-CD
           MOVE  SEC-SBT-CD            OF  PARM-SCZY4156-ARG1
             TO  H-SEC-SBT-CD
           MOVE  SPACE                 TO  H-SAKUJO-KBN
           MOVE  SQL-SELECT-DATA       TO  SQLSTMT
           STRING SQLSTMT(1:FUNCTION  LENGTH(SQL-SELECT-DATA))
                    "[:V1="
                     H-RIYO-CMP-CD
                    "][:V2="
                     H-SEC-SBT-CD
                    "][:V3="
                     H-SAKUJO-KBN
                    "]"
                    DELIMITED BY SIZE
                    INTO  WK-SQLSTMT
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":WK-SQLSTMT=[" WK-SQLSTMT "]"
      D    DISPLAY  CNS-PGM-ID ":H-RIYO-CMP-CD=[" H-RIYO-CMP-CD "]"
      D    DISPLAY  CNS-PGM-ID ":H-SEC-SBT-CD=["H-SEC-SBT-CD"]"
      D    DISPLAY  CNS-PGM-ID ":H-SAKUJO-KBN=[" H-SAKUJO-KBN "]"
      D    DISPLAY  CNS-PGM-ID ":SQLSTMT=[" SQLSTMT "]"
      D    DISPLAY  CNS-PGM-ID ":*** 商品属性全項目取得SQL文作成END ***"
      *-----------------------------------------------------------------
      *----------  商品属性全項目取得CURSOR OPEN
      *----------  PREPARE CURSOR
           EXEC  SQL
             PREPARE  S1  FROM  :SQLSTMT
           END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":商品属性全項目取得"
      D             "CURSOR PREPARE SQLCODE =[" SQLCODE "]"
      *-----------------------------------------------------------------
           IF  SQLCODE = CNS-SQL-NORMAL
           THEN
             CONTINUE
           ELSE
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID          OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-003
               TO  ERR-POS             OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-900
               TO  RTN-CD              OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  SQLCODE
               TO  DETAIL-RTN-CD       OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-1
               TO  DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
             MOVE  WK-SQLSTMT
               TO  FREE-MSG            OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *----------DECLARE CURSOR
           EXEC  SQL
             DECLARE  CR1  CURSOR  FOR  S1
           END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":商品属性取得CURSOR DECLARE SQLCODE"
      D                        "=[" SQLCODE "]"
      D    DISPLAY  CNS-PGM-ID ":*** 商品属性全項目取得"
      D                        "CURSOR PREPARE END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       CR1-PREPARE-EXT.
           EXIT.
      ******************************************************************
      *    商品属性全項目取得CURSOR OPEN
      ******************************************************************
       CR1-OPEN-RTN           SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 商品属性全項目取得"
      D                       "CURSOR OPEN START ***"
      *-----------------------------------------------------------------
      *----------OPEN    CURSOR
           EXEC  SQL
             OPEN  CR1         USING :H-RIYO-CMP-CD,
                                     :H-SEC-SBT-CD,
                                     :H-SAKUJO-KBN
           END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":商品属性全項目取得CURSOR OPEN SQLCODE"
                               "=["SQLCODE "]"
      *-----------------------------------------------------------------
           IF  SQLCODE  =  CNS-SQL-NORMAL
           THEN
             MOVE  CNS-ON              TO  FLG-CR1-OPEN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":CURSOR OPEN 正常"
      D    DISPLAY  CNS-PGM-ID ":CR1-OPEN-FLAG =[" FLG-CR1-OPEN "]"
      *-----------------------------------------------------------------
           ELSE
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID          OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-004
               TO  ERR-POS             OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-900
               TO  RTN-CD              OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  SQLCODE
               TO  DETAIL-RTN-CD       OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-1
               TO  DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
             MOVE  WK-SQLSTMT
               TO  FREE-MSG            OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 商品属性全項目取得"
      D                        "CURSOR OPEN END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       CR1-OPEN-EXT.
           EXIT.
      ******************************************************************
      *    商品属性全項目取得CURSOR FETCH
      ******************************************************************
       CR1-FETCH-RTN          SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 商品属性全項目取得"
      D                       "CURSOR FETCH START ***"
      *-----------------------------------------------------------------
      *----------  初期化
           INITIALIZE  HST-TBL-TKCMSZ00
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":HST-TBL-TKCMSZ00 =["
      D                          HST-TBL-TKCMSZ00 "]"
      *-----------------------------------------------------------------
      *******    テーマNo.834  20090806 修正  開始    ******************
      *----------  CR1 FETCH
           EXEC SQL
             FETCH CR1
             INTO  :HST-SYOHIN-BUNRUI-CD,
                   :HST-SEC-SBT-CD,
                   :HST-SHOHIN-NAME-J,
                   :HST-SHOHIN-NAME-E,
                   :HST-SHOHIN-AB-NAME-J,
                   :HST-SHOHIN-AB-NAME-E,
                   :HST-SYOHIN-KBN,
                   :HST-KANRI-SURYO-TANI,
                   :HST-ZOUKUSEI-KANRI-KBN,
                   :HST-JIKA-SHTK-KBN,
                   :HST-HYK-PATTERN-CD,
                   :HST-SYOHIN-ZOK-YOUTO,
                   :HST-ZOK-SANS1,
                   :HST-ZOK-SANS2,
                   :HST-ZOK-SANS3,
                   :HST-ZOK-SANS4,
                   :HST-ZOK-SANS5,
                   :HST-REF-MANA-KAHI,
                   :HST-REP-OUT-BUNRUI,
                   :HST-COMENT,
                   :HST-OUT-JUN
           END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":商品属性全項目取得CURSOR FETCH SQLCODE"
      D                        " =[" SQLCODE "]"
      *-----------------------------------------------------------------
           EVALUATE  SQLCODE
      *----------  正常
             WHEN  CNS-SQL-NORMAL
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 出力データ編集 START ***"
      *-----------------------------------------------------------------
      *----------  商品分類コード
               MOVE  HST-SYOHIN-BUNRUI-CD
                 TO  SYOHIN-BUNRUI-CD  OF  PARM-SCZY4156-ARG2
      *----------  証券種別コード
               MOVE  HST-SEC-SBT-CD
                 TO  SEC-SBT-CD        OF  PARM-SCZY4156-ARG2
      *----------  商品名称（日）
               MOVE  HST-SHOHIN-NAME-J
                 TO  SHOHIN-NAME-J     OF  PARM-SCZY4156-ARG2
      *----------  商品名称（英）
               MOVE  HST-SHOHIN-NAME-E
                 TO  SHOHIN-NAME-E     OF  PARM-SCZY4156-ARG2
      *----------  商品名称_略称（日）
               MOVE  HST-SHOHIN-AB-NAME-J
                 TO  SHOHIN-AB-NAME-J  OF  PARM-SCZY4156-ARG2
      *----------  商品名称_略称（英）
               MOVE  HST-SHOHIN-AB-NAME-E
                 TO  SHOHIN-AB-NAME-E  OF  PARM-SCZY4156-ARG2
      *----------  商品区分
               MOVE  HST-SYOHIN-KBN
                 TO  SYOHIN-KBN        OF  PARM-SCZY4156-ARG2
      *----------  管理数量単位
               MOVE  HST-KANRI-SURYO-TANI
                 TO  KANRI-SURYO-TANI  OF  PARM-SCZY4156-ARG2
      *----------  属性管理区分
               MOVE  HST-ZOUKUSEI-KANRI-KBN
                 TO  ZOUKUSEI-KANRI-KBN
                                       OF  PARM-SCZY4156-ARG2
      *----------  時価等取得区分
               MOVE  HST-JIKA-SHTK-KBN
                 TO  JIKA-SHTK-KBN     OF  PARM-SCZY4156-ARG2
      *----------  評価パターンコード
               MOVE  HST-HYK-PATTERN-CD
                 TO  HYK-PATTERN-CD    OF  PARM-SCZY4156-ARG2
      *----------  商品属性用途
               MOVE  HST-SYOHIN-ZOK-YOUTO
                 TO  SYOHIN-ZOK-YOUTO  OF  PARM-SCZY4156-ARG2
      *----------  属性参照先１
               MOVE  HST-ZOK-SANS1
                 TO  ZOK-SANS1         OF  PARM-SCZY4156-ARG2
      *----------  属性参照先２
               MOVE  HST-ZOK-SANS2
                 TO  ZOK-SANS2         OF  PARM-SCZY4156-ARG2
      *----------  属性参照先３
               MOVE  HST-ZOK-SANS3
                 TO  ZOK-SANS3         OF  PARM-SCZY4156-ARG2
      *----------  属性参照先４
               MOVE  HST-ZOK-SANS4
                 TO  ZOK-SANS4         OF  PARM-SCZY4156-ARG2
      *----------  属性参照先５
               MOVE  HST-ZOK-SANS5
                 TO  ZOK-SANS5         OF  PARM-SCZY4156-ARG2
      *----------  リファレンス項目管理可否
               MOVE  HST-REF-MANA-KAHI
                 TO  REF-MANA-KAHI     OF  PARM-SCZY4156-ARG2
      *----------  帳票出力分類
               MOVE  HST-REP-OUT-BUNRUI
                 TO  REP-OUT-BUNRUI    OF  PARM-SCZY4156-ARG2
      *----------  コメント
               MOVE  HST-COMENT
                 TO  COMENT            OF  PARM-SCZY4156-ARG2
      *----------  出力順
               MOVE  HST-OUT-JUN
                 TO  OUT-JUN            OF  PARM-SCZY4156-ARG2
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4156-ARG2編集:"
      D    DISPLAY  CNS-PGM-ID ":商品分類コード=["
      D             SYOHIN-BUNRUI-CD    OF      PARM-SCZY4156-ARG2"]"
      D    DISPLAY  CNS-PGM-ID ":証券種別コード=["
      D             SEC-SBT-CD          OF      PARM-SCZY4156-ARG2"]"
      D    DISPLAY  CNS-PGM-ID ":商品名称（日）=["
      D             SHOHIN-NAME-J       OF      PARM-SCZY4156-ARG2"]"
      D    DISPLAY  CNS-PGM-ID ":商品名称（英）=["
      D             SHOHIN-NAME-E       OF      PARM-SCZY4156-ARG2"]"
      D    DISPLAY  CNS-PGM-ID ":商品名称_略称（日）=["
      D             SHOHIN-AB-NAME-J    OF      PARM-SCZY4156-ARG2"]"
      D    DISPLAY  CNS-PGM-ID ":商品名称_略称（英）=["
      D             SHOHIN-AB-NAME-E    OF      PARM-SCZY4156-ARG2"]"
      D    DISPLAY  CNS-PGM-ID ":商品区分=["
      D             SYOHIN-KBN          OF      PARM-SCZY4156-ARG2"]"
      D    DISPLAY  CNS-PGM-ID ":管理数量単位=["
      D             KANRI-SURYO-TANI    OF      PARM-SCZY4156-ARG2"]"
      D    DISPLAY  CNS-PGM-ID ":属性管理区分=["
      D             ZOUKUSEI-KANRI-KBN  OF      PARM-SCZY4156-ARG2"]"
      D    DISPLAY  CNS-PGM-ID ":時価等取得区分=["
      D             JIKA-SHTK-KBN       OF      PARM-SCZY4156-ARG2"]"
      D    DISPLAY  CNS-PGM-ID ":評価パターンコード=["
      D             HYK-PATTERN-CD      OF      PARM-SCZY4156-ARG2"]"
      D    DISPLAY  CNS-PGM-ID ":商品属性用途=["
      D             SYOHIN-ZOK-YOUTO    OF      PARM-SCZY4156-ARG2"]"
      D    DISPLAY  CNS-PGM-ID ":属性参照先１=["
      D             ZOK-SANS1           OF      PARM-SCZY4156-ARG2"]"
      D    DISPLAY  CNS-PGM-ID ":属性参照先２=["
      D             ZOK-SANS2           OF      PARM-SCZY4156-ARG2"]"
      D    DISPLAY  CNS-PGM-ID ":属性参照先３=["
      D             ZOK-SANS3           OF      PARM-SCZY4156-ARG2"]"
      D    DISPLAY  CNS-PGM-ID ":属性参照先４=["
      D             ZOK-SANS4           OF      PARM-SCZY4156-ARG2"]"
      D    DISPLAY  CNS-PGM-ID ":属性参照先５=["
      D             ZOK-SANS5           OF      PARM-SCZY4156-ARG2"]"
      D    DISPLAY  CNS-PGM-ID ":リファレンス項目管理可否=["
      D             REF-MANA-KAHI       OF      PARM-SCZY4156-ARG2"]"
      D    DISPLAY  CNS-PGM-ID ":帳票出力分類=["
      D             REP-OUT-BUNRUI      OF      PARM-SCZY4156-ARG2"]"
      D    DISPLAY  CNS-PGM-ID ":コメント=["
      D             COMENT              OF      PARM-SCZY4156-ARG2"]"
      D    DISPLAY  CNS-PGM-ID ":出力順  =["
      D             OUT-JUN             OF      PARM-SCZY4156-ARG2"]"
      D    DISPLAY  CNS-PGM-ID ":*** 出力データ編集 END ***"
      *-----------------------------------------------------------------
      *******    テーマNo.834  20090806 修正  終了    ******************
             WHEN  CNS-SQL-NOTFOUND
      *----------  エラー検知プログラム名
               MOVE  CNS-PGM-ID
                 TO  ERR-PGM-ID        OF  PARM-SCZY1170-ARG1
      *----------  ポジション
               MOVE  CNS-POS-005
                 TO  ERR-POS           OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
               MOVE  CNS-RTN-110
                 TO  RTN-CD            OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
               MOVE  SQLCODE
                 TO  DETAIL-RTN-CD     OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
               MOVE  CNS-DETAIL-1
                 TO  DETAIL-RTN-CD-FLG OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
               MOVE  WK-SQLSTMT
                 TO  FREE-MSG          OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
               PERFORM  ERR-RTN
             WHEN  OTHER
      *----------  エラー検知プログラム名
               MOVE  CNS-PGM-ID
                 TO  ERR-PGM-ID        OF  PARM-SCZY1170-ARG1
      *----------  ポジション
               MOVE  CNS-POS-006
                 TO  ERR-POS           OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
               MOVE  CNS-RTN-900
                 TO  RTN-CD            OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
               MOVE  SQLCODE
                 TO  DETAIL-RTN-CD     OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
               MOVE  CNS-DETAIL-1
                 TO  DETAIL-RTN-CD-FLG OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
               MOVE  WK-SQLSTMT
                 TO  FREE-MSG          OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
               PERFORM  ERR-RTN
           END-EVALUATE
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 商品属性全項目取得"
      D                        "CURSOR FETCH END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       CR1-FETCH-EXT.
           EXIT.
      ******************************************************************
      *    商品属性全項目取得CURSOR CLOSE
      ******************************************************************
       CR1-CLOSE-RTN          SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 商品属性全項目取得"
      D                       "CURSOR CLOSE START ***"
      D    DISPLAY  CNS-PGM-ID ":FLG-CR1-OPEN= [" FLG-CR1-OPEN "]"
      *-----------------------------------------------------------------
           EXEC  SQL
             CLOSE CR1
           END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":商品属性全項目取得CURSOR CLOSE SQLCODE"
      D                          " =[" SQLCODE "]"
      *-----------------------------------------------------------------
           IF  SQLCODE = CNS-SQL-NORMAL
           THEN
             MOVE  CNS-OFF           TO  FLG-CR1-OPEN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":FLG-CR1-OPEN =[" FLG-CR1-OPEN "]"
      *-----------------------------------------------------------------
           ELSE
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID        OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-007
               TO  ERR-POS           OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-900
               TO  RTN-CD            OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  SQLCODE
               TO  DETAIL-RTN-CD     OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-1
               TO  DETAIL-RTN-CD-FLG OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
             MOVE  WK-SQLSTMT
               TO  FREE-MSG          OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 商品属性全項目取得"
      D                        "CURSOR CLOSE END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       CR1-CLOSE-EXT.
           EXIT.
      ******************************************************************
      *    3.3 後処理
      ******************************************************************
       END-RTN                SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 後処理 START ***"
      *-----------------------------------------------------------------
           MOVE  CNS-RTN-000
             TO  RTN-CD                OF  PARM-SCZY1170-ARG1
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":エラー検知プログラム名 =["
      D             ERR-PGM-ID         OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":ポジション =["
      D             ERR-POS            OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":リターンコード =["
      D             RTN-CD             OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":詳細リターンコード =["
      D             DETAIL-RTN-CD      OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":詳細リターンコードデコードフラグ =["
      D             DETAIL-RTN-CD-FLG  OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":フリーメッセージ =["
      D             FREE-MSG           OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":*** CALL SCZY1170 START ***"
      *-----------------------------------------------------------------
           CALL   "SCZY1170"           USING  PARM-SCZY1170-IN
                                              PARM-RTNPKG
           MOVE  PARM-RTNPKG           TO  PARM-SCZY4156-ARG3
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** CALL SCZY1170 END ***"
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4156-ARG2 =["
      D             PARM-SCZY4156-ARG2 "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4156-ARG3 =["
      D             PARM-SCZY4156-ARG3 "]"
      D    DISPLAY  CNS-PGM-ID ":*** 後処理 END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       END-EXT.
           EXIT.
      ******************************************************************
      *    3.4 エラー処理
      ******************************************************************
       ERR-RTN                SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** エラー処理 START ***"
      D    DISPLAY  CNS-PGM-ID ":FLG-CR1-OPEN =[" FLG-CR1-OPEN "]"
      *-----------------------------------------------------------------
           IF  FLG-CR1-OPEN = CNS-ON
           THEN
             EXEC  SQL
                 CLOSE CR1
             END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":SQLCODE(CLOSE) =[" SQLCODE "]"
      *-----------------------------------------------------------------
             MOVE  CNS-OFF             TO  FLG-CR1-OPEN
           ELSE
             CONTINUE
           END-IF
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":FLG-CR1-OPEN =[" FLG-CR1-OPEN "]"
      D    DISPLAY  CNS-PGM-ID ":エラー検知プログラム名 =["
      D             ERR-PGM-ID         OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":ポジション =["
      D             ERR-POS            OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":リターンコード =["
      D             RTN-CD             OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":詳細リターンコード =["
      D             DETAIL-RTN-CD      OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":詳細リターンコードデコードフラグ =["
      D             DETAIL-RTN-CD-FLG  OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":フリーメッセージ =["
      D             FREE-MSG           OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":*** CALL SCZY1170 START ***"
      *-----------------------------------------------------------------
           CALL   "SCZY1170"           USING  PARM-SCZY1170-IN
                                              PARM-RTNPKG
           MOVE  PARM-RTNPKG           TO  PARM-SCZY4156-ARG3
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** CALL SCZY1170 END ***"
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4156-ARG2 =["
      D             PARM-SCZY4156-ARG2 "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4156-ARG3 =["
      D             PARM-SCZY4156-ARG3 "]"
      D    DISPLAY  CNS-PGM-ID ":*** エラー処理 END ***"
      D    DISPLAY  CNS-PGM-ID ":*** SCZY4156 END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       ERR-EXT.
           GOBACK.
