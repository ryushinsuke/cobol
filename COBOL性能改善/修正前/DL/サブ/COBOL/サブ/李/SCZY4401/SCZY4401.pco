      ******************************************************************
      *$Header$
      ******************************************************************
      *
      *     システムＩＤ   : T-STAR
      *     システム名称   : 次期TSTARシステム
      *     プログラムＩＤ : SCZY4401
      *     プログラム名称 : 通貨情報メモリ格納処理ルーチン
      *     処理概要       : アーギュメントINより、
      *                      通貨テーブルからデータを全件抽出（SELECT）して、メモリに格納する。
      *     更新ＤＢ       : なし
      *     参照ＤＢ       : 通貨(TKCMTK00)
      *     引数           : IN  利用会社コード
      *                      OUT 通貨コード
      *                      OUT 通貨名称_全角（日）
      *                      OUT 通貨名称_半角（日）
      *                      OUT 通貨名称（英）
      *                      OUT 通貨名称_略称（日）
      *                      OUT 通貨名称_略称（英）
      *                      OUT 小数点以下桁数
      *                      OUT 端数処理
      *                      OUT 出力順
      *     改訂履歴
      *     年月日     区分  所属       担当者  内容
      *     ---------- ----  ---------  ------  ------------------------
      *     20100107   新規  SCS        陳曄    新規作成
      ******************************************************************
      ******************************************************************
       IDENTIFICATION         DIVISION.
       PROGRAM-ID.    SCZY4401.
       AUTHOR.        Nomura.
       DATE-WRITTEN.  10-01-07.
       DATE-COMPILED.
      ******************************************************************
      *
      *    環  境  部
      *
      ******************************************************************
       ENVIRONMENT            DIVISION.
       CONFIGURATION          SECTION.
       SOURCE-COMPUTER.       L1000V.
      *SOURCE-COMPUTER.       L1000V   WITH DEBUGGING MODE.
       OBJECT-COMPUTER.       RP5470.
       INPUT-OUTPUT           SECTION.
       FILE-CONTROL.
      ******************************************************************
      *
      *    デ  ー  タ  部
      *
      ******************************************************************
       DATA                   DIVISION.
      ******************************************************************
      *    作  業  場  所  節
      ******************************************************************
       WORKING-STORAGE        SECTION.
       01  IDNAME                  PIC  X(80)  VALUE
       "$Id$".
      ******************************************************************
      *    コンスタント  エリア
      ******************************************************************
      *----------  共通コンスタント定義の宣言
       01  CONSTANT-AREA.
         05  CNS-SCZYCNS.
           COPY  "KOMONCNS.cpy".
           COPY  "SCZY_PFCM.cpy".
      *----------  プログラム名
         05  CNS-PGM-ID            PIC  X(08)  VALUE  "SCZY4401".
      *----------  通貨テーブル名
         05  CNS-TBL-TKCMTK00      PIC  X(08)  VALUE  "TKCMTK00".
         05  CNS-1                 PIC  9(01)  VALUE  1.
         05  CNS-0                 PIC  9(01)  VALUE  0.
      ******************************************************************
      *    ORACLE ホスト変数定義
      ******************************************************************
           EXEC  SQL  INCLUDE  SQLCA             END-EXEC.
           EXEC  SQL  INCLUDE  ORACA             END-EXEC.
           EXEC  SQL  BEGIN    DECLARE  SECTION  END-EXEC.
       01  HST-AREA.
         05  HST-TBL-TKCMTK00.
           10  HST-TKCMTK00    OCCURS  501.
      *----------  通貨コード
             20  TUKA-CD           PIC  X(04).
      *----------  通貨名称_全角（日）
             20  TUKA-NAME-J-ZK    PIC  X(60).
      *----------  通貨名称_半角（日）
             20  TUKA-NAME-J-HK    PIC  X(60).
      *----------  通貨名称（英）
             20  TUKA-NAME-E       PIC  X(60).
      *----------  通貨名称_略称（日）
             20  TUKA-AB-NAME-J    PIC  X(30).
      *----------  通貨名称_略称（英）
             20  TUKA-AB-NAME-E    PIC  X(30).
      *----------  小数点以下桁数
             20  SYOSU-TENYIKA-KETASU   PIC  S9(01).
      *----------  端数処理
             20  HASU-KBN          PIC  X(01).
      *----------  出力順
             20  OUT-JUN           PIC  S9(04).
         05  HST-KEY-TKCMTK00.
      *----------  利用会社コード
           10  HST-RIYO-CMP-CD     PIC  X(04).
      *----------  削除区分
           10  HST-SAKUJO-KBN      PIC  X(01).
      *----------  動的SQL文をいれるバッファ
         05  SQLSTMT               PIC  X(2500).
           EXEC  SQL  END  DECLARE  SECTION      END-EXEC.
      ******************************************************************
      *    フラグエリア
      ******************************************************************
       01  FLG-AREA.
      *----------  通貨情報メモリ格納処理CURSOR OPEN FLAG
         05  FLG-CR1-OPEN          PIC  9(01).
      ******************************************************************
      *    サブプログラム  パラメ−タ  エリア
      ******************************************************************
      *----------  テーブルID取得ルーチンパラメータ
       01  PARM-SCZY1160-INOUT.
           COPY  "SCZY1160.cpy".
      *----------  リターンパッケージセットルーチン
       01  PARM-SCZY1170-IN.
           COPY  "SCZY1170.cpy".
      *----------  RTNパッケージコピー句
       01  PARM-RTNPKG.
         05  PARM-RTNPKG-ARG.
           COPY  "CZRTNPACKAGE.cpy".
      ******************************************************************
      *    SQL  エリア
      ******************************************************************
       01  SQL-AREA.
         05  SQL-SELECT-DATA.
           10  FILLER              PIC  X(07)
               VALUE  "SELECT ".
      *----------  通貨コード
           10  FILLER              PIC  X(08)
               VALUE  "TUKA_CD,".
      *----------  通貨名称_全角（日）
           10  FILLER              PIC  X(15)
               VALUE  "TUKA_NAME_J_ZK,".
      *----------  通貨名称_半角（日）
           10  FILLER              PIC  X(15)
               VALUE  "TUKA_NAME_J_HK,".
      *----------  通貨名称（英）
           10  FILLER              PIC  X(12)
               VALUE  "TUKA_NAME_E,".
      *----------  通貨名称_略称（日）
           10  FILLER              PIC  X(15)
               VALUE  "TUKA_AB_NAME_J,".
      *----------  通貨名称_略称（英）
           10  FILLER              PIC  X(15)
               VALUE  "TUKA_AB_NAME_E,".
      *----------  小数点以下桁数
           10  FILLER              PIC  X(21)
               VALUE  "SYOSU_TENYIKA_KETASU,".
      *----------  端数処理
           10  FILLER              PIC  X(09)
               VALUE  "HASU_KBN,".
      *----------  出力順
           10  FILLER              PIC  X(07)
               VALUE  "OUT_JUN".
      *----------  FROM
           10  FILLER              PIC  X(06)
               VALUE  " FROM ".
           10  SQL-TKCMTK00        PIC  X(13).
      *----------  WHERE
           10  FILLER              PIC  X(07)
               VALUE  " WHERE ".
      *----------  利用会社コード
           10  FILLER              PIC  X(28)
               VALUE  "RIYO_CMP_CD = :V_RIYO_CMP_CD".
      *----------  削除区分
           10  FILLER              PIC  X(31)
               VALUE  " AND SAKUJO_KBN = :V_SAKUJO_KBN".
      ******************************************************************
      *                ワ−ク  エリア
      ******************************************************************
       01  WORK-AREA.
      *----------  ワークのフリーメッセージ
         05  WK-SQLSTMT            PIC  X(2500).
      ******************************************************************
      *    連  絡  節
      ******************************************************************
       LINKAGE                SECTION.
       01  PARM-SCZY4401-INOUT.
           COPY  "SCZY4401.cpy".
       01  PARM-SCZY4401-RTNPKG.
         05  PARM-SCZY4401-ARG3.
           COPY  "CZRTNPACKAGE.cpy".
      ******************************************************************
      *
      *    手 続 き 部
      *
      ******************************************************************
       PROCEDURE              DIVISION
           USING  PARM-SCZY4401-INOUT
                  PARM-SCZY4401-RTNPKG.
      ******************************************************************
      *    メイン処理
      ******************************************************************
       BASE-RTN               SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** SCZY4401 "
      D                      "通貨情報メモリ格納処理ルーチン START ***"
      *-----------------------------------------------------------------
      *----------  3.1 前処理
           PERFORM  INIT-RTN
      *----------  3.2 主処理
           PERFORM  MAIN-RTN
      *----------  3.3 後処理
           PERFORM  END-RTN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** SCZY4401 "
      D                      "通貨情報メモリ格納処理ルーチン END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       BASE-EXT.
           GOBACK.
      ******************************************************************
      *    3.1 前処理
      ******************************************************************
       INIT-RTN               SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  前処理  START  ***"
      *-----------------------------------------------------------------
      *----------  3.1.1 初期化処理
           PERFORM  INITIALIZE-RTN
      *----------  3.1.2 パラメータチェック
           PERFORM  PARM-CHK-RTN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  前処理  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       INIT-EXT.
           EXIT.
      ******************************************************************
      *    3.1.1.初期化処理
      ******************************************************************
       INITIALIZE-RTN         SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 初期化処理 START ***"
      *-----------------------------------------------------------------
           INITIALIZE  FLG-AREA
                       SQL-AREA
                       PARM-RTNPKG
                       HST-AREA
                       WORK-AREA
                       PARM-SCZY1160-INOUT
                       PARM-SCZY1170-IN
                       PARM-SCZY4401-ARG2
                       PARM-SCZY4401-RTNPKG
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":FLG-AREA =[" FLG-AREA "]"
      D    DISPLAY  CNS-PGM-ID ":SQL-AREA =[" SQL-AREA "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":HST-AREA =["
      D                         HST-AREA "]"
      D    DISPLAY  CNS-PGM-ID ":WORK-AREA =[" WORK-AREA "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY1160-INOUT =["
      D                         PARM-SCZY1160-INOUT "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY1170-IN =["
      D                         PARM-SCZY1170-IN  "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4401-ARG2 =["
      D                         PARM-SCZY4401-ARG2 "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4401-RTNPKG =["
      D                         PARM-SCZY4401-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":*** 初期化処理 END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       INITIALIZE-EXT.
           EXIT.
      ******************************************************************
      *    3.1.2.パラメータチェック
      ******************************************************************
       PARM-CHK-RTN           SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** パラメータチェック START ***"
      D    DISPLAY  CNS-PGM-ID ":利用会社コード =["
      D             RIYO-CMP-CD   OF  PARM-SCZY4401-ARG1 "]"
      *-----------------------------------------------------------------
           IF  RIYO-CMP-CD      OF  PARM-SCZY4401-ARG1 <> SPACE
           THEN
             CONTINUE
           ELSE
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*****  パラメータ  エラー  *****"
      *-----------------------------------------------------------------
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID          OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-001
               TO  ERR-POS             OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-800
               TO  RTN-CD              OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  ZERO
               TO  DETAIL-RTN-CD       OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-0
               TO  DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
             MOVE  CNS-PARM-ERR
               TO  FREE-MSG            OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** パラメータチェック END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       PARM-CHK-EXT.
           EXIT.
      ******************************************************************
      *    3.2.主処理
      ******************************************************************
       MAIN-RTN               SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  主処理  START  ***"
      *-----------------------------------------------------------------
      *----------  3.2.1.テーブルID取得処理
           PERFORM  TBL-ID-GET-RTN
      *----------  3.2.2.通貨情報メモリ格納処理処理
           PERFORM  TUKA-MEISYO-GET-RTN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  主処理  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       MAIN-EXT.
           EXIT.
      ******************************************************************
      *    3.2.1.テーブルID取得処理
      ******************************************************************
       TBL-ID-GET-RTN         SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** SCZY1160:"
      D                       "テーブルID取得処理 START *** "
      *-----------------------------------------------------------------
           INITIALIZE  PARM-RTNPKG
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      *-----------------------------------------------------------------
      *----------  利用会社コード
           MOVE  RIYO-CMP-CD           OF  PARM-SCZY4401-ARG1
             TO  RIYO-CMP-CD           OF  PARM-SCZY1160-ARG1
      *----------  テーブルID
           MOVE  CNS-TBL-TKCMTK00
             TO  TBL-ID                OF  PARM-SCZY1160-ARG1(1)
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY1160-ARG1:"
      D    DISPLAY  CNS-PGM-ID ":利用会社コード =["
      D              RIYO-CMP-CD       OF PARM-SCZY1160-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":テーブルID(1)  =["
      D              TBL-ID            OF PARM-SCZY1160-ARG1(1) "]"
      D    DISPLAY  CNS-PGM-ID ":***  CALL SCZY1160 START  ***"
      *-----------------------------------------------------------------
           CALL  "SCZY1160"            USING  PARM-SCZY1160-INOUT
                                              PARM-RTNPKG
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  CALL SCZY1160 END  ***"
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG=[" PARM-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":RTN-CD OF SCZY1160=["
      D              RTN-CD OF PARM-RTNPKG(COUNTER  OF  PARM-RTNPKG) "]"
      D    DISPLAY  CNS-PGM-ID ":COUNTER  OF  PARM-RTNPKG=["
      D                          COUNTER  OF  PARM-RTNPKG "]"
      *-----------------------------------------------------------------
           IF  RTN-CD OF PARM-RTNPKG(COUNTER  OF  PARM-RTNPKG) =
               CNS-RTN-000
           THEN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*****  テーブルID正常取得  *****"
      D    DISPLAY  CNS-PGM-ID ":TKCMTK00 ID =["
      D             HENSYUGO-TBL-ID    OF  PARM-SCZY1160-ARG2(1) "]"
      *-----------------------------------------------------------------
             MOVE  HENSYUGO-TBL-ID     OF  PARM-SCZY1160-ARG2(1)
               TO  SQL-TKCMTK00
             INITIALIZE  PARM-RTNPKG
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      *-----------------------------------------------------------------
           ELSE
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*****  テーブルID取得エラー  *****"
      *-----------------------------------------------------------------
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID          OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-002
               TO  ERR-POS             OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-901
               TO  RTN-CD              OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  ZERO
               TO  DETAIL-RTN-CD       OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-0
               TO  DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
             MOVE  SPACE
               TO  FREE-MSG            OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY CNS-PGM-ID ":*** SCZY1160:テーブルID取得処理  END"
      D                       " ***"
      *-----------------------------------------------------------------
           CONTINUE.
       TBL-ID-GET-EXT.
           EXIT.
      ******************************************************************
      *    3.2.2.通貨情報メモリ格納処理処理
      ******************************************************************
       TUKA-MEISYO-GET-RTN    SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D                  ":*** 通貨情報メモリ格納処理処理 START *** "
      *-----------------------------------------------------------------
      *----------  通貨情報メモリ格納処理CURSOR PREPARE
           PERFORM  CR1-PREPARE-RTN
      *----------  通貨情報メモリ格納処理CURSOR OPEN
           PERFORM  CR1-OPEN-RTN
      *----------  通貨情報メモリ格納処理CURSOR FETCH
           PERFORM  CR1-FETCH-RTN
      *----------  通貨情報メモリ格納処理CURSOR CLOSE
           PERFORM  CR1-CLOSE-RTN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 通貨情報メモリ格納処理処理 END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       TUKA-MEISYO-GET-EXT.
           EXIT.
      ******************************************************************
      *    通貨情報メモリ格納処理CURSOR PREPARE
      ******************************************************************
       CR1-PREPARE-RTN        SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 通貨情報メモリ格納処理"
      D                       "CURSOR PREPARE START ***"
      D    DISPLAY  CNS-PGM-ID
      D                ":*** 通貨情報メモリ格納処理SQL文作成 START***"
      *-----------------------------------------------------------------
      *----------  フリーメッセージ用動的SQL文作成
           MOVE  RIYO-CMP-CD           OF  PARM-SCZY4401-ARG1
             TO  HST-RIYO-CMP-CD
           MOVE  SPACE                 TO  HST-SAKUJO-KBN
           MOVE  SQL-SELECT-DATA       TO  SQLSTMT
           STRING SQLSTMT(1:FUNCTION  LENGTH(SQL-SELECT-DATA))
                    "[:V1="
                     HST-RIYO-CMP-CD
                    "]"
                    DELIMITED BY SIZE
                    INTO  WK-SQLSTMT
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":WK-SQLSTMT=[" WK-SQLSTMT "]"
      D    DISPLAY  CNS-PGM-ID ":HST-RIYO-CMP-CD=[" HST-RIYO-CMP-CD "]"
      D    DISPLAY  CNS-PGM-ID ":HST-SAKUJO-KBN=[" HST-SAKUJO-KBN "]"
      D    DISPLAY  CNS-PGM-ID ":SQLSTMT=[" SQLSTMT "]"
      D    DISPLAY  CNS-PGM-ID
      D                  ":*** 通貨情報メモリ格納処理SQL文作成END ***"
      *-----------------------------------------------------------------
      *----------  通貨情報メモリ格納処理CURSOR OPEN
      *----------  PREPARE CURSOR
           EXEC  SQL
             PREPARE  S1  FROM  :SQLSTMT
           END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":通貨情報メモリ格納処理"
      D             "CURSOR PREPARE SQLCODE =[" SQLCODE "]"
      *-----------------------------------------------------------------
           IF  SQLCODE = CNS-SQL-NORMAL
           THEN
             CONTINUE
           ELSE
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID          OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-003
               TO  ERR-POS             OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-900
               TO  RTN-CD              OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  SQLCODE
               TO  DETAIL-RTN-CD       OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-1
               TO  DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
             MOVE  WK-SQLSTMT
               TO  FREE-MSG            OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *----------DECLARE CURSOR
           EXEC  SQL
             DECLARE  CR1  CURSOR  FOR  S1
           END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":商品属性取得CURSOR DECLARE SQLCODE"
      D                        "=[" SQLCODE "]"
      D    DISPLAY  CNS-PGM-ID ":*** 通貨情報メモリ格納処理"
      D                        "CURSOR PREPARE END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       CR1-PREPARE-EXT.
           EXIT.
      ******************************************************************
      *    通貨情報メモリ格納処理CURSOR OPEN
      ******************************************************************
       CR1-OPEN-RTN           SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 通貨情報メモリ格納処理"
      D                       "CURSOR OPEN START ***"
      *-----------------------------------------------------------------
      *----------OPEN    CURSOR
           EXEC  SQL
             OPEN  CR1         USING :HST-RIYO-CMP-CD,
                                     :HST-SAKUJO-KBN
           END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":通貨情報メモリ格納処理"
      D          "CURSOR OPEN SQLCODE=[" SQLCODE "]"
      *-----------------------------------------------------------------
           IF  SQLCODE  =  CNS-SQL-NORMAL
           THEN
             MOVE  CNS-ON              TO  FLG-CR1-OPEN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":CURSOR OPEN 正常"
      D    DISPLAY  CNS-PGM-ID ":CR1-OPEN-FLAG =[" FLG-CR1-OPEN "]"
      *-----------------------------------------------------------------
           ELSE
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID          OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-004
               TO  ERR-POS             OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-900
               TO  RTN-CD              OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  SQLCODE
               TO  DETAIL-RTN-CD       OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-1
               TO  DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
             MOVE  WK-SQLSTMT
               TO  FREE-MSG            OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 通貨情報メモリ格納処理"
      D                        "CURSOR OPEN END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       CR1-OPEN-EXT.
           EXIT.
      ******************************************************************
      *    通貨情報メモリ格納処理CURSOR FETCH
      ******************************************************************
       CR1-FETCH-RTN          SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 通貨情報メモリ格納処理"
      D                       "CURSOR FETCH START ***"
      *-----------------------------------------------------------------
      *----------  初期化
           INITIALIZE  HST-TBL-TKCMTK00
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":HST-TBL-TKCMTK00 =["
      D                          HST-TBL-TKCMTK00 "]"
      *-----------------------------------------------------------------
      *----------  CR1 FETCH
           EXEC SQL
             FETCH CR1
             INTO  :HST-TKCMTK00
           END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D     ":通貨情報メモリ格納処理CURSOR FETCH SQLCODE=[" SQLCODE "]"
      D    DISPLAY  CNS-PGM-ID
      D     ":通貨情報メモリ格納処理CURSOR FETCH 件数=[" SQLERRD(3) "]"
      *-----------------------------------------------------------------
           EVALUATE  SQLCODE
      *----------  正常
             WHEN  CNS-SQL-NORMAL
      *----------  エラー検知プログラム名
               MOVE  CNS-PGM-ID
                 TO  ERR-PGM-ID        OF  PARM-SCZY1170-ARG1
      *----------  ポジション
               MOVE  CNS-POS-008
                 TO  ERR-POS           OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
               MOVE  CNS-RTN-002
                 TO  RTN-CD            OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
               MOVE  SQLCODE
                 TO  DETAIL-RTN-CD     OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
               MOVE  CNS-DETAIL-1
                 TO  DETAIL-RTN-CD-FLG OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
               MOVE  WK-SQLSTMT
                 TO  FREE-MSG          OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
               PERFORM  ERR-RTN
             WHEN  CNS-SQL-NOTFOUND
               IF  SQLERRD(3)  =  CNS-0
               THEN
      *----------  エラー検知プログラム名
                 MOVE  CNS-PGM-ID
                   TO  ERR-PGM-ID      OF  PARM-SCZY1170-ARG1
      *----------  ポジション
                 MOVE  CNS-POS-005
                   TO  ERR-POS         OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
                 MOVE  CNS-RTN-110
                   TO  RTN-CD          OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
                 MOVE  SQLCODE
                   TO  DETAIL-RTN-CD   OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
                 MOVE  CNS-DETAIL-1
                   TO  DETAIL-RTN-CD-FLG
                                       OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
                 MOVE  WK-SQLSTMT
                   TO  FREE-MSG        OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
                 PERFORM  ERR-RTN
               ELSE
      *----------  ARG2の通貨データ配列　 ← 通貨データホスト表
                 MOVE  HST-TBL-TKCMTK00
                   TO  TBL-TKCMTK00    OF  PARM-SCZY4401-ARG2
      *----------  ARG2.有効件数　        ← SQLERRD(3)
                 MOVE  SQLERRD(3)
                   TO  YUUKOU-KENSUU   OF  PARM-SCZY4401-ARG2
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":ARG2.有効件数 =["YUUKOU-KENSUU "]"
      *-----------------------------------------------------------------
               END-IF
             WHEN  OTHER
      *----------  エラー検知プログラム名
               MOVE  CNS-PGM-ID
                 TO  ERR-PGM-ID        OF  PARM-SCZY1170-ARG1
      *----------  ポジション
               MOVE  CNS-POS-006
                 TO  ERR-POS           OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
               MOVE  CNS-RTN-900
                 TO  RTN-CD            OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
               MOVE  SQLCODE
                 TO  DETAIL-RTN-CD     OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
               MOVE  CNS-DETAIL-1
                 TO  DETAIL-RTN-CD-FLG OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
               MOVE  WK-SQLSTMT
                 TO  FREE-MSG          OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
               PERFORM  ERR-RTN
           END-EVALUATE
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 通貨情報メモリ格納処理"
      D                        "CURSOR FETCH END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       CR1-FETCH-EXT.
           EXIT.
      ******************************************************************
      *    通貨情報メモリ格納処理CURSOR CLOSE
      ******************************************************************
       CR1-CLOSE-RTN          SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 通貨情報メモリ格納処理"
      D                       "CURSOR CLOSE START ***"
      D    DISPLAY  CNS-PGM-ID ":FLG-CR1-OPEN= [" FLG-CR1-OPEN "]"
      *-----------------------------------------------------------------
           EXEC  SQL
             CLOSE CR1
           END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":通貨情報メモリ格納処理"
      D                  "CURSOR CLOSE SQLCODE=[" SQLCODE "]"
      *-----------------------------------------------------------------
           IF  SQLCODE = CNS-SQL-NORMAL
           THEN
             MOVE  CNS-OFF           TO  FLG-CR1-OPEN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":FLG-CR1-OPEN =[" FLG-CR1-OPEN "]"
      *-----------------------------------------------------------------
           ELSE
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID        OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-007
               TO  ERR-POS           OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-900
               TO  RTN-CD            OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  SQLCODE
               TO  DETAIL-RTN-CD     OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-1
               TO  DETAIL-RTN-CD-FLG OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
             MOVE  WK-SQLSTMT
               TO  FREE-MSG          OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 通貨情報メモリ格納処理"
      D                        "CURSOR CLOSE END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       CR1-CLOSE-EXT.
           EXIT.
      ******************************************************************
      *    3.3 後処理
      ******************************************************************
       END-RTN                SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 後処理 START ***"
      *-----------------------------------------------------------------
           MOVE  CNS-RTN-000
             TO  RTN-CD                OF  PARM-SCZY1170-ARG1
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** CALL SCZY1170 START ***"
      *-----------------------------------------------------------------
           CALL   "SCZY1170"           USING  PARM-SCZY1170-IN
                                              PARM-RTNPKG
           MOVE  PARM-RTNPKG           TO  PARM-SCZY4401-ARG3
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** CALL SCZY1170 END ***"
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4401-ARG2 =["
      D             PARM-SCZY4401-ARG2 "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4401-ARG3 =["
      D             PARM-SCZY4401-ARG3 "]"
      D    DISPLAY  CNS-PGM-ID ":*** 後処理 END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       END-EXT.
           EXIT.
      ******************************************************************
      *    3.4 エラー処理
      ******************************************************************
       ERR-RTN                SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** エラー処理 START ***"
      D    DISPLAY  CNS-PGM-ID ":FLG-CR1-OPEN =[" FLG-CR1-OPEN "]"
      *-----------------------------------------------------------------
           IF  FLG-CR1-OPEN = CNS-ON
           THEN
             EXEC  SQL
                 CLOSE CR1
             END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":SQLCODE(CLOSE) =[" SQLCODE "]"
      *-----------------------------------------------------------------
             MOVE  CNS-OFF             TO  FLG-CR1-OPEN
           ELSE
             CONTINUE
           END-IF
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":FLG-CR1-OPEN =[" FLG-CR1-OPEN "]"
      D    DISPLAY  CNS-PGM-ID ":エラー検知プログラム名 =["
      D             ERR-PGM-ID         OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":ポジション =["
      D             ERR-POS            OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":リターンコード =["
      D             RTN-CD             OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":詳細リターンコード =["
      D             DETAIL-RTN-CD      OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":詳細リターンコードデコードフラグ =["
      D             DETAIL-RTN-CD-FLG  OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":フリーメッセージ =["
      D             FREE-MSG           OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":*** CALL SCZY1170 START ***"
      *-----------------------------------------------------------------
           CALL   "SCZY1170"           USING  PARM-SCZY1170-IN
                                              PARM-RTNPKG
           MOVE  PARM-RTNPKG           TO  PARM-SCZY4401-ARG3
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** CALL SCZY1170 END ***"
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4401-ARG2 =["
      D             PARM-SCZY4401-ARG2 "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4401-ARG3 =["
      D             PARM-SCZY4401-ARG3 "]"
      D    DISPLAY  CNS-PGM-ID ":*** エラー処理 END ***"
      D    DISPLAY  CNS-PGM-ID ":*** SCZY4401 END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       ERR-EXT.
           GOBACK.
