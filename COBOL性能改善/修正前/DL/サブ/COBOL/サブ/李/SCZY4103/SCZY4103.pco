      ******************************************************************
      * $Header$
      ******************************************************************
      *
      *     システムＩＤ   : T-STAR
      *     システム名称   : 次期TSTARシステム
      *     プログラムＩＤ : SCZY4103
      *     プログラム名称 : 通貨コード名称取得(復数)ルーチン
      *     処理概要       : 入力の利用会社コードより、
      *                      通貨コード名称を取得する。
      *     参照ＤＢ       : 通貨（TKCMTK00）
      *     更新ＤＢ       : なし
      *     引数           : IN   利用会社コード
      *                      OUT  取得件数
      *                      OUT  通貨エリア
      *                      OUT  RTNパッケージ
      *
      *     改訂履歴
      *     年月日   区分 所属     担当者  内容
      *     -------- ---- -------- ------- -----------------------------
      *     20080319 新規 SCS      楊暁波  新規作成
      *     20081226 修正 SCS      宋  珈  残No.158
      ******************************************************************
       IDENTIFICATION         DIVISION.
       PROGRAM-ID.    SCZY4103.
       AUTHOR.        Nomura.
       DATE-WRITTEN.  08-03-19.
       DATE-COMPILED.
      ******************************************************************
      *
      *    環  境  部
      *
      ******************************************************************
       ENVIRONMENT            DIVISION.
       CONFIGURATION          SECTION.
       SOURCE-COMPUTER.       L1000V.
      *SOURCE-COMPUTER.       L1000V   WITH DEBUGGING MODE.
       OBJECT-COMPUTER.       RP5470.
       INPUT-OUTPUT           SECTION.
       FILE-CONTROL.
      ******************************************************************
      *
      *    デ  ー  タ  部
      *
      ******************************************************************
       DATA                   DIVISION.
      ******************************************************************
      *    作  業  場  所  節
      ******************************************************************
       WORKING-STORAGE        SECTION.
       01  IDNAME                  PIC  X(80)  VALUE
       "$Id$".
      ******************************************************************
      *    コンスタント  エリア
      ******************************************************************
      *----------  共通コンスタント定義の宣言
       01  CONSTANT-AREA.
         05  CNS-SCZYCNS.
           COPY  "KOMONCNS.cpy".
      *----------  プログラム名
         05  CNS-PGM-ID            PIC  X(08)  VALUE  "SCZY4103".
      *----------  通貨
         05  CNS-TBL-TKCMTK00      PIC  X(08)  VALUE  "TKCMTK00".
      *----------  抽出の件数は500件超過したメッセージ
         05  CNS-ERRID-E04         PIC  X(21)
             VALUE  "抽出件数超過500件警告".
      *----------  カウント用
         05  CNS-1                 PIC  9(01)  VALUE  1.
         05  CNS-MAX               PIC  9(03)  VALUE  500.
      ******************************************************************
      *    オラクルホスト  変数
      ******************************************************************
      *----------  ＳＱＬコミュニケーション領域
           EXEC  SQL  INCLUDE  SQLCA             END-EXEC.
           EXEC  SQL  INCLUDE  ORACA             END-EXEC.
           EXEC  SQL  BEGIN   DECLARE   SECTION  END-EXEC.
       01  HST-AREA.
      *----------  通貨エリア（セレクト）
         05  H-TKCMTK00-S.
      *----------  通貨コード
           10  H-TUKA-CD           PIC  X(04).
      *----------  通貨名称_全角（日）
           10  H-TUKA-NAME-J-ZK    PIC  X(60).
      *----------  通貨名称（英）
           10  H-TUKA-NAME-E       PIC  X(60).
      *******           残No.158 20081226 修正  開始           *********
      *----------  通貨名称_略称（日）
           10  H-TUKA-AB-NAME-J    PIC  X(30).
      *----------  通貨名称_略称（英）
           10  H-TUKA-AB-NAME-E    PIC  X(30).
      *******           残No.158 20081226 修正  終了           *********
      *----------  小数点以下桁数
           10  H-SYOSU-TENYIKA-KETASU
                                   PIC  S9(01).
      *----------  端数処理
           10  H-HASU-KBN          PIC  X(01).
      *----------  通貨エリア（セレクト）
         05  H-TKCMTK00-W.
      *----------  利用会社コード
           10  H-RIYO-CMP-CD       PIC  X(04).
      *----------  削除区分
           10  H-SAKUJO-KBN        PIC  X(01).
         05  SQLSTMT               PIC  X(2500).
           EXEC  SQL  END DECLARE       SECTION  END-EXEC.
      ******************************************************************
      *    フラグエリア
      ******************************************************************
       01  FLG-AREA.
      *----------  カーソルオープンフラグ
         05  FLG-CR1-OPEN          PIC  9(01).
      *----------  フェッチ処理フラグ
         05  FLG-FETCH-EOF         PIC  9(01).
      ******************************************************************
      *    サブプログラム  パラメ−タ  エリア
      ******************************************************************
       01  PARM-SCZY1160-INOUT.
      *----------  テーブルID取得ルーチンコピー句
           COPY  "SCZY1160.cpy".
      *----------  SCZY1170 リターンパッケージセットルーチン
       01  PARM-SCZY1170-IN.
           COPY  "SCZY1170.cpy".
      *----------  RTNパッケージコピー句
       01  PARM-RTNPKG.
         05  PARM-RTNPKG-ARG.
           COPY  "CZRTNPACKAGE.cpy".
      ******************************************************************
      *    SQL  エリア
      ******************************************************************
       01  SQL-AREA.
         05  SQL-SELECT-DATA.
           10  FILLER              PIC  X(07)
               VALUE  "SELECT ".
      *----------  通貨コード
           10  FILLER              PIC  X(08)
               VALUE  "TUKA_CD,".
      *----------  通貨名称_全角（日）
           10  FILLER              PIC  X(15)
               VALUE  "TUKA_NAME_J_ZK,".
      *----------  通貨名称（英）
           10  FILLER              PIC  X(12)
               VALUE  "TUKA_NAME_E,".
      *******           残No.158 20081226 修正  開始           *********
      *----------  通貨名称_略称（日）
           10  FILLER              PIC  X(15)
               VALUE  "TUKA_AB_NAME_J,".
      *----------  通貨名称_略称（英）
           10  FILLER              PIC  X(15)
               VALUE  "TUKA_AB_NAME_E,".
      *******           残No.158 20081226 修正  終了           *********
      *----------  小数点以下桁数
           10  FILLER              PIC  X(21)
               VALUE  "SYOSU_TENYIKA_KETASU,".
      *----------  端数処理
           10  FILLER              PIC  X(09)
               VALUE  "HASU_KBN ".
      *----------  FROM
           10  FILLER              PIC  X(05)
               VALUE  "FROM ".
      *----------  テーブルＩＤ
           10  SQL-TBL-ID          PIC  X(13).
      *----------  ＳＱＬ文編集用
         05  SQL-SELECT-WHERE.
      *----------  WHERE
           10  FILLER              PIC  X(07)
               VALUE  " WHERE ".
      *----------  利用会社コード
           10  FILLER              PIC  X(28)
               VALUE  "RIYO_CMP_CD = :V_RIYO_CMP_CD".
      *----------  削除区分
           10  FILLER              PIC  X(31)
               VALUE  " AND SAKUJO_KBN = :V_SAKUJO_KBN".
      *----------  ORDER BY
           10  FILLER              PIC  X(17)
               VALUE  " ORDER BY OUT_JUN".
      *----------  フリーメッセージ編集用
         05  SQL-SELECT-WHERE-E.
      *----------  WHERE
           10  FILLER              PIC  X(07)
               VALUE  " WHERE ".
      *----------  利用会社コード
           10  FILLER              PIC  X(15)  VALUE  "RIYO_CMP_CD = '".
           10  SQL-RIYO-CMP-CD     PIC  X(04).
           10  FILLER              PIC  X(01)  VALUE  "'".
      *----------  削除区分
           10  FILLER              PIC  X(21)
               VALUE  " AND SAKUJO_KBN = ' '".
      *----------  ORDER BY
           10  FILLER              PIC  X(17)
               VALUE  " ORDER BY OUT_JUN".
      ******************************************************************
      *    ワ−ク  エリア
      ******************************************************************
       01  WORK-AREA.
      *----------  ワーク抽出件数
         05  WK-SYTK-KENSU         PIC  9(03).
      *----------  ワークのフリーメッセージ
         05  WK-SQLSTMT            PIC  X(2500).
      ******************************************************************
      *    連  絡  節
      ******************************************************************
       LINKAGE                SECTION.
       01  PARM-SCZY4103-INOUT.
           COPY  "SCZY4103.cpy".
       01  PARM-SCZY4103-RTNPKG.
         05  PARM-SCZY4103-ARG3.
           COPY "CZRTNPACKAGE.cpy".
      ******************************************************************
      *
      *    手 続 き 部
      *
      ******************************************************************
       PROCEDURE              DIVISION
           USING  PARM-SCZY4103-INOUT
                  PARM-SCZY4103-RTNPKG.
      ******************************************************************
      *    メイン処理
      ******************************************************************
       BASE-RTN               SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** SCZY4103 START ***"
      *-----------------------------------------------------------------
      *----------  3.1 前処理
           PERFORM  INIT-RTN
      *----------  3.2 主処理
           PERFORM  MAIN-RTN
      *----------  3.3 後処理
           PERFORM  END-RTN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** SCZY4103 END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       BASE-EXT.
           GOBACK.
      ******************************************************************
      *    3.1.前処理
      ******************************************************************
       INIT-RTN               SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 前処理 START ***"
      *-----------------------------------------------------------------
      *----------  3.1.1 初期化処理
           PERFORM  INITIALIZE-RTN
      *----------  3.1.2 パラメータチェック
           PERFORM  PARM-CHK-RTN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ": *** 前処理 END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       INIT-EXT.
           EXIT.
      ******************************************************************
      *    3.1.1.初期化処理
      ******************************************************************
       INITIALIZE-RTN         SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 初期化処理 START ***"
      *-----------------------------------------------------------------
      *----------  3.1.1 初期化処理
           INITIALIZE     WORK-AREA
                          HST-AREA
                          SQL-AREA
                          FLG-AREA
                          PARM-SCZY1160-INOUT
                          PARM-SCZY1170-IN
                          PARM-RTNPKG
                          PARM-SCZY4103-ARG2
                          PARM-SCZY4103-ARG3
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":WORK-AREA =[" WORK-AREA "]"
      D    DISPLAY  CNS-PGM-ID ":HST-AREA =[" HST-AREA "]"
      D    DISPLAY  CNS-PGM-ID ":SQL-AREA =[" SQL-AREA "]"
      D    DISPLAY  CNS-PGM-ID ":FLG-AREA =[" FLG-AREA "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY1160-INOUT =["
      D                         PARM-SCZY1160-INOUT "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY1170-IN =["
      D                         PARM-SCZY1170-IN "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4103-ARG2 =["
      D             PARM-SCZY4103-ARG2 "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4103-ARG3 =["
      D             PARM-SCZY4103-ARG3 "]"
      D    DISPLAY  CNS-PGM-ID ":*** 初期化処理 END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       INITIALIZE-EXT.
           EXIT.
      ******************************************************************
      *    3.1.2.パラメータチェック
      ******************************************************************
       PARM-CHK-RTN           SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** パラメータチェック START ***"
      D    DISPLAY  CNS-PGM-ID ":上り電文・利用会社コード =["
      D             RIYO-CMP-CD        OF  PARM-SCZY4103-ARG1 "]"
      *-----------------------------------------------------------------
      *----------  3.1.2 パラメータチェック
           IF  RIYO-CMP-CD             OF  PARM-SCZY4103-ARG1 NOT= SPACE
           THEN
             CONTINUE
           ELSE
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***** パラメータエラー *****"
      *-----------------------------------------------------------------
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID          OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-001
               TO  ERR-POS             OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-800
               TO  RTN-CD              OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  ZERO
               TO  DETAIL-RTN-CD       OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-0
               TO  DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
             MOVE  CNS-PARM-ERR
               TO  FREE-MSG            OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** パラメータチェック END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       PARM-CHK-EXT.
           EXIT.
      ******************************************************************
      *    3.2.主処理
      ******************************************************************
       MAIN-RTN               SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 主処理 START ***"
      *-----------------------------------------------------------------
      *----------  3.2.1.テーブルID取得処理
           PERFORM  TBL-ID-GET-RTN
      *----------  3.2.2.通貨コード名称取得処理
           PERFORM  TKCD-NAME-GET-RTN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 主処理 END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       MAIN-EXT.
           EXIT.
      ******************************************************************
      *    3.2.1.テーブルID取得処理
      ******************************************************************
       TBL-ID-GET-RTN         SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** テーブルID取得 START ***"
      *-----------------------------------------------------------------
           INITIALIZE  PARM-RTNPKG
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      *-----------------------------------------------------------------
      *----------  利用会社コード
           MOVE  RIYO-CMP-CD           OF  PARM-SCZY4103-ARG1
             TO  RIYO-CMP-CD           OF  PARM-SCZY1160-ARG1
      *----------  テーブルID
           MOVE  CNS-TBL-TKCMTK00
             TO  TBL-ID                OF  PARM-SCZY1160-ARG1(1)
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY1160-ARG1:"
      D    DISPLAY  CNS-PGM-ID ":利用会社コード =["
      D             RIYO-CMP-CD        OF  PARM-SCZY1160-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":テーブルID(1) =["
      D             TBL-ID             OF  PARM-SCZY1160-ARG1(1) "]"
      D    DISPLAY  CNS-PGM-ID ":***  CALL SCZY1160 START  ***"
      *-----------------------------------------------------------------
           CALL  "SCZY1160"            USING  PARM-SCZY1160-INOUT
                                              PARM-RTNPKG
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  CALL SCZY1160 END  ***"
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":RTN-CD(SCZY1160) =["
      D             RTN-CD OF PARM-RTNPKG(COUNTER OF PARM-RTNPKG) "]"
      D    DISPLAY  CNS-PGM-ID ":COUNTER OF PARM-RTNPKG =["
      D             COUNTER            OF  PARM-RTNPKG "]"
      *-----------------------------------------------------------------
           IF  RTN-CD OF PARM-RTNPKG(COUNTER  OF  PARM-RTNPKG)
            =  CNS-RTN-000
           THEN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":テーブルID正常取得"
      *-----------------------------------------------------------------
             MOVE  HENSYUGO-TBL-ID     OF  PARM-SCZY1160-ARG2(1)
               TO  SQL-TBL-ID          OF  SQL-SELECT-DATA
             INITIALIZE  PARM-RTNPKG
           ELSE
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":テーブルID取得エラー"
      *-----------------------------------------------------------------
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID          OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-002
               TO  ERR-POS             OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-901
               TO  RTN-CD              OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  ZERO
               TO  DETAIL-RTN-CD       OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-0
               TO  DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
             MOVE  SPACE
               TO  FREE-MSG            OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":取得テーブルID =["
      D             SQL-TBL-ID         OF  SQL-SELECT-DATA "]"
      D    DISPLAY  CNS-PGM-ID ":***  テーブルID取得  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       TBL-ID-GET-EXT.
           EXIT.
      ******************************************************************
      *    3.2.2.通貨コード名称取得処理
      ******************************************************************
       TKCD-NAME-GET-RTN      SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 通貨コード名称取得処理 START ***"
      *-----------------------------------------------------------------
      *----------  通貨コード名称取得CURSOR PREPARE
           PERFORM  CR1-PREPARE-RTN
      *----------  通貨コード名称取得CURSOR OPEN
           PERFORM  CR1-OPEN-RTN
      *----------  通貨コード名称取得CURSOR FETCH
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":FLG-FETCH-EOF =[" FLG-FETCH-EOF "]"
      D    DISPLAY  CNS-PGM-ID ":WK-SYTK-KENSU =[" WK-SYTK-KENSU "]"
      *-----------------------------------------------------------------
           PERFORM  UNTIL  FLG-FETCH-EOF = CNS-ON
             PERFORM  CR1-FETCH-RTN
           END-PERFORM
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":WK-SYTK-KENSU =[" WK-SYTK-KENSU "]"
      D    DISPLAY  CNS-PGM-ID ":SYTK-KENSU =["
      D             SYTK-KENSU         OF  PARM-SCZY4103-ARG2 "]"
      D    DISPLAY  CNS-PGM-ID ":FLG-FETCH-EOF =[" FLG-FETCH-EOF "]"
      *-----------------------------------------------------------------
      *----------  通貨コード名称取得CURSOR CLOSE
           PERFORM  CR1-CLOSE-RTN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 通貨コード名称取得処理 END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       TKCD-NAME-GET-EXT.
           EXIT.
      ******************************************************************
      *    通貨コード名称取得CURSOR PREPARE
      ******************************************************************
       CR1-PREPARE-RTN        SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D             ":*** 通貨コード名称取得CURSOR PREPARE START ***"
      *-----------------------------------------------------------------
      *----------  フリーメッセージ用動的SQL文作成
      *----------  利用会社コード
           MOVE  RIYO-CMP-CD           OF  PARM-SCZY4103-ARG1
             TO  SQL-RIYO-CMP-CD
           STRING  SQL-SELECT-DATA,
                   SQL-SELECT-WHERE-E
           DELIMITED BY SIZE
           INTO  WK-SQLSTMT
      *----------  通貨コード名称取得動的SQL文作成
      *----------  利用会社コード
           MOVE  RIYO-CMP-CD           OF  PARM-SCZY4103-ARG1
             TO  H-RIYO-CMP-CD
      *----------  削除区分
           MOVE  SPACE                 TO  H-SAKUJO-KBN
           STRING  SQL-SELECT-DATA,
                   SQL-SELECT-WHERE
           DELIMITED BY SIZE
           INTO  SQLSTMT
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":SQL-RIYO-CMP-CD =[" SQL-RIYO-CMP-CD "]"
      D    DISPLAY  CNS-PGM-ID ":WK-SQLSTMT =[" WK-SQLSTMT "]"
      D    DISPLAY  CNS-PGM-ID ":H-RIYO-CMP-CD =[" H-RIYO-CMP-CD "]"
      D    DISPLAY  CNS-PGM-ID ":H-SAKUJO-KBN =[" H-SAKUJO-KBN "]"
      D    DISPLAY  CNS-PGM-ID ":SQLSTMT =[" SQLSTMT "]"
      *-----------------------------------------------------------------
           EXEC  SQL
             PREPARE  SQLRECORD  FROM  :SQLSTMT
           END-EXEC
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":CURSOR PREPARE SQLCODE =[" SQLCODE "]"
      *-----------------------------------------------------------------
           IF  SQLCODE = CNS-SQL-NORMAL
           THEN
             CONTINUE
           ELSE
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID          OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-003
               TO  ERR-POS             OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-900
               TO  RTN-CD              OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  SQLCODE
               TO  DETAIL-RTN-CD       OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-1
               TO  DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
             MOVE  WK-SQLSTMT
               TO  FREE-MSG            OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** PREPARE CURSOR END ***"
      D    DISPLAY  CNS-PGM-ID ":*** DECLARE CURSOR START ***"
      *-----------------------------------------------------------------
      *----------  DECLARE CURSOR
           EXEC  SQL
             DECLARE  CR1  CURSOR  FOR  SQLRECORD
           END-EXEC
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":SQLCODE(DECLARE) =[" SQLCODE "]"
      D    DISPLAY  CNS-PGM-ID ":*** DECLARE CURSOR END ***"
      D    DISPLAY  CNS-PGM-ID
      D             ":*** 通貨コード名称取得CURSOR PREPARE END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       CR1-PREPARE-EXT.
           EXIT.
      ******************************************************************
      *    通貨コード名称取得CURSOR OPEN
      ******************************************************************
       CR1-OPEN-RTN           SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D             ":*** 通貨コード名称取得CURSOR OPEN START ***"
      *-----------------------------------------------------------------
      *----------OPEN CURSOR
           EXEC  SQL
             OPEN  CR1
             USING :H-RIYO-CMP-CD,
                   :H-SAKUJO-KBN
           END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":SQLCODE(OPEN) =[" SQLCODE "]"
      *-----------------------------------------------------------------
           IF  SQLCODE  =  CNS-SQL-NORMAL
           THEN
             MOVE  CNS-ON              TO  FLG-CR1-OPEN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":CURSOR OPEN 正常"
      D    DISPLAY  CNS-PGM-ID ":FLG-CR1-OPEN =[" FLG-CR1-OPEN "]"
      *-----------------------------------------------------------------
           ELSE
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":CURSOR OPEN エラー"
      *-----------------------------------------------------------------
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID          OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-004
               TO  ERR-POS             OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-900
               TO  RTN-CD              OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  SQLCODE
               TO  DETAIL-RTN-CD       OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-1
               TO  DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
             MOVE  WK-SQLSTMT
               TO  FREE-MSG            OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** OPEN CURSOR  END  ***"
      D    DISPLAY  CNS-PGM-ID
      D             ":*** 通貨コード名称取得CURSOR OPEN END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       CR1-OPEN-EXT.
           EXIT.
      ******************************************************************
      *    通貨コード名称取得CURSOR FETCH
      ******************************************************************
       CR1-FETCH-RTN          SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D             ":*** 通貨コード名称取得CURSOR FETCH START ***"
      *-----------------------------------------------------------------
      *----------初期化
           INITIALIZE  H-TKCMTK00-S
      *----------CR1 FETCH
           EXEC SQL
             FETCH CR1
             INTO  :H-TUKA-CD,
                   :H-TUKA-NAME-J-ZK,
                   :H-TUKA-NAME-E,
      *******           残No.158 20081226 修正  開始           *********
                   :H-TUKA-AB-NAME-J,
                   :H-TUKA-AB-NAME-E,
      *******           残No.158 20081226 修正  終了           *********
                   :H-SYOSU-TENYIKA-KETASU,
                   :H-HASU-KBN
           END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":SQLCODE(FETCH) =[" SQLCODE "]"
      *-----------------------------------------------------------------
           EVALUATE   SQLCODE
      *----------  正常終了の場合
             WHEN  CNS-SQL-NORMAL
      *----------  ワーク抽出件数＋1
                 COMPUTE  WK-SYTK-KENSU  =  WK-SYTK-KENSU + CNS-1
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":ワーク抽出件数 =[" WK-SYTK-KENSU "]"
      *-----------------------------------------------------------------
                 IF  WK-SYTK-KENSU <= CNS-MAX
                 THEN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 出力データ編集 START ***"
      D    DISPLAY  CNS-PGM-ID ":FETCH取得データ:"
      D    DISPLAY  CNS-PGM-ID ":ホスト通貨コード =[" H-TUKA-CD "]"
      D    DISPLAY  CNS-PGM-ID ":ホスト通貨名称_全角（日） =["
      D             H-TUKA-NAME-J-ZK "]"
      D    DISPLAY  CNS-PGM-ID ":ホスト通貨名称（英） =["
      D             H-TUKA-NAME-E "]"
      *******           残No.158 20081226 修正  開始           *********
      D    DISPLAY  CNS-PGM-ID ":ホスト通貨名称_略称（日）=["
      D             H-TUKA-AB-NAME-J "]"
      D    DISPLAY  CNS-PGM-ID ":ホスト通貨名称_略称（英）=["
      D             H-TUKA-AB-NAME-E "]"
      *******           残No.158 20081226 修正  終了           *********
      D    DISPLAY  CNS-PGM-ID ":ホスト小数点以下桁数 =["
      D             H-SYOSU-TENYIKA-KETASU "]"
      D    DISPLAY  CNS-PGM-ID ":ホスト端数処理 =["H-HASU-KBN "]"
      *-----------------------------------------------------------------
      *----------  通貨コード
                   MOVE  H-TUKA-CD     OF  H-TKCMTK00-S
                     TO  TUKA-CD       OF  TUKA-AREA
                     OF  PARM-SCZY4103-ARG2(WK-SYTK-KENSU)
      *----------  通貨名称_全角（日）
                   MOVE  H-TUKA-NAME-J-ZK
                                       OF  H-TKCMTK00-S
                     TO  TUKA-NAME-J-ZK
                                       OF  TUKA-AREA
                     OF  PARM-SCZY4103-ARG2(WK-SYTK-KENSU)
      *----------  通貨名称（英）
                   MOVE  H-TUKA-NAME-E
                                       OF  H-TKCMTK00-S
                     TO  TUKA-NAME-E   OF  TUKA-AREA
                     OF  PARM-SCZY4103-ARG2(WK-SYTK-KENSU)
      *******           残No.158 20081226 修正  開始           *********
      *----------  通貨名称_略称（日）
                   MOVE  H-TUKA-AB-NAME-J
                                       OF  H-TKCMTK00-S
                     TO  TUKA-AB-NAME-J
                                       OF  TUKA-AREA
                     OF  PARM-SCZY4103-ARG2(WK-SYTK-KENSU)
      *----------  通貨名称_略称（英）
                   MOVE  H-TUKA-AB-NAME-E
                                       OF  H-TKCMTK00-S
                     TO  TUKA-AB-NAME-E
                                       OF  TUKA-AREA
                     OF  PARM-SCZY4103-ARG2(WK-SYTK-KENSU)
      *******           残No.158 20081226 修正  終了           *********
      *----------  小数点以下桁数
                   MOVE  H-SYOSU-TENYIKA-KETASU
                                       OF  H-TKCMTK00-S
                     TO  SYOSU-TENYIKA-KETASU
                                       OF  TUKA-AREA
                     OF  PARM-SCZY4103-ARG2(WK-SYTK-KENSU)
      *----------  端数処理
                   MOVE  H-HASU-KBN    OF  H-TKCMTK00-S
                     TO  HASU-KBN      OF  TUKA-AREA
                     OF  PARM-SCZY4103-ARG2(WK-SYTK-KENSU)
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":出力データ:"
      D    DISPLAY  CNS-PGM-ID ":通貨コード("WK-SYTK-KENSU") =["
      D             TUKA-CD            OF  TUKA-AREA
      D             OF  PARM-SCZY4103-ARG2(WK-SYTK-KENSU) "]"
      D    DISPLAY  CNS-PGM-ID ":通貨名称_全角（日）("WK-SYTK-KENSU")=["
      D             TUKA-NAME-J-ZK     OF  TUKA-AREA
      D             OF  PARM-SCZY4103-ARG2(WK-SYTK-KENSU) "]"
      D    DISPLAY  CNS-PGM-ID ":通貨名称（英）("WK-SYTK-KENSU") =["
      D             TUKA-NAME-E        OF  TUKA-AREA
      D             OF  PARM-SCZY4103-ARG2(WK-SYTK-KENSU) "]"
      *******           残No.158 20081226 修正  開始           *********
      D    DISPLAY  CNS-PGM-ID ":通貨名称_略称（日）("WK-SYTK-KENSU")=["
      D             TUKA-AB-NAME-J     OF  TUKA-AREA
      D             OF  PARM-SCZY4103-ARG2(WK-SYTK-KENSU) "]"
      D    DISPLAY  CNS-PGM-ID ":通貨名称_略称（英）("WK-SYTK-KENSU")=["
      D             TUKA-AB-NAME-E     OF  TUKA-AREA
      D             OF  PARM-SCZY4103-ARG2(WK-SYTK-KENSU) "]"
      *******           残No.158 20081226 修正  終了           *********
      D    DISPLAY  CNS-PGM-ID ":小数点以下桁数("WK-SYTK-KENSU") =["
      D             SYOSU-TENYIKA-KETASU
      D                                OF  TUKA-AREA
      D             OF  PARM-SCZY4103-ARG2(WK-SYTK-KENSU) "]"
      D    DISPLAY  CNS-PGM-ID ":端数処理("WK-SYTK-KENSU") =["
      D             HASU-KBN           OF  TUKA-AREA
      D             OF  PARM-SCZY4103-ARG2(WK-SYTK-KENSU) "]"
      D    DISPLAY  CNS-PGM-ID ":***  出力データ編集  END  ***"
      *-----------------------------------------------------------------
                 ELSE
                   MOVE  CNS-ON        TO  FLG-FETCH-EOF
                   MOVE  CNS-MAX
                     TO  SYTK-KENSU    OF  PARM-SCZY4103-ARG2
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":抽出件数 =["
      D             SYTK-KENSU         OF  PARM-SCZY4103-ARG2 "]"
      *-----------------------------------------------------------------
      *----------  エラー検知プログラム名
                   MOVE  CNS-PGM-ID
                     TO  ERR-PGM-ID    OF  PARM-SCZY1170-ARG1
      *----------  ポジション
                   MOVE  CNS-POS-005
                     TO  ERR-POS       OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
                   MOVE  CNS-RTN-002
                     TO  RTN-CD        OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
                   MOVE  SQLCODE
                     TO  DETAIL-RTN-CD OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
                   MOVE  CNS-DETAIL-0
                     TO  DETAIL-RTN-CD-FLG
                                       OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
                   MOVE  CNS-ERRID-E04
                     TO  FREE-MSG      OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
                   PERFORM  ERR-RTN
                 END-IF
      *----------  データなし の場合
             WHEN  CNS-SQL-NOTFOUND
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":ワーク抽出件数 =[" WK-SYTK-KENSU "]"
      *-----------------------------------------------------------------
               IF  WK-SYTK-KENSU = ZERO
               THEN
      *----------  エラー検知プログラム名
                 MOVE  CNS-PGM-ID
                   TO  ERR-PGM-ID      OF  PARM-SCZY1170-ARG1
      *----------  ポジション
                 MOVE  CNS-POS-006
                   TO  ERR-POS         OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
                 MOVE  CNS-RTN-110
                   TO  RTN-CD          OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
                 MOVE  SQLCODE
                   TO  DETAIL-RTN-CD   OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
                 MOVE  CNS-DETAIL-1
                   TO  DETAIL-RTN-CD-FLG
                                       OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
                 MOVE  WK-SQLSTMT
                   TO  FREE-MSG        OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
                 PERFORM  ERR-RTN
               ELSE
                 MOVE  CNS-ON          TO  FLG-FETCH-EOF
                 MOVE  WK-SYTK-KENSU
                   TO  SYTK-KENSU      OF  PARM-SCZY4103-ARG2
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":抽出件数 =["
      D             SYTK-KENSU         OF  PARM-SCZY4103-ARG2 "]"
      *-----------------------------------------------------------------
               END-IF
      *----------  上記以外の場合
             WHEN  OTHER
      *----------  エラー検知プログラム名
               MOVE  CNS-PGM-ID
                 TO  ERR-PGM-ID        OF  PARM-SCZY1170-ARG1
      *----------  ポジション
               MOVE  CNS-POS-007
                 TO  ERR-POS           OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
               MOVE  CNS-RTN-900
                 TO  RTN-CD            OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
               MOVE  SQLCODE
                 TO  DETAIL-RTN-CD     OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
               MOVE  CNS-DETAIL-1
                 TO  DETAIL-RTN-CD-FLG OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
               MOVE  WK-SQLSTMT
                 TO  FREE-MSG          OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
               PERFORM  ERR-RTN
           END-EVALUATE
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D             ":*** 通貨コード名称取得CURSOR FETCH END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       CR1-FETCH-EXT.
           EXIT.
      ******************************************************************
      *    通貨コード名称取得CURSOR CLOSE
      ******************************************************************
       CR1-CLOSE-RTN          SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D             ":*** 通貨コード名称取得CURSOR CLOSE START ***"
      D    DISPLAY  CNS-PGM-ID ":FLG-CR1-OPEN =[" FLG-CR1-OPEN "]"
      *-----------------------------------------------------------------
           EXEC  SQL
               CLOSE CR1
           END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":SQLCODE(CLOSE) =[" SQLCODE "]"
      *-----------------------------------------------------------------
           IF  SQLCODE = CNS-SQL-NORMAL
           THEN
             MOVE  CNS-OFF             TO  FLG-CR1-OPEN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":FLG-CR1-OPEN =[" FLG-CR1-OPEN "]"
      *-----------------------------------------------------------------
           ELSE
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID          OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-008
               TO  ERR-POS             OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-900
               TO  RTN-CD              OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  SQLCODE
               TO  DETAIL-RTN-CD       OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-1
               TO  DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
             MOVE  WK-SQLSTMT
               TO  FREE-MSG            OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D             ":*** 通貨コード名称取得CURSOR CLOSE END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       CR1-CLOSE-EXT.
           EXIT.
      ******************************************************************
      *    3.3 後処理
      ******************************************************************
       END-RTN                SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 後処理 START ***"
      *-----------------------------------------------------------------
           MOVE  CNS-RTN-000
             TO  RTN-CD                OF  PARM-SCZY1170-ARG1
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":エラー検知プログラム名 =["
      D             ERR-PGM-ID         OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":ポジション =["
      D             ERR-POS            OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":リターンコード =["
      D             RTN-CD             OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":詳細リターンコード =["
      D             DETAIL-RTN-CD      OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":詳細リターンコードデコードフラグ =["
      D             DETAIL-RTN-CD-FLG  OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":フリーメッセージ =["
      D             FREE-MSG           OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":*** CALL SCZY1170 START ***"
      *-----------------------------------------------------------------
           CALL   "SCZY1170"           USING  PARM-SCZY1170-IN
                                              PARM-RTNPKG
           MOVE  PARM-RTNPKG           TO  PARM-SCZY4103-ARG3
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** CALL SCZY1170 END ***"
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4103-ARG2 =["
      D             PARM-SCZY4103-ARG2 "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4103-ARG3 =["
      D             PARM-SCZY4103-ARG3 "]"
      D    DISPLAY  CNS-PGM-ID ":*** 後処理 END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       END-EXT.
           EXIT.
      ******************************************************************
      *    3.4 エラー処理
      ******************************************************************
       ERR-RTN                SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** エラー処理 START ***"
      D    DISPLAY  CNS-PGM-ID ":FLG-CR1-OPEN =[" FLG-CR1-OPEN "]"
      *-----------------------------------------------------------------
           IF  FLG-CR1-OPEN = CNS-ON
           THEN
             EXEC  SQL
                 CLOSE CR1
             END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":SQLCODE(CLOSE) =[" SQLCODE "]"
      *-----------------------------------------------------------------
             MOVE  CNS-OFF             TO  FLG-CR1-OPEN
           ELSE
             CONTINUE
           END-IF
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":FLG-CR1-OPEN =[" FLG-CR1-OPEN "]"
      D    DISPLAY  CNS-PGM-ID ":エラー検知プログラム名 =["
      D             ERR-PGM-ID         OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":ポジション =["
      D             ERR-POS            OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":リターンコード =["
      D             RTN-CD             OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":詳細リターンコード =["
      D             DETAIL-RTN-CD      OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":詳細リターンコードデコードフラグ =["
      D             DETAIL-RTN-CD-FLG  OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":フリーメッセージ =["
      D             FREE-MSG           OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":*** CALL SCZY1170 START ***"
      *-----------------------------------------------------------------
           CALL   "SCZY1170"           USING  PARM-SCZY1170-IN
                                              PARM-RTNPKG
           MOVE  PARM-RTNPKG           TO  PARM-SCZY4103-ARG3
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** CALL SCZY1170 END ***"
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4103-ARG2 =["
      D             PARM-SCZY4103-ARG2 "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4103-ARG3 =["
      D             PARM-SCZY4103-ARG3 "]"
      D    DISPLAY  CNS-PGM-ID ":*** エラー処理 END ***"
      D    DISPLAY  CNS-PGM-ID ":*** SCZY4103 END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       ERR-EXT.
           GOBACK.
