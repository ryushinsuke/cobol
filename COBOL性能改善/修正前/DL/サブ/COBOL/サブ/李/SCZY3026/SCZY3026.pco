      ******************************************************************
      *$Header:$
      ******************************************************************
      *
      *    システムＩＤ   : T-STAR
      *    システム名称   : 次期TSTARシステム
      *    プログラム名称 : GX接続用銘柄コード編集ルーチン
      *    処理概要       : 銘柄コードに株式種別付加処理
      *    引数           : IN  銘柄コード
      *                   : IN  銘柄通貨
      *                   : IN  証券種別
      *                   : IN  株式種別
      *                   : OUT 銘柄コード
      *                   : OUT RTNパッケージ
      *    改訂履歴
      *    年月日     区分  所属       担当者  内容
      *    ---------- ----  ---------  ------  ------------------------
      *    20100108   新規  ISC        凌超波  新規作成
      ******************************************************************
      ******************************************************************
       IDENTIFICATION         DIVISION.
       PROGRAM-ID.    SCZY3026.
       AUTHOR.        Nomura.
       DATE-WRITTEN.  10-01-08.
       DATE-COMPILED.
      ******************************************************************
      *
      *                 環  境  部
      *
      ******************************************************************
       ENVIRONMENT            DIVISION.
       CONFIGURATION          SECTION.
       SOURCE-COMPUTER.       L1000V.
      *SOURCE-COMPUTER.       L1000V   WITH DEBUGGING MODE.
       OBJECT-COMPUTER.       RP5470.
       INPUT-OUTPUT           SECTION.
       FILE-CONTROL.
      ******************************************************************
      *
      *                 デ  ー  タ  部
      *
      ******************************************************************
       DATA                   DIVISION.
       WORKING-STORAGE        SECTION.
       01  IDNAME                  PIC  X(80)  VALUE
       "$Id:$".
      ******************************************************************
      *                 コンスタント  エリア
      ******************************************************************
       01  CONSTANT-AREA.
      *----------  プログラム名
         05  CNS-PROGRAM-ID        PIC  X(08)  VALUE "SCZY3026".
      *----------  TSTAR共通用コンスタント
         05  CNS-JCCNS.
           COPY  "JCCNS.cpy".
      *----------  TSTAR業務コードコンスタント
         05  CNS-JCGYOMU.
           COPY "JCGYOMU.cpy".
      *----------  TSTAR業務MESSAGEコンスタント
         05  CNS-JCMSG.
           COPY "JCMSG.cpy".
      *----------  ワークRTNパッケージ
       01  WK-RTNPKG.
         05  WK-PARM-RTNPKG.
           COPY "CZRTNPACKAGE.cpy".
      ******************************************************************
      *                メッセージエリア
      ******************************************************************
       01  MSG-AREA.
      *----------  パラメータ存在エラー
         05  MSG-SONZAI.
      *----------  銘柄コード
           10  FILLER              PIC  X(08)  VALUE "MEG-CD=[".
           10  MSG-MEG-CD          PIC  X(20).
      *----------  銘柄通貨
           10  FILLER              PIC  X(15)
               VALUE "] MEG-TUKA-CD=[".
           10  MSG-MEG-TUKA-CD     PIC  X(04).
      *----------  証券種別
           10  FILLER              PIC  X(14)
               VALUE "] SEC-SBT-CD=[".
           10  MSG-SEC-SBT-CD      PIC  X(04).
           10  FILLER              PIC  X(01)  VALUE "]".
      ******************************************************************
      *                 サブプログラム  パラメータ  エリア
      ******************************************************************
      *----------  SCZY1170 リターンパッケージセットルーチン
       01  PARM-SCZY1170-INOUT.
      *----------  リターンパッケージセットルーチンコピー句
           COPY "SCZY1170.cpy".
       01  PARM-SCZY1170-RTNPKG.
         05  PARM-SCZY1170-ARG2.
      *----------  RTNパッケージ
           COPY "CZRTNPACKAGE.cpy".
      ******************************************************************
      *                 連  絡  節
      ******************************************************************
       LINKAGE                SECTION.
       01  PARM-SCZY3026-INOUT.
      *----------   GX接続用銘柄コード編集ルーチンコピー句
           COPY "SCZY3026.cpy".
       01  PARM-SCZY3026-RTNPKG.
         05  PARM-SCZY3026-ARG3.
      *----------  RTNパッケージ
           COPY "CZRTNPACKAGE.cpy".
      ******************************************************************
      *
      *        PROCEDURE      DIVISION
      *
      ******************************************************************
       PROCEDURE              DIVISION
            USING   PARM-SCZY3026-INOUT
                    PARM-SCZY3026-RTNPKG.
      ******************************************************************
      *             SCZY3026 GX接続用銘柄コード編集ルーチン
      ******************************************************************
       PRG-SCZY3026-RTN       SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PROGRAM-ID
      D            ":★☆★ GX接続用銘柄コード編集ルーチン START ★☆★"
      *-----------------------------------------------------------------
      *----------  前処理
           PERFORM  PRG-INIT-RTN
      *----------  主処理
           PERFORM  PRG-MAIN-RTN
      *----------  後処理
           PERFORM  PRG-END-RTN.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PROGRAM-ID
      D            ":★☆★ GX接続用銘柄コード編集ルーチン END ★☆★".
      *-----------------------------------------------------------------
       PRG-SCZY3026-EXT.
           GOBACK.
      ******************************************************************
      *                       前処理
      ******************************************************************
       PRG-INIT-RTN           SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PROGRAM-ID ":***  前処理  START  ***"
      *-----------------------------------------------------------------
      *----------  初期化
           INITIALIZE  WK-RTNPKG
                       MSG-AREA
                       PARM-SCZY1170-INOUT
                       PARM-SCZY1170-RTNPKG
                       PARM-SCZY3026-ARG2
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PROGRAM-ID ":銘柄コード=["
      D             MEG-CD            OF  PARM-SCZY3026-ARG1 "]"
      D    DISPLAY  CNS-PROGRAM-ID ":銘柄通貨=["
      D             MEG-TUKA-CD       OF  PARM-SCZY3026-ARG1 "]"
      D    DISPLAY  CNS-PROGRAM-ID ":証券種別=["
      D             SEC-SBT-CD        OF  PARM-SCZY3026-ARG1 "]"
      *-----------------------------------------------------------------
           IF  (  MEG-CD              OF  PARM-SCZY3026-ARG1
                                 NOT  =   SPACE
             AND  MEG-TUKA-CD         OF  PARM-SCZY3026-ARG1
                                 NOT  =   SPACE
             AND  SEC-SBT-CD          OF  PARM-SCZY3026-ARG1
                                 NOT  =   SPACE  )
           THEN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PROGRAM-ID ":パラメータチェック成功"
      *-----------------------------------------------------------------
               CONTINUE
           ELSE
      *----------  検知プログラムID
               MOVE  CNS-PROGRAM-ID   TO  ERR-PGM-ID
                                      OF  PARM-SCZY1170-INOUT
      *----------  ポジション
               MOVE  CNS-POS-001      TO  ERR-POS
                                      OF  PARM-SCZY1170-INOUT
      *----------  リターンコード
               MOVE  CNS-RTN-800      TO  RTN-CD
                                      OF  PARM-SCZY1170-INOUT
      *----------  詳細リターンコード
               MOVE  ZERO             TO  DETAIL-RTN-CD
                                      OF  PARM-SCZY1170-INOUT
      *----------  詳細リターンコードデコードフラグ
               MOVE  CNS-DETAIL-0     TO  DETAIL-RTN-CD-FLG
                                      OF  PARM-SCZY1170-INOUT
      *----------  フリーメッセージ
               MOVE  MSG-PARM-ERR     TO  FREE-MSG
                                      OF  PARM-SCZY1170-INOUT
      *----------  パラメータ銘柄コード
               MOVE  MEG-CD           OF  PARM-SCZY3026-ARG1
                                      TO  MSG-MEG-CD
      *----------  パラメータ銘柄通貨
               MOVE  MEG-TUKA-CD      OF  PARM-SCZY3026-ARG1
                                      TO  MSG-MEG-TUKA-CD
      *----------  パラメータ証券種別
               MOVE  SEC-SBT-CD       OF  PARM-SCZY3026-ARG1
                                      TO  MSG-SEC-SBT-CD
               MOVE  MSG-SONZAI       TO  FREE-MSG
                                      OF  PARM-SCZY1170-ARG1(101:66)
               PERFORM  COM-ERR-RTN
           END-IF.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PROGRAM-ID ":***  前処理  END  ***".
      *-----------------------------------------------------------------
       PRG-INIT-EXT.
           EXIT.
      ******************************************************************
      *                      主処理
      ******************************************************************
       PRG-MAIN-RTN           SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PROGRAM-ID ":***  主処理  START  ***"
      D    DISPLAY  CNS-PROGRAM-ID ":銘柄コードに株式種別付加処理"
      D    DISPLAY  CNS-PROGRAM-ID ":銘柄コード=["
      D             MEG-CD            OF  PARM-SCZY3026-ARG1 "]"
      D    DISPLAY  CNS-PROGRAM-ID ":銘柄通貨=["
      D             MEG-TUKA-CD       OF  PARM-SCZY3026-ARG1 "]"
      D    DISPLAY  CNS-PROGRAM-ID ":証券種別=["
      D             SEC-SBT-CD        OF  PARM-SCZY3026-ARG1 "]"
      D    DISPLAY  CNS-PROGRAM-ID ":株式種別=["
      D             KABU-SBT          OF  PARM-SCZY3026-ARG1 "]"
      *-----------------------------------------------------------------
           IF  (  MEG-TUKA-CD         OF  PARM-SCZY3026-ARG1
                                      =   CNS-TUKA-JPY
             AND  SEC-SBT-CD          OF  PARM-SCZY3026-ARG1
                                      =   CNS-SEC-SBT-CD-KABU  )
           THEN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PROGRAM-ID
      D             ":銘柄通貨='JPY'且つ証券種別='S'の場合"
      *-----------------------------------------------------------------
               IF  (  MEG-CD          OF  PARM-SCZY3026-ARG1(1:4)
                                      IS  NUMERIC
                 AND  MEG-CD          OF  PARM-SCZY3026-ARG1(5:16)
                                      =   SPACE  )
               THEN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PROGRAM-ID ":銘柄コードは４桁整数の場合"
      *-----------------------------------------------------------------
                   EVALUATE  KABU-SBT OF  PARM-SCZY3026-ARG1
      *----------  普通株
                     WHEN  CNS-FUTU-KABU
                       MOVE  MEG-CD   OF  PARM-SCZY3026-ARG1
                                      TO
                               MEG-CD OF  PARM-SCZY3026-ARG2
      *----------  第一新株
                     WHEN  CNS-1-NEW-KABU
                       STRING  MEG-CD OF  PARM-SCZY3026-ARG1
                                 "1"
                               DELIMITED  BY SPACE
                         INTO  MEG-CD OF  PARM-SCZY3026-ARG2
                       END-STRING
      *----------  第二新株
                     WHEN  CNS-2-NEW-KABU
                       STRING  MEG-CD OF  PARM-SCZY3026-ARG1
                                 "2"
                               DELIMITED  BY SPACE
                         INTO  MEG-CD OF  PARM-SCZY3026-ARG2
                       END-STRING
      *----------  第三新株
                     WHEN  CNS-3-NEW-KABU
                       STRING  MEG-CD OF  PARM-SCZY3026-ARG1
                                 "3"
                               DELIMITED  BY SPACE
                         INTO  MEG-CD OF  PARM-SCZY3026-ARG2
                       END-STRING
      *----------  第四新株
                     WHEN  CNS-4-NEW-KABU
                       STRING  MEG-CD OF  PARM-SCZY3026-ARG1
                                 "4"
                               DELIMITED  BY SPACE
                         INTO  MEG-CD OF  PARM-SCZY3026-ARG2
                       END-STRING
      *----------  優先株
                     WHEN  CNS-YUSEN-KABU
                       STRING  MEG-CD OF  PARM-SCZY3026-ARG1
                                 "5"
                               DELIMITED  BY SPACE
                         INTO  MEG-CD OF  PARM-SCZY3026-ARG2
                       END-STRING
      *----------  第一新株（優先株）
                     WHEN  CNS-1-YUSEN-NEW-KABU
                       STRING  MEG-CD OF  PARM-SCZY3026-ARG1
                                 "51"
                               DELIMITED  BY SPACE
                         INTO  MEG-CD OF  PARM-SCZY3026-ARG2
                       END-STRING
      *----------  第二新株（優先株）
                     WHEN  CNS-2-YUSEN-NEW-KABU
                       STRING  MEG-CD OF  PARM-SCZY3026-ARG1
                                 "52"
                               DELIMITED  BY SPACE
                         INTO  MEG-CD OF  PARM-SCZY3026-ARG2
                       END-STRING
      *----------  第三新株（優先株）
                     WHEN  CNS-3-YUSEN-NEW-KABU
                       STRING  MEG-CD OF  PARM-SCZY3026-ARG1
                                 "53"
                               DELIMITED  BY SPACE
                         INTO  MEG-CD OF  PARM-SCZY3026-ARG2
                       END-STRING
      *----------  第四新株（優先株）
                     WHEN  CNS-4-YUSEN-NEW-KABU
                       STRING  MEG-CD OF  PARM-SCZY3026-ARG1
                                 "54"
                               DELIMITED  BY SPACE
                         INTO  MEG-CD OF  PARM-SCZY3026-ARG2
                       END-STRING
      *----------  後配株
                     WHEN  CNS-KOUHAI-KABU
                       STRING  MEG-CD OF  PARM-SCZY3026-ARG1
                                 "7"
                               DELIMITED  BY SPACE
                         INTO  MEG-CD OF  PARM-SCZY3026-ARG2
                       END-STRING
      *----------  第一新株（後配株）
                     WHEN  CNS-1-KOUHAI-NEW-KABU
                       STRING  MEG-CD OF  PARM-SCZY3026-ARG1
                                 "71"
                               DELIMITED  BY SPACE
                         INTO  MEG-CD OF  PARM-SCZY3026-ARG2
                       END-STRING
      *----------  第二新株（後配株）
                     WHEN  CNS-2-KOUHAI-NEW-KABU
                       STRING  MEG-CD OF  PARM-SCZY3026-ARG1
                                 "72"
                               DELIMITED  BY SPACE
                         INTO  MEG-CD OF  PARM-SCZY3026-ARG2
                       END-STRING
      *----------  第三新株（後配株）
                     WHEN  CNS-3-KOUHAI-NEW-KABU
                       STRING  MEG-CD OF  PARM-SCZY3026-ARG1
                                 "73"
                               DELIMITED  BY SPACE
                         INTO  MEG-CD OF  PARM-SCZY3026-ARG2
                       END-STRING
      *----------  第四新株（後配株）
                     WHEN  CNS-4-KOUHAI-NEW-KABU
                       STRING  MEG-CD OF  PARM-SCZY3026-ARG1
                                 "74"
                               DELIMITED  BY SPACE
                         INTO  MEG-CD OF  PARM-SCZY3026-ARG2
                       END-STRING
                     WHEN  OTHER
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PROGRAM-ID ":株式種別はその他の場合"
      *-----------------------------------------------------------------
                       MOVE  MEG-CD   OF  PARM-SCZY3026-ARG1
                                      TO
                               MEG-CD OF  PARM-SCZY3026-ARG2
                   END-EVALUATE
               ELSE
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PROGRAM-ID ":銘柄コードは４桁整数以外の場合"
      *-----------------------------------------------------------------
                   MOVE  MEG-CD       OF  PARM-SCZY3026-ARG1
                                      TO
                         MEG-CD       OF  PARM-SCZY3026-ARG2
               END-IF
           ELSE
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PROGRAM-ID
      D             ":銘柄通貨≠'JPY'又は証券種別≠'S'の場合"
      *-----------------------------------------------------------------
               MOVE  MEG-CD           OF  PARM-SCZY3026-ARG1
                                      TO
                     MEG-CD           OF  PARM-SCZY3026-ARG2
           END-IF.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PROGRAM-ID ":出力.銘柄コード=["
      D             MEG-CD            OF  PARM-SCZY3026-ARG2     "]"
      D    DISPLAY  CNS-PROGRAM-ID ":***  主処理  END  ***".
      *-----------------------------------------------------------------
       PRG-MAIN-EXT.
           EXIT.
      ******************************************************************
      *                       後処理
      ******************************************************************
       PRG-END-RTN            SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PROGRAM-ID ":***  後処理  START  ***"
      *-----------------------------------------------------------------
      *----------  リターンコードを「000:正常終了」をセット
           MOVE  CNS-RTN-000          TO  RTN-CD
                                      OF  PARM-SCZY1170-INOUT
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PROGRAM-ID ":リターンコード   =["
      D              RTN-CD           OF  PARM-SCZY1170-ARG1        "]"
      *-----------------------------------------------------------------
      *
      *----------  リターンパッケージセットルーチン
           PERFORM  APL-SET-RTNPKG-RTN
      *----------  戻したRTNパッケージをARG3にセットする
           MOVE  PARM-SCZY1170-RTNPKG TO  PARM-SCZY3026-ARG3.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PROGRAM-ID ":PARM-SCZY3026-ARG3=["
      D                                   PARM-SCZY3026-ARG3(1:300)  "]"
      *-----------------------------------------------------------------
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PROGRAM-ID ":***   後処理  END  ***".
      *-----------------------------------------------------------------
       PRG-END-EXT.
           GOBACK.
      ******************************************************************
      *            リターンパッケージセットルーチン処理
      ******************************************************************
       APL-SET-RTNPKG-RTN     SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PROGRAM-ID
      D             ":リターンパッケージセットルーチン処理開始"
      D    DISPLAY  CNS-PROGRAM-ID
      D             ":SCZY1170 リターンパッケージセットルーチン"
      *-----------------------------------------------------------------
      *----------  SCZY1170 リターンパッケージセットルーチン
           CALL  "SCZY1170"           USING  PARM-SCZY1170-INOUT
                                             PARM-SCZY1170-RTNPKG.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PROGRAM-ID
      D             ":リターンパッケージセットルーチン処理終了".
      *-----------------------------------------------------------------
       APL-SET-RTNPKG-EXT.
           EXIT.
      ******************************************************************
      *                 エラー処理
      ******************************************************************
       COM-ERR-RTN            SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PROGRAM-ID ":***  エラー処理  START  ***"
      *-----------------------------------------------------------------
      *
      *----------  リターンパッケージセットルーチン
           PERFORM  APL-SET-RTNPKG-RTN
      *----------  戻したRTNパッケージをARG3にセットする
           MOVE  PARM-SCZY1170-RTNPKG TO  PARM-SCZY3026-ARG3.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PROGRAM-ID ":PARM-SCZY3026-ARG3=["
      D                                   PARM-SCZY3026-ARG3(1:300)  "]"
      *-----------------------------------------------------------------
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PROGRAM-ID ":***   エラー処理  END  ***".
      *-----------------------------------------------------------------
       COM-ERR-EXT.
           GOBACK.
