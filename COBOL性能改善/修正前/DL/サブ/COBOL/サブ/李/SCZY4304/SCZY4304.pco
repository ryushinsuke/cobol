      ******************************************************************
      *$Header$
      ******************************************************************
      *
      *  システムＩＤ   : T-STAR
      *  システム名称   : 次期TSTARシステム
      *  プログラムＩＤ : SCZY4304
      *  プログラム名称 : 異動計算パターン全項目取得ルーチン
      *  処理概要       : アーギュメントINより、メモリから対象の
      *                   異動計算パターン情報を一件取得する。
      *                   指定入力条件に満たさない場合、
      *                   汎用データを抽出する。
      *  更新ＤＢ       : なし
      *  参照ＤＢ       : 異動計算パターン(TKPFPT01)
      *  改訂履歴
      *  年月日     区分  所属       担当者  内容
      *  ---------- ----  ---------  ------  ------------------------
      *  20100115   新規  SCS        汪一琪  新規作成
      ******************************************************************
       IDENTIFICATION         DIVISION.
       PROGRAM-ID.    SCZY4304.
       AUTHOR.        Nomura.
       DATE-WRITTEN.  10-01-15.
       DATE-COMPILED.
      ******************************************************************
      *
      *    環  境  部
      *
      ******************************************************************
       ENVIRONMENT            DIVISION.
       CONFIGURATION          SECTION.
       SOURCE-COMPUTER.       L1000V.
      *SOURCE-COMPUTER.       L1000V   WITH DEBUGGING MODE.
       OBJECT-COMPUTER.       RP5470.
       INPUT-OUTPUT           SECTION.
       FILE-CONTROL.
      ******************************************************************
      *
      *    デ  ー  タ  部
      *
      ******************************************************************
       DATA                   DIVISION.
      ******************************************************************
      *    作  業  場  所  節
      ******************************************************************
       WORKING-STORAGE        SECTION.
       01  IDNAME                  PIC  X(80)  VALUE
       "$Id: $".
      ******************************************************************
      *    コンスタント  エリア
      ******************************************************************
       01  CONSTANT-AREA.
         05  CNS-SCZYCNS.
           COPY  "KOMONCNS.cpy".
           COPY  "SCZY_PFCM.cpy".
      *----------  エラー検知プログラム名
         05  CNS-PGM-ID            PIC  X(08)  VALUE  "SCZY4304".
      *----------  その他
         05  CNS-X-9               PIC  X(01)  VALUE  "9".
         05  CNS-JPY               PIC  X(03)  VALUE  "JPY".
         05  CNS-1                 PIC  9(01)  VALUE  1.
      ******************************************************************
      *    フラグエリア
      ******************************************************************
       01  FLG-AREA.
      *----------  データフラグ
         05  FLG-DATA              PIC  9(01).
      ******************************************************************
      *    サブプログラム  パラメ－タ  エリア
      ******************************************************************
      *----------  リターンパッケージセットルーチン
       01  PARM-SCZY1170-IN.
           COPY  "SCZY1170.cpy".
      *----------  RTNパッケージコピー句
       01  PARM-RTNPKG.
         05  PARM-RTNPKG-ARG.
           COPY  "CZRTNPACKAGE.cpy".
      ******************************************************************
      *                ワ－ク  エリア
      ******************************************************************
       01  WORK-AREA.
      *----------  IDX-CNT
         05  WK-COUNT              PIC  9(05).
         05  WK-IN.
           10  WK-TBL-TKPFPT01.
             15  WK-TKPFPT01   OCCURS 10001.
               COPY  "TKPFPT01.cpy".
      *----------  有効件数
           10  WK-YUUKOU-KENSUU    PIC  9(05).
      *----------  異動計算パターンコード
         05  WK-IDO-CALC-PATTERN-CD
                                   PIC  X(04).
      *----------  市場通貨コード
         05  WK-SIJO-TUKA-CD       PIC  X(04).
      *----------  証券種別コード
         05  WK-SEC-SBT-CD         PIC  X(04).
      *----------  保有目的
         05  WK-POSSESSION-OBJECT  PIC  X(01).
      *----------  ワークテーブルID
         05  WK-TBL-TKPFPT01       PIC  X(13).
      ******************************************************************
      *    連  絡  節
      ******************************************************************
       LINKAGE                SECTION.
       01  PARM-SCZY4304-INOUT.
           COPY  "SCZY4304.cpy".
       01  PARM-SCZY4304-RTNPKG.
         05  PARM-SCZY4304-ARG4.
           COPY  "CZRTNPACKAGE.cpy".
      ******************************************************************
      *
      *    手 続 き 部
      *
      ******************************************************************
       PROCEDURE              DIVISION
           USING  PARM-SCZY4304-INOUT
                  PARM-SCZY4304-RTNPKG.
      ******************************************************************
      *    メイン処理
      ******************************************************************
       BASE-RTN               SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** SCZY4304 "
      D                    "異動計算パターン取得ルーチン START ***"
      *-------------------<DEBUG>---------------------------------------
      *----------  3.1 前処理
           PERFORM  INIT-RTN
      *----------  3.2 主処理
           PERFORM  MAIN-RTN
      *----------  3.3 終了処理
           PERFORM  END-RTN
           CONTINUE.
       BASE-EXT.
           EXIT.
      ******************************************************************
      *    3.1 前処理
      ******************************************************************
       INIT-RTN               SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  SCZY4304:前処理  START  ***"
      *-------------------<DEBUG>---------------------------------------
           INITIALIZE  FLG-AREA
                       PARM-RTNPKG
                       WORK-AREA
                       PARM-SCZY1170-IN
                       PARM-SCZY4304-ARG3
                       PARM-SCZY4304-RTNPKG
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":FLG-AREA =[" FLG-AREA "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":WORK-AREA =[" WORK-AREA "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY1170-IN =["
      D                         PARM-SCZY1170-IN  "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4304-ARG2 =["
      D                         PARM-SCZY4304-ARG2 "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4304-ARG3 =["
      D                         PARM-SCZY4304-ARG3 "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4304-RTNPKG =["
      D                         PARM-SCZY4304-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":**  ワーク変数編集  START **"
      D    DISPLAY  CNS-PGM-ID ":ARG1.市場通貨コード=["
      D             SIJO-TUKA-CD        OF  PARM-SCZY4304-ARG1"]"
      *-----------------------------------------------------------------
           IF  SIJO-TUKA-CD             OF  PARM-SCZY4304-ARG1 = SPACE
           THEN
             MOVE  CNS-D-ALL            TO  WK-SIJO-TUKA-CD
           ELSE
             MOVE  SIJO-TUKA-CD         OF  PARM-SCZY4304-ARG1
               TO  WK-SIJO-TUKA-CD
           END-IF
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":ワーク市場通貨コード=["
      D             WK-SIJO-TUKA-CD"]"
      D    DISPLAY  CNS-PGM-ID ":ARG1.証券種別コード=["
      D             SEC-SBT-CD          OF  PARM-SCZY4304-ARG1"]"
      *-----------------------------------------------------------------
           IF  SEC-SBT-CD               OF  PARM-SCZY4304-ARG1 = SPACE
           THEN
             MOVE  CNS-D-ALL            TO  WK-SEC-SBT-CD
           ELSE
             MOVE  SEC-SBT-CD           OF  PARM-SCZY4304-ARG1
               TO  WK-SEC-SBT-CD
           END-IF
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":ワーク証券種別コード=["
      D             WK-SEC-SBT-CD"]"
      D    DISPLAY  CNS-PGM-ID ":ARG1.保有目的=["
      D             POSSESSION-OBJECT   OF  PARM-SCZY4304-ARG1"]"
      *-----------------------------------------------------------------
           IF  POSSESSION-OBJECT        OF  PARM-SCZY4304-ARG1 = SPACE
           THEN
             MOVE  CNS-X-9              TO  WK-POSSESSION-OBJECT
           ELSE
             MOVE  POSSESSION-OBJECT    OF  PARM-SCZY4304-ARG1
               TO  WK-POSSESSION-OBJECT
           END-IF
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":ワーク保有目的=["
      D             WK-POSSESSION-OBJECT"]"
      D    DISPLAY  CNS-PGM-ID ":**  ワーク変数編集  END **"
      D    DISPLAY  CNS-PGM-ID ":**  パラメータチェック  START **"
      D    DISPLAY  CNS-PGM-ID ":ARG1.異動計算パターンコード=["
      D             IDO-CALC-PATTERN-CD     OF  PARM-SCZY4304-ARG1"]"
      *-----------------------------------------------------------------
           IF   IDO-CALC-PATTERN-CD    OF  PARM-SCZY4304-ARG1 NOT= SPACE
           THEN
             CONTINUE
           ELSE
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID          OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-001
               TO  ERR-POS             OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-800
               TO  RTN-CD              OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  ZERO
               TO  DETAIL-RTN-CD       OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-0
               TO  DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1
      *---------- フリーメッセージ
             MOVE  CNS-PARM-ERR
               TO  FREE-MSG            OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":ワーク市場通貨コード、証券種別コード、"
      D             "保有目的のパラメータチェック START"
      D    DISPLAY  CNS-PGM-ID ":ワーク市場通貨コード=["
      D             WK-SIJO-TUKA-CD"]"
      D    DISPLAY  CNS-PGM-ID ":ワーク証券種別コード=["
      D             WK-SEC-SBT-CD"]"
      D    DISPLAY  CNS-PGM-ID ":ワーク保有目的=["
      D             WK-POSSESSION-OBJECT"]"
      *-----------------------------------------------------------------
           IF  WK-SIJO-TUKA-CD = CNS-D-ALL
           THEN
             IF   WK-SEC-SBT-CD = CNS-D-ALL
              AND WK-POSSESSION-OBJECT = CNS-X-9
             THEN
               CONTINUE
             ELSE
      *----------  エラー検知プログラム名
               MOVE  CNS-PGM-ID
                 TO  ERR-PGM-ID          OF  PARM-SCZY1170-ARG1
      *----------  ポジション
               MOVE  CNS-POS-002
                 TO  ERR-POS             OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
               MOVE  CNS-RTN-800
                 TO  RTN-CD              OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
               MOVE  ZERO
                 TO  DETAIL-RTN-CD        OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
               MOVE  CNS-DETAIL-0
                 TO  DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1
      *---------- フリーメッセージ
               MOVE  CNS-PARM-ERR
                 TO  FREE-MSG            OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
               PERFORM  ERR-RTN
             END-IF
           ELSE
             IF  ( WK-SEC-SBT-CD NOT= CNS-D-ALL
              AND WK-POSSESSION-OBJECT NOT= CNS-X-9 )
              OR ( WK-SEC-SBT-CD NOT= CNS-D-ALL
              AND WK-POSSESSION-OBJECT = CNS-X-9 )
              OR ( WK-SEC-SBT-CD = CNS-D-ALL
              AND WK-POSSESSION-OBJECT = CNS-X-9 )
             THEN
               CONTINUE
             ELSE
      *----------  エラー検知プログラム名
               MOVE  CNS-PGM-ID
                 TO  ERR-PGM-ID          OF  PARM-SCZY1170-ARG1
      *----------  ポジション
               MOVE  CNS-POS-003
                 TO  ERR-POS             OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
               MOVE  CNS-RTN-800
                 TO  RTN-CD              OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
               MOVE  ZERO
                 TO  DETAIL-RTN-CD        OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
               MOVE  CNS-DETAIL-0
                 TO  DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1
      *---------- フリーメッセージ
               MOVE  CNS-PARM-ERR
                 TO  FREE-MSG            OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
               PERFORM  ERR-RTN
             END-IF
           END-IF
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  パラメータチェック  END  ***"
      D    DISPLAY  CNS-PGM-ID ":***  SCZY4304:前処理  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       INIT-EXT.
           EXIT.
      ******************************************************************
      *    3.2.主処理
      ******************************************************************
       MAIN-RTN               SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  SCZY4304:主処理  START  ***"
      *-----------------------------------------------------------------
      *----------  3.2.1.異動計算パターン全項目取得処理
           PERFORM  IDO-CALC-GET-RTN
      *----------  3.2.2.汎用異動計算パターン全項目取得処理
           PERFORM  HANYOU-IDO-GET-RTN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  SCZY4304:主処理  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       MAIN-EXT.
           EXIT.
      ******************************************************************
      *    3.2.1.異動計算パターン全項目取得処理
      ******************************************************************
       IDO-CALC-GET-RTN       SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 異動計算パターン取得処理 START ***"
      D    DISPLAY  CNS-PGM-ID ":ARG1.異動計算パターンコード=["
      D             IDO-CALC-PATTERN-CD     OF  PARM-SCZY4304-ARG1"]"
      D    DISPLAY  CNS-PGM-ID ":ARG1.市場通貨コード=["
      D             SIJO-TUKA-CD        OF  PARM-SCZY4304-ARG1"]"
      D    DISPLAY  CNS-PGM-ID ":ARG1.証券種別コード=["
      D             SEC-SBT-CD          OF  PARM-SCZY4304-ARG1"]"
      D    DISPLAY  CNS-PGM-ID ":ARG1.保有目的=["
      D             POSSESSION-OBJECT   OF  PARM-SCZY4304-ARG1"]"
      D    DISPLAY  CNS-PGM-ID ":ARG2.有効件数=["
      D             YUUKOU-KENSUU       OF  PARM-SCZY4304-ARG2"]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4304-ARG2=["
      D             PARM-SCZY4304-ARG2"]"
      *-----------------------------------------------------------------
           MOVE  IDO-CALC-PATTERN-CD        OF  PARM-SCZY4304-ARG1
             TO  WK-IDO-CALC-PATTERN-CD
           MOVE  PARM-SCZY4304-ARG2     TO  WK-IN
           PERFORM             VARYING  WK-COUNT
             FROM CNS-1 BY CNS-1  UNTIL WK-COUNT > WK-YUUKOU-KENSUU
             OR  FLG-DATA = CNS-ON
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":ワーク異動計算パターンコード=["
      D             WK-IDO-CALC-PATTERN-CD"]"
      D    DISPLAY  CNS-PGM-ID ":ワーク市場通貨コード=["
      D             WK-SIJO-TUKA-CD"]"
      D    DISPLAY  CNS-PGM-ID ":ワーク証券種別コード=["
      D             WK-SEC-SBT-CD"]"
      D    DISPLAY  CNS-PGM-ID ":ワーク保有目的=["
      D             WK-POSSESSION-OBJECT"]"
      D    DISPLAY  CNS-PGM-ID ":ARG2.異動計算パターンコード=["
      D             IDO-CALC-PATTERN-CD OF  WK-TKPFPT01(WK-COUNT)"]"
      D    DISPLAY  CNS-PGM-ID ":ARG2.市場通貨コード=["
      D             SIJO-TUKA-CD        OF  WK-TKPFPT01(WK-COUNT)"]"
      D    DISPLAY  CNS-PGM-ID ":ARG2.証券種別コード=["
      D             SEC-SBT-CD          OF  WK-TKPFPT01(WK-COUNT)"]"
      D    DISPLAY  CNS-PGM-ID ":ARG2.保有目的=["
      D             POSSESSION-OBJECT   OF  WK-TKPFPT01(WK-COUNT)"]"
      D    DISPLAY  CNS-PGM-ID "IDX-CNT:=[" WK-COUNT "]"
      *-----------------------------------------------------------------
             IF    IDO-CALC-PATTERN-CD      OF   WK-TKPFPT01(WK-COUNT)
                =  WK-IDO-CALC-PATTERN-CD
              AND  SIJO-TUKA-CD         OF   WK-TKPFPT01(WK-COUNT)
                =  WK-SIJO-TUKA-CD
              AND  SEC-SBT-CD           OF   WK-TKPFPT01(WK-COUNT)
                =  WK-SEC-SBT-CD
              AND  POSSESSION-OBJECT    OF   WK-TKPFPT01(WK-COUNT)
                =  WK-POSSESSION-OBJECT
             THEN
               MOVE  WK-TKPFPT01        OF   WK-IN(WK-COUNT)
                 TO  PARM-SCZY4304-ARG3
               MOVE  CNS-ON             TO   FLG-DATA
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":ARG2.異動計算パターンデータ配列["
      D             WK-COUNT"]=["
      D             WK-TKPFPT01         OF   WK-IN(WK-COUNT)"]"
      D    DISPLAY  CNS-PGM-ID ":ARG3.異動計算パターン情報=["
      D             PARM-SCZY4304-ARG3"]"
      D    DISPLAY  CNS-PGM-ID ":取得フラグ=["FLG-DATA"]"
      *-----------------------------------------------------------------
             ELSE
               CONTINUE
             END-IF
           END-PERFORM
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 異動計算パターン取得処理 END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       IDO-CALC-GET-EXT.
           EXIT.
      ******************************************************************
      *   3.2.2.汎用異動計算パターン取得処理
      ******************************************************************
       HANYOU-IDO-GET-RTN     SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D        ":***  SCZY4304:汎用異動計算パターン取得処理  START  ***"
      D    DISPLAY  CNS-PGM-ID ":取得フラグ =[" FLG-DATA "]"
      *-----------------------------------------------------------------
           IF  FLG-DATA = CNS-OFF
           THEN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":ワーク市場通貨コード=["
      D             WK-SIJO-TUKA-CD"]"
      D    DISPLAY  CNS-PGM-ID ":ワーク証券種別コード=["WK-SEC-SBT-CD"]"
      D    DISPLAY  CNS-PGM-ID ":ワーク保有目的      =["
      D             WK-POSSESSION-OBJECT"]"
      *-----------------------------------------------------------------
             IF   WK-SIJO-TUKA-CD = CNS-D-ALL
              AND WK-SEC-SBT-CD = CNS-D-ALL
              AND WK-POSSESSION-OBJECT = CNS-X-9
             THEN
      *----------  エラー検知プログラム名
               MOVE  CNS-PGM-ID
                 TO  ERR-PGM-ID        OF  PARM-SCZY1170-ARG1
      *----------  ポジション
               MOVE  CNS-POS-004
                 TO  ERR-POS           OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
               MOVE  CNS-RTN-110
                 TO  RTN-CD            OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
               MOVE  ZERO
                 TO  DETAIL-RTN-CD        OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
               MOVE  CNS-DETAIL-1
                 TO  DETAIL-RTN-CD-FLG OF  PARM-SCZY1170-ARG1
      *---------- フリーメッセージ
               MOVE  "該当異動計算パターン情報なし"
                 TO  FREE-MSG   OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
               PERFORM  ERR-RTN
             ELSE
      *----------  1）保有目的汎用
               PERFORM  HANYOU-PSN-OBJ-RTN
      *----------  2）通貨＝$FORと指定された証券種別で検索
               PERFORM  SCH-SEC-SBT-RTN
      *----------  3）証券種別コード汎用
               PERFORM  HANYOU-SEC-SBT-RTN
      *----------  4）市場通貨コード汎用
               PERFORM  HANYOU-SIJOTUKA-RTN
             END-IF
           ELSE
      *----------  後処理
             PERFORM  END-RTN
           END-IF
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D          ":***  SCZY4304:汎用異動計算パターン取得処理  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       HANYOU-IDO-GET-EXT.
           EXIT.
      ******************************************************************
      *   1）保有目的汎用
      ******************************************************************
       HANYOU-PSN-OBJ-RTN     SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D             ":***  SCZY4304:保有目的汎用処理  START  ***"
      D    DISPLAY  CNS-PGM-ID ":ワーク保有目的      =["
      D             WK-POSSESSION-OBJECT"]"
      *-----------------------------------------------------------------
           IF  WK-POSSESSION-OBJECT NOT= CNS-X-9
           THEN
      *----------  ワーク保有目的
             MOVE  CNS-X-9             TO  WK-POSSESSION-OBJECT
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":ワーク保有目的      =["
      D             WK-POSSESSION-OBJECT"]"
      *-----------------------------------------------------------------
      *----------  異動計算パターンテーブルからデータを取得する
             PERFORM  IDO-CALC-GET-RTN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":取得フラグ =[" FLG-DATA "]"
      *-----------------------------------------------------------------
             IF  FLG-DATA = CNS-OFF
             THEN
               CONTINUE
             ELSE
      *----------  後処理
               PERFORM  END-RTN
             END-IF
           ELSE
             CONTINUE
           END-IF
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D                   ":***  SCZY4304:保有目的汎用処理  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       HANYOU-PSN-OBJ-EXT.
           EXIT.
      ******************************************************************
      *   2）通貨＝$FORと指定された証券種別で検索
      ******************************************************************
       SCH-SEC-SBT-RTN        SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D             ":***  SCZY4304:通貨＝$FORと指定された証券種別"
      D             "で検索  START  ***"
      D    DISPLAY  CNS-PGM-ID ":ワーク市場通貨コード=["
      D             WK-SIJO-TUKA-CD"]"
      D    DISPLAY  CNS-PGM-ID ":ワーク証券種別コード=["
      D             WK-SEC-SBT-CD"]"
      *-----------------------------------------------------------------
           IF  WK-SIJO-TUKA-CD     NOT = CNS-JPY
             AND  WK-SIJO-TUKA-CD  NOT = CNS-D-FORN
             AND  WK-SIJO-TUKA-CD  NOT = CNS-D-ALL
             AND  WK-SEC-SBT-CD    NOT = CNS-D-ALL
           THEN
      *----------  ワーク市場通貨コード
             MOVE  CNS-D-FORN          TO  WK-SIJO-TUKA-CD
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":ワーク市場通貨コード=["
      D             WK-SIJO-TUKA-CD"]"
      *-----------------------------------------------------------------
      *----------  異動計算パターンテーブルからデータを取得する
             PERFORM  IDO-CALC-GET-RTN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":取得フラグ =[" FLG-DATA "]"
      *-----------------------------------------------------------------
             IF  FLG-DATA = CNS-OFF
             THEN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":ARG1.市場通貨コード=["
      D             SIJO-TUKA-CD       OF  PARM-SCZY4304-ARG1"]"
      *-----------------------------------------------------------------
               MOVE  SIJO-TUKA-CD      OF  PARM-SCZY4304-ARG1
                 TO  WK-SIJO-TUKA-CD
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":ワーク市場通貨コード=["
      D             WK-SIJO-TUKA-CD"]"
      *-----------------------------------------------------------------
             ELSE
      *----------  後処理
               PERFORM  END-RTN
             END-IF
           ELSE
             CONTINUE
           END-IF
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D             ":***  SCZY4304:通貨＝$FORと指定された証券種別"
      D             "で検索  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       SCH-SEC-SBT-EXT.
           EXIT.
      ******************************************************************
      *   3）証券種別コード汎用
      ******************************************************************
       HANYOU-SEC-SBT-RTN     SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D             ":***  SCZY4304:証券種別コード汎用  START  ***"
      D    DISPLAY  CNS-PGM-ID ":ワーク証券種別コード=["WK-SEC-SBT-CD"]"
      *-----------------------------------------------------------------
           IF  WK-SEC-SBT-CD NOT= CNS-D-ALL
           THEN
      *----------  ワーク証券種別コード
             MOVE  CNS-D-ALL           TO  WK-SEC-SBT-CD
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":ワーク証券種別コード=["WK-SEC-SBT-CD"]"
      *-----------------------------------------------------------------
      *----------  異動計算パターンテーブルからデータを取得する
             PERFORM  IDO-CALC-GET-RTN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":取得フラグ =[" FLG-DATA "]"
      *-----------------------------------------------------------------
             IF  FLG-DATA = CNS-OFF
             THEN
               CONTINUE
             ELSE
      *----------  後処理
               PERFORM  END-RTN
             END-IF
           ELSE
             CONTINUE
           END-IF
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D                   ":***  SCZY4304:証券種別コード汎用  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       HANYOU-SEC-SBT-EXT.
           EXIT.
      ******************************************************************
      *   4）市場通貨コード汎用
      ******************************************************************
       HANYOU-SIJOTUKA-RTN    SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D             ":***  SCZY4304:市場通貨コード汎用  START  ***"
      D    DISPLAY  CNS-PGM-ID ":ワーク市場通貨コード=["
      D             WK-SIJO-TUKA-CD"]"
      *-----------------------------------------------------------------
           IF   WK-SIJO-TUKA-CD = CNS-JPY
             OR WK-SIJO-TUKA-CD = CNS-D-FORN
           THEN
      *----------  ワーク市場通貨コード
             MOVE  CNS-D-ALL           TO  WK-SIJO-TUKA-CD
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":ワーク市場通貨コード=["
      D             WK-SIJO-TUKA-CD"]"
      *-----------------------------------------------------------------
      *----------  異動計算パターンテーブルからデータを取得する
             PERFORM  IDO-CALC-GET-RTN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":取得フラグ =[" FLG-DATA "]"
      *-----------------------------------------------------------------
             IF  FLG-DATA = CNS-OFF
             THEN
      *--------  エラー検知プログラム名
               MOVE  CNS-PGM-ID
                 TO  ERR-PGM-ID        OF  PARM-SCZY1170-ARG1
      *--------  ポジション
               MOVE  CNS-POS-005
                 TO  ERR-POS           OF  PARM-SCZY1170-ARG1
      *--------  リターンコード
               MOVE  CNS-RTN-110
                 TO  RTN-CD            OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
               MOVE  ZERO
                 TO  DETAIL-RTN-CD        OF  PARM-SCZY1170-ARG1
      *--------  詳細リターンコードデコードフラグ
               MOVE  CNS-DETAIL-1
                 TO  DETAIL-RTN-CD-FLG OF  PARM-SCZY1170-ARG1
      *-------- フリーメッセージ
               MOVE  "該当異動計算パターン情報なし"
                 TO  FREE-MSG          OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
               PERFORM  ERR-RTN
             ELSE
      *----------  後処理
               PERFORM  END-RTN
             END-IF
           ELSE
      *----------  ワーク市場通貨コード
               MOVE  CNS-D-FORN          TO  WK-SIJO-TUKA-CD
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":ワーク市場通貨コード=["
      D             WK-SIJO-TUKA-CD"]"
      *-----------------------------------------------------------------
      *----------  異動計算パターンテーブルからデータを取得する
             PERFORM  IDO-CALC-GET-RTN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":取得フラグ =[" FLG-DATA "]"
      *-----------------------------------------------------------------
             IF  FLG-DATA = CNS-OFF
             THEN
      *----------  ワーク市場通貨コード
               MOVE  CNS-D-ALL         TO  WK-SIJO-TUKA-CD
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":ワーク市場通貨コード=["
      D             WK-SIJO-TUKA-CD"]"
      *-----------------------------------------------------------------
      *----------  異動計算パターンテーブルからデータを取得する
               PERFORM  IDO-CALC-GET-RTN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":取得フラグ判定6  START"
      D    DISPLAY  CNS-PGM-ID ":取得フラグ =[" FLG-DATA "]"
      *-----------------------------------------------------------------
               IF  FLG-DATA = CNS-OFF
               THEN
      *--------  エラー検知プログラム名
                 MOVE  CNS-PGM-ID
                   TO  ERR-PGM-ID        OF  PARM-SCZY1170-ARG1
      *--------  ポジション
                 MOVE  CNS-POS-006
                   TO  ERR-POS           OF  PARM-SCZY1170-ARG1
      *--------  リターンコード
                 MOVE  CNS-RTN-110
                   TO  RTN-CD            OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
               MOVE  ZERO
                 TO  DETAIL-RTN-CD        OF  PARM-SCZY1170-ARG1
      *--------  詳細リターンコードデコードフラグ
                 MOVE  CNS-DETAIL-1
                   TO  DETAIL-RTN-CD-FLG OF  PARM-SCZY1170-ARG1
      *-------- フリーメッセージ
                 MOVE  "該当異動計算パターン情報なし"
                   TO  FREE-MSG          OF  PARM-SCZY1170-ARG1
      *--------  エラー処理
                 PERFORM  ERR-RTN
               ELSE
      *--------  後処理
                 PERFORM  END-RTN
               END-IF
             ELSE
      *--------  後処理
               PERFORM  END-RTN
             END-IF
           END-IF
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D                   ":***  SCZY4304:市場通貨コード汎用  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       HANYOU-SIJOTUKA-EXT.
           EXIT.
      ******************************************************************
      *    3.3 後処理
      ******************************************************************
       END-RTN                SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  SCZY4304:後処理  START  ***"
      *-----------------------------------------------------------------
           INITIALIZE  PARM-RTNPKG
           MOVE  CNS-RTN-000
             TO  RTN-CD                OF  PARM-SCZY1170-IN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":ERR-PGM-ID  OF  PARM-SCZY1170-IN=["
      D             ERR-PGM-ID         OF  PARM-SCZY1170-IN"]"
      D    DISPLAY  CNS-PGM-ID ":ERR-POS  OF  PARM-SCZY1170-IN=["
      D             ERR-POS            OF  PARM-SCZY1170-IN"]"
      D    DISPLAY  CNS-PGM-ID ":RTN-CD  OF  PARM-SCZY1170-IN=["
      D             RTN-CD             OF  PARM-SCZY1170-IN"]"
      D    DISPLAY  CNS-PGM-ID ":DETAIL-RTN-CD  OF  PARM-SCZY1170-IN"
      D             "=["DETAIL-RTN-CD  OF  PARM-SCZY1170-IN "]"
      D    DISPLAY  CNS-PGM-ID ":DETAIL-RTN-CD-FLG  OF"
      D             "PARM-SCZY1170-IN=["
      D             DETAIL-RTN-CD-FLG  OF  PARM-SCZY1170-IN"]"
      D    DISPLAY  CNS-PGM-ID ":FREE-MSG  OF  PARM-SCZY1170-IN=["
      D             FREE-MSG           OF  PARM-SCZY1170-IN"]"
      D    DISPLAY  CNS-PGM-ID ":***  CALL  SCZY1170  START ***"
      *-----------------------------------------------------------------
           CALL   "SCZY1170"          USING  PARM-SCZY1170-IN
                                             PARM-RTNPKG
           MOVE  PARM-RTNPKG-ARG      TO  PARM-SCZY4304-ARG4
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  CALL  SCZY1170  START ***"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4304-ARG2=["
      D                          PARM-SCZY4304-ARG2"]"
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG-ARG   =["
      D                          PARM-RTNPKG-ARG"]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4304-ARG3=["
      D                          PARM-SCZY4304-ARG3"]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4304-ARG4=["
      D                          PARM-SCZY4304-ARG4"]"
      D    DISPLAY  CNS-PGM-ID ":***  SCZY4304:後処理  END  ***"
      D    DISPLAY  CNS-PGM-ID ":***  SCZY4304 "
      D                  "異動計算パターン取得ルーチン END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       END-EXT.
           GOBACK.
      ******************************************************************
      *    3.4 エラー処理
      ******************************************************************
       ERR-RTN                SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  SCZY4304:エラー処理  START  ***"
      D    DISPLAY  CNS-PGM-ID ":ERR-PGM-ID  OF  PARM-SCZY1170-IN=["
      D             ERR-PGM-ID         OF  PARM-SCZY1170-IN"]"
      D    DISPLAY  CNS-PGM-ID ":ERR-POS  OF  PARM-SCZY1170-IN=["
      D             ERR-POS            OF  PARM-SCZY1170-IN"]"
      D    DISPLAY  CNS-PGM-ID ":RTN-CD  OF  PARM-SCZY1170-IN=["
      D             RTN-CD             OF  PARM-SCZY1170-IN"]"
      D    DISPLAY  CNS-PGM-ID ":DETAIL-RTN-CD  OF  PARM-SCZY1170-IN"
      D             "=["DETAIL-RTN-CD  OF  PARM-SCZY1170-IN "]"
      D    DISPLAY  CNS-PGM-ID ":DETAIL-RTN-CD-FLG  OF"
      D             "PARM-SCZY1170-IN=["
      D             DETAIL-RTN-CD-FLG  OF  PARM-SCZY1170-IN"]"
      D    DISPLAY  CNS-PGM-ID ":FREE-MSG  OF  PARM-SCZY1170-IN=["
      D             FREE-MSG           OF  PARM-SCZY1170-IN"]"
      D    DISPLAY  CNS-PGM-ID ":***  CALL  SCZY1170  START ***"
      *-----------------------------------------------------------------
           CALL   "SCZY1170"          USING  PARM-SCZY1170-IN
                                             PARM-RTNPKG
           MOVE  PARM-RTNPKG-ARG      TO  PARM-SCZY4304-ARG4
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  CALL  SCZY1170  END ***"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4304-ARG2=["
      D                          PARM-SCZY4304-ARG2"]"
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG-ARG   =["
      D                          PARM-RTNPKG-ARG"]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4304-ARG3=["
      D                          PARM-SCZY4304-ARG3"]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4304-ARG4=["
      D                          PARM-SCZY4304-ARG4"]"
      D    DISPLAY  CNS-PGM-ID ":***  SCZY4304:エラー処理  END  ***"
      D    DISPLAY  CNS-PGM-ID ":***  SCZY4304 "
      D                      "異動計算パターン取得ルーチン END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       ERR-EXT.
           GOBACK.
