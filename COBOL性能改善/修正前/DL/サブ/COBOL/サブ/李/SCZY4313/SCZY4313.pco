      ******************************************************************
      *$Header$
      ******************************************************************
      *
      *     システムＩＤ   : T-STAR
      *     システム名称   : 次期TSTARシステム
      *     プログラムＩＤ : SCZY4313
      *     プログラム名称 : ポートフォリオ属性情報メモリ格納処理
      *     処理概要       : アーギュメントINより、ホスト表を利用して
      *                      ポートフォリオ属性テーブルからデータを
      *                      全件抽出（SELECT）して、メモリ配列に
      *                      格納する
      *     更新ＤＢ       : なし
      *     参照ＤＢ       : ポートフォリオ属性(TKPFZK01)
      *     引数           : IN  利用会社コード
      *                      IN  ファンドコード
      *                      IN  ポートフォリオコード
      *                      OUT ポートフォリオ属性データ
      *                      OUT 有効件数
      *     改訂履歴
      *     年月日     区分  所属       担当者  内容
      *     ---------- ----  ---------  ------  ------------------------
      *     20100107   新規  SCS        王蕾    新規作成
      *     20100603   修正  SCS        常峰    テーマ2678　ポートフォリオ属性TBLに項目追加
      ******************************************************************
      ******************************************************************
       IDENTIFICATION         DIVISION.
       PROGRAM-ID.    SCZY4313.
       AUTHOR.        Nomura.
       DATE-WRITTEN.  08-04-15.
       DATE-COMPILED.
      ******************************************************************
      *
      *    環  境  部
      *
      ******************************************************************
       ENVIRONMENT            DIVISION.
       CONFIGURATION          SECTION.
       SOURCE-COMPUTER.       L1000V.
      *SOURCE-COMPUTER.       L1000V   WITH DEBUGGING MODE.
       OBJECT-COMPUTER.       RP5470.
       INPUT-OUTPUT           SECTION.
       FILE-CONTROL.
      ******************************************************************
      *
      *    デ  ー  タ  部
      *
      ******************************************************************
       DATA                   DIVISION.
      ******************************************************************
      *    作  業  場  所  節
      ******************************************************************
       WORKING-STORAGE        SECTION.
       01  IDNAME                  PIC  X(80)  VALUE
       "$Id$".
      ******************************************************************
      *    コンスタント  エリア
      ******************************************************************
      *----------  共通コンスタント定義の宣言
       01  CONSTANT-AREA.
         05  CNS-SCZYCNS.
           COPY  "KOMONCNS.cpy".
           COPY  "SCZY_PFCM.cpy".
      *----------  プログラム名
         05  CNS-PGM-ID            PIC  X(08)  VALUE  "SCZY4313".
      *----------  ポートフォリオ属性テーブル名
         05  CNS-TBL-TKPFZK01      PIC  X(08)  VALUE  "TKPFZK01".
         05  CNS-0                 PIC  9(01)  VALUE  0.
      ******************************************************************
      *    ORACLE ホスト変数定義
      ******************************************************************
           EXEC  SQL  INCLUDE  SQLCA             END-EXEC.
           EXEC  SQL  INCLUDE  ORACA             END-EXEC.
           EXEC  SQL  BEGIN    DECLARE  SECTION  END-EXEC.
       01  HST-AREA.
         05  HST-TBL-TKPFZK01.
           10  HST-TKPFZK01                        OCCURS  101.
           EXEC  SQL  INCLUDE  'TKPFZK01.cpy'    END-EXEC.
         05  HST-KEY-TKPFZK01.
      *----------  利用会社コード
           10  H-RIYO-CMP-CD       PIC  X(04).
      *----------  ファンドコード
           10  H-FUND-CD           PIC  X(12).
      *----------  ポートフォリオコード
           10  H-PTF-CD            PIC  X(04).
      *----------  動的SQL文をいれるバッファ
         05  SQLSTMT               PIC  X(2500).
           EXEC  SQL  END  DECLARE  SECTION      END-EXEC.
      ******************************************************************
      *    フラグエリア
      ******************************************************************
       01  FLG-AREA.
      *----------  ポートフォリオ属性情報取得CURSOR OPEN FLAG
         05  FLG-CR1-OPEN          PIC  9(01).
      ******************************************************************
      *    サブプログラム  パラメ−タ  エリア
      ******************************************************************
      *----------  テーブルID取得ルーチンパラメータ
       01  PARM-SCZY1160-INOUT.
           COPY  "SCZY1160.cpy".
      *----------  リターンパッケージセットルーチン
       01  PARM-SCZY1170-IN.
           COPY  "SCZY1170.cpy".
      *----------  RTNパッケージコピー句
       01  PARM-RTNPKG.
         05  PARM-RTNPKG-ARG.
           COPY  "CZRTNPACKAGE.cpy".
      ******************************************************************
      *    SQL  エリア
      ******************************************************************
       01  SQL-AREA.
         05  SQL-SELECT-DATA.
           10  FILLER              PIC  X(07)
             VALUE  "SELECT ".
      *----------  利用会社コード
           10  FILLER              PIC  X(12)
             VALUE  "RIYO_CMP_CD,".
      *----------  ファンドコード
           10  FILLER              PIC  X(08)
             VALUE  "FUND_CD,".
      *----------  ポートフォリオコード
           10  FILLER              PIC  X(07)
             VALUE  "PTF_CD,".
      *----------  適用開始日
           10  FILLER              PIC  X(12)
             VALUE  "TEKIYO_FROM,".
      *----------  ファンド属性変更日
           10  FILLER              PIC  X(16)
             VALUE  "FUND_REVISE_YMD,".
      *----------  サブポートフォリオ区分
           10  FILLER              PIC  X(12)
             VALUE  "SUB_PTF_KBN,".
      *----------  ダミーポートフォリオ区分
           10  FILLER              PIC  X(14)
             VALUE  "DUMMY_PTF_KBN,".
      *----------  ファンド名(日)
           10  FILLER              PIC  X(12)
             VALUE  "FND_MEI_JPN,".
      *----------  ファンド名（英）
           10  FILLER              PIC  X(12)
             VALUE  "FND_MEI_ENG,".
      *----------  マネージャーコード
           10  FILLER              PIC  X(11)
             VALUE  "MANAGER_CD,".
      *----------  設定日
           10  FILLER              PIC  X(08)
             VALUE  "SET_YMD,".
      *----------  保有目的
           10  FILLER              PIC  X(18)
             VALUE  "POSSESSION_OBJECT,".
      *----------  決算回数
           10  FILLER              PIC  X(13)
             VALUE  "KESSAN_TIMES,".
      *----------  本決算月日
           10  FILLER              PIC  X(16)
             VALUE  "HON_KESSAN_MMDD,".
      *----------  決算月日１
           10  FILLER              PIC  X(13)
             VALUE  "KESSAN_MMDD1,".
      *----------  決算月日２
           10  FILLER              PIC  X(13)
             VALUE  "KESSAN_MMDD2,".
      *----------  決算月日３
           10  FILLER              PIC  X(13)
             VALUE  "KESSAN_MMDD3,".
      *----------  決算月日４
           10  FILLER              PIC  X(13)
             VALUE  "KESSAN_MMDD4,".
      *----------  決算休日処理区分
           10  FILLER              PIC  X(16)
             VALUE  "KESSAN_QJT_SYRI,".
      *----------  運用国コード
           10  FILLER              PIC  X(13)
             VALUE  "OPER_KUNI_CD,".
      *----------  運用通貨コード
           10  FILLER              PIC  X(16)
             VALUE  "CAPITAL_TUKA_CD,".
      *----------  資金出資者区分
           10  FILLER              PIC  X(16)
             VALUE  "SIKIN_SYUSI_KBN,".
      *----------  財産区分
           10  FILLER              PIC  X(11)
             VALUE  "ZAISAN_KBN,".
      *----------  信託銀行コード
           10  FILLER              PIC  X(12)
             VALUE  "TRST_BNK_ID,".
      *----------  契約番号
           10  FILLER              PIC  X(17)
             VALUE  "TRST_CONTRACT_NO,".
      *----------  契約日
           10  FILLER              PIC  X(19)
             VALUE  "TRST_CONTRACT_DATE,".
      *----------  料率パターンコード
           10  FILLER              PIC  X(19)
             VALUE  "COMMISSION_PATTERN,".
      *----------  運用区分１
           10  FILLER              PIC  X(16)
             VALUE  "TOKIN_UNYO_KBN1,".
      *----------  運用区分２
           10  FILLER              PIC  X(16)
             VALUE  "TOKIN_UNYO_KBN2,".
      *----------  運用区分３
           10  FILLER              PIC  X(16)
             VALUE  "TOKIN_UNYO_KBN3,".
      *----------  契約形態１
           10  FILLER              PIC  X(12)
             VALUE  "KEI_KEITAI1,".
      *----------  契約形態２
           10  FILLER              PIC  X(12)
             VALUE  "KEI_KEITAI2,".
      *----------  解約区分
           10  FILLER              PIC  X(10)
             VALUE  "CANCL_KBN,".
      *----------  解約日
           10  FILLER              PIC  X(10)
             VALUE  "CANCL_YMD,".
      *----------  年金私募投信区分
           10  FILLER              PIC  X(16)
             VALUE  "NENKIN_TRST_KBN,".
      *----------  投信証券種別
           10  FILLER              PIC  X(13)
             VALUE  "TRST_SEC_SBT,".
      *----------  投信銘柄コード
           10  FILLER              PIC  X(12)
             VALUE  "TRST_MEG_CD,".
      *----------  投信計算口数
           10  FILLER              PIC  X(17)
             VALUE  "TRST_CALC_KUTISU,".
      *----------  ノンカバー取引区分
           10  FILLER              PIC  X(11)
             VALUE  "NONCOV_KBN,".
      *----------  為替換算方法（買）
           10  FILLER              PIC  X(16)
             VALUE  "BUY_KANSAN_HASU,".
      *----------  為替換算方法（売）
           10  FILLER              PIC  X(18)
             VALUE  "SELL_KANNSAN_HASU,".
      *----------  評価換え方式
           10  FILLER              PIC  X(12)
             VALUE  "HYK_KAE_HOU,".
      *----------  約定為替換算時採用系列
           10  FILLER              PIC  X(12)
             VALUE  "YKJ_RATE_CD,".
      *----------  ポートフォリオ名（日）
           10  FILLER              PIC  X(12)
             VALUE  "PTF_MEI_JPN,".
      *----------  ポートフォリオ名（英）
           10  FILLER              PIC  X(12)
             VALUE  "PTF_MEI_ENG,".
      *----------  仕訳計算パターンコード
           10  FILLER              PIC  X(15)
             VALUE  "SWK_KJN_PTN_CD,".
      *----------  異動計算パターンコード
           10  FILLER              PIC  X(20)
             VALUE  "IDO_CALC_PATTERN_CD,".
      *----------  未収配当計上割合
           10  FILLER              PIC  X(20)
             VALUE  "MISYU_HAITOU_WARIAI,".
      *----------  利回り等端数
           10  FILLER              PIC  X(14)
             VALUE  "RIMAWARI_HASU,".
      *----------  カストディーフィー扱い区分
           10  FILLER              PIC  X(19)
             VALUE  "CUSTODY_FEE_ATUKAI,".
      *----------  利回り年率換算方法
           10  FILLER              PIC  X(20)
             VALUE  "RIMAWARI_KANSAN_HOU,".
      *---------- 外貨預金為替差損益
           10  FILLER              PIC  X(18)
             VALUE  "G_YOKIN_KWS_SNEKI,".
      *----------  月中決算報告区分（月次）
           10  FILLER              PIC  X(13)
             VALUE  "KS_RPT_MONTH,".
      *----------  月中決算報告区分（四半期）
           10  FILLER              PIC  X(15)
             VALUE  "KS_RPT_QUARTER,".
      *----------  外貨決済取引区分
           10  FILLER              PIC  X(17)
             VALUE  "GAIKA_KESSAI_KBN,".
      *----------  日次評価法フラグ
           10  FILLER              PIC  X(15)
             VALUE  "NITIJI_HYK_FLG,".
      *----------  約定主義会計フラグ
           10  FILLER              PIC  X(17)
             VALUE  "YKJ_SYUGI_KAIKEI,".
      *----------  約定主義会計適用開始日
           10  FILLER              PIC  X(21)
             VALUE  "YKJ_SYUGI_KAIKEI_YMD,".
      *----------  ＧＸ債券キャッシュフロー
           10  FILLER              PIC  X(14)
             VALUE  "CASH_FLOW_KBN,".
      *----------  経過利息計上レート区分
           10  FILLER              PIC  X(09)
             VALUE  "KJY_RATE,".
      *----------  特別イベント日１
           10  FILLER              PIC  X(11)
             VALUE  "EVENT_YMD1,".
      *----------  特別イベント日２
           10  FILLER              PIC  X(11)
             VALUE  "EVENT_YMD2,".
      *----------  特別イベント日３
           10  FILLER              PIC  X(11)
             VALUE  "EVENT_YMD3,".
      *----------  備考
           10  FILLER              PIC  X(06)
             VALUE  "BIKOU,".
      *----------  削除区分
           10  FILLER              PIC  X(11)
             VALUE  "SAKUJO_KBN,".
      *----------  元ファンドコード
           10  FILLER              PIC  X(13)
             VALUE  "MOTO_FUND_CD,".
      *----------  元ポートフォリオコード
           10  FILLER              PIC  X(12)
             VALUE  "MOTO_PTF_CD,".
      *----------  更新日時
           10  FILLER              PIC  X(37)
             VALUE  "TO_CHAR(UPD_DATE,'YYYYMMDDHH24MISS'),".
      *----------  更新ログインユーザ会社コード
           10  FILLER              PIC  X(17)
             VALUE  "UPD_LOGIN_CMP_CD,".
      *----------  更新ユーザID
           10  FILLER              PIC  X(08)
             VALUE  "UPD_SID,".
      *----------  更新プログラムID
           10  FILLER              PIC  X(10)
             VALUE  "UPD_PGMID,".
      *----------  合成ファンド区分
           10  FILLER              PIC  X(11)
             VALUE  "COM_FND_KBN".
      *----------  FROM
           10  FILLER              PIC  X(06)
             VALUE  " FROM ".
           10  SQL-TKPFZK01        PIC  X(13).
      *----------  WHERE
           10  FILLER              PIC  X(07)
             VALUE  " WHERE ".
      *----------  利用会社コード
           10  FILLER              PIC  X(17)
               VALUE  "RIYO_CMP_CD = :V1".
      *----------  ファンドコード
           10  FILLER              PIC  X(18)
               VALUE  " AND FUND_CD = :V2".
      *----------  ポートフォリオコード
           10  FILLER              PIC  X(17)
               VALUE  " AND PTF_CD = :V3".
      *----------  削除区分
           10  FILLER              PIC  X(20)
               VALUE  " AND SAKUJO_KBN =' '".
      *----------  ORDER BY
           10  FILLER              PIC  X(10)
               VALUE  " ORDER BY ".
      *----------  適用開始日
           10  FILLER              PIC  X(11)
               VALUE  "TEKIYO_FROM".
      ******************************************************************
      *                ワ−ク  エリア
      ******************************************************************
       01  WORK-AREA.
      *----------  ワークのフリーメッセージ
         05  WK-SQLSTMT            PIC  X(2500).
      ******************************************************************
      *    連  絡  節
      ******************************************************************
       LINKAGE                SECTION.
       01  PARM-SCZY4313-INOUT.
           COPY  "SCZY4313.cpy".
       01  PARM-SCZY4313-RTNPKG.
         05  PARM-SCZY4313-ARG3.
           COPY  "CZRTNPACKAGE.cpy".
      ******************************************************************
      *
      *    手 続 き 部
      *
      ******************************************************************
       PROCEDURE              DIVISION
           USING  PARM-SCZY4313-INOUT
                  PARM-SCZY4313-RTNPKG.
      ******************************************************************
      *    メイン処理
      ******************************************************************
       BASE-RTN               SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** SCZY4313 "
      D                "ポートフォリオ属性情報メモリ格納処理 START ***"
      *-----------------------------------------------------------------
      *----------  3.1 前処理
           PERFORM  INIT-RTN
      *----------  3.2 主処理
           PERFORM  MAIN-RTN
      *----------  3.3 後処理
           PERFORM  END-RTN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** SCZY4313 "
      D                  "ポートフォリオ属性情報メモリ格納処理 END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       BASE-EXT.
           GOBACK.
      ******************************************************************
      *    3.1 前処理
      ******************************************************************
       INIT-RTN               SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  前処理  START  ***"
      *-----------------------------------------------------------------
      *----------  3.1.1 初期化処理
           PERFORM  INITIALIZE-RTN
      *----------  3.1.2 パラメータチェック
           PERFORM  PARM-CHK-RTN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  前処理  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       INIT-EXT.
           EXIT.
      ******************************************************************
      *    3.1.1.初期化処理
      ******************************************************************
       INITIALIZE-RTN         SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 初期化処理 START ***"
      *-----------------------------------------------------------------
           INITIALIZE  FLG-AREA
                       SQL-AREA
                       PARM-RTNPKG
                       HST-AREA
                       WORK-AREA
                       PARM-SCZY1160-INOUT
                       PARM-SCZY1170-IN
                       PARM-SCZY4313-ARG2
                       PARM-SCZY4313-RTNPKG
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":FLG-AREA =[" FLG-AREA "]"
      D    DISPLAY  CNS-PGM-ID ":SQL-AREA =[" SQL-AREA "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":HST-AREA =["
      D                         HST-AREA "]"
      D    DISPLAY  CNS-PGM-ID ":WORK-AREA =[" WORK-AREA "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY1160-INOUT =["
      D                         PARM-SCZY1160-INOUT "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY1170-IN =["
      D                         PARM-SCZY1170-IN  "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4313-ARG2 =["
      D                         PARM-SCZY4313-ARG2 "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4313-RTNPKG =["
      D                         PARM-SCZY4313-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":*** 初期化処理 END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       INITIALIZE-EXT.
           EXIT.
      ******************************************************************
      *    3.1.2.パラメータチェック
      ******************************************************************
       PARM-CHK-RTN           SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** パラメータチェック START ***"
      D    DISPLAY  CNS-PGM-ID ":利用会社コード =["
      D             RIYO-CMP-CD   OF  PARM-SCZY4313-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":ファンドコード =["
      D             FUND-CD   OF  PARM-SCZY4313-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":ポートフォリオコード =["
      D             PTF-CD   OF  PARM-SCZY4313-ARG1 "]"
      *-----------------------------------------------------------------
           IF  RIYO-CMP-CD      OF  PARM-SCZY4313-ARG1 <> SPACE
             AND FUND-CD        OF  PARM-SCZY4313-ARG1 <> SPACE
             AND PTF-CD         OF  PARM-SCZY4313-ARG1 <> SPACE
           THEN
             CONTINUE
           ELSE
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*****  パラメータ  エラー  *****"
      *-----------------------------------------------------------------
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID          OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-001
               TO  ERR-POS             OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-800
               TO  RTN-CD              OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  ZERO
               TO  DETAIL-RTN-CD       OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-0
               TO  DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
             MOVE  CNS-PARM-ERR
               TO  FREE-MSG            OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** パラメータチェック END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       PARM-CHK-EXT.
           EXIT.
      ******************************************************************
      *    3.2.主処理
      ******************************************************************
       MAIN-RTN               SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  主処理  START  ***"
      *-----------------------------------------------------------------
      *----------  3.2.1.テーブルID取得処理
           PERFORM  TBL-ID-GET-RTN
      *----------  3.2.2.ポートフォリオ属性情報取得処理
           PERFORM  TKPFZK01-GET-RTN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  主処理  END  ***"
      *-----------------------------------------------------------------
           CONTINUE.
       MAIN-EXT.
           EXIT.
      ******************************************************************
      *    3.2.1.テーブルID取得処理
      ******************************************************************
       TBL-ID-GET-RTN         SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** SCZY1160:"
      D                       "テーブルID取得処理 START *** "
      *-----------------------------------------------------------------
           INITIALIZE  PARM-RTNPKG
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      *-----------------------------------------------------------------
      *----------  利用会社コード
           MOVE  RIYO-CMP-CD           OF  PARM-SCZY4313-ARG1
             TO  RIYO-CMP-CD           OF  PARM-SCZY1160-ARG1
      *----------  テーブルID
           MOVE  CNS-TBL-TKPFZK01
             TO  TBL-ID                OF  PARM-SCZY1160-ARG1(1)
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY1160-ARG1:"
      D    DISPLAY  CNS-PGM-ID ":利用会社コード =["
      D              RIYO-CMP-CD       OF PARM-SCZY1160-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":テーブルID(1)  =["
      D              TBL-ID            OF PARM-SCZY1160-ARG1(1) "]"
      D    DISPLAY  CNS-PGM-ID ":***  CALL SCZY1160 START  ***"
      *-----------------------------------------------------------------
           CALL  "SCZY1160"            USING  PARM-SCZY1160-INOUT
                                              PARM-RTNPKG
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":***  CALL SCZY1160 END  ***"
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG=[" PARM-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":RTN-CD OF SCZY1160=["
      D              RTN-CD OF PARM-RTNPKG(COUNTER  OF  PARM-RTNPKG) "]"
      D    DISPLAY  CNS-PGM-ID ":COUNTER  OF  PARM-RTNPKG=["
      D                          COUNTER  OF  PARM-RTNPKG "]"
      *-----------------------------------------------------------------
           IF  RTN-CD OF PARM-RTNPKG(COUNTER  OF  PARM-RTNPKG) =
               CNS-RTN-000
           THEN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*****  テーブルID正常取得  *****"
      D    DISPLAY  CNS-PGM-ID ":TKPFZK01 ID =["
      D             HENSYUGO-TBL-ID    OF  PARM-SCZY1160-ARG2(1) "]"
      *-----------------------------------------------------------------
             MOVE  HENSYUGO-TBL-ID     OF  PARM-SCZY1160-ARG2(1)
               TO  SQL-TKPFZK01
             INITIALIZE  PARM-RTNPKG
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      *-----------------------------------------------------------------
           ELSE
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*****  テーブルID取得エラー  *****"
      *-----------------------------------------------------------------
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID          OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-002
               TO  ERR-POS             OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-901
               TO  RTN-CD              OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  ZERO
               TO  DETAIL-RTN-CD       OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-0
               TO  DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
             MOVE  SPACE
               TO  FREE-MSG            OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY CNS-PGM-ID ":*** SCZY1160:テーブルID取得処理  END"
      D                       " ***"
      *-----------------------------------------------------------------
           CONTINUE.
       TBL-ID-GET-EXT.
           EXIT.
      ******************************************************************
      *    3.2.2.ポートフォリオ属性情報取得処理
      ******************************************************************
       TKPFZK01-GET-RTN       SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D                ":*** ポートフォリオ属性情報取得処理 START *** "
      *-----------------------------------------------------------------
      *----------  ポートフォリオ属性情報取得CURSOR PREPARE
           PERFORM  CR1-PREPARE-RTN
      *----------  ポートフォリオ属性情報取得CURSOR OPEN
           PERFORM  CR1-OPEN-RTN
      *----------  ポートフォリオ属性情報取得CURSOR FETCH
           PERFORM  CR1-FETCH-RTN
      *----------  ポートフォリオ属性情報取得CURSOR CLOSE
           PERFORM  CR1-CLOSE-RTN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D                  ":*** ポートフォリオ属性情報取得処理 END *** "
      *-----------------------------------------------------------------
           CONTINUE.
       TKPFZK01-GET-EXT.
           EXIT.
      ******************************************************************
      *    ポートフォリオ属性情報取得CURSOR PREPARE
      ******************************************************************
       CR1-PREPARE-RTN        SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** ポートフォリオ属性情報取得"
      D                       "CURSOR PREPARE START ***"
      D    DISPLAY  CNS-PGM-ID
      D             ":*** ポートフォリオ属性情報取得SQL文作成 START***"
      *-----------------------------------------------------------------
      *----------  ポートフォリオ属性情報取得動的SQL文作成
           MOVE  RIYO-CMP-CD     OF    PARM-SCZY4313-ARG1
             TO  H-RIYO-CMP-CD
           MOVE  FUND-CD         OF    PARM-SCZY4313-ARG1
             TO  H-FUND-CD
           MOVE  PTF-CD          OF    PARM-SCZY4313-ARG1
             TO  H-PTF-CD
           MOVE  SQL-SELECT-DATA       TO  SQLSTMT
           STRING SQLSTMT(1:FUNCTION  LENGTH(SQL-SELECT-DATA))
                    "[:V1="
                     H-RIYO-CMP-CD
                    ":V2="
                     H-FUND-CD
                    ":V3="
                     H-PTF-CD
                    "]"
                    DELIMITED BY SIZE
                    INTO  WK-SQLSTMT
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":WK-SQLSTMT=[" WK-SQLSTMT "]"
      D    DISPLAY  CNS-PGM-ID ":H-RIYO-CMP-CD=[" H-RIYO-CMP-CD "]"
      D    DISPLAY  CNS-PGM-ID ":H-FUND-CD=[" H-FUND-CD "]"
      D    DISPLAY  CNS-PGM-ID ":H-PTF-CD=[" H-PTF-CD "]"
      D    DISPLAY  CNS-PGM-ID ":削除区分 ' ' "
      D    DISPLAY  CNS-PGM-ID ":SQLSTMT=[" SQLSTMT "]"
      D    DISPLAY  CNS-PGM-ID
      D               ":*** ポートフォリオ属性情報取得SQL文作成END ***"
      *-----------------------------------------------------------------
      *----------  ポートフォリオ属性情報取得CURSOR OPEN
      *----------  PREPARE CURSOR
           EXEC  SQL
             PREPARE  S1  FROM  :SQLSTMT
           END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":ポートフォリオ属性情報取得"
      D             "CURSOR PREPARE SQLCODE =[" SQLCODE "]"
      *-----------------------------------------------------------------
           IF  SQLCODE = CNS-SQL-NORMAL
           THEN
             CONTINUE
           ELSE
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID          OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-003
               TO  ERR-POS             OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-900
               TO  RTN-CD              OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  SQLCODE
               TO  DETAIL-RTN-CD       OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-1
               TO  DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
             MOVE  WK-SQLSTMT
               TO  FREE-MSG            OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *----------DECLARE CURSOR
           EXEC  SQL
             DECLARE  CR1  CURSOR  FOR  S1
           END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D             ":ポートフォリオ属性情報取得CURSOR DECLARE SQLCODE"
      D                        "=[" SQLCODE "]"
      D    DISPLAY  CNS-PGM-ID ":*** ポートフォリオ属性情報取得"
      D                        "CURSOR PREPARE END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       CR1-PREPARE-EXT.
           EXIT.
      ******************************************************************
      *    ポートフォリオ属性情報取得CURSOR OPEN
      ******************************************************************
       CR1-OPEN-RTN           SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** ポートフォリオ属性情報取得"
      D                       "CURSOR OPEN START ***"
      *-----------------------------------------------------------------
      *----------OPEN    CURSOR
           EXEC  SQL
             OPEN  CR1         USING :H-RIYO-CMP-CD,
                                     :H-FUND-CD,
                                     :H-PTF-CD
           END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D                ":ポートフォリオ属性情報取得CURSOR OPEN SQLCODE"
      D                        "=["SQLCODE "]"
      *-----------------------------------------------------------------
           IF  SQLCODE  =  CNS-SQL-NORMAL
           THEN
             MOVE  CNS-ON              TO  FLG-CR1-OPEN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":CURSOR OPEN 正常"
      D    DISPLAY  CNS-PGM-ID ":CR1-OPEN-FLAG =[" FLG-CR1-OPEN "]"
      *-----------------------------------------------------------------
           ELSE
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID          OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-004
               TO  ERR-POS             OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-900
               TO  RTN-CD              OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  SQLCODE
               TO  DETAIL-RTN-CD       OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-1
               TO  DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
             MOVE  WK-SQLSTMT
               TO  FREE-MSG            OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** ポートフォリオ属性情報取得"
      D                        "CURSOR OPEN END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       CR1-OPEN-EXT.
           EXIT.
      ******************************************************************
      *    ポートフォリオ属性情報取得CURSOR FETCH
      ******************************************************************
       CR1-FETCH-RTN          SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** ポートフォリオ属性情報取得"
      D                       "CURSOR FETCH START ***"
      *-----------------------------------------------------------------
      *----------  初期化
           INITIALIZE  HST-TBL-TKPFZK01
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":HST-TBL-TKPFZK01 =["
      D                          HST-TBL-TKPFZK01 "]"
      *-----------------------------------------------------------------
      *----------  CR1 FETCH
           EXEC SQL
             FETCH CR1
             INTO  :HST-TKPFZK01
           END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D               ":ポートフォリオ属性情報取得CURSOR FETCH SQLCODE"
      D                        " =[" SQLCODE "]"
      D    DISPLAY  CNS-PGM-ID
      D                  ":ポートフォリオ属性情報取得CURSOR FETCH 明細"
      D                        " =[" SQLERRD(3) "]"
      *-----------------------------------------------------------------
           EVALUATE  SQLCODE
      *----------  正常
             WHEN  CNS-SQL-NORMAL
      *----------  エラー検知プログラム名
                 MOVE  CNS-PGM-ID
                   TO  ERR-PGM-ID      OF  PARM-SCZY1170-ARG1
      *----------  ポジション
                 MOVE  CNS-POS-005
                   TO  ERR-POS         OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
                 MOVE  CNS-RTN-002
                   TO  RTN-CD          OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
                 MOVE  SQLCODE
                   TO  DETAIL-RTN-CD   OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
                 MOVE  CNS-DETAIL-1
                   TO  DETAIL-RTN-CD-FLG
                                       OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
                 MOVE  WK-SQLSTMT
                   TO  FREE-MSG        OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
                 PERFORM  ERR-RTN
             WHEN  CNS-SQL-NOTFOUND
               IF  SQLERRD(3)  =  CNS-0
               THEN
      *----------  エラー検知プログラム名
                 MOVE  CNS-PGM-ID
                   TO  ERR-PGM-ID      OF  PARM-SCZY1170-ARG1
      *----------  ポジション
                 MOVE  CNS-POS-006
                   TO  ERR-POS         OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
                 MOVE  CNS-RTN-110
                   TO  RTN-CD          OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
                 MOVE  SQLCODE
                   TO  DETAIL-RTN-CD   OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
                 MOVE  CNS-DETAIL-1
                   TO  DETAIL-RTN-CD-FLG
                                       OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
                 MOVE  WK-SQLSTMT
                   TO  FREE-MSG        OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
                 PERFORM  ERR-RTN
               ELSE
                 MOVE  HST-TBL-TKPFZK01
                   TO  TBL-TKPFZK01-1  OF PARM-SCZY4313-ARG2
                 MOVE  SQLERRD(3)
                   TO  YUUKOU-KENSUU   OF PARM-SCZY4313-ARG2
               END-IF
             WHEN  OTHER
      *----------  エラー検知プログラム名
               MOVE  CNS-PGM-ID
                 TO  ERR-PGM-ID        OF  PARM-SCZY1170-ARG1
      *----------  ポジション
               MOVE  CNS-POS-007
                 TO  ERR-POS           OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
               MOVE  CNS-RTN-900
                 TO  RTN-CD            OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
               MOVE  SQLCODE
                 TO  DETAIL-RTN-CD     OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
               MOVE  CNS-DETAIL-1
                 TO  DETAIL-RTN-CD-FLG OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
               MOVE  WK-SQLSTMT
                 TO  FREE-MSG          OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
               PERFORM  ERR-RTN
           END-EVALUATE
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** ポートフォリオ属性情報取得"
      D                        "CURSOR FETCH END ***"
      D    DISPLAY  CNS-PGM-ID ":有効件数=["
      D                        YUUKOU-KENSUU OF PARM-SCZY4313-ARG2 "]"
      *-----------------------------------------------------------------
           CONTINUE.
       CR1-FETCH-EXT.
           EXIT.
      ******************************************************************
      *    ポートフォリオ属性情報取得CURSOR CLOSE
      ******************************************************************
       CR1-CLOSE-RTN          SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** ポートフォリオ属性情報取得"
      D                       "CURSOR CLOSE START ***"
      D    DISPLAY  CNS-PGM-ID ":FLG-CR1-OPEN= [" FLG-CR1-OPEN "]"
      *-----------------------------------------------------------------
           EXEC  SQL
             CLOSE CR1
           END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID
      D               ":ポートフォリオ属性情報取得CURSOR CLOSE SQLCODE"
      D                          " =[" SQLCODE "]"
      *-----------------------------------------------------------------
           IF  SQLCODE = CNS-SQL-NORMAL
           THEN
             MOVE  CNS-OFF           TO  FLG-CR1-OPEN
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":FLG-CR1-OPEN =[" FLG-CR1-OPEN "]"
      *-----------------------------------------------------------------
           ELSE
      *----------  エラー検知プログラム名
             MOVE  CNS-PGM-ID
               TO  ERR-PGM-ID        OF  PARM-SCZY1170-ARG1
      *----------  ポジション
             MOVE  CNS-POS-008
               TO  ERR-POS           OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
             MOVE  CNS-RTN-900
               TO  RTN-CD            OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
             MOVE  SQLCODE
               TO  DETAIL-RTN-CD     OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
             MOVE  CNS-DETAIL-1
               TO  DETAIL-RTN-CD-FLG OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
             MOVE  WK-SQLSTMT
               TO  FREE-MSG          OF  PARM-SCZY1170-ARG1
      *----------  エラー処理
             PERFORM  ERR-RTN
           END-IF
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** ポートフォリオ属性情報取得"
      D                        "CURSOR CLOSE END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       CR1-CLOSE-EXT.
           EXIT.
      ******************************************************************
      *    3.3 後処理
      ******************************************************************
       END-RTN                SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** 後処理 START ***"
      *-----------------------------------------------------------------
           MOVE  CNS-RTN-000
             TO  RTN-CD                OF  PARM-SCZY1170-ARG1
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** CALL SCZY1170 START ***"
      *-----------------------------------------------------------------
           CALL   "SCZY1170"           USING  PARM-SCZY1170-IN
                                              PARM-RTNPKG
           MOVE  PARM-RTNPKG           TO  PARM-SCZY4313-ARG3
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** CALL SCZY1170 END ***"
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4313-ARG2 =["
      D             PARM-SCZY4313-ARG2 "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4313-ARG3 =["
      D             PARM-SCZY4313-ARG3 "]"
      D    DISPLAY  CNS-PGM-ID ":*** 後処理 END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       END-EXT.
           EXIT.
      ******************************************************************
      *    3.4 エラー処理
      ******************************************************************
       ERR-RTN                SECTION.
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** エラー処理 START ***"
      D    DISPLAY  CNS-PGM-ID ":FLG-CR1-OPEN =[" FLG-CR1-OPEN "]"
      *-----------------------------------------------------------------
           IF  FLG-CR1-OPEN = CNS-ON
           THEN
             EXEC  SQL
                 CLOSE CR1
             END-EXEC
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":SQLCODE(CLOSE) =[" SQLCODE "]"
      *-----------------------------------------------------------------
             MOVE  CNS-OFF             TO  FLG-CR1-OPEN
           ELSE
             CONTINUE
           END-IF
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":FLG-CR1-OPEN =[" FLG-CR1-OPEN "]"
      D    DISPLAY  CNS-PGM-ID ":エラー検知プログラム名 =["
      D             ERR-PGM-ID         OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":ポジション =["
      D             ERR-POS            OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":リターンコード =["
      D             RTN-CD             OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":詳細リターンコード =["
      D             DETAIL-RTN-CD      OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":詳細リターンコードデコードフラグ =["
      D             DETAIL-RTN-CD-FLG  OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":フリーメッセージ =["
      D             FREE-MSG           OF  PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  CNS-PGM-ID ":*** CALL SCZY1170 START ***"
      *-----------------------------------------------------------------
           CALL   "SCZY1170"           USING  PARM-SCZY1170-IN
                                              PARM-RTNPKG
           MOVE  PARM-RTNPKG           TO  PARM-SCZY4313-ARG3
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  CNS-PGM-ID ":*** CALL SCZY1170 END ***"
      D    DISPLAY  CNS-PGM-ID ":PARM-RTNPKG =[" PARM-RTNPKG "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4313-ARG2 =["
      D             PARM-SCZY4313-ARG2 "]"
      D    DISPLAY  CNS-PGM-ID ":PARM-SCZY4313-ARG3 =["
      D             PARM-SCZY4313-ARG3 "]"
      D    DISPLAY  CNS-PGM-ID ":*** エラー処理 END ***"
      D    DISPLAY  CNS-PGM-ID ":*** SCZY4313 END ***"
      *-----------------------------------------------------------------
           CONTINUE.
       ERR-EXT.
           GOBACK.
