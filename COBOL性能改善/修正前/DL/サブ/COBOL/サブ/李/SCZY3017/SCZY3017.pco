      ******************************************************************
      *$Header: /TSTAR2/apl/debug/JC/MAKE/src/bt/sub/SCZY3017/SCZY3017.pco,v 1.17 2008/10/30 14:47:51 isc061 Exp $
      ******************************************************************
      *
      *     システムＩＤ   : TSTAR
      *     システム名称   : 次期TSTARシステム
      *     プログラム名称 : 為替換算情報取得ルーチン
      *     処理概要       : 為替換算情報取得ルーチン
      *     参照ＤＢ       : 為替換算情報TBL(TKJCKK00)
      *     引数           : IN  利用会社コード
      *                    : IN  為替系列コード
      *                    : IN  ベース通貨コード
      *                    : IN  目的通貨コード
      *                    : OUT  媒介通貨
      *                    : OUT  乗算/除算区分
      *     改訂履歴
      *     年月日     区分  所属       担当者  内容
      *     ---------- ----  ---------  ------  ------------------------
      *     20080423   新規  ITS        劉  海  新規作成
      ******************************************************************
      ******************************************************************
       IDENTIFICATION         DIVISION.
       PROGRAM-ID.    SCZY3017.
       AUTHOR.        Nomura.
       DATE-WRITTEN.  08-04-23.
       DATE-COMPILED.
      ******************************************************************
      *
      *                 環  境  部
      *
      ******************************************************************
       ENVIRONMENT            DIVISION.
       CONFIGURATION          SECTION.
       SOURCE-COMPUTER.       L1000V.
      *SOURCE-COMPUTER.       L1000V   WITH DEBUGGING MODE.
       OBJECT-COMPUTER.       RP5470.
       INPUT-OUTPUT           SECTION.
       FILE-CONTROL.
      ******************************************************************
      *
      *                 デ  ー  タ  部
      *
      ******************************************************************
       DATA                   DIVISION.
       WORKING-STORAGE        SECTION.
       01  IDNAME                  PIC  X(80)  VALUE
       "$Id: SCZY3017.pco,v 1.17 2008/10/30 14:47:51 isc061 Exp $".
      ******************************************************************
      *                   コンスタント  エリア
      ******************************************************************
       01  CONSTANT-AREA.
      *-----------  エラー検知プログラム名
         05  CNS-PROGRAM-ID        PIC  X(08)  VALUE  "SCZY3017".
         05  CNS-TBL-ID            PIC  X(08)  VALUE  "TKJCKK00".
      *----------  TSTAR共通用コンスタント
         05  CNS-AREA.
           COPY  "JCCNS.cpy".
      *----------  TSTAR業務コードコンスタント
         05  CNS-JCGYOMU.
           COPY "JCGYOMU.cpy".
      *----------  TSTAR業務MESSAGEコンスタント
         05  CNS-JCMSG.
           COPY "JCMSG.cpy".
      ******************************************************************
      *                フラグエリア
      ******************************************************************
       01  FLAG-AREA.
      *----------  カーソル1 CLOSE判定フラグ
         05  FLG-CR-CLS-01         PIC  X(01).
      *----------  カーソル2 CLOSE判定フラグ
         05  FLG-CR-CLS-02         PIC  X(01).
      ******************************************************************
      *                 ワ−クエリア
      ******************************************************************
       01  WK-AREA.
         05  WK-TUKA-CD1           PIC  X(04).
         05  WK-TUKA-CD2           PIC  X(04).
         05  WK-CUR1-CLOSE-POS     PIC  X(04).
         05  WK-CUR2-CLOSE-POS     PIC  X(04).
      *----------  テープルIDワーク
         05  WK-TBL-ID             PIC  X(13).
      *----------  ワークRTNパッケージ
       01  WK-RTNPKG.
         05  WK-PARM-RTNPKG.
           COPY "CZRTNPACKAGE.cpy".
      *
      ******************************************************************
      *                メッセージエリア
      ******************************************************************
       01  MSG-AREA.
      *----------  パラメータ存在エラー
         05  MSG-SONZAI.
      *----------  パラメータ利用会社コード
           10  FILLER              PIC  X(13)  VALUE "RIYO-CMP-CD=[".
           10  MSG-RIYO-CMP-CD     PIC  X(04).
      *----------  パラメータ為替系列コード
           10  FILLER              PIC  X(14)  VALUE "]KWS-KRTU-CD=[".
           10  MSG-KWS-KRTU-CD     PIC  X(04).
      *----------  パラメータベース通貨コード
           10  FILLER              PIC  X(15)  VALUE "]BASE-TUKA-CD=[".
           10  MSG-BASE-TUKA-CD    PIC  X(04).
      *----------  パラメータ目的通貨コード
           10  FILLER              PIC  X(19)
                                   VALUE "]MOKUTEKI-TUKA-CD=[".
           10  MSG-MOKUTEKI-TUKA-CD
                                   PIC  X(04).
           10  FILLER              PIC  X(01)  VALUE "]".
      ******************************************************************
      *                 サブプログラム  パラメータ  エリア
      ******************************************************************
      *----------  【SCZY1170】リターンパッケージセットルーチン
       01  PARM-SCZY1170-INOUT.
      *----------  リターンパッケージセットルーチンコピー句
           COPY  "SCZY1170.cpy".
       01  PARM-SCZY1170-RTNPKG.
         05  PARM-SCZY1170-ARG2.
           COPY  "CZRTNPACKAGE.cpy".
      *----------  SCZY1160 テーブルID取得ルーチン
       01  PARM-SCZY1160-INOUT.
      *----------  テーブルID取得ルーチンコピー句
           COPY  "SCZY1160.cpy".
      ******************************************************************
      *                ＳＱＬ通信領域
      ******************************************************************
           EXEC  SQL  INCLUDE  SQLCA  END-EXEC.
           EXEC  SQL  BEGIN    DECLARE  SECTION  END-EXEC.
      *----------  動的SQL文をいれるバッファ
       01  ORA-WORKING-AREA.
         05  SQLSTMT1              PIC  X(5000) VARYING.
      *----------  コードインスタンスTBL構造体
         05  H-SQL-SEL-WHERE.
      *----------  利用会社コード
           10  H-RIYO-CMP-CD       PIC  X(04).
      *----------  為替系列コード
           10  H-KWS-KRTU-CD       PIC  X(04).
      *----------  通貨コード1
           10  H-TUKA-CD-1         PIC  X(04).
      *----------  通貨コード2
           10  H-TUKA-CD-2         PIC  X(04).
      *----------  DBレイアウト
         05  H-TBL.
           EXEC  SQL  INCLUDE  'TKJCKK00.cpy'    END-EXEC.
           EXEC  SQL  END      DECLARE  SECTION  END-EXEC.
      ******************************************************************
      *                 連  絡  節
      ******************************************************************
       LINKAGE                SECTION.
       01  PARM-SCZY3017-INOUT.
      *----------   為替換算情報取得ルーチンコピー句
           COPY "SCZY3017.cpy".
       01  PARM-SCZY3017-RTNPKG.
         05  PARM-SCZY3017-ARG3.
      *----------  RTNパッケージ
           COPY "CZRTNPACKAGE.cpy".
      ******************************************************************
      *
      *        PROCEDURE      DIVISION
      *
      ******************************************************************
       PROCEDURE              DIVISION
           USING  PARM-SCZY3017-INOUT
                  PARM-SCZY3017-RTNPKG.
      ******************************************************************
      *                 メイン処理
      ******************************************************************
       PRG-SCZY3017-RTN       SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  SCZY3017処理  START  ***"
      *-----------------------------------------------------------------
      *----------  前処理
           PERFORM  PRG-START-RTN
      *
      *----------  主処理
           PERFORM  PRG-MAIN-RTN
      *
      *----------  後処理
           PERFORM  PRG-END-RTN.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  SCZY3017処理  END  ***".
      *-----------------------------------------------------------------
       PRG-SCZY3017-EXT.
           GOBACK.
      ******************************************************************
      *                前処理
      ******************************************************************
       PRG-START-RTN          SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  前処理  START  ***"
      *-----------------------------------------------------------------
      *----------  初期化処理
           PERFORM  PRG-INIT-RTN
      *----------  通貨コード判定
           PERFORM  APL-TUKA-CD-RTN
      *----------  SCZY1160 テーブルID取得
           PERFORM  APL-GET-TBLID-RTN
      *----------  ワーク通貨コード１ ← ARG1.ベース通貨コード
           MOVE  BASE-TUKA-CD         OF  PARM-SCZY3017-ARG1
                                      TO  WK-TUKA-CD1
      *----------  ワーク通貨コード２ ← ARG1.目的通貨コード
           MOVE  MOKUTEKI-TUKA-CD     OF  PARM-SCZY3017-ARG1
                                      TO  WK-TUKA-CD2
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "ワーク通貨コード１ = "  WK-TUKA-CD1
      D    DISPLAY  "ワーク通貨コード２ = "  WK-TUKA-CD2
      *-----------------------------------------------------------------
      *----------  カーソル1 PREPARE処理
           PERFORM  DB-CUR1-PREPARE-RTN.
      *
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  前処理  END  ***".
      *-----------------------------------------------------------------
       PRG-START-EXT.
           EXIT.
      ******************************************************************
      *                 初期化処理
      ******************************************************************
       PRG-INIT-RTN           SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "初期化処理開始"
      *-----------------------------------------------------------------
           INITIALIZE         WK-AREA
                              WK-RTNPKG
                              FLAG-AREA
                              ORA-WORKING-AREA
                              PARM-SCZY1170-INOUT
                              PARM-SCZY1170-RTNPKG
                              PARM-SCZY1160-INOUT
                              PARM-SCZY3017-ARG2
                              PARM-SCZY3017-ARG3
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "利用会社コード = "  RIYO-CMP-CD
      D                               OF  PARM-SCZY3017-ARG1
      D    DISPLAY  "為替系列コード = "  KWS-KRTU-CD
      D                               OF  PARM-SCZY3017-ARG1
      D    DISPLAY  "ベース通貨コード = "  BASE-TUKA-CD
      D                               OF  PARM-SCZY3017-ARG1
      D    DISPLAY  "目的通貨コード  = "  MOKUTEKI-TUKA-CD
      D                               OF  PARM-SCZY3017-ARG1
      *-----------------------------------------------------------------
           IF  (
      *----------  ARG1の利用会社コード未入力の場合
                  RIYO-CMP-CD         OF  PARM-SCZY3017-ARG1 = SPACE
      *----------  ARG1のベース通貨コード未入力の場合
             OR   BASE-TUKA-CD        OF  PARM-SCZY3017-ARG1 = SPACE
      *----------  ARG1の目的通貨コード未入力の場合
             OR   MOKUTEKI-TUKA-CD    OF  PARM-SCZY3017-ARG1 = SPACE  )
           THEN
      *----------  プログラム名
               MOVE  CNS-PROGRAM-ID   TO  ERR-PGM-ID
                                      OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
               MOVE  CNS-RTN-800      OF  CNS-AREA
                                      TO  RTN-CD
                                      OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコード
               MOVE  ZERO             TO  DETAIL-RTN-CD
                                      OF  PARM-SCZY1170-INOUT
      *----------  詳細リターンコードデコード
               MOVE  CNS-DETAIL-0     OF  CNS-AREA
                                      TO  DETAIL-RTN-CD-FLG
                                      OF  PARM-SCZY1170-ARG1
      *----------  パラメータ利用会社コード
               MOVE  RIYO-CMP-CD      OF  PARM-SCZY3017-ARG1
                                      TO  MSG-RIYO-CMP-CD
      *----------  パラメータ内部銘柄コード
               MOVE  KWS-KRTU-CD      OF  PARM-SCZY3017-ARG1
                                      TO  MSG-KWS-KRTU-CD
      *----------  パラメータ適用開始日
               MOVE  BASE-TUKA-CD     OF  PARM-SCZY3017-ARG1
                                      TO  MSG-BASE-TUKA-CD
      *----------  パラメータ作成フラグ
               MOVE  MOKUTEKI-TUKA-CD OF  PARM-SCZY3017-ARG1
                                      TO  MSG-MOKUTEKI-TUKA-CD
      *----------  ポジション
               MOVE  CNS-POS-001      OF  CNS-AREA
                                      TO  ERR-POS
                                      OF  PARM-SCZY1170-ARG1
      *----------  FREE-MSG
               MOVE  MSG-PARM-ERR     TO  FREE-MSG
                                      OF  PARM-SCZY1170-ARG1
               MOVE  MSG-SONZAI       TO  FREE-MSG
                                      OF  PARM-SCZY1170-ARG1(101:78)
               PERFORM  COM-ERR-RTN
           ELSE
               CONTINUE
           END-IF.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "初期化処理終了".
      *-----------------------------------------------------------------
       PRG-INIT-EXT.
           EXIT.
      ******************************************************************
      *                 主処理
      ******************************************************************
       PRG-MAIN-RTN           SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  主処理  START  ***"
      *-----------------------------------------------------------------
      *----------  為替換算情報取得処理１
           PERFORM  APL-GET-KWS-KT1-RTN
      *----------  為替換算情報1取得出来ないの場合、為替換算情報取得処理２をコール
           IF  (  SQLCODE             =   CNS-SQL-NOTFOUND   )
           THEN
      *----------  為替換算情報取得処理２
               PERFORM  APL-GET-KWS-KT2-RTN
           ELSE
               CONTINUE
           END-IF.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  主処理  END  ***".
      *-----------------------------------------------------------------
       PRG-MAIN-EXT.
           EXIT.
      ******************************************************************
      *                後処理
      ******************************************************************
       PRG-END-RTN            SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***   後処理  START  ***"
      *-----------------------------------------------------------------
           MOVE  CNS-POS-012          TO  WK-CUR1-CLOSE-POS
      *----------  カーソル１クローズ処理
           PERFORM  DB-CUR1-CLOSE-RTN
           MOVE  CNS-POS-013      TO  WK-CUR2-CLOSE-POS
      *----------  カーソル2クローズ処理
           PERFORM  DB-CUR2-CLOSE-RTN
      *----------  リターンコード
           MOVE  CNS-RTN-000          TO  RTN-CD
                                      OF  PARM-SCZY1170-ARG1
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "RTN-CD           OF  PARM-SCZY1170-ARG1         =["
      D              RTN-CD           OF  PARM-SCZY1170-ARG1         "]"
      *-----------------------------------------------------------------
      *----------  リターンパッケージセットルーチン
           PERFORM  APL-SET-RTNPKG-RTN
      *----------  戻したRTNパッケージをARG2にセットする
           MOVE  PARM-SCZY1170-RTNPKG TO  PARM-SCZY3017-RTNPKG.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***   後処理  END  ***".
      *-----------------------------------------------------------------
       PRG-END-EXT.
           GOBACK.
      ******************************************************************
      *                 エラー処理
      ******************************************************************
       COM-ERR-RTN            SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  エラー処理  START  ***"
      *-----------------------------------------------------------------
           MOVE  CNS-POS-014          TO  WK-CUR1-CLOSE-POS
      *----------  カーソル1クローズ処理
           PERFORM  DB-CUR1-CLOSE-RTN
           MOVE  CNS-POS-015          TO  WK-CUR2-CLOSE-POS
      *----------  カーソル2クローズ処理
           PERFORM  DB-CUR2-CLOSE-RTN
      *----------  リターンパッケージセットルーチン
           PERFORM  APL-SET-RTNPKG-RTN
      *----------  戻したRTNパッケージをARG2にセットする
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "PARM-SCZY1170-RTNPKG"
      *-----------------------------------------------------------------
           MOVE  PARM-SCZY1170-RTNPKG TO  PARM-SCZY3017-RTNPKG.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***   エラー処理  END  ***".
      *-----------------------------------------------------------------
       COM-ERR-EXT.
           GOBACK.
      ******************************************************************
      *                 カーソル PREPARE処理
      ******************************************************************
       DB-CUR1-PREPARE-RTN    SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "カーソル1 PREPARE処理 開始"
      *-----------------------------------------------------------------
      *----------  SQL文作成
           MOVE  1                    TO  SQLSTMT1-LEN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "SQLSTMT1-LEN     =[" SQLSTMT1-LEN "]"
      *-----------------------------------------------------------------
      *----------  ＳＱＬ文作成
           PERFORM  DB-SQL-SAKUSEI-RTN
      *----------  SQL文 PREPARE
           EXEC SQL
               PREPARE S01  FROM :SQLSTMT1
           END-EXEC
           STRING
                   "[:V1="
                   H-RIYO-CMP-CD      OF  H-SQL-SEL-WHERE "]"
                   "[:V2="
                   H-KWS-KRTU-CD      OF  H-SQL-SEL-WHERE "]"
                   "[:V3="
                   H-TUKA-CD-1        OF  H-SQL-SEL-WHERE "]"
                   "[:V4="
                   H-TUKA-CD-2        OF  H-SQL-SEL-WHERE "]"
                   DELIMITED BY SIZE
                   INTO  SQLSTMT1-ARR
                   WITH  POINTER  SQLSTMT1-LEN
           END-STRING
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "SQLSTMT1-LEN  =["  SQLSTMT1-LEN  "]"
      D    DISPLAY  "SQLSTMT1-ARR  =["  SQLSTMT1-ARR(1:SQLSTMT1-LEN) "]"
      D    DISPLAY  "SQLCODE(S01 PREPARE)=["SQLCODE  "]"
      *-----------------------------------------------------------------
           EVALUATE  SQLCODE
      *----------  正常の場合
             WHEN  CNS-SQL-NORMAL
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "カーソル1 PREPARE処理 成功"
      *-----------------------------------------------------------------
               CONTINUE
             WHEN  OTHER
      *----------  検知プログラムID
               MOVE  CNS-PROGRAM-ID   TO  ERR-PGM-ID
                                      OF  PARM-SCZY1170-INOUT
      *----------  ポジション
               MOVE  CNS-POS-003      TO  ERR-POS
                                      OF  PARM-SCZY1170-INOUT
      *----------  リターンコード
               MOVE  CNS-RTN-900      TO  RTN-CD
                                      OF  PARM-SCZY1170-INOUT
      *----------  詳細リターンコード
               MOVE  SQLCODE          TO  DETAIL-RTN-CD
                                      OF  PARM-SCZY1170-INOUT
      *----------  詳細リターンコードデコードフラグ
               MOVE  CNS-DETAIL-1     TO  DETAIL-RTN-CD-FLG
                                      OF  PARM-SCZY1170-INOUT
      *----------  フリーメッセージ
               MOVE  SQLSTMT1-ARR     TO  FREE-MSG
                                      OF  PARM-SCZY1170-INOUT
               PERFORM  COM-ERR-RTN
           END-EVALUATE.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "カーソル1 PREPARE処理 END".
      *-----------------------------------------------------------------
       DB-CUR1-PREPARE-EXT.
           EXIT.
      ******************************************************************
      *                  カーソルCUR  OPEN
      ******************************************************************
       DB-CUR1-OPEN-RTN       SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "****  カーソル1CUR  OPEN  START  ****"
      *-----------------------------------------------------------------
           EXEC SQL
               DECLARE CR01 CURSOR  FOR S01
           END-EXEC
      *---------- カーソルCUR OPEN
           EXEC  SQL
             OPEN  CR01
             USING  :H-SQL-SEL-WHERE.H-RIYO-CMP-CD,
                    :H-SQL-SEL-WHERE.H-KWS-KRTU-CD,
                    :H-SQL-SEL-WHERE.H-TUKA-CD-1,
                    :H-SQL-SEL-WHERE.H-TUKA-CD-2
           END-EXEC
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "SQLCODE =["SQLCODE"]"
      *-----------------------------------------------------------------
           EVALUATE  SQLCODE
      *----------  SQLCODE＝0(正常)の場合
             WHEN  CNS-SQL-NORMAL     OF  CNS-AREA
                 MOVE  CNS-ON         OF  CNS-AREA
                                      TO  FLG-CR-CLS-01
             WHEN  OTHER
      *----------  検知プログラムID
                 MOVE  CNS-PROGRAM-ID TO  ERR-PGM-ID
                                      OF  PARM-SCZY1170-ARG1
      *---------- リターンコード
                 MOVE  CNS-RTN-900    OF  CNS-AREA
                   TO  RTN-CD         OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコード
                 MOVE  CNS-DETAIL-1   OF  CNS-AREA
                                      TO  DETAIL-RTN-CD-FLG
                                      OF  PARM-SCZY1170-ARG1
                 MOVE  CNS-POS-005    OF  CNS-AREA
      *----------  ポジション
                                      TO  ERR-POS
                                      OF  PARM-SCZY1170-ARG1
                 MOVE  SQLCODE        TO  DETAIL-RTN-CD
                                      OF  PARM-SCZY1170-ARG1
                 MOVE  SQLSTMT1-ARR   TO  FREE-MSG
                                      OF  PARM-SCZY1170-ARG1
                 PERFORM  COM-ERR-RTN
           END-EVALUATE.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "****  カーソル1CUR  OPEN  END  ****".
      *-----------------------------------------------------------------
       DB-CUR1-OPEN-EXT.
           EXIT.
      ******************************************************************
      *                 為替換算情報取得処理１
      ******************************************************************
       DB-CUR1-FETCH-RTN      SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "為替換算情報取得処理１ 開始"
      *-----------------------------------------------------------------
      *----------  カーソル FETCH
           EXEC SQL
               FETCH  CR01
                INTO :H-TBL.BAYIKAYI-TUKA,
                     :H-TBL.JZ-JS-KBN
           END-EXEC
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "BAYIKAYI-TUKA    =[" BAYIKAYI-TUKA
      D                               OF  H-TBL "]"
      D    DISPLAY  "JZ-JS-KBN        =[" JZ-JS-KBN
      D                               OF  H-TBL "]"
      D    DISPLAY  "SQLCODE(FETCH)   =[" SQLCODE  "]"
      *-----------------------------------------------------------------
           EVALUATE  SQLCODE
      *----------  正常の場合
             WHEN  CNS-SQL-NORMAL
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "カーソル1 FETCH処理 成功"
      *-----------------------------------------------------------------
      *----------  ARG2.媒介通貨 ← 取得した媒介通貨
               MOVE  BAYIKAYI-TUKA    OF  H-TBL
                                      TO  BAYIKAYI-TUKA
                                      OF  PARM-SCZY3017-ARG2
               EVALUATE  ( JZ-JS-KBN  OF  H-TBL  )
      *----------  取得した乗算/除算区分＝'0'(乗算)の場合
                 WHEN  CNS-JYOUZAN
      *----------  ARG2.乗算/除算区分 ← '1'(乗算)
                     MOVE  CNS-JYOUZAN-OUT
                                      TO  JZ-JS-KBN
                                      OF  PARM-SCZY3017-ARG2
      *----------  取得した乗算/除算区分＝'1'(除算)の場合
                 WHEN  CNS-JYOZAN
      *----------  ARG2.乗算/除算区分 ← '2'(除算)
                     MOVE  CNS-JYOZAN-OUT
                                      TO  JZ-JS-KBN
                                      OF  PARM-SCZY3017-ARG2
                 WHEN  OTHER
                     CONTINUE
               END-EVALUATE
      *----------  データなしの場合
             WHEN  CNS-SQL-NOTFOUND
               CONTINUE
             WHEN  OTHER
      *----------  検知プログラムID
               MOVE  CNS-PROGRAM-ID   TO  ERR-PGM-ID
                                      OF  PARM-SCZY1170-INOUT
      *----------  ポジション
               MOVE  CNS-POS-006      TO  ERR-POS
                                      OF  PARM-SCZY1170-INOUT
      *----------  リターンコード
               MOVE  CNS-RTN-900      TO  RTN-CD
                                      OF  PARM-SCZY1170-INOUT
      *----------  詳細リターンコード
               MOVE  SQLCODE          TO  DETAIL-RTN-CD
                                      OF  PARM-SCZY1170-INOUT
      *----------  詳細リターンコードデコードフラグ
               MOVE  CNS-DETAIL-1     TO  DETAIL-RTN-CD-FLG
                                      OF  PARM-SCZY1170-INOUT
      *----------  フリーメッセージ
               MOVE  SQLSTMT1-ARR     TO  FREE-MSG
                                      OF  PARM-SCZY1170-INOUT
               PERFORM  COM-ERR-RTN
           END-EVALUATE.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "為替換算情報取得処理１ 終了".
      *-----------------------------------------------------------------
       DB-CUR1-FETCH-EXT.
           EXIT.
      ******************************************************************
      *                 カーソル PREPARE処理
      ******************************************************************
       DB-CUR2-PREPARE-RTN    SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "カーソル2 PREPARE処理 開始"
      *-----------------------------------------------------------------
      *
      *----------  ＳＱＬ文作成
           PERFORM  DB-SQL-SAKUSEI-RTN
      *----------  SQL文 PREPARE
           EXEC SQL
               PREPARE S02  FROM :SQLSTMT1
           END-EXEC
           STRING
                   "[:V1="
                   H-RIYO-CMP-CD      OF  H-SQL-SEL-WHERE "]"
                   "[:V2="
                   H-KWS-KRTU-CD      OF  H-SQL-SEL-WHERE "]"
                   "[:V3="
                   H-TUKA-CD-1        OF  H-SQL-SEL-WHERE "]"
                   "[:V4="
                   H-TUKA-CD-2        OF  H-SQL-SEL-WHERE "]"
                   DELIMITED BY SIZE
                   INTO  SQLSTMT1-ARR
                   WITH  POINTER  SQLSTMT1-LEN
           END-STRING
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "SQLSTMT1-LEN  =["  SQLSTMT1-LEN  "]"
      D    DISPLAY  "SQLSTMT1-ARR  =["  SQLSTMT1-ARR(1:SQLSTMT1-LEN) "]"
      D    DISPLAY  "SQLCODE(S02 PREPARE)=["SQLCODE  "]"
      *-----------------------------------------------------------------
           EVALUATE  SQLCODE
      *----------  正常の場合
             WHEN  CNS-SQL-NORMAL
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "カーソル2 PREPARE処理 成功"
      *-----------------------------------------------------------------
               CONTINUE
             WHEN  OTHER
      *----------  検知プログラムID
               MOVE  CNS-PROGRAM-ID   TO  ERR-PGM-ID
                                      OF  PARM-SCZY1170-INOUT
      *----------  ポジション
               MOVE  CNS-POS-007      TO  ERR-POS
                                      OF  PARM-SCZY1170-INOUT
      *----------  リターンコード
               MOVE  CNS-RTN-900      TO  RTN-CD
                                      OF  PARM-SCZY1170-INOUT
      *----------  詳細リターンコード
               MOVE  SQLCODE          TO  DETAIL-RTN-CD
                                      OF  PARM-SCZY1170-INOUT
      *----------  詳細リターンコードデコードフラグ
               MOVE  CNS-DETAIL-1     TO  DETAIL-RTN-CD-FLG
                                      OF  PARM-SCZY1170-INOUT
      *----------  フリーメッセージ
               MOVE  SQLSTMT1-ARR     TO  FREE-MSG
                                      OF  PARM-SCZY1170-INOUT
               PERFORM  COM-ERR-RTN
           END-EVALUATE.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "カーソル2 PREPARE処理 END".
      *-----------------------------------------------------------------
       DB-CUR2-PREPARE-EXT.
           EXIT.
      ******************************************************************
      *                  カーソルCUR  OPEN
      ******************************************************************
       DB-CUR2-OPEN-RTN       SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "****  カーソル2CUR  OPEN  START  ****"
      *-----------------------------------------------------------------
           EXEC SQL
               DECLARE CR02 CURSOR  FOR S02
           END-EXEC
      *---------- カーソルCUR OPEN
           EXEC  SQL
             OPEN  CR02
             USING  :H-SQL-SEL-WHERE.H-RIYO-CMP-CD,
                    :H-SQL-SEL-WHERE.H-KWS-KRTU-CD,
                    :H-SQL-SEL-WHERE.H-TUKA-CD-1,
                    :H-SQL-SEL-WHERE.H-TUKA-CD-2
           END-EXEC
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "SQLCODE =["SQLCODE"]"
      *-----------------------------------------------------------------
           EVALUATE  SQLCODE
      *----------  SQLCODE＝0(正常)の場合
             WHEN  CNS-SQL-NORMAL     OF  CNS-AREA
                 MOVE  CNS-ON         OF  CNS-AREA
                                      TO  FLG-CR-CLS-02
             WHEN  OTHER
      *----------  検知プログラムID
                 MOVE  CNS-PROGRAM-ID TO  ERR-PGM-ID
                                      OF  PARM-SCZY1170-ARG1
      *---------- リターンコード
                 MOVE  CNS-RTN-900    OF  CNS-AREA
                   TO  RTN-CD         OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコード
                 MOVE  CNS-DETAIL-1   OF  CNS-AREA
                                      TO  DETAIL-RTN-CD-FLG
                                      OF  PARM-SCZY1170-ARG1
                 MOVE  CNS-POS-009    OF  CNS-AREA
      *----------  ポジション
                                      TO  ERR-POS
                                      OF  PARM-SCZY1170-ARG1
                 MOVE  SQLCODE        TO  DETAIL-RTN-CD
                                      OF  PARM-SCZY1170-ARG1
                 MOVE  SQLSTMT1-ARR   TO  FREE-MSG
                                      OF  PARM-SCZY1170-ARG1
                 PERFORM  COM-ERR-RTN
           END-EVALUATE.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "****  カーソル2CUR  OPEN  END  ****".
      *-----------------------------------------------------------------
       DB-CUR2-OPEN-EXT.
           EXIT.
      ******************************************************************
      *                 為替換算情報取得処理2
      ******************************************************************
       DB-CUR2-FETCH-RTN      SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "為替換算情報取得処理2 開始"
      *-----------------------------------------------------------------
      *----------  カーソル FETCH
           EXEC SQL
               FETCH  CR02
                INTO :H-TBL.BAYIKAYI-TUKA,
                     :H-TBL.JZ-JS-KBN
           END-EXEC
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "BAYIKAYI-TUKA    =[" BAYIKAYI-TUKA
      D                               OF  H-TBL "]"
      D    DISPLAY  "JZ-JS-KBN        =[" JZ-JS-KBN
      D                               OF  H-TBL "]"
      D    DISPLAY  "SQLCODE(FETCH)   =[" SQLCODE  "]"
      *-----------------------------------------------------------------
           EVALUATE  SQLCODE
      *----------  正常の場合
             WHEN  CNS-SQL-NORMAL
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "カーソル2 FETCH処理 成功"
      *-----------------------------------------------------------------
      *----------  ARG2.媒介通貨 ← 取得した媒介通貨
               MOVE  BAYIKAYI-TUKA    OF  H-TBL
                                      TO  BAYIKAYI-TUKA
                                      OF  PARM-SCZY3017-ARG2
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "BEFORE 乗算/除算区分 = " JZ-JS-KBN  OF  H-TBL
      *-----------------------------------------------------------------
               EVALUATE  ( JZ-JS-KBN  OF  H-TBL  )
      *----------  取得した乗算/除算区分＝'0'(乗算)の場合
                 WHEN  CNS-JYOUZAN
      *----------  ARG2.乗算/除算区分 ← '2'(除算)
                     MOVE  CNS-JYOZAN-OUT
                                      TO  JZ-JS-KBN
                                      OF  PARM-SCZY3017-ARG2
      *----------  取得した乗算/除算区分＝'1'(除算)の場合
                 WHEN  CNS-JYOZAN
      *----------  ARG2.乗算/除算区分 ← '1'(乗算)
                     MOVE  CNS-JYOUZAN-OUT
                                      TO  JZ-JS-KBN
                                      OF  PARM-SCZY3017-ARG2
                 WHEN  OTHER
                     CONTINUE
               END-EVALUATE
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "AFTER 乗算/除算区分 = " JZ-JS-KBN
      D                               OF  PARM-SCZY3017-ARG2
      *-----------------------------------------------------------------
      *----------  データなしの場合
             WHEN  CNS-SQL-NOTFOUND
      *----------  検知プログラムID
               MOVE  CNS-PROGRAM-ID   TO  ERR-PGM-ID
                                      OF  PARM-SCZY1170-INOUT
      *----------  ポジション
               MOVE  CNS-POS-010      TO  ERR-POS
                                      OF  PARM-SCZY1170-INOUT
      *----------  リターンコード
               MOVE  CNS-RTN-110      TO  RTN-CD
                                      OF  PARM-SCZY1170-INOUT
      *----------  詳細リターンコード
               MOVE  SQLCODE          TO  DETAIL-RTN-CD
                                      OF  PARM-SCZY1170-INOUT
      *----------  詳細リターンコードデコードフラグ
               MOVE  CNS-DETAIL-0     TO  DETAIL-RTN-CD-FLG
                                      OF  PARM-SCZY1170-INOUT
      *----------  フリーメッセージ
               MOVE  MSG-KAWASE-KANSAN-DEKINAI
                                      TO  FREE-MSG
                                      OF  PARM-SCZY1170-INOUT
               PERFORM  COM-ERR-RTN
             WHEN  OTHER
      *----------  検知プログラムID
               MOVE  CNS-PROGRAM-ID   TO  ERR-PGM-ID
                                      OF  PARM-SCZY1170-INOUT
      *----------  ポジション
               MOVE  CNS-POS-011      TO  ERR-POS
                                      OF  PARM-SCZY1170-INOUT
      *----------  リターンコード
               MOVE  CNS-RTN-900      TO  RTN-CD
                                      OF  PARM-SCZY1170-INOUT
      *----------  詳細リターンコード
               MOVE  SQLCODE          TO  DETAIL-RTN-CD
                                      OF  PARM-SCZY1170-INOUT
      *----------  詳細リターンコードデコードフラグ
               MOVE  CNS-DETAIL-1     TO  DETAIL-RTN-CD-FLG
                                      OF  PARM-SCZY1170-INOUT
      *----------  フリーメッセージ
               MOVE  SQLSTMT1-ARR     TO  FREE-MSG
                                      OF  PARM-SCZY1170-INOUT
               PERFORM  COM-ERR-RTN
           END-EVALUATE.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "為替換算情報取得処理2 終了".
      *-----------------------------------------------------------------
       DB-CUR2-FETCH-EXT.
           EXIT.
      ******************************************************************
      *                 カーソル1クローズ処理
      ******************************************************************
       DB-CUR1-CLOSE-RTN      SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "カーソル 1CLOSE処理  START"
      *-----------------------------------------------------------------
           IF  FLG-CR-CLS-01          =   CNS-ON
           THEN
      *----------  カーソル CLOSE
               EXEC SQL
                   CLOSE CR01
               END-EXEC
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "SQLCODE(CLOSE)=[" SQLCODE  "]"
      *-----------------------------------------------------------------
               MOVE  CNS-OFF          TO  FLG-CR-CLS-01
               EVALUATE  SQLCODE
                 WHEN  CNS-SQL-NORMAL
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "カーソル 1CLOSE 成功"
      *-----------------------------------------------------------------
                   CONTINUE
                 WHEN  OTHER
      *----------  検知プログラムID
                   MOVE  CNS-PROGRAM-ID
                                      TO  ERR-PGM-ID
                                      OF  PARM-SCZY1170-INOUT
      *----------  ポジション
                   MOVE  WK-CUR1-CLOSE-POS
                                      TO  ERR-POS
                                      OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
                   MOVE  CNS-RTN-900  TO  RTN-CD
                                      OF  PARM-SCZY1170-INOUT
      *----------  詳細リターンコード
                   MOVE  SQLCODE      TO  DETAIL-RTN-CD
                                      OF  PARM-SCZY1170-INOUT
      *----------  詳細リターンコードデコードフラグ
                   MOVE  CNS-DETAIL-1 TO  DETAIL-RTN-CD-FLG
                                      OF  PARM-SCZY1170-INOUT
      *----------  フリーメッセージ
                   MOVE  SQLSTMT1-ARR TO  FREE-MSG
                                      OF  PARM-SCZY1170-INOUT
                   PERFORM  COM-ERR-RTN
               END-EVALUATE
           ELSE
               CONTINUE
           END-IF.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "カーソル1CLOSE処理  END".
      *-----------------------------------------------------------------
       DB-CUR1-CLOSE-EXT.
           EXIT.
      ******************************************************************
      *                 カーソル2クローズ処理
      ******************************************************************
       DB-CUR2-CLOSE-RTN      SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "カーソル 2CLOSE処理  START"
      *-----------------------------------------------------------------
           IF  FLG-CR-CLS-02          =   CNS-ON
           THEN
      *----------  カーソル CLOSE
               EXEC SQL
                   CLOSE CR02
               END-EXEC
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "SQLCODE(CLOSE)=[" SQLCODE  "]"
      *-----------------------------------------------------------------
               MOVE  CNS-OFF          TO  FLG-CR-CLS-02
               EVALUATE  SQLCODE
                 WHEN  CNS-SQL-NORMAL
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "カーソル 2CLOSE 成功"
      *-----------------------------------------------------------------
                   CONTINUE
                 WHEN  OTHER
      *----------  検知プログラムID
                   MOVE  CNS-PROGRAM-ID
                                      TO  ERR-PGM-ID
                                      OF  PARM-SCZY1170-INOUT
      *----------  ポジション
                   MOVE  WK-CUR2-CLOSE-POS
                                      TO  ERR-POS
                                      OF  PARM-SCZY1170-ARG1
      *----------  リターンコード
                   MOVE  CNS-RTN-900  TO  RTN-CD
                                      OF  PARM-SCZY1170-INOUT
      *----------  詳細リターンコード
                   MOVE  SQLCODE      TO  DETAIL-RTN-CD
                                      OF  PARM-SCZY1170-INOUT
      *----------  詳細リターンコードデコードフラグ
                   MOVE  CNS-DETAIL-1 TO  DETAIL-RTN-CD-FLG
                                      OF  PARM-SCZY1170-INOUT
      *----------  フリーメッセージ
                   MOVE  SQLSTMT1-ARR TO  FREE-MSG
                                      OF  PARM-SCZY1170-INOUT
                   PERFORM  COM-ERR-RTN
               END-EVALUATE
           ELSE
               CONTINUE
           END-IF.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "カーソル 2CLOSE処理  END".
      *-----------------------------------------------------------------
       DB-CUR2-CLOSE-EXT.
           EXIT.
      ******************************************************************
      *                  カーソルCUR  OPEN
      ******************************************************************
       DB-SQL-SAKUSEI-RTN     SECTION.
      *----------  SQL文作成
           INITIALIZE         SQLSTMT1
           INITIALIZE         H-SQL-SEL-WHERE
           MOVE  1                    TO  SQLSTMT1-LEN
      *----------  利用会社コード
           MOVE  RIYO-CMP-CD          OF  PARM-SCZY3017-ARG1
                                      TO  H-RIYO-CMP-CD
                                      OF  H-SQL-SEL-WHERE
      *----------  為替系列コード
           MOVE  KWS-KRTU-CD          OF  PARM-SCZY3017-ARG1
                                      TO  H-KWS-KRTU-CD
                                      OF  H-SQL-SEL-WHERE
      *----------  通貨コード1
           MOVE  WK-TUKA-CD1          TO  H-TUKA-CD-1
                                      OF  H-SQL-SEL-WHERE
      *----------  通貨コード2
           MOVE  WK-TUKA-CD2          TO  H-TUKA-CD-2
                                      OF  H-SQL-SEL-WHERE
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "SQLSTMT1-LEN     =[" SQLSTMT1-LEN "]"
      *-----------------------------------------------------------------
           STRING  "SELECT "
                   "BAYIKAYI_TUKA"
                   ","
                   "JZ_JS_KBN"
                   " FROM "  WK-TBL-ID  " "
      *----------  WHERE
      *----------  利用会社コード
                   " WHERE  RIYO_CMP_CD = :V1"
      *----------  為替系列コード
                   " AND KWS_KRTU_CD = :V2"
      *----------  ベース通貨コード
                   " AND BASE_TUKA_CD = :V3"
      *----------  目的通貨コード
                   " AND MOKUTEKI_TUKA_CD = :V4"
      *----------  削除区分
                   " AND SAKUJO_KBN = '"
                   CNS-TUJYO
                   "'"
                   DELIMITED  BY SIZE
                   INTO  SQLSTMT1-ARR
                   WITH  POINTER  SQLSTMT1-LEN
           END-STRING.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "利用会社コード  = [" H-RIYO-CMP-CD
      D                               OF  H-SQL-SEL-WHERE "]"
      D    DISPLAY  "為替系列コード  = [" H-KWS-KRTU-CD
      D                               OF  H-SQL-SEL-WHERE "]"
      D    DISPLAY  "通貨コード1  = [" H-TUKA-CD-1
      D                               OF  H-SQL-SEL-WHERE "]"
      D    DISPLAY  "通貨コード2  = [" H-TUKA-CD-2
      D                               OF  H-SQL-SEL-WHERE "]"
      D    DISPLAY  "SQLSTMT1-LEN  = [" SQLSTMT1-LEN  "]"
      D    DISPLAY  "SQLSTMT1-ARR  =[" SQLSTMT1-ARR(1:SQLSTMT1-LEN) "]".
      *-----------------------------------------------------------------
       DB-SQL-SAKUSEI-EXT.
           EXIT.
      ******************************************************************
      *            リターンパッケージセットルーチン処理
      ******************************************************************
       APL-SET-RTNPKG-RTN     SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "リターンパッケージセットルーチン処理開始"
      D    DISPLAY  "SCZY1170 リターンパッケージセットルーチン"
      *-----------------------------------------------------------------
      *----------  SCZY1170 リターンパッケージセットルーチン
           CALL  "SCZY1170"           USING  PARM-SCZY1170-INOUT
                                             PARM-SCZY1170-RTNPKG.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "リターンパッケージセットルーチン処理終了".
      *-----------------------------------------------------------------
       APL-SET-RTNPKG-EXT.
           EXIT.
      ******************************************************************
      *                為替換算情報取得処理１
      ******************************************************************
       APL-GET-KWS-KT1-RTN    SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  為替換算情報取得処理１  START  ***"
      *-----------------------------------------------------------------
      *----------  カーソル 1 CUR  OPEN
           PERFORM  DB-CUR1-OPEN-RTN
      *----------  カーソル 1 CUR  FETCH
           PERFORM  DB-CUR1-FETCH-RTN.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  為替換算情報取得処理１  END  ***".
      *-----------------------------------------------------------------
       APL-GET-KWS-KT1-EXT.
           EXIT.
      ******************************************************************
      *               為替換算情報取得処理2
      ******************************************************************
       APL-GET-KWS-KT2-RTN    SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  為替換算情報取得処理2  START  ***"
      *-----------------------------------------------------------------
           INITIALIZE      WK-TUKA-CD1
           INITIALIZE      WK-TUKA-CD2
      *----------  ワーク通貨コード１ ← ARG1.目的通貨コード
           MOVE  MOKUTEKI-TUKA-CD     OF  PARM-SCZY3017-ARG1
                                      TO  WK-TUKA-CD1
      *----------  ワーク通貨コード２ ← ARG1.ベース通貨コード
           MOVE  BASE-TUKA-CD         OF  PARM-SCZY3017-ARG1
                                      TO  WK-TUKA-CD2
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "ワーク通貨コード１ = "  WK-TUKA-CD1
      D    DISPLAY  "ワーク通貨コード２ = "  WK-TUKA-CD2
      *-----------------------------------------------------------------
      *----------  カーソル2 PREPARE処理
           PERFORM  DB-CUR2-PREPARE-RTN
      *----------  カーソル 2 CUR  OPEN
           PERFORM  DB-CUR2-OPEN-RTN
      *----------  カーソル 2 CUR  FETCH
           PERFORM  DB-CUR2-FETCH-RTN.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  為替換算情報取得処理2  END  ***".
      *-----------------------------------------------------------------
       APL-GET-KWS-KT2-EXT.
           EXIT.
      ******************************************************************
      *                 テーブルID取得ルーチン
      ******************************************************************
       APL-GET-TBLID-RTN      SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "テーブルID取得ルーチン処理開始"
      D    DISPLAY  "SCZY1160 テーブルID取得ルーチン"
      *-----------------------------------------------------------------
      *----------  ワークRTNパッケージを初期化する
           INITIALIZE         WK-RTNPKG
      *----------  利用会社コード
           MOVE  RIYO-CMP-CD          OF  PARM-SCZY3017-ARG1
                                      TO  RIYO-CMP-CD
                                      OF  PARM-SCZY1160-INOUT
      *----------  テーブルID
           MOVE  CNS-TBL-ID           TO  TBL-ID
                                      OF  PARM-SCZY1160-ARG1(1)
      *----------  SCZY1160 テーブルID取得ルーチン
           CALL  "SCZY1160"           USING  PARM-SCZY1160-INOUT
                                             WK-RTNPKG
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "RTN-CD(" COUNTER OF  WK-RTNPKG
      D             ") OF SCZY1160=[" RTN-CD
      D                               OF  WK-RTNPKG
      D              ( COUNTER        OF  WK-RTNPKG )                "]"
      *-----------------------------------------------------------------
           EVALUATE  RTN-CD           OF  WK-RTNPKG
                   ( COUNTER          OF  WK-RTNPKG )
      *----------  正常の場合
             WHEN  CNS-RTN-000
      *----------  テーブルID取得
               MOVE  HENSYUGO-TBL-ID
                                      OF  PARM-SCZY1160-ARG2(1)
                                      TO  WK-TBL-ID
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "WK-TBL-ID        =[" WK-TBL-ID                  "]"
      D    DISPLAY  "SCZY1160正常終了"
      *-----------------------------------------------------------------
      *----------  システムエラー（Oracle以外）
             WHEN  OTHER
      *----------  ポジション
               MOVE  CNS-POS-002      TO  ERR-POS
                                      OF  PARM-SCZY1170-INOUT
      *----------  リターンコード
               MOVE  CNS-RTN-901      TO  RTN-CD
                                      OF  PARM-SCZY1170-INOUT
      *----------  詳細リターンコード
               MOVE  ZERO             TO  DETAIL-RTN-CD
                                      OF  PARM-SCZY1170-INOUT
      *----------  エラー検知プログラム名
               MOVE  CNS-PROGRAM-ID   TO  ERR-PGM-ID
                                      OF  PARM-SCZY1170-ARG1
      *----------  詳細リターンコードデコードフラグ
               MOVE  CNS-DETAIL-0     TO  DETAIL-RTN-CD-FLG
                                      OF  PARM-SCZY1170-ARG1
      *----------  フリーメッセージ
               MOVE  SPACE            TO  FREE-MSG
                                      OF  PARM-SCZY1170-INOUT
               MOVE  WK-RTNPKG        TO  PARM-SCZY1170-RTNPKG
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  "SCZY1160エラー：システムエラー（Oracle以外）"
      *-----------------------------------------------------------------
               PERFORM  COM-ERR-RTN
           END-EVALUATE.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "テーブルID取得ルーチン処理終了".
      *-----------------------------------------------------------------
       APL-GET-TBLID-EXT.
           EXIT.
      ******************************************************************
      *                 通貨コード判定
      ******************************************************************
       APL-TUKA-CD-RTN        SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "通貨コード判定処理開始"
      D    DISPLAY  "通貨コード判定"
      *-----------------------------------------------------------------
           IF  (
      *----------  ARG1.ベース通貨コード = ARG1.目的通貨コードの場合
                 BASE-TUKA-CD         OF  PARM-SCZY3017-ARG1
                                      =   MOKUTEKI-TUKA-CD
                                      OF  PARM-SCZY3017-ARG1)
           THEN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "ARG1.ベース通貨コード = ARG1.目的通貨コードの場合"
      *-----------------------------------------------------------------
      *----------  媒介通貨 ← スペース
               MOVE  SPACE            TO  BAYIKAYI-TUKA
                                      OF  PARM-SCZY3017-ARG2
      *----------  乗算/除算区分 ← '1'(乗算)
               MOVE  CNS-JYOUZAN-OUT  TO  JZ-JS-KBN
                                      OF  PARM-SCZY3017-ARG2
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "媒介通貨     =[" BAYIKAYI-TUKA
      D                               OF  PARM-SCZY3017-ARG2         "]"
      D    DISPLAY  "乗算/除算区分=[" JZ-JS-KBN
      D                               OF  PARM-SCZY3017-ARG2         "]"
      D    DISPLAY  "後処理へ通貨コード判定処理"
      *-----------------------------------------------------------------
      *----------  後処理 へ
               PERFORM  PRG-END-RTN
      *----------  上記以外の場合
           ELSE
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "ARG1.ベース通貨コード≠ARG1.目的通貨コードCONTINUE"
      *-----------------------------------------------------------------
               CONTINUE
           END-IF.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "通貨コード判定処理終了".
      *-----------------------------------------------------------------
       APL-TUKA-CD-EXT.
           EXIT.
