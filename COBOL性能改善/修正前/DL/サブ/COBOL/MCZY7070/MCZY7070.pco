      ******************************************************************
      *$Header: $
      ******************************************************************
      *
      *     システムＩＤ   : T-STAR
      *     システム名称   : 次期TSTARシステム
      *     プログラム名称 : 実行状況管理UPDメイン
      *     処理概要       : 実行状況管理UPDサブをコールして、実行状況
      *                      管理テーブルを更新する。戻り値は0(正常)
      *                      16(システムエラー)のいずれかとする。
      *     参照ＤＢ       :
      *     引数           : IN  ARG1.利用会社コード
      *                          ARG2.処理実行日
      *                          ARG3.起動ID
      *                          ARG4.セッション区分
      *                          ARG5.アップロード総件数
      *                          ARG6.アップロードエラー件数
      *                          ARG7.DLファイル出力区分
      *
      *     改訂履歴
      *     年月日     区分  所属       担当者  内容
      *     ---------- ----  ---------  ------  ------------------------
      *     20070821   新規  SCS        王梅娟  新規作成
      *     20090709   変更  SCS        王瑞娟  テーマ930対応
      ******************************************************************
      ******************************************************************
       IDENTIFICATION         DIVISION.
       PROGRAM-ID.    MCZY7070.
       AUTHOR.        Nomura.
       DATE-WRITTEN.  07-08-21.
       DATE-COMPILED.
      ******************************************************************
      *
      *                 環  境  部
      *
      ******************************************************************
       ENVIRONMENT            DIVISION.
       CONFIGURATION          SECTION.
       SOURCE-COMPUTER.       L1000V.
      *SOURCE-COMPUTER.       L1000V  WITH  DEBUGGING  MODE.
       OBJECT-COMPUTER.       RP5470.
       SPECIAL-NAMES.
       ENVIRONMENT-NAME       ENV-NAME
       ENVIRONMENT-VALUE      ENV-VALUE.
       INPUT-OUTPUT           SECTION.
       FILE-CONTROL.
      ******************************************************************
      *
      *                 デ  ー  タ  部
      *
      ******************************************************************
       DATA                   DIVISION.
       WORKING-STORAGE        SECTION.
       01  IDNAME                  PIC  X(80)  VALUE
       "$Id: $".
      ******************************************************************
      *     コンスタント  エリア
      ******************************************************************
       01  CONSTANT-AREA.
      *----------  プログラムID
         05  CNS-PROGRAM-ID        PIC  X(08)  VALUE  "MCZY7070".
      *----------  リターンコード: システムエラー（Oracle）
         05  CNS-RET-CD-ORAERR     PIC  X(03)  VALUE  "900".
      *----------  リターンコード: システムエラー（Oracle以外）
         05  CNS-RET-CD-SYSERR     PIC  X(03)  VALUE  "901".
      *----------  OPEN FLG
         05  CNS-ON                PIC  9(01)  VALUE  1.
      *----------  CLOSE FLG
         05  CNS-OFF               PIC  9(01)  VALUE  0.
      ******************************************************************
      *                フリーメッセージエリア
      ******************************************************************
       01  FLAG-AREA.
      *----------  Oracle接続判定フラグ
         05  FLG-ORA-CONNECT       PIC  9(01).
         05  WK-JIKOU-SYORI-ID     PIC  X(07).
      ******************************************************************
      *                   ワーク  エリア
      ******************************************************************
       01  WORK-AREA.
      *----------  利用会社コード
         05  WK-RIYO-CMP-CD        PIC  X(04).
      *----------  処理実行日
         05  WK-SYORI-YMD          PIC  9(08).
         05  WK-SYORI-YMD-X        REDEFINES  WK-SYORI-YMD PIC  X(08).
      *----------  起動ID
         05  WK-KIDOU-ID           PIC  X(04).
      *----------  セッション区分
         05  WK-SETION-KBN         PIC  X(01).
      *----------  アップロード総件数
         05  WK-UPLOAD-FILE-KENSU  PIC  9(06).
         05  WK-UPLOAD-FILE-KENSU-X
             REDEFINES  WK-UPLOAD-FILE-KENSU  PIC  X(06).
      *----------  アップロードエラー件数
         05  WK-UPLOAD-ERR-KENSU   PIC  9(06).
         05  WK-UPLOAD-ERR-KENSU-X
             REDEFINES  WK-UPLOAD-ERR-KENSU  PIC  X(06).
      *----------  DLファイル出力区分
         05  WK-DOWNLOAD-KBN       PIC  X(01).
         05  WK-SQL-CONNECT        PIC  X(100).
         05  WK-COMMIT-RELEASE     PIC  X(15).
      *----------  プログラム終了ステータス
         05  WK-END-STS            PIC  S9(04).
      ******************************************************************
      *                サブプログラム  パラメ−タ  エリア
      ******************************************************************
      *----------  共通RTNパッケージ
       01  PARM-KYOUTUU-RTNPKG.
           COPY  "CZRTNPACKAGE.cpy".
      *----------  テーブルID取得ルーチン
       01  PARM-SCZY1160-INOUT.
           COPY  "SCZY1160.cpy".
      *----------  リターンパッケージセットルーチン
       01  PARM-SCZY1170-INOUT.
           COPY  "SCZY1170.cpy".
       01  PARM-SCZY1170-RTNPKG.
         05  PARM-SCZY1170-ARG2.
           COPY  "CZRTNPACKAGE.cpy".
      *----------  バッチ開始メッセージ出力ルーチン
       01  PARM-SCZY7010-INOUT.
           COPY  "SCZY7010.cpy".
      *----------  バッチ終了メッセージ出力ルーチン
       01  PARM-SCZY7020-INOUT.
           COPY  "SCZY7020.cpy".
      *----------  バッチ引数取得ルーチン
       01  PARM-SCZY7090-INOUT.
           COPY  "SCZY7090.cpy".
      *----------  バッチＤＢ接続情報取得ルーチン
       01  PARM-SCZY7100-INOUT.
           COPY  "SCZY7100.cpy".
      *----------  実行状況管理UPDルーチン
       01  PARM-SCZY7070-INOUT.
      *----------   実行状況管理UPDルーチンコピー句
           COPY "SCZY7070.cpy".
      ******************************************************************
      *                SQL通信, ORACLE通信,ホスト変数定義 エリア
      ******************************************************************
           EXEC  SQL INCLUDE  SQLCA               END-EXEC.
           EXEC  SQL  BEGIN   DECLARE  SECTION    END-EXEC.
       01  ORA-WORKING-ITEMS.
      *----------  ORACLE接続
         05  HST-ORA-USER          PIC  X(80).
           EXEC  SQL  END     DECLARE  SECTION    END-EXEC.
      ******************************************************************
      *
      *        手続き部
      *
      ******************************************************************
       PROCEDURE              DIVISION.
      ******************************************************************
      *                 MCZY7070処理
      ******************************************************************
       MCZY7070-RTN           SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  MCZY7070-RTN  START  *****"
      *-----------------------------------------------------------------
      *---------- 初期処理
           PERFORM  INIT-RTN
      *---------- メイン処理
           PERFORM  MAIN-RTN
      *---------- 終了処理
           PERFORM  END-RTN.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  MCZY7070-RTN  END  *****".
      *-----------------------------------------------------------------
       MCZY7070-EXT.
           STOP RUN WK-END-STS.
      ******************************************************************
      *                 初期処理
      ******************************************************************
       INIT-RTN               SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  初期処理  START  *****"
      *-----------------------------------------------------------------
      *---------- 初期化
           INITIALIZE              FLAG-AREA
                                   WORK-AREA
                                   PARM-KYOUTUU-RTNPKG
                                   PARM-SCZY1160-INOUT
                                   PARM-SCZY1170-INOUT
                                   PARM-SCZY1170-RTNPKG
                                   PARM-SCZY7010-INOUT
                                   PARM-SCZY7020-INOUT
                                   PARM-SCZY7090-INOUT
                                   PARM-SCZY7100-INOUT
                                   PARM-SCZY7070-INOUT
                                   ORA-WORKING-ITEMS
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "FLAG-AREA            =["FLAG-AREA"]"
      D    DISPLAY  "WORK-AREA            =["WORK-AREA"]"
      D    DISPLAY  "PARM-KYOUTUU-RTNPKG  =["PARM-KYOUTUU-RTNPKG"]"
      D    DISPLAY  "PARM-SCZY1160-INOUT  =["PARM-SCZY1160-INOUT"]"
      D    DISPLAY  "PARM-SCZY1170-INOUT  =["PARM-SCZY1170-INOUT"]"
      D    DISPLAY  "PARM-SCZY1170-RTNPKG =["PARM-SCZY1170-RTNPKG"]"
      D    DISPLAY  "PARM-SCZY7010-INOUT  =["PARM-SCZY7010-INOUT"]"
      D    DISPLAY  "PARM-SCZY7020-INOUT  =["PARM-SCZY7020-INOUT"]"
      D    DISPLAY  "PARM-SCZY7090-INOUT  =["PARM-SCZY7090-INOUT"]"
      D    DISPLAY  "PARM-SCZY7100-INOUT  =["PARM-SCZY7100-INOUT"]"
      D    DISPLAY  "ORA-WORKING-ITEMS    =["ORA-WORKING-ITEMS"]"
      *-----------------------------------------------------------------
      *----------  バッチ開始メッセージ出力
           PERFORM  CALL-SCZY7010-RTN
      *----------  入力引数を取得
           PERFORM  CALL-SCZY7090-RTN
      *----------  バッチＤＢ接続情報取得
           PERFORM  CALL-SCZY7100-RTN
      *----------  ＤＢ接続処理
           PERFORM  DB-BEGIN-RTN.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  初期処理  END  *****".
      *-----------------------------------------------------------------
       INIT-EXT.
           EXIT.
      ******************************************************************
      *                 バッチ開始メッセージ出力
      ******************************************************************
       CALL-SCZY7010-RTN      SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "**** バッチ開始メッセージ出力(SCZY7010) START ****"
      *-----------------------------------------------------------------
      *----------  プログラムID
           MOVE  CNS-PROGRAM-ID         TO  PGM-ID
                                        OF PARM-SCZY7010-INOUT
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "プログラムID =["PGM-ID  OF PARM-SCZY7010-INOUT"]"
      *-----------------------------------------------------------------
           CALL  "SCZY7010"             USING  PARM-SCZY7010-INOUT.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "**** バッチ開始メッセージ出力(SCZY7010) END ****".
      *-----------------------------------------------------------------
       CALL-SCZY7010-EXT.
           EXIT.
      ******************************************************************
      *                 入力引数を取得
      ******************************************************************
       CALL-SCZY7090-RTN      SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  入力引数を取得(SCZY7090)  START  *****"
      *-----------------------------------------------------------------
           INITIALIZE                   PARM-KYOUTUU-RTNPKG
      *----------  引数個数
           MOVE  7                      TO  ARG-NUM
                                        OF PARM-SCZY7090-INOUT
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "引数個数 =["ARG-NUM   OF PARM-SCZY7090-INOUT"]"
      *-----------------------------------------------------------------
           CALL  "SCZY7090"             USING  PARM-SCZY7090-INOUT
                                               PARM-KYOUTUU-RTNPKG
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "SCZY7090 RTN-CD =["RTN-CD  OF PARM-KYOUTUU-RTNPKG
      D                           (COUNTER  OF PARM-KYOUTUU-RTNPKG)"]"
      *-----------------------------------------------------------------
           IF  RTN-CD  OF  PARM-KYOUTUU-RTNPKG
                          (COUNTER  OF PARM-KYOUTUU-RTNPKG) <> "000"
           THEN
      *----------  ポジション
               MOVE  "0001"               TO  ERR-POS
                                          OF PARM-SCZY1170-INOUT
      *----------  リターンコード
               MOVE  CNS-RET-CD-SYSERR    TO  RTN-CD
                                          OF PARM-SCZY1170-INOUT
      *----------  詳細リターンコード
               MOVE  ZERO                 TO  DETAIL-RTN-CD
                                          OF PARM-SCZY1170-INOUT
      *----------  詳細ﾘﾀｰﾝｺｰﾄﾞﾃﾞｺｰﾄﾞﾌﾗｸﾞ
               MOVE  SPACE                TO  DETAIL-RTN-CD-FLG
                                          OF PARM-SCZY1170-INOUT
      *----------  フリーメッセージ
               MOVE  SPACE                TO  FREE-MSG
                                          OF PARM-SCZY1170-INOUT
      *----------  RTNパッケージ
               MOVE  PARM-KYOUTUU-RTNPKG  TO  PARM-SCZY1170-RTNPKG
               PERFORM  ERR-RTN
           END-IF
           MOVE  ARG-STRING           OF PARM-SCZY7090-INOUT(1)
                                      TO  WK-RIYO-CMP-CD
           MOVE  ARG-STRING           OF PARM-SCZY7090-INOUT(2)
                                      TO  WK-SYORI-YMD-X
           MOVE  ARG-STRING           OF PARM-SCZY7090-INOUT(3)
                                      TO  WK-KIDOU-ID
           MOVE  ARG-STRING           OF PARM-SCZY7090-INOUT(4)
                                      TO  WK-SETION-KBN
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "取得したARG5 =["
      D                ARG-STRING OF PARM-SCZY7090-INOUT(5)"]"
      *-----------------------------------------------------------------
           IF  ARG-STRING OF PARM-SCZY7090-INOUT(5) = SPACE
           THEN
               MOVE  ZERO             TO  WK-UPLOAD-FILE-KENSU
           ELSE
              MOVE  ARG-STRING        OF PARM-SCZY7090-INOUT(5)
                                      TO  WK-UPLOAD-FILE-KENSU-X
           END-IF
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "取得したARG6 =["
      D                ARG-STRING OF PARM-SCZY7090-INOUT(6)"]"
      *-----------------------------------------------------------------
           IF  ARG-STRING  OF PARM-SCZY7090-INOUT(6) = SPACE
           THEN
               MOVE  ZERO             TO  WK-UPLOAD-ERR-KENSU
           ELSE
               MOVE  ARG-STRING       OF PARM-SCZY7090-INOUT(6)
                                      TO  WK-UPLOAD-ERR-KENSU-X
           END-IF
           MOVE  ARG-STRING           OF PARM-SCZY7090-INOUT(7)
                                      TO  WK-DOWNLOAD-KBN.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "利用会社コード         =["WK-RIYO-CMP-CD        "]"
      D    DISPLAY  "処理実行日             =["WK-SYORI-YMD-X        "]"
      D    DISPLAY  "起動ID                 =["WK-KIDOU-ID           "]"
      D    DISPLAY  "セッション区分         =["WK-SETION-KBN         "]"
      D    DISPLAY  "アップロード総件数     =["WK-UPLOAD-FILE-KENSU  "]"
      D    DISPLAY  "アップロードエラー件数 =["WK-UPLOAD-ERR-KENSU   "]"
      D    DISPLAY  "DLファイル出力区分     =["WK-DOWNLOAD-KBN       "]"
      D    DISPLAY  "*****  入力引数を取得(SCZY7090)  END  *****".
      *-----------------------------------------------------------------
       CALL-SCZY7090-EXT.
           EXIT.
      ******************************************************************
      *                 バッチＤＢ接続情報取得
      ******************************************************************
       CALL-SCZY7100-RTN      SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***** バッチＤＢ接続情報取得(SCZY7100) START *****"
      *-----------------------------------------------------------------
           INITIALIZE  PARM-KYOUTUU-RTNPKG
      *----------  利用会社コード
           MOVE  WK-RIYO-CMP-CD         TO  RIYO-CMP-CD
                                        OF PARM-SCZY7100-INOUT
           CALL  "SCZY7100"             USING  PARM-SCZY7100-INOUT
                                               PARM-KYOUTUU-RTNPKG
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "SCZY7100 RTN-CD =["RTN-CD  OF PARM-KYOUTUU-RTNPKG
      D                           (COUNTER  OF PARM-KYOUTUU-RTNPKG)"]"
      *-----------------------------------------------------------------
           IF  RTN-CD  OF  PARM-KYOUTUU-RTNPKG
                          (COUNTER  OF PARM-KYOUTUU-RTNPKG) <> "000"
           THEN
      *----------  ポジション
               MOVE  "0006"               TO  ERR-POS
                                          OF PARM-SCZY1170-INOUT
      *----------  リターンコード
               MOVE  CNS-RET-CD-SYSERR    TO  RTN-CD
                                          OF PARM-SCZY1170-INOUT
      *----------  詳細リターンコード
               MOVE  ZERO                 TO  DETAIL-RTN-CD
                                          OF PARM-SCZY1170-INOUT
      *----------  詳細ﾘﾀｰﾝｺｰﾄﾞﾃﾞｺｰﾄﾞﾌﾗｸﾞ
               MOVE  SPACE                TO  DETAIL-RTN-CD-FLG
                                          OF PARM-SCZY1170-INOUT
      *----------  フリーメッセージ
               MOVE  SPACE                TO  FREE-MSG
                                          OF PARM-SCZY1170-INOUT
      *----------  RTNパッケージ
               MOVE  PARM-KYOUTUU-RTNPKG  TO  PARM-SCZY1170-RTNPKG
               PERFORM  ERR-RTN
           END-IF.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "DB接続情報 =["DB-SETUZOKU-JYOUHOU
      D             OF PARM-SCZY7100-INOUT"]"
      D    DISPLAY  "*****  バッチＤＢ接続情報取得(SCZY7100) END *****".
      *-----------------------------------------------------------------
       CALL-SCZY7100-EXT.
           EXIT.
      ******************************************************************
      *                 ＤＢ接続処理
      ******************************************************************
       DB-BEGIN-RTN           SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  ＤＢ接続処理  START  *****"
      *-----------------------------------------------------------------
           EXEC SQL
               WHENEVER SQLERROR CONTINUE
           END-EXEC
           MOVE  DB-SETUZOKU-JYOUHOU    OF PARM-SCZY7100-INOUT
                                        TO  HST-ORA-USER
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "HST-ORA-USER=["HST-ORA-USER"]"
      *-----------------------------------------------------------------
           EXEC SQL
               CONNECT :HST-ORA-USER
           END-EXEC
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "SQLCODE=["SQLCODE"]"
      *-----------------------------------------------------------------
           IF  SQLCODE = ZERO
           THEN
      *----------  ORACLE接続成功
               MOVE  CNS-ON                    TO  FLG-ORA-CONNECT
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "FLG-ORA-CONNECT=["FLG-ORA-CONNECT"]"
      *-----------------------------------------------------------------
           ELSE
               STRING  "CONNECT :",
                     HST-ORA-USER,
                     DELIMITED BY SIZE
                     INTO  WK-SQL-CONNECT
      *----------  ポジション
               MOVE  "0007"               TO  ERR-POS
                                          OF PARM-SCZY1170-INOUT
      *----------  リターンコード
               MOVE  CNS-RET-CD-ORAERR    TO  RTN-CD
                                          OF PARM-SCZY1170-INOUT
      *----------  詳細リターンコード
               MOVE  SQLCODE              TO  DETAIL-RTN-CD
                                          OF PARM-SCZY1170-INOUT
      *----------  詳細ﾘﾀｰﾝｺｰﾄﾞﾃﾞｺｰﾄﾞﾌﾗｸﾞ
               MOVE  "1"                  TO  DETAIL-RTN-CD-FLG
                                          OF PARM-SCZY1170-INOUT
      *----------  フリーメッセージ
               MOVE  WK-SQL-CONNECT       TO  FREE-MSG
                                          OF PARM-SCZY1170-INOUT
               PERFORM  ERR-RTN
           END-IF.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  ＤＢ接続処理  END  *****".
      *-----------------------------------------------------------------
       DB-BEGIN-EXT.
           EXIT.
      ******************************************************************
      *                 メイン処理
      ******************************************************************
       MAIN-RTN               SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  メイン処理  START  *****"
      *-----------------------------------------------------------------
           PERFORM  GET-ENV-RTN.
      *----------  実行状況管理UPDルーチンをコール
           PERFORM  CALL-SCZY7070-RTN.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  メイン処理  END  *****".
      *-----------------------------------------------------------------
       MAIN-EXT.
           EXIT.
      ******************************************************************
      *           環境変数から、実行処理IDを取得する
      ******************************************************************
       GET-ENV-RTN            SECTION.
      *----------  実行処理ID保存用ワーク ← 環境変数TS_EXECID
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "環境変数実行処理ID取得 START"
      *-----------------------------------------------------------------
           DISPLAY  "TS_EXECID"  UPON  ENV-NAME
           ACCEPT  WK-JIKOU-SYORI-ID   FROM  ENV-VALUE  ON EXCEPTION
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "環境変数実行処理ID取得 END"
      D    DISPLAY  "実行処理ID:["WK-JIKOU-SYORI-ID"]"
      *-----------------------------------------------------------------
           CONTINUE.
       GET-ENV-EXT.
           EXIT.
      ******************************************************************
      *                 実行状況管理UPD処理
      ******************************************************************
       CALL-SCZY7070-RTN      SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  実行状況管理UPD処理  START  *****"
      *-----------------------------------------------------------------
           INITIALIZE  PARM-KYOUTUU-RTNPKG
      *----------  利用会社コード
           MOVE  WK-RIYO-CMP-CD        TO  RIYO-CMP-CD
                                       OF PARM-SCZY7070-INOUT
      *----------  処理実行日
           MOVE  WK-SYORI-YMD          TO  SYORI-YMD
                                       OF PARM-SCZY7070-INOUT
      *----------  起動ID
           MOVE  WK-KIDOU-ID           TO  KIDOU-ID
                                       OF PARM-SCZY7070-INOUT
      *----------  セッション区分
           MOVE  WK-SETION-KBN         TO  SETION-KBN
                                       OF PARM-SCZY7070-INOUT
      *----------  アップロード総件数
           MOVE  WK-UPLOAD-FILE-KENSU  TO  UPLOAD-FILE-KENSU
                                       OF PARM-SCZY7070-INOUT
      *----------  アップロードエラー件数
           MOVE  WK-UPLOAD-ERR-KENSU   TO  UPLOAD-ERR-KENSU
                                       OF PARM-SCZY7070-INOUT
      *----------  DLファイル出力区分
           MOVE  WK-DOWNLOAD-KBN       TO  DOWNLOAD-KBN
                                       OF PARM-SCZY7070-INOUT
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "利用会社コード =["RIYO-CMP-CD
      D                                OF PARM-SCZY7070-INOUT"]"
      D    DISPLAY  "処理実行日     =["SYORI-YMD
      D                                OF PARM-SCZY7070-INOUT"]"
      D    DISPLAY  "起動ID         =["KIDOU-ID
      D                                OF PARM-SCZY7070-INOUT"]"
      D    DISPLAY  "セッション区分 =["SETION-KBN
      D                                OF PARM-SCZY7070-INOUT"]"
      D    DISPLAY  "アップロード総件数 =["UPLOAD-FILE-KENSU
      D                                OF PARM-SCZY7070-INOUT"]"
      D    DISPLAY  "アップロードエラー件数 =["UPLOAD-ERR-KENSU
      D                                OF PARM-SCZY7070-INOUT"]"
      D    DISPLAY  "DLファイル出力区分 =["DOWNLOAD-KBN
      D                                OF PARM-SCZY7070-INOUT"]"
      *-----------------------------------------------------------------
           CALL  "SCZY7070"            USING  PARM-SCZY7070-INOUT
                                              PARM-KYOUTUU-RTNPKG
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "RTN-CD =["RTN-CD  OF  PARM-KYOUTUU-RTNPKG
      D                   (COUNTER  OF PARM-KYOUTUU-RTNPKG)"]"
      *-----------------------------------------------------------------
           EVALUATE  RTN-CD  OF  PARM-KYOUTUU-RTNPKG
                          (COUNTER  OF PARM-KYOUTUU-RTNPKG)
           WHEN  "900"
      *----------  ポジション
               MOVE  "0008"               TO  ERR-POS
                                          OF PARM-SCZY1170-INOUT
      *----------  リターンコード
               MOVE  CNS-RET-CD-ORAERR    TO  RTN-CD
                                          OF PARM-SCZY1170-INOUT
      *----------  詳細リターンコード
               MOVE  ZERO                 TO  DETAIL-RTN-CD
                                          OF PARM-SCZY1170-INOUT
      *----------  詳細ﾘﾀｰﾝｺｰﾄﾞﾃﾞｺｰﾄﾞﾌﾗｸﾞ
               MOVE  SPACE                TO  DETAIL-RTN-CD-FLG
                                          OF PARM-SCZY1170-INOUT
      *----------  フリーメッセージ
               MOVE  SPACE                TO  FREE-MSG
                                          OF PARM-SCZY1170-INOUT
      *----------  RTNパッケージ
               MOVE  PARM-KYOUTUU-RTNPKG  TO  PARM-SCZY1170-RTNPKG
               PERFORM  ERR-RTN
           WHEN  "000"
               CONTINUE
           WHEN  OTHER
      *----------  ポジション
               MOVE  "0009"               TO  ERR-POS
                                          OF PARM-SCZY1170-INOUT
      *----------  リターンコード
               MOVE  CNS-RET-CD-SYSERR    TO  RTN-CD
                                          OF PARM-SCZY1170-INOUT
      *----------  詳細リターンコード
               MOVE  ZERO                 TO  DETAIL-RTN-CD
                                          OF PARM-SCZY1170-INOUT
      *----------  詳細ﾘﾀｰﾝｺｰﾄﾞﾃﾞｺｰﾄﾞﾌﾗｸﾞ
               MOVE  SPACE                TO  DETAIL-RTN-CD-FLG
                                          OF PARM-SCZY1170-INOUT
      *----------  フリーメッセージ
               MOVE  SPACE                TO  FREE-MSG
                                          OF PARM-SCZY1170-INOUT
      *----------  RTNパッケージ
               MOVE  PARM-KYOUTUU-RTNPKG  TO  PARM-SCZY1170-RTNPKG
               PERFORM  ERR-RTN
           END-EVALUATE.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  実行状況管理UPD処理  END  *****".
      *-----------------------------------------------------------------
       CALL-SCZY7070-EXT.
           EXIT.
      ******************************************************************
      *                 後処理
      ******************************************************************
       END-RTN                SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  後処理  START  ***"
      *-----------------------------------------------------------------
      *----------  DBコミット
           PERFORM  DB-COMMIT-RTN
           MOVE  "000"                  TO  RTN-CD
                                        OF PARM-SCZY1170-INOUT
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "RTN-CD =["RTN-CD OF PARM-SCZY1170-INOUT"]"
      D    DISPLAY  "***  CALL  SCZY1170  START  ***"
      *-----------------------------------------------------------------
           CALL  "SCZY1170"             USING  PARM-SCZY1170-INOUT
                                               PARM-SCZY1170-RTNPKG
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "RTN-CD =["RTN-CD   OF PARM-SCZY1170-RTNPKG
      D                  (COUNTER  OF  PARM-SCZY1170-RTNPKG)"]"
      D    DISPLAY  "PARM-SCZY1170-RTNPKG =["PARM-SCZY1170-RTNPKG"]"
      D    DISPLAY   "***  CALL  SCZY1170  END  ***"
      *-----------------------------------------------------------------
      *----------  終了メッセージを出力
           PERFORM  CALL-SCZY7020-RTN
           MOVE  0                      TO  WK-END-STS.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "WK-END-STS =["WK-END-STS"]"
      D    DISPLAY  "***  後処理  END  ***".
      *-----------------------------------------------------------------
       END-EXT.
           EXIT.
      ******************************************************************
      *                 DBコミット処理
      ******************************************************************
       DB-COMMIT-RTN          SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****   ORACLE接続終了(COMMIT)  START  *****"
      *-----------------------------------------------------------------
           EXEC SQL
                 COMMIT  RELEASE
           END-EXEC
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "SQLCODE(COMMIT)=["SQLCODE"]"
      *-----------------------------------------------------------------
           IF  SQLCODE <> ZERO
           THEN
               STRING  "COMMIT  RELEASE"
                   DELIMITED BY SIZE
                   INTO  WK-COMMIT-RELEASE
      *----------  ポジション
               MOVE  "0010"               TO  ERR-POS
                                          OF PARM-SCZY1170-INOUT
      *----------  リターンコード
               MOVE  CNS-RET-CD-ORAERR    TO  RTN-CD
                                          OF PARM-SCZY1170-INOUT
      *----------  詳細リターンコード
               MOVE  SQLCODE              TO  DETAIL-RTN-CD
                                          OF PARM-SCZY1170-INOUT
      *----------  詳細ﾘﾀｰﾝｺｰﾄﾞﾃﾞｺｰﾄﾞﾌﾗｸﾞ
               MOVE  "1"                  TO  DETAIL-RTN-CD-FLG
                                          OF PARM-SCZY1170-INOUT
      *----------  フリーメッセージ
               MOVE  WK-COMMIT-RELEASE    TO  FREE-MSG
                                          OF PARM-SCZY1170-INOUT
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "ERR-PGM-ID              OF PARM-SCZY1170-ARG1=["
      D              ERR-PGM-ID              OF PARM-SCZY1170-ARG1"]"
      D    DISPLAY  "ERR-POS                 OF PARM-SCZY1170-ARG1=["
      D              ERR-POS                 OF PARM-SCZY1170-ARG1"]"
      D    DISPLAY  "RTN-CD                  OF PARM-SCZY1170-ARG1=["
      D              RTN-CD                  OF PARM-SCZY1170-ARG1"]"
      D    DISPLAY  "DETAIL-RTN-CD           OF PARM-SCZY1170-ARG1=["
      D              DETAIL-RTN-CD           OF PARM-SCZY1170-ARG1"]"
      D    DISPLAY  "DETAIL-RTN-CD-FLG       OF PARM-SCZY1170-ARG1=["
      D              DETAIL-RTN-CD-FLG       OF PARM-SCZY1170-ARG1"]"
      D    DISPLAY  "FREE-MSG                OF PARM-SCZY1170-ARG1=["
      D              FREE-MSG                OF PARM-SCZY1170-ARG1"]"
      *-----------------------------------------------------------------
             PERFORM  ERR-RTN
           END-IF.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  ORACLE接続終了(COMMIT)  END  *****".
      *-----------------------------------------------------------------
       DB-COMMIT-EXT.
           EXIT.
      ******************************************************************
      *                 終了メッセージを出力処理
      ******************************************************************
       CALL-SCZY7020-RTN      SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  終了メッセージを出力処理  START  *****"
      *-----------------------------------------------------------------
           INITIALIZE  PARM-KYOUTUU-RTNPKG
      *----------  エラー種別
           MOVE  "N"                    TO  ERR-SYUBETU
                                        OF PARM-SCZY7020-INOUT
      *----------  プログラム名
           MOVE  CNS-PROGRAM-ID         TO  PGM-ID
                                        OF PARM-SCZY7020-INOUT
      *----------  利用会社コード:ARG1.利用会社コード
           MOVE  WK-RIYO-CMP-CD         TO  RIYO-CMP-CD
                                        OF PARM-SCZY7020-INOUT
      *----------  実行処理ID:初期値
      *----------  起動ID:ARG3.起動ID
           MOVE  WK-KIDOU-ID            TO  KIDOU-ID
                                        OF PARM-SCZY7020-INOUT
      *----------  基準日:ARG2.処理実行日
           MOVE  WK-SYORI-YMD           TO  KJN-YMD
                                        OF PARM-SCZY7020-INOUT
      *----------  実行処理ID
           MOVE  WK-JIKOU-SYORI-ID      TO  JIKOU-SYORI-ID
                                        OF  PARM-SCZY7020-INOUT
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "実行処理ID =" JIKOU-SYORI-ID
      D                            OF  PARM-SCZY7020-INOUT"]"
      *-----------------------------------------------------------------
           CALL  "SCZY7020"             USING  PARM-SCZY7020-INOUT
                                               PARM-KYOUTUU-RTNPKG.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  終了メッセージを出力処理  END  *****".
      *-----------------------------------------------------------------
       CALL-SCZY7020-EXT.
           EXIT.
      ******************************************************************
      *                 エラー処理
      ******************************************************************
       ERR-RTN                SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  エラー処理  START  *****"
      *-----------------------------------------------------------------
      *----------  DBロールバック
           PERFORM  DB-ROLLBACK-RTN
      *----------  リターンパッケージセットルーチンをコールする
           MOVE  CNS-PROGRAM-ID         TO  ERR-PGM-ID
                                        OF PARM-SCZY1170-INOUT
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  CALL  SCZY1170  START  *****"
      *-----------------------------------------------------------------
           CALL  "SCZY1170"             USING  PARM-SCZY1170-INOUT
                                               PARM-SCZY1170-RTNPKG
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "ERR-PGM-ID =["ERR-PGM-ID  OF PARM-SCZY1170-RTNPKG
      D                          (COUNTER  OF  PARM-SCZY1170-RTNPKG)"]"
      D    DISPLAY  "ERR-POS =["ERR-POS  OF PARM-SCZY1170-RTNPKG
      D                          (COUNTER  OF  PARM-SCZY1170-RTNPKG)"]"
      D    DISPLAY  "RTN-CD =["RTN-CD  OF PARM-SCZY1170-RTNPKG
      D                          (COUNTER  OF  PARM-SCZY1170-RTNPKG)"]"
      D    DISPLAY  "DETAIL-RTN-CD =["DETAIL-RTN-CD
      D     OF PARM-SCZY1170-RTNPKG(COUNTER  OF PARM-SCZY1170-RTNPKG)"]"
      D    DISPLAY  "DETAIL-RTN-CD-FLG =["DETAIL-RTN-CD-FLG
      D     OF PARM-SCZY1170-RTNPKG(COUNTER  OF PARM-SCZY1170-RTNPKG)"]"
      D    DISPLAY  "FREE-MSG =["FREE-MSG OF PARM-SCZY1170-RTNPKG
      D                          (COUNTER  OF  PARM-SCZY1170-RTNPKG)"]"
      D    DISPLAY  "PARM-SCZY1170-RTNPKG =["PARM-SCZY1170-RTNPKG"]"
      D    DISPLAY  "*****  CALL  SCZY1170  END  *****"
      *-----------------------------------------------------------------
      *----------  終了メッセージを出力
           PERFORM  CALL-SCZY7020ERR-RTN
           MOVE  16                      TO  WK-END-STS.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "WK-END-STS =["WK-END-STS"]"
      D    DISPLAY  "*****  エラー処理  END  *****".
      *-----------------------------------------------------------------
       ERR-EXT.
           STOP RUN  WK-END-STS.
      ******************************************************************
      *        DBロールバック処理
      ******************************************************************
       DB-ROLLBACK-RTN        SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "****  ROLLBACK-RTN START ****"
      *-----------------------------------------------------------------
      *---------- DB ROLLBACK処理
           IF  FLG-ORA-CONNECT  =  CNS-ON
           THEN
      *---------- 使用したカーソルの全クローズ
               EXEC SQL
                   ROLLBACK RELEASE
               END-EXEC
               MOVE   CNS-OFF                TO  FLG-ORA-CONNECT
           END-IF.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "FLG-ORA-CONNECT=["FLG-ORA-CONNECT"]"
      D    DISPLAY  "****  ROLLBACK-RTN END ****".
      *-----------------------------------------------------------------
       DB-ROLLBACK-EXT.
           EXIT.
      ******************************************************************
      *                 終了メッセージを出力処理
      ******************************************************************
       CALL-SCZY7020ERR-RTN   SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  終了メッセージを出力処理  START  *****"
      *-----------------------------------------------------------------
           INITIALIZE  PARM-KYOUTUU-RTNPKG
      *----------  エラー種別
           MOVE  "S"                    TO  ERR-SYUBETU
                                        OF PARM-SCZY7020-INOUT
      *----------  プログラム名
           MOVE  CNS-PROGRAM-ID         TO  PGM-ID
                                        OF PARM-SCZY7020-INOUT
      *----------  利用会社コード:ARG1.利用会社コード
           MOVE  WK-RIYO-CMP-CD         TO  RIYO-CMP-CD
                                        OF PARM-SCZY7020-INOUT
      *----------  実行処理ID:初期値
      *----------  起動ID:ARG3.起動ID
           MOVE  WK-KIDOU-ID            TO  KIDOU-ID
                                        OF PARM-SCZY7020-INOUT
      *----------  基準日:ARG2.処理実行日
           MOVE  WK-SYORI-YMD           TO  KJN-YMD
                                        OF PARM-SCZY7020-INOUT
      *----------  実行処理ID
           MOVE  WK-JIKOU-SYORI-ID      TO  JIKOU-SYORI-ID
                                        OF  PARM-SCZY7020-INOUT
           MOVE  PARM-SCZY1170-RTNPKG   TO  PARM-KYOUTUU-RTNPKG
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "実行処理ID =[" JIKOU-SYORI-ID
      D                            OF  PARM-SCZY7020-INOUT"]"
      *-----------------------------------------------------------------
           CALL  "SCZY7020"             USING  PARM-SCZY7020-INOUT
                                               PARM-KYOUTUU-RTNPKG.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "PARM-KYOUTUU-RTNPKG =["PARM-KYOUTUU-RTNPKG"]"
      D    DISPLAY  "*****  終了メッセージを出力処理  END  *****".
      *-----------------------------------------------------------------
       CALL-SCZY7020ERR-EXT.
           EXIT.
