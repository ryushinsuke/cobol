      ******************************************************************
      *$Header: $
      ******************************************************************
      *
      *     システムＩＤ   : T-STAR
      *     システム名称   : 次期TSTARシステム
      *     プログラム名称 : ポートフォリオ別メッセージＩＮＳルーチン
      *     処理概要       : バッチ処理で、業務エラーの場合に、
      *                      メッセージコードとメッセージ文言を実行状況
      *                      エラーメッセージテーブルへINSERTする。
      *                      共通メッセージ取得ルーチンをCALLして、
      *                      メッセージコードよりメッセージ文言を取得。
      *                      最終的に、画面からエンドユーザがメッセージ
      *                      内容を照会できる。
      *     参照ＤＢ       : 実行状況エラーメッセージ（TKDYAA36）
      *     引数           : IN  利用会社コード
      *                    : IN  処理実行日
      *                    : IN  起動ID
      *                    : IN  業務タスクID
      *                    : IN  ファンドコード
      *                    : IN  ポートフォリオコード
      *                    : IN  評価系列コード
      *                    : IN  帳票ID
      *                    : IN  帳票任意コード
      *                    : IN  基準日From
      *                    : IN  基準日To
      *                    : IN  メッセージID
      *                    : IN  置換文字
      *                    : IN  フリーメッセージ
      *                    : OUT RTNパッケージ
      *     改訂履歴
      *     年月日     区分  所属       担当者  内容
      *     ---------- ----  ---------  ------  ------------------------
      *     20070827   新規  SCS        銭曉鳴  新規作成
      ******************************************************************
      ******************************************************************
       IDENTIFICATION         DIVISION.
       PROGRAM-ID.    SCZY7080.
       AUTHOR.        Nomura.
       DATE-WRITTEN.  07-08-27.
       DATE-COMPILED.
      ******************************************************************
      *
      *                         環  境  部
      *
      ******************************************************************
       ENVIRONMENT            DIVISION.
       CONFIGURATION          SECTION.
       SOURCE-COMPUTER.       L1000V.
      *SOURCE-COMPUTER.       L1000V   WITH DEBUGGING MODE.
       OBJECT-COMPUTER.       RP5470.
      ******************************************************************
      *
      *                      デ  ー  タ  部
      *
      ******************************************************************
       DATA                   DIVISION.
      ******************************************************************
      ******************************************************************
      *                      作業場所節
      ******************************************************************
       WORKING-STORAGE        SECTION.
       01  IDNAME                       PIC  X(80)  VALUE
       "$Id: $".
      ******************************************************************
      ******************************************************************
      *                   コンスタント  エリア
      ******************************************************************
       01  CONSTANT-AREA.
         05  CNS-PROGRAM-ID             PIC  X(08)  VALUE  "SCZY7080".
      *----------  リターンコード(正常終了)
         05  CNS-RET-000                PIC  X(03)  VALUE  "000".
      *----------  SQLリターンコード(正常終了)
         05  CNS-SQL-NORMAL             PIC  9(01)  VALUE   0.
      ******************************************************************
      *                      ワーク  エリア
      ******************************************************************
       01  WORK-AREA.
      *---------- ワークフリーメッセージ
         05  WK-FREE-MSG                PIC  X(2000).
      *---------- 文言編集用ワーク
         05  WK-MSG-MESSAGE             PIC  X(60).
      *---------- ワークメッセージID
         05  WK-MSG-ID                  PIC  X(07).
      *---------- ワークCOUNT
         05  IDX                        PIC  9(05).
      *----------  ワークRTNパッケージ
       01  WK-RTNPKG.
         05  WK-PARM-RTNPKG.
           COPY "CZRTNPACKAGE.cpy".
      ******************************************************************
      *                サブプログラム  パラメ−タ  エリア
      ******************************************************************
      *----------  SCZY1170 リターンパッケージセットルーチン
       01  PARM-SCZY1170-INOUT.
      *----------  リターンパッケージセットルーチンコピー句
         COPY  "SCZY1170.cpy".
       01  PARM-SCZY1170-RTNPKG.
         05  PARM-SCZY1170-ARG2.
      *----------  RTNパッケージ
           COPY  "CZRTNPACKAGE.cpy".
      *----------  SCZY1140 共通メッセージ取得ルーチン
       01  PARM-SCZY1140-INOUT.
      *----------  共通メッセージ取得ルーチンコピー句
         COPY  "SCZY1140.cpy".
      *----------  【TIDYAA36】DBアクセスルーチン（INSERT）
       01  PARM-TIDYAA36-INOUT.
         05  PARM-TIDYAA36-ARG1.
      *----------  利用会社コード
           10  RIYO-CMP-CD         PIC  X(04).
         05  PARM-TIDYAA36-ARG2.
           COPY  "TKDYAA36.cpy".
      ******************************************************************
      *                ホスト変数  エリア
      ******************************************************************
      *----------  ＳＱＬ通信領域
           EXEC  SQL  INCLUDE  SQLCA  END-EXEC.
           EXEC  SQL  BEGIN    DECLARE  SECTION  END-EXEC.
       01  ORA-WORKING-AREA.
      *----------  シーケンス
         05  HST-BANGO             PIC  9(10).
           EXEC  SQL  END      DECLARE  SECTION  END-EXEC.
      ******************************************************************
      *                 連絡節
      ******************************************************************
       LINKAGE                SECTION.
       01  PARM-SCZY7080-INOUT.
      *----------  バッチ業務エラーＩＮＳコピー句
         COPY  "SCZY7080.cpy".
       01  PARM-SCZY7080-RTNPKG.
         05  PARM-SCZY7080-ARG3.
      *----------  RTNパッケージ
           COPY  "CZRTNPACKAGE.cpy".
      ******************************************************************
      *
      *        PROCEDURE      DIVISION
      *
      ******************************************************************
       PROCEDURE              DIVISION
            USING   PARM-SCZY7080-INOUT
                    PARM-SCZY7080-RTNPKG.
      ******************************************************************
      *             SCZY7080 共通メッセージ取得処理
      ******************************************************************
       BASE-RTN               SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  SCZY7080 メッセージ取得処理  START  ***"
      *-----------------------------------------------------------------
      *---------- 前処理
           PERFORM  INIT-RTN
      *---------- 主処理
           PERFORM  MAIN-RTN
      *---------- 後処理
           PERFORM  END-RTN.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  SCZY7080 メッセージ取得処理  END  ***".
      *-----------------------------------------------------------------
       BASE-EXT.
           GOBACK.
      ******************************************************************
      *                        前処理
      ******************************************************************
       INIT-RTN               SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  INIT  START  ***"
      *-----------------------------------------------------------------
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  初期化処理  START  ***"
      *-----------------------------------------------------------------
      *---------- 初期化
           INITIALIZE  WORK-AREA
                       ORA-WORKING-AREA
                       PARM-SCZY1170-INOUT
                       PARM-SCZY1170-RTNPKG
                       PARM-SCZY1140-INOUT
                       PARM-TIDYAA36-INOUT
                       PARM-SCZY7080-RTNPKG
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "WORK-AREA:["WORK-AREA"]"
      D    DISPLAY  "ORA-WORKING-AREA:["ORA-WORKING-AREA"]"
      D    DISPLAY  "PARM-SCZY1170-INOUT:["PARM-SCZY1170-INOUT"]"
      D    DISPLAY  "PARM-SCZY1170-RTNPKG:["PARM-SCZY1170-RTNPKG"]"
      D    DISPLAY  "PARM-SCZY1140-INOUT:["PARM-SCZY1140-INOUT"]"
      D    DISPLAY  "PARM-TIDYAA36-INOUT:["PARM-TIDYAA36-INOUT"]"
      D    DISPLAY  "PARM-SCZY7080-RTNPKG:["PARM-SCZY7080-RTNPKG"]"
      D    DISPLAY  "***  初期化処理  END  ***"
      *-----------------------------------------------------------------
      *-------------------ARG1の必須項目＝'△'の場合---------------
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "利用会社コード:["
      D                             RIYO-CMP-CD OF PARM-SCZY7080-ARG1"]"
      D    DISPLAY  "処理実行日:["SYORI-YMD OF PARM-SCZY7080-ARG1"]"
      D    DISPLAY  "起動ID:["KIDOU-ID OF PARM-SCZY7080-ARG1"]"
      D    DISPLAY  "業務タスクID:["
      D                           GYOMU-TASK-ID OF PARM-SCZY7080-ARG1"]"
      *-----------------------------------------------------------------
           IF  RIYO-CMP-CD              OF  PARM-SCZY7080-ARG1
                                        =  SPACE  OR
               SYORI-YMD                OF  PARM-SCZY7080-ARG1
                                        =  SPACE  OR
               KIDOU-ID                 OF  PARM-SCZY7080-ARG1
                                        =  SPACE  OR
               GYOMU-TASK-ID            OF  PARM-SCZY7080-ARG1
                                        =  SPACE
           THEN
               MOVE  CNS-PROGRAM-ID
                 TO  ERR-PGM-ID         OF  PARM-SCZY1170-ARG1
               MOVE  "800"
                 TO  RTN-CD             OF  PARM-SCZY1170-ARG1
               MOVE  "0"
                 TO  DETAIL-RTN-CD-FLG  OF  PARM-SCZY1170-ARG1
               MOVE  "0001"
                 TO  ERR-POS            OF  PARM-SCZY1170-ARG1
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "ERR-PGM-ID OF PARM-SCZY1170-ARG1:["
      D                        ERR-PGM-ID OF PARM-SCZY1170-ARG1"]"
      D    DISPLAY  "RTN-CD OF PARM-SCZY1170-ARG1:["
      D                                  RTN-CD OF PARM-SCZY1170-ARG1"]"
      D    DISPLAY  "DETAIL-RTN-CD-FLG OF PARM-SCZY1170-ARG1:["
      D                       DETAIL-RTN-CD-FLG OF PARM-SCZY1170-ARG1"]"
      D    DISPLAY  "ERR-POS OF PARM-SCZY1170-ARG1:["
      D                                 ERR-POS OF PARM-SCZY1170-ARG1"]"
      *-----------------------------------------------------------------
               PERFORM  ERR-RTN
           END-IF.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  INIT  END  ***".
      *-----------------------------------------------------------------
       INIT-EXT.
           EXIT.
      ******************************************************************
      *                        主処理
      ******************************************************************
       MAIN-RTN               SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  主処理  START  *****"
      *-----------------------------------------------------------------
           MOVE  MSG-ID                 OF  PARM-SCZY7080-ARG2(1)
             TO  WK-MSG-ID
           MOVE  1                      TO  IDX
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "メッセージID:["WK-MSG-ID"]"
      *-----------------------------------------------------------------
           PERFORM  UNTIL  WK-MSG-ID = SPACE OR IDX > 999
      *---------------------メッセージ文言取得処理----------------------
               PERFORM MSG-SYUTOKU-RTN
      *-----実行状況エラーメッセージテーブルのシーケンスを取得する------
               PERFORM SEQ-SYUTOKU-RTN
      *---------------------実行状況エラーメッセージＩＮＳ処理----------
               PERFORM JIKOU-INS-RTN
               COMPUTE IDX = IDX + 1
               IF IDX < 1000 THEN
                   MOVE  MSG-ID         OF  PARM-SCZY7080-ARG2(IDX)
                   TO  WK-MSG-ID
               END-IF
           END-PERFORM.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "*****  主処理  END  *****".
      *-----------------------------------------------------------------
       MAIN-EXT.
           EXIT.
      ******************************************************************
      *                     文言を取得ルーチン
      ******************************************************************
       MSG-SYUTOKU-RTN        SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***   文言を取得処理  START  ***"
      *-----------------------------------------------------------------
           MOVE  WK-MSG-ID
             TO  MSG-ID                 OF  PARM-SCZY1140-ARG1
           MOVE  TIKAN-MOJI             OF  PARM-SCZY7080-ARG2(IDX)
             TO  TIKAN-MOJI             OF  PARM-SCZY1140-ARG1
           CALL  "SCZY1140"             USING  PARM-SCZY1140-INOUT
                                               WK-RTNPKG
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "RTN-CD("  COUNTER       OF  WK-RTNPKG
      D      ") OF SCZY1140=["  RTN-CD       OF
      D      WK-RTNPKG(COUNTER    OF WK-RTNPKG)"]"
      D    DISPLAY "MSG-MESSAGE:[" MSG-MESSAGE
      D      OF  PARM-SCZY1140-ARG2"]"
      *-----------------------------------------------------------------
           IF  RTN-CD
             OF  WK-RTNPKG(COUNTER OF WK-RTNPKG) = CNS-RET-000
           THEN
               MOVE  MSG-MESSAGE             OF  PARM-SCZY1140-ARG2
                 TO  WK-MSG-MESSAGE
           ELSE
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY "文言を取得 ERROR !"
      *-----------------------------------------------------------------
               MOVE  CNS-PROGRAM-ID
                 TO  ERR-PGM-ID              OF  PARM-SCZY1170-ARG1
               MOVE  "901"
                 TO  RTN-CD                  OF  PARM-SCZY1170-ARG1
               MOVE  "0"
                 TO  DETAIL-RTN-CD-FLG       OF  PARM-SCZY1170-ARG1
               MOVE  "0002"
                 TO  ERR-POS                 OF  PARM-SCZY1170-ARG1
               MOVE  WK-RTNPKG               TO  PARM-SCZY1170-ARG2
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "ERR-PGM-ID OF PARM-SCZY1170-ARG1:["
      D                        ERR-PGM-ID OF PARM-SCZY1170-ARG1"]"
      D    DISPLAY  "RTN-CD OF PARM-SCZY1170-ARG1:["
      D                                  RTN-CD OF PARM-SCZY1170-ARG1"]"
      D    DISPLAY  "DETAIL-RTN-CD-FLG OF PARM-SCZY1170-ARG1:["
      D                       DETAIL-RTN-CD-FLG OF PARM-SCZY1170-ARG1"]"
      D    DISPLAY  "ERR-POS OF PARM-SCZY1170-ARG1:["
      D                                 ERR-POS OF PARM-SCZY1170-ARG1"]"
      D    DISPLAY  "PARM-SCZY1170-ARG2:["PARM-SCZY1170-ARG2"]"
      *-----------------------------------------------------------------
               PERFORM  ERR-RTN
           END-IF.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***   文言を取得処理  END  ***".
      *-----------------------------------------------------------------
       MSG-SYUTOKU-EXT.
           EXIT.
      ******************************************************************
      *       実行状況エラーメッセージテーブルのシーケンスを取得する
      ******************************************************************
       SEQ-SYUTOKU-RTN        SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "シーケンスを取得開始"
      *-----------------------------------------------------------------
           EXEC  SQL
             SELECT  seq_0003_9999.NEXTVAL
               INTO  :HST-BANGO
               FROM  DUAL
           END-EXEC
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "SQLCODE:["SQLCODE"]"
      *-----------------------------------------------------------------
           IF  SQLCODE NOT =  CNS-SQL-NORMAL
           THEN
               STRING  "SELECT  seq_0003_9999.NEXTVAL "
                       "INTO  :HST-BANGO "
                       "FROM  DUAL"
               DELIMITED BY SIZE
               INTO  WK-FREE-MSG
               MOVE  CNS-PROGRAM-ID
                 TO  ERR-PGM-ID              OF PARM-SCZY1170-ARG1
               MOVE  "900"
                 TO  RTN-CD                  OF PARM-SCZY1170-ARG1
               MOVE  "0003"
                 TO  ERR-POS                 OF PARM-SCZY1170-ARG1
               MOVE  SQLCODE
                 TO  DETAIL-RTN-CD           OF PARM-SCZY1170-ARG1
               MOVE  "1"
                 TO  DETAIL-RTN-CD-FLG       OF PARM-SCZY1170-ARG1
               MOVE  WK-FREE-MSG
                 TO  FREE-MSG                OF PARM-SCZY1170-ARG1
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "ERR-PGM-ID OF PARM-SCZY1170-ARG1:["
      D                              ERR-PGM-ID OF PARM-SCZY1170-ARG1"]"
      D    DISPLAY  "RTN-CD OF PARM-SCZY1170-ARG1:["
      D                                  RTN-CD OF PARM-SCZY1170-ARG1"]"
      D    DISPLAY  "DETAIL-RTN-CD-FLG OF PARM-SCZY1170-ARG1:["
      D                       DETAIL-RTN-CD-FLG OF PARM-SCZY1170-ARG1"]"
      D    DISPLAY  "ERR-POS OF PARM-SCZY1170-ARG1:["
      D                                 ERR-POS OF PARM-SCZY1170-ARG1"]"
      D    DISPLAY  "DETAIL-RTN-CD-FLG OF PARM-SCZY1170-ARG1:["
      D                       DETAIL-RTN-CD-FLG OF PARM-SCZY1170-ARG1"]"
      D    DISPLAY  "FREE-MSG OF PARM-SCZY1170-ARG1:["
      D                                FREE-MSG OF PARM-SCZY1170-ARG1"]"
      *-----------------------------------------------------------------
               PERFORM  ERR-RTN
           END-IF.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "シーケンスを取得終了".
      *-----------------------------------------------------------------
       SEQ-SYUTOKU-EXT.
           EXIT.
      ******************************************************************
      *                 実行状況エラーメッセージＩＮＳ処理
      ******************************************************************
       JIKOU-INS-RTN          SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "実行状況エラーメッセージＩＮＳ処理開始"
      *-----------------------------------------------------------------
           INITIALIZE  WK-RTNPKG
      *----------  利用会社コード ← ARG1．同名項目
           MOVE  RIYO-CMP-CD                 OF  PARM-SCZY7080-ARG1
             TO  RIYO-CMP-CD                 OF  PARM-TIDYAA36-ARG1
           MOVE  RIYO-CMP-CD                 OF  PARM-SCZY7080-ARG1
             TO  RIYO-CMP-CD                 OF  PARM-TIDYAA36-ARG2
      *----------  処理実行日 ← ARG1．同名項目
           MOVE  SYORI-YMD                   OF  PARM-SCZY7080-ARG1
             TO  SYORI-YMD                   OF  PARM-TIDYAA36-ARG2
      *----------  起動ID ← ARG1．同名項目
           MOVE  KIDOU-ID                    OF  PARM-SCZY7080-ARG1
             TO  KIDOU-ID                    OF  PARM-TIDYAA36-ARG2
      *----------  帳票ID ← ARG2．帳票ID
           MOVE  REP-ID                      OF  PARM-SCZY7080-ARG2(IDX)
             TO  REP-ID                      OF  PARM-TIDYAA36-ARG2
      *----------  帳票任意コード ← ARG2．帳票任意コード
           MOVE  REP-ANY-CD                  OF  PARM-SCZY7080-ARG2(IDX)
             TO  REP-ANY-CD                  OF  PARM-TIDYAA36-ARG2
      *----------  基準日From ← ARG2．基準日From
           MOVE  KJN-YMD-FROM                OF  PARM-SCZY7080-ARG2(IDX)
             TO  KJN-YMD-FROM                OF  PARM-TIDYAA36-ARG2
      *----------  基準日To ← ARG2．基準日To
           MOVE  KJN-YMD-TO                  OF  PARM-SCZY7080-ARG2(IDX)
             TO  KJN-YMD-TO                  OF  PARM-TIDYAA36-ARG2
      *----------  ファンドコード ← ARG2．同名項目
           MOVE  FUND-CD                     OF  PARM-SCZY7080-ARG2(IDX)
             TO  FUND-CD                     OF  PARM-TIDYAA36-ARG2
      *----------  ポートフォリオコード ← ARG2．同名項目
           MOVE  PTF-CD                      OF  PARM-SCZY7080-ARG2(IDX)
             TO  PTF-CD                      OF  PARM-TIDYAA36-ARG2
      *----------  評価系列コード ← ARG2．同名項目
           MOVE  HYK-KRTU-CD                 OF  PARM-SCZY7080-ARG2(IDX)
             TO  HYK-KRTU-CD                 OF  PARM-TIDYAA36-ARG2
      *----------  番号
           MOVE  HST-BANGO
             TO  BANGO                       OF  PARM-TIDYAA36-ARG2
      *----------  業務タスクID ← ARG1．同名項目
           MOVE  GYOMU-TASK-ID               OF  PARM-SCZY7080-ARG1
             TO  GYOMU-TASK-ID               OF  PARM-TIDYAA36-ARG2
      *----------  メッセージID ← ARG2．同名項目
           MOVE  WK-MSG-ID
             TO  MSG-ID                      OF  PARM-TIDYAA36-ARG2
      *----------  エラーメッセージ ← SCZY1140の戻り値
           MOVE  WK-MSG-MESSAGE
             TO  ERR-MSG                     OF  PARM-TIDYAA36-ARG2
      *----------  フリーメッセージ ← ARG2．同名項目
           MOVE  FREE-MSG                    OF  PARM-SCZY7080-ARG2(IDX)
             TO  FREE-MSG                    OF  PARM-TIDYAA36-ARG2
      *----------  更新日時 ← 初期値
      *----------  更新ログインユーザ会社コード ← ARG1．利用会社コード
           MOVE  RIYO-CMP-CD                 OF  PARM-SCZY7080-ARG1
             TO  UPD-LOGIN-CMP-CD            OF  PARM-TIDYAA36-ARG2
      *----------  更新ユーザID ← 初期値
      *----------  更新プログラムID ← 初期値
      *-----------------------------------------------------------------
           CALL  "TIDYAA36"                  USING  PARM-TIDYAA36-INOUT
                                                    WK-RTNPKG
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "利用会社コード:["
      D                             RIYO-CMP-CD OF PARM-TIDYAA36-ARG2"]"
      D    DISPLAY  "起動ID:["KIDOU-ID OF PARM-TIDYAA36-ARG2"]"
      D    DISPLAY  "処理実行日:["SYORI-YMD   OF PARM-TIDYAA36-ARG2"]"
      D    DISPLAY  "ファンドコード:["FUND-CD OF PARM-TIDYAA36-ARG2"]"
      D    DISPLAY  "ポートフォリオコード:["
      D                                  PTF-CD OF PARM-TIDYAA36-ARG2"]"
      D    DISPLAY  "評価系列コード:["
      D                             HYK-KRTU-CD OF PARM-TIDYAA36-ARG2"]"
      D    DISPLAY  "帳票ID:["REP-ID  OF PARM-TIDYAA36-ARG2"]"
      D    DISPLAY  "帳票任意コード:["
      D                             REP-ANY-CD  OF PARM-TIDYAA36-ARG2"]"
      D    DISPLAY  "基準日From:["KJN-YMD-FROM  OF PARM-TIDYAA36-ARG2"]"
      D    DISPLAY  "基準日To  :["KJN-YMD-TO    OF PARM-TIDYAA36-ARG2"]"
      D    DISPLAY  "番号:["BANGO OF PARM-TIDYAA36-ARG2"]"
      D    DISPLAY  "業務タスクID:["
      D                           GYOMU-TASK-ID OF PARM-TIDYAA36-ARG2"]"
      D    DISPLAY  "メッセージID:["MSG-ID OF PARM-TIDYAA36-ARG2"]"
      D    DISPLAY  "エラーメッセージ:["ERR-MSG OF PARM-TIDYAA36-ARG2"]"
      D    DISPLAY  "フリーメッセージ:["
      D                                FREE-MSG OF PARM-TIDYAA36-ARG2"]"
      D    DISPLAY  "更新ログインユーザ会社コード:["
      D                        UPD-LOGIN-CMP-CD OF PARM-TIDYAA36-ARG2"]"
      D    DISPLAY  "RTN-CD("  COUNTER       OF  WK-RTNPKG
      D      ") OF TIDYAA36=["  RTN-CD       OF
      D      WK-RTNPKG(COUNTER    OF WK-RTNPKG)"]"
      *-----------------------------------------------------------------
           EVALUATE  RTN-CD
             OF  WK-RTNPKG(COUNTER  OF  WK-RTNPKG)
               WHEN  "000"
                   CONTINUE
               WHEN  "901"
                   MOVE  CNS-PROGRAM-ID
                     TO  ERR-PGM-ID          OF  PARM-SCZY1170-ARG1
                   MOVE  "0004"
                     TO  ERR-POS             OF  PARM-SCZY1170-ARG1
                   MOVE  "901"
                     TO  RTN-CD              OF  PARM-SCZY1170-ARG1
                   MOVE  "0"
                     TO  DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1
                   MOVE  WK-RTNPKG           TO  PARM-SCZY1170-RTNPKG
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  "PARM-SCZY1170-ARG1 =[" PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  "PARM-SCZY1170-RTNPKG =[" PARM-SCZY1170-RTNPKG "]"
      *-----------------------------------------------------------------
                   PERFORM  ERR-RTN
               WHEN  OTHER
                   MOVE  CNS-PROGRAM-ID
                     TO  ERR-PGM-ID          OF  PARM-SCZY1170-ARG1
                   MOVE  "0005"
                     TO  ERR-POS             OF  PARM-SCZY1170-ARG1
                   MOVE  "900"
                     TO  RTN-CD              OF  PARM-SCZY1170-ARG1
                   MOVE  "0"
                     TO  DETAIL-RTN-CD-FLG   OF  PARM-SCZY1170-ARG1
                   MOVE  WK-RTNPKG           TO  PARM-SCZY1170-RTNPKG
      *--------------------<DEBUG>--------------------------------------
      D    DISPLAY  "PARM-SCZY1170-ARG1 =[" PARM-SCZY1170-ARG1 "]"
      D    DISPLAY  "PARM-SCZY1170-RTNPKG =[" PARM-SCZY1170-RTNPKG "]"
      *-----------------------------------------------------------------
                   PERFORM  ERR-RTN
           END-EVALUATE.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "実行状況エラーメッセージＩＮＳ処理終了".
      *-----------------------------------------------------------------
       JIKOU-INS-EXT.
           EXIT.
      ******************************************************************
      *                        後処理
      ******************************************************************
       END-RTN                SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***   後処理  START  ***"
      *-----------------------------------------------------------------
           MOVE  CNS-RET-000
             TO  RTN-CD                 OF  PARM-SCZY1170-ARG1
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "RTN-CD OF PARM-SCZY1170-ARG1:["
      D                                  RTN-CD OF PARM-SCZY1170-ARG1"]"
      *-----------------------------------------------------------------
           CALL  "SCZY1170"             USING  PARM-SCZY1170-INOUT
                                               PARM-SCZY1170-RTNPKG
           MOVE  PARM-SCZY1170-ARG2     TO  PARM-SCZY7080-ARG3.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "PARM-SCZY7080-ARG3:["PARM-SCZY7080-ARG3"]"
      *-----------------------------------------------------------------
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***   後処理  END  ***".
      *-----------------------------------------------------------------
       END-EXT.
         EXIT.
      ******************************************************************
      *                 エラー処理
      ******************************************************************
       ERR-RTN                SECTION.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "***  ERR-RTN  START  ***"
      *-----------------------------------------------------------------
           CALL  "SCZY1170"             USING  PARM-SCZY1170-INOUT
                                               PARM-SCZY1170-RTNPKG
           MOVE  PARM-SCZY1170-ARG2     TO  PARM-SCZY7080-ARG3.
      *-------------------<DEBUG>---------------------------------------
      D    DISPLAY  "PARM-SCZY7080-ARG3:["PARM-SCZY7080-ARG3"]"
      D    DISPLAY  "***  ERR-RTN  END  ***".
      *-----------------------------------------------------------------
       ERR-EXT.
           GOBACK.
      ******************************************************************
